OBJECT Report 5183 Resend Attachments
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=DateianhÑnge erneut senden;
               ENU=Resend Attachments];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  LoggedSeg@1000 : Record 5075;
                BEGIN
                  IF "Interaction Log Entry".GETFILTER("Logged Segment Entry No.") = '' THEN
                    ERROR(Text000,"Interaction Log Entry".FIELDCAPTION("Logged Segment Entry No."));

                  IF "Interaction Log Entry".GETFILTER("Logged Segment Entry No.") <> '0' THEN BEGIN
                    LoggedSeg.SETFILTER("Entry No.","Interaction Log Entry".GETFILTER("Logged Segment Entry No."));
                    IF LoggedSeg.COUNT <> 1 THEN
                      ERROR(
                        Text001,LoggedSeg.TABLECAPTION);
                  END;
                END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table5065;
        DataItemTableView=SORTING(Logged Segment Entry No.)
                          WHERE(Postponed=CONST(No));
        OnAfterGetRecord=VAR
                           Attachment@1000 : Record 5062;
                           InteractionLogEntryNew@1001 : Record 5065;
                           SegLine@1007 : Record 5077;
                           InterLogEntryCommentLine@1008 : Record 5123;
                           SegManagement@1006 : Codeunit 5051;
                           NewEntryNo@1002 : Integer;
                         BEGIN
                           IF NOT Attachment.GET("Attachment No.") THEN
                             CurrReport.SKIP;
                           IF Attachment."Storage Type" = Attachment."Storage Type"::"Exchange Storage" THEN
                             CurrReport.SKIP;
                           IF CorrespondenceType = CorrespondenceType::"Same as Entry" THEN
                             TESTFIELD("Correspondence Type");

                           IF UpdateMergeFields THEN BEGIN
                             IF DeliveryBufferTemp.GET("Entry No.") THEN
                               CurrReport.SKIP;
                             InteractionLogEntryNew.TRANSFERFIELDS("Interaction Log Entry",FALSE);
                             InteractionLogEntryNew.VALIDATE(Date,WORKDATE);
                             InteractionLogEntryNew.VALIDATE("Time of Interaction",TIME);
                             IF STRPOS("Interaction Log Entry".Description,Text003) <> 1 THEN BEGIN
                               IF STRLEN("Interaction Log Entry".Description) + STRLEN(Text003) <= MAXSTRLEN(InteractionLogEntryNew.Description) THEN
                                 InteractionLogEntryNew.VALIDATE(Description,Text003 + "Interaction Log Entry".Description)
                               ELSE
                                 InteractionLogEntryNew.VALIDATE(Description,COPYSTR(
                                   Text003 + "Interaction Log Entry".Description,1,MAXSTRLEN(InteractionLogEntryNew.Description)))
                             END;
                             IF CorrespondenceType <> CorrespondenceType::"Same as Entry" THEN
                               InteractionLogEntryNew."Correspondence Type" := CorrespondenceType;
                             SegManagement.CopyFieldsFromInteractLogEntry(SegLine,InteractionLogEntryNew);
                             InterLogEntryCommentLine.SETRANGE("Entry No.","Entry No.");
                             IF InterLogEntryCommentLine.FIND('-') THEN;
                             Attachment.CALCFIELDS(Attachment);
                             CLEAR(Attachment."Merge Source");
                             NewEntryNo := SegManagement.LogInteraction(SegLine,Attachment,InterLogEntryCommentLine,FALSE,FALSE);
                             InteractionLogEntryNew.GET(NewEntryNo);
                             InteractionLogEntryNew.VALIDATE("Logged Segment Entry No.","Logged Segment Entry No.");
                             InteractionLogEntryNew.VALIDATE("Delivery Status","Delivery Status"::"In Progress");
                             InteractionLogEntryNew.VALIDATE("E-Mail Logged",FALSE);
                             InteractionLogEntryNew.MODIFY(TRUE)
                           END ELSE BEGIN
                             IF CorrespondenceType <> CorrespondenceType::"Same as Entry" THEN
                               "Correspondence Type" := CorrespondenceType;
                             "Delivery Status" := "Delivery Status"::"In Progress";
                             "E-Mail Logged" := FALSE;
                             MODIFY
                           END;

                           DeliveryBufferTemp.INIT;
                           IF UpdateMergeFields THEN BEGIN
                             DeliveryBufferTemp."No." := NewEntryNo;
                             DeliveryBufferTemp."Attachment No." := InteractionLogEntryNew."Attachment No.";
                             DeliveryBufferTemp."Correspondence Type" := InteractionLogEntryNew."Correspondence Type"
                           END ELSE BEGIN
                             DeliveryBufferTemp."No." := "Entry No.";
                             DeliveryBufferTemp."Attachment No." := "Attachment No.";
                             DeliveryBufferTemp."Correspondence Type" := "Correspondence Type"
                           END;
                           DeliveryBufferTemp.Subject := Subject;
                           DeliveryBufferTemp."Send Word Docs. as Attmt." := "Send Word Docs. as Attmt.";
                           DeliveryBufferTemp.INSERT;
                         END;

        OnPostDataItem=BEGIN
                         IF DeliveryBufferTemp.COUNT = 0 THEN
                           ERROR(Text002);

                         COMMIT;
                         AttachmentManagement.Send(DeliveryBufferTemp);
                       END;

        ReqFilterFields=Logged Segment Entry No.,Entry No.,Delivery Status,Correspondence Type,Contact No.,Campaign No.;
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=6160;
      Height=1100;
    }
    CONTROLS
    {
      { 3   ;TextBox      ;3410 ;0    ;2750 ;440  ;CaptionML=[DEU=Korrespondenzart;
                                                              ENU=Correspondence Type];
                                                   OptionCaptionML=[DEU=Wie Posten,Brief,E-Mail,Fax;
                                                                    ENU=Same as Entry,Hard Copy,E-Mail,Fax];
                                                   SourceExpr=CorrespondenceType }
      { 4   ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=3 }
      { 7   ;CheckBox     ;3410 ;660  ;440  ;440  ;Name=Update Merge Fields;
                                                   ShowCaption=No;
                                                   CaptionML=[DEU=Seriendruckfelder aktualisieren;
                                                              ENU=Update Merge Fields];
                                                   SourceExpr=UpdateMergeFields }
      { 8   ;Label        ;0    ;660  ;3300 ;440  ;ParentControl=7 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options] }

      { 3   ;2   ;Field     ;
                  CaptionML=[DEU=Korrespondenzart;
                             ENU=Correspondence Type];
                  OptionCaptionML=[DEU=Wie Posten,Brief,E-Mail,Fax;
                                   ENU=Same as Entry,Hard Copy,E-Mail,Fax];
                  SourceExpr=CorrespondenceType }

      { 7   ;2   ;Field     ;
                  Name=Update Merge Fields;
                  CaptionML=[DEU=Seriendruckfelder aktualisieren;
                             ENU=Update Merge Fields];
                  SourceExpr=UpdateMergeFields }

    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=%1 muss angegeben werden.;ENU=%1 must be specified.';
      Text001@1001 : TextConst 'DEU=Die AktivitÑtenprotokollposten mÅssen immer vom gleichen %1 sein.;ENU=The interaction log entries must always be from the same %1.';
      Text002@1002 : TextConst 'DEU=Es gibt nichts zu senden.\\Bitte beachten Sie, dass nur Microsoft Word-Dokumente erneut gesendet werden kînnen.;ENU=Nothing to send.\\Please note that only Microsoft Word documents can be resend.';
      DeliveryBufferTemp@1003 : TEMPORARY Record 5074;
      AttachmentManagement@1004 : Codeunit 5052;
      CorrespondenceType@1005 : 'Same as Entry,Hard Copy,E-Mail,Fax';
      UpdateMergeFields@1006 : Boolean;
      Text003@1007 : TextConst 'DEU=Erneut senden:;ENU=Resend:';

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

