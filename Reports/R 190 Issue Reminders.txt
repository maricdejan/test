OBJECT Report 190 Issue Reminders
{
  OBJECT-PROPERTIES
  {
    Date=14.08.09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Mahnungen registrieren;
               ENU=Issue Reminders];
    ProcessingOnly=Yes;
  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table295;
        DataItemTableView=SORTING(No.);
        ReqFilterHeadingML=[DEU=Mahnung;
                            ENU=Reminder];
        OnPreDataItem=BEGIN
                        IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                          ERROR(Text000);
                        NoOfRecords := COUNT;
                        IF NoOfRecords = 1 THEN
                          Window.OPEN(Text001)
                        ELSE BEGIN
                          Window.OPEN(Text002);
                          OldTime := TIME;
                        END;
                      END;

        OnAfterGetRecord=BEGIN
                           RecordNo := RecordNo + 1;
                           UpdateReminderRounding("Reminder Header");
                           CLEAR(ReminderIssue);
                           ReminderIssue.Set("Reminder Header",ReplacePostingDate,PostingDateReq);
                           IF NoOfRecords = 1 THEN BEGIN
                             ReminderIssue.RUN;
                             "Reminder Header".MARK := FALSE;
                           END ELSE BEGIN
                             NewTime := TIME;
                             IF (NewTime - OldTime > 100) OR (NewTime < OldTime) THEN BEGIN
                               NewProgress := ROUND(RecordNo / NoOfRecords * 100,1);
                               IF NewProgress <> OldProgress THEN BEGIN
                                 Window.UPDATE(1,NewProgress * 100);
                                 OldProgress := NewProgress;
                               END;
                               OldTime := TIME;
                             END;
                             "Reminder Header".MARK := NOT ReminderIssue.RUN;
                           END;

                           IF PrintDoc AND NOT "Reminder Header".MARK THEN BEGIN
                             ReminderIssue.GetIssuedReminder(IssuedReminderHeader);
                             IssuedReminderHeader.MARK := TRUE;
                           END;
                         END;

        OnPostDataItem=BEGIN
                         Window.CLOSE;
                         COMMIT;
                         IF PrintDoc THEN BEGIN
                           IssuedReminderHeader.MARKEDONLY := TRUE;
                           IssuedReminderHeader.PrintRecords(FALSE);
                         END;
                         "Reminder Header".MARKEDONLY := TRUE;
                         IF "Reminder Header".FIND('-') THEN
                           IF CONFIRM(
                               Text003 +
                               Text004,
                               TRUE)
                           THEN
                             FORM.RUNMODAL(0,"Reminder Header");
                       END;

        ReqFilterFields=No.;
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=5060;
      Height=1540;
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1   ;TextBox      ;3410 ;1100 ;1650 ;440  ;CaptionML=[DEU=Buchungsdatum;
                                                              ENU=Posting Date];
                                                   SourceExpr=PostingDateReq }
      { 21  ;Label        ;0    ;1100 ;3300 ;440  ;ParentControl=1 }
      { 2   ;CheckBox     ;3410 ;550  ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[DEU=Buchungsdatum ersetzen;
                                                              ENU=Replace Posting Date];
                                                   SourceExpr=ReplacePostingDate }
      { 22  ;Label        ;0    ;550  ;3300 ;440  ;ParentControl=2 }
      { 3   ;CheckBox     ;3410 ;0    ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[DEU=Drucken;
                                                              ENU=Print];
                                                   SourceExpr=PrintDoc }
      { 4   ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=3 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options] }

      { 3   ;2   ;Field     ;
                  CaptionML=[DEU=Drucken;
                             ENU=Print];
                  SourceExpr=PrintDoc }

      { 2   ;2   ;Field     ;
                  CaptionML=[DEU=Buchungsdatum ersetzen;
                             ENU=Replace Posting Date];
                  SourceExpr=ReplacePostingDate }

      { 1   ;2   ;Field     ;
                  CaptionML=[DEU=Buchungsdatum;
                             ENU=Posting Date];
                  SourceExpr=PostingDateReq }

    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Bitte das Buchungsdatum eingeben.;ENU=Please enter the posting date.';
      Text001@1001 : TextConst 'DEU=Mahnung registrieren...;ENU=Issuing reminder...';
      Text002@1002 : TextConst 'DEU=Mahnungen registrieren @1@@@@@@@@@@@@@;ENU=Issuing reminders @1@@@@@@@@@@@@@';
      Text003@1003 : TextConst 'DEU=Es war nicht m”glich, einige der ausgew„hlten Mahnungen zu registrieren\\;ENU=It was not possible to issue some of the selected reminders.\\';
      Text004@1004 : TextConst 'DEU=M”chten Sie diese Mahnungen sehen?;ENU=Do you want to see these reminders?';
      IssuedReminderHeader@1005 : Record 297;
      ReminderIssue@1006 : Codeunit 393;
      Window@1007 : Dialog;
      NoOfRecords@1008 : Integer;
      RecordNo@1009 : Integer;
      NewProgress@1010 : Integer;
      OldProgress@1011 : Integer;
      NewTime@1012 : Time;
      OldTime@1013 : Time;
      PostingDateReq@1014 : Date;
      ReplacePostingDate@1015 : Boolean;
      PrintDoc@1016 : Boolean;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

