OBJECT Report 491 Delete Invd Blnkt Purch Orders
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00,NAVDACH6.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Erledigte Rahmenbest. l”schen;
               ENU=Delete Invd Blnkt Purch Orders];
    ProcessingOnly=Yes;
  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table38;
        DataItemTableView=SORTING(Document Type,No.)
                          WHERE(Document Type=CONST(Blanket Order));
        ReqFilterHeadingML=[DEU=Rahmenbestellung;
                            ENU=Blanket Purchase Order];
        OnPreDataItem=BEGIN
                        Window.OPEN(Text000);
                      END;

        OnAfterGetRecord=BEGIN
                           Window.UPDATE(1,"No.");

                           DocDim.RESET;
                           DocDim.SETRANGE("Document Type","Document Type");
                           DocDim.SETRANGE("Document No.","No.");
                           PurchLine.RESET;
                           PurchLine.SETRANGE("Document Type","Document Type");
                           PurchLine.SETRANGE("Document No.","No.");
                           PurchLine.SETFILTER("Quantity Invoiced",'<>0');
                           IF PurchLine.FIND('-') THEN BEGIN
                             PurchLine.SETRANGE("Quantity Invoiced");
                             PurchLine.SETFILTER("Outstanding Quantity",'<>0');
                             IF NOT PurchLine.FIND('-') THEN BEGIN
                               PurchLine.SETRANGE("Outstanding Quantity");
                               PurchLine.SETFILTER("Qty. Rcd. Not Invoiced",'<>0');
                               IF NOT PurchLine.FIND('-') THEN BEGIN
                                 PurchLine.LOCKTABLE;
                                 IF NOT PurchLine.FIND('-') THEN BEGIN
                                   PurchLine.SETRANGE("Qty. Rcd. Not Invoiced");
                                   PurchLine2.SETRANGE("Blanket Order No.","No.");
                                   IF NOT PurchLine2.FIND('-') THEN BEGIN

                                     PurchSetup.GET;
                                     IF PurchSetup."Archiving Blanket Purch. Order" THEN
                                       ArchiveManagement.ArchPurchDocumentNoConfirm("Purchase Header");

                                     DocDim.SETRANGE("Table ID",DATABASE::"Purchase Line");
                                     IF PurchLine.FIND('-') THEN BEGIN
                                       REPEAT
                                         DocDim.SETRANGE("Line No.",PurchLine."Line No.");
                                         DocDim.DELETEALL;
                                       UNTIL PurchLine.NEXT = 0;
                                     END;

                                     PurchLine.DELETEALL;

                                     PurchCommentLine.SETRANGE("Document Type","Document Type");
                                     PurchCommentLine.SETRANGE("No.","No.");
                                     PurchCommentLine.DELETEALL;

                                     DocDim.SETRANGE("Table ID",DATABASE::"Purchase Header");
                                     DocDim.SETRANGE("Line No.",0);
                                     DocDim.DELETEALL;

                                     DELETE;

                                     COMMIT;
                                   END;
                                 END;
                               END;
                             END;
                           END;
                         END;

        ReqFilterFields=No.,Buy-from Vendor No.,Pay-to Vendor No.;
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Bearbeite Bestellungen #1##########;ENU=Processing purch. orders #1##########';
      PurchLine@1001 : Record 39;
      PurchLine2@1002 : Record 39;
      PurchCommentLine@1003 : Record 43;
      DocDim@1004 : Record 357;
      Window@1005 : Dialog;
      PurchSetup@1140000 : Record 312;
      ArchiveManagement@1140001 : Codeunit 5063;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

