OBJECT Report 496 Batch Post Purchase Orders
{
  OBJECT-PROPERTIES
  {
    Date=14.08.09;
    Time=12:00:00;
    Version List=NAVW16.00.01,NAVDACH6.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Bestellungen stapelbuchen;
               ENU=Batch Post Purchase Orders];
    ProcessingOnly=Yes;
  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table38;
        DataItemTableView=SORTING(Document Type,No.)
                          WHERE(Document Type=CONST(Order));
        ReqFilterHeadingML=[DEU=Einkaufsbestellung;
                            ENU=Purchase Order];
        OnPreDataItem=BEGIN
                        IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                          ERROR(Text000);

                        Approvaltemplate.SETCURRENTKEY(Approvaltemplate."Table ID",Approvaltemplate."Document Type",Approvaltemplate.Enabled);
                        Approvaltemplate.SETRANGE("Table ID",DATABASE::"Purchase Header");
                        Approvaltemplate.SETRANGE("Document Type","Purchase Header"."Document Type"::Order);
                        Approvaltemplate.SETRANGE(Enabled,TRUE);
                        IF Approvaltemplate.FINDFIRST THEN
                          "Purchase Header".SETRANGE(Status,"Purchase Header".Status::Released);

                        CounterTotal := COUNT;
                        Window.OPEN(Text001);
                      END;

        OnAfterGetRecord=BEGIN
                           IF CalcInvDisc THEN
                             CalculateInvoiceDiscount;

                           Counter := Counter + 1;
                           Window.UPDATE(1,"No.");
                           Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));
                           "Purchase Header".Receive := ReceiveReq;
                           "Purchase Header".Invoice := InvReq;
                           CLEAR(PurchPost);
                           PurchPost.SetPostingDate(ReplacePostingDate,ReplaceDocumentDate,PostingDateReq);
                           IF PurchPost.RUN("Purchase Header") THEN BEGIN
                             CounterOK := CounterOK + 1;
                             IF MARKEDONLY THEN
                               MARK(FALSE);
                           END;

                             IF Print THEN BEGIN
                               CASE "Document Type" OF
                               "Document Type"::Order:
                                 BEGIN
                                   IF Receive THEN BEGIN
                                     PurchRcptHeader."No." := "Last Receiving No.";
                                     PurchRcptHeader.SETRECFILTER;
                                     PrintReport(ReportSelection.Usage::"P.Receipt");
                                   END;
                                   IF Invoice THEN BEGIN
                                     PurchInvHeader."No." := "Last Posting No.";
                                     PurchInvHeader.SETRECFILTER;
                                     PrintReport(ReportSelection.Usage::"P.Invoice");
                                   END;
                                 END;
                               "Document Type"::Invoice:
                                 BEGIN
                                   IF "Last Posting No." = '' THEN
                                     PurchInvHeader."No." := "No."
                                   ELSE
                                     PurchInvHeader."No." := "Last Posting No.";
                                   PurchInvHeader.SETRECFILTER;
                                   PrintReport(ReportSelection.Usage::"P.Invoice");
                                 END;
                               "Document Type"::"Credit Memo":
                                 BEGIN
                                   IF "Last Posting No." = '' THEN
                                     PurchCrMemoHeader."No." := "No."
                                   ELSE
                                     PurchCrMemoHeader."No." := "Last Posting No.";
                                   PurchCrMemoHeader.SETRECFILTER;
                                   PrintReport(ReportSelection.Usage::"P.Cr.Memo");
                                 END;
                               END;
                             END;
                         END;

        OnPostDataItem=BEGIN
                         Window.CLOSE;
                         MESSAGE(Text002,CounterOK,CounterTotal);
                       END;

        ReqFilterFields=No.,Status;
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=5170;
      Height=3960;
      SaveValues=Yes;
      OnOpenForm=BEGIN
                   PurchSetup.GET;
                   CalcInvDisc := PurchSetup."Calc. Inv. Discount";
                 END;

    }
    CONTROLS
    {
      { 1   ;CheckBox     ;3410 ;0    ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[DEU=Lieferung;
                                                              ENU=Receive];
                                                   SourceExpr=ReceiveReq }
      { 21  ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=1 }
      { 2   ;CheckBox     ;3410 ;550  ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[DEU=Rechnung;
                                                              ENU=Invoice];
                                                   SourceExpr=InvReq }
      { 22  ;Label        ;0    ;550  ;3300 ;440  ;ParentControl=2 }
      { 3   ;TextBox      ;3410 ;1210 ;1650 ;440  ;CaptionML=[DEU=Buchungsdatum;
                                                              ENU=Posting Date];
                                                   SourceExpr=PostingDateReq }
      { 23  ;Label        ;0    ;1210 ;3300 ;440  ;ParentControl=3 }
      { 4   ;CheckBox     ;3410 ;1760 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[DEU=Buchungsdatum ersetzen;
                                                              ENU=Replace Posting Date];
                                                   SourceExpr=ReplacePostingDate;
                                                   OnValidate=BEGIN
                                                                IF ReplacePostingDate THEN
                                                                  MESSAGE(Text003);
                                                              END;
                                                               }
      { 24  ;Label        ;0    ;1760 ;3300 ;440  ;ParentControl=4 }
      { 5   ;CheckBox     ;3410 ;2310 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[DEU=Belegdatum ersetzen;
                                                              ENU=Replace Document Date];
                                                   SourceExpr=ReplaceDocumentDate }
      { 6   ;Label        ;0    ;2310 ;3300 ;440  ;ParentControl=5 }
      { 7   ;CheckBox     ;3410 ;2860 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[DEU=Rechnungsrab. berechnen;
                                                              ENU=Calc. Inv. Discount];
                                                   SourceExpr=CalcInvDisc;
                                                   OnValidate=BEGIN
                                                                PurchSetup.GET;
                                                                PurchSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                                                              END;
                                                               }
      { 8   ;Label        ;0    ;2860 ;3300 ;440  ;ParentControl=7 }
      { 1150000;CheckBox  ;3410 ;3410 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[DEU=Drucken;
                                                              ENU=Print];
                                                   SourceExpr=Print }
      { 1150001;Label     ;0    ;3410 ;3300 ;440  ;ParentControl=1150000 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   PurchSetup.GET;
                   CalcInvDisc := PurchSetup."Calc. Inv. Discount";
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options] }

      { 1   ;2   ;Field     ;
                  CaptionML=[DEU=Lieferung;
                             ENU=Receive];
                  SourceExpr=ReceiveReq }

      { 2   ;2   ;Field     ;
                  CaptionML=[DEU=Rechnung;
                             ENU=Invoice];
                  SourceExpr=InvReq }

      { 3   ;2   ;Field     ;
                  CaptionML=[DEU=Buchungsdatum;
                             ENU=Posting Date];
                  SourceExpr=PostingDateReq }

      { 4   ;2   ;Field     ;
                  CaptionML=[DEU=Buchungsdatum ersetzen;
                             ENU=Replace Posting Date];
                  SourceExpr=ReplacePostingDate;
                  OnValidate=BEGIN
                               IF ReplacePostingDate THEN
                                 MESSAGE(Text003);
                             END;
                              }

      { 5   ;2   ;Field     ;
                  CaptionML=[DEU=Belegdatum ersetzen;
                             ENU=Replace Document Date];
                  SourceExpr=ReplaceDocumentDate }

      { 7   ;2   ;Field     ;
                  CaptionML=[DEU=Rechnungsrab. berechnen;
                             ENU=Calc. Inv. Discount];
                  SourceExpr=CalcInvDisc;
                  OnValidate=BEGIN
                               PurchSetup.GET;
                               PurchSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                             END;
                              }

      { 1150000;2;Field     ;
                  CaptionML=[DEU=Drucken;
                             ENU=Print];
                  SourceExpr=Print }

    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Bitte das Buchungsdatum eingeben.;ENU=Please enter the posting date.';
      Text001@1001 : TextConst 'DEU=Bestellungen werden gebucht #1########## @2@@@@@@@@@@@@@;ENU=Posting orders  #1########## @2@@@@@@@@@@@@@';
      Text002@1002 : TextConst 'DEU=%1 Bestellungen von insgesamt %2 wurden gebucht.;ENU=%1 orders out of a total of %2 have now been posted.';
      PurchLine@1003 : Record 39;
      PurchSetup@1004 : Record 312;
      Approvaltemplate@1018 : Record 464;
      PurchCalcDisc@1005 : Codeunit 70;
      PurchPost@1006 : Codeunit 90;
      Window@1007 : Dialog;
      ReceiveReq@1008 : Boolean;
      InvReq@1009 : Boolean;
      PostingDateReq@1010 : Date;
      CounterTotal@1011 : Integer;
      Counter@1012 : Integer;
      CounterOK@1013 : Integer;
      ReplacePostingDate@1014 : Boolean;
      ReplaceDocumentDate@1015 : Boolean;
      CalcInvDisc@1016 : Boolean;
      Text003@1017 : TextConst 'DEU=Der mit dem neuen Buchungsdatum im Einkaufskopf verknÅpfte Wechselkurs wird nicht auf die Einkaufszeilen angewendet.;ENU=The exchange rate associated with the new posting date on the purchase header will not apply to the purchase lines.';
      Print@1150000 : Boolean;
      ReportSelection@1150004 : Record 77;
      PurchInvHeader@1150003 : Record 122;
      PurchCrMemoHeader@1150002 : Record 124;
      PurchRcptHeader@1150001 : Record 120;

    PROCEDURE CalculateInvoiceDiscount@1();
    BEGIN
      PurchLine.RESET;
      PurchLine.SETRANGE("Document Type","Purchase Header"."Document Type");
      PurchLine.SETRANGE("Document No.","Purchase Header"."No.");
      IF PurchLine.FIND('-') THEN
        IF PurchCalcDisc.RUN(PurchLine) THEN BEGIN
          "Purchase Header".GET("Purchase Header"."Document Type","Purchase Header"."No.");
          COMMIT;
        END;
    END;

    LOCAL PROCEDURE PrintReport@1150000(ReportUsage@1000 : Integer);
    BEGIN
      ReportSelection.RESET;
      ReportSelection.SETRANGE(Usage,ReportUsage);
      ReportSelection.FIND('-');
      REPEAT
        ReportSelection.TESTFIELD("Report ID");
        CASE ReportUsage OF
          ReportSelection.Usage::"P.Invoice":
            REPORT.RUN(ReportSelection."Report ID",FALSE,FALSE,PurchInvHeader);
          ReportSelection.Usage::"P.Cr.Memo":
            REPORT.RUN(ReportSelection."Report ID",FALSE,FALSE,PurchCrMemoHeader);
          ReportSelection.Usage::"P.Receipt":
            REPORT.RUN(ReportSelection."Report ID",FALSE,FALSE,PurchRcptHeader);
      //    ReportSelection.Usage::"P.Ret.Shpt.":
      //      REPORT.RUN(ReportSelection."Report ID",FALSE,FALSE,PurchShptHeader);
        END;
      UNTIL ReportSelection.NEXT = 0;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

