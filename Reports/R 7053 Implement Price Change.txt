OBJECT Report 7053 Implement Price Change
{
  OBJECT-PROPERTIES
  {
    Date=14.08.09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Preisvorschlag Åbernehmen;
               ENU=Implement Price Change];
    ProcessingOnly=Yes;
  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table7023;
        DataItemTableView=SORTING(Starting Date,Ending Date,Sales Type,Sales Code,Currency Code,Item No.,Variant Code,Unit of Measure Code,Minimum Quantity);
        OnPreDataItem=BEGIN
                        Window.OPEN(
                          Text000 +
                          Text007 +
                          Text008 +
                          Text009 +
                          Text010 +
                          Text011);
                      END;

        OnAfterGetRecord=VAR
                           RecRef@1000 : RecordRef;
                           xRecRef@1001 : RecordRef;
                           ChangeLogMgt@1002 : Codeunit 423;
                         BEGIN
                           Window.UPDATE(1,"Item No.");
                           Window.UPDATE(2,"Sales Type");
                           Window.UPDATE(3,"Sales Code");
                           Window.UPDATE(4,"Currency Code");
                           Window.UPDATE(5,"Starting Date");

                           SalesPrice.INIT;
                           SalesPrice.VALIDATE("Item No.","Item No.");
                           SalesPrice.VALIDATE("Sales Type","Sales Type");
                           SalesPrice.VALIDATE("Sales Code","Sales Code");
                           SalesPrice.VALIDATE("Unit of Measure Code","Unit of Measure Code");
                           SalesPrice.VALIDATE("Variant Code","Variant Code");
                           SalesPrice.VALIDATE("Starting Date","Starting Date");
                           SalesPrice.VALIDATE("Ending Date","Ending Date");
                           SalesPrice."Minimum Quantity" := "Minimum Quantity";
                           SalesPrice."Currency Code" := "Currency Code";
                           SalesPrice."Unit Price" := "New Unit Price";
                           SalesPrice."Price Includes VAT" := "Price Includes VAT";
                           SalesPrice."Allow Line Disc." := "Allow Line Disc.";
                           SalesPrice."Allow Invoice Disc." := "Allow Invoice Disc.";
                           SalesPrice."VAT Bus. Posting Gr. (Price)" := "VAT Bus. Posting Gr. (Price)";
                           IF SalesPrice."Unit Price" <> 0 THEN
                             IF NOT SalesPrice.INSERT(TRUE) THEN BEGIN
                               xRecRef.GETTABLE(SalesPrice);
                               xRecRef.FIND('=');
                               SalesPrice.MODIFY(TRUE);
                               RecRef.GETTABLE(SalesPrice);
                               ChangeLogMgt.LogModification(RecRef,xRecRef);
                             END ELSE BEGIN
                               RecRef.GETTABLE(SalesPrice);
                               ChangeLogMgt.LogInsertion(RecRef);
                             END;
                         END;

        OnPostDataItem=BEGIN
                         COMMIT;
                         IF NOT DeleteWhstLine THEN
                           DeleteWhstLine := CONFIRM(Text005);
                         IF DeleteWhstLine THEN
                           "Sales Price Worksheet".DELETEALL;
                         COMMIT;
                         IF SalesPrice.FIND('-') THEN;
                       END;

        ReqFilterFields=Item No.,Sales Type,Sales Code,Unit of Measure Code,Currency Code;
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Aktualisieren von VK-Preisen...\\;ENU=Updating Unit Prices...\\';
      Text005@1001 : TextConst 'DEU=Die VK-Preise wurden aufgrund der vorgeschlagenen PreisÑnderungen aktualisiert.\\Mîchten Sie die vorgeschlagenen PreisÑnderungen lîschen?;ENU=The item prices have now been updated in accordance with the suggested price changes.\\Do you want to delete the suggested price changes?';
      Text007@1003 : TextConst 'DEU=Artikelnr.             #1##########\;ENU=Item No.               #1##########\';
      Text008@1004 : TextConst 'DEU=Verkaufsart            #2##########\;ENU=Sales Type             #2##########\';
      Text009@1005 : TextConst 'DEU=Verkaufscode           #3##########\;ENU=Sales Code             #3##########\';
      Text010@1006 : TextConst 'DEU=WÑhrungscode           #4##########\;ENU=Currency Code          #4##########\';
      Text011@1007 : TextConst 'DEU=Startdatum             #5######;ENU=Starting Date          #5######';
      SalesPrice@1008 : Record 7002;
      Window@1009 : Dialog;
      DeleteWhstLine@1010 : Boolean;

    PROCEDURE InitializeRequest@1(NewDeleteWhstLine@1000 : Boolean);
    BEGIN
      DeleteWhstLine := NewDeleteWhstLine;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

