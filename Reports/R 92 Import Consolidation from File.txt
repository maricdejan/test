OBJECT Report 92 Import Consolidation from File
{
  OBJECT-PROPERTIES
  {
    Date=14.08.09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Konsol. v. Datei importieren;
               ENU=Import Consolidation from File];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  TempGLAccount@1000 : TEMPORARY Record 15;
                  AnalysisView@1004 : Record 363;
                  TempAnalysisView@1003 : TEMPORARY Record 363;
                  AnalysisViewEntry@1002 : Record 365;
                  BusUnit2@1006 : Record 220;
                  PrevAccountNo@1001 : Code[20];
                  AnalysisViewFound@1005 : Boolean;
                  GLSetup@1007 : Record 98;
                BEGIN
                  IF GLDocNo = '' THEN
                    ERROR(Text015);

                  IF ISSERVICETIER THEN
                    ReadFromFile(FileName);

                  IF FileName = '' THEN
                    ERROR(Text000);

                  IF FileFormat = FileFormat::"Version 4.00 or Later (.xml)" THEN BEGIN
                    Consolidate.ImportFromXML(FileName);
                    Consolidate.GetGlobals(
                      ProductVersion,FormatVersion,BusUnit."Company Name",
                      SubsidCurrencyCode,AdditionalCurrencyCode,ParentCurrencyCode,
                      CheckSum,ConsolidStartDate,ConsolidEndDate);
                    CalculatedCheckSum := Consolidate.CalcCheckSum;
                    IF CheckSum <> CalculatedCheckSum THEN
                      ERROR(Text036,CheckSum,CalculatedCheckSum);
                    TransferPerDay := TRUE;
                  END ELSE BEGIN
                    CLEAR(GLEntryFile);
                    GLEntryFile.TEXTMODE := TRUE;
                    GLEntryFile.OPEN(FileName);
                    GLEntryFile.READ(TextLine);
                    IF COPYSTR(TextLine,1,4) = '<01>' THEN BEGIN
                      BusUnit."Company Name" := DELCHR(COPYSTR(TextLine,5,30),'>');
                      EVALUATE(ConsolidStartDate,COPYSTR(TextLine,36,9));
                      EVALUATE(ConsolidEndDate,COPYSTR(TextLine,46,9));
                      EVALUATE(TransferPerDay,COPYSTR(TextLine,56,3));
                    END;
                  END;

                  IF (BusUnit."Company Name" = '') OR (ConsolidStartDate = 0D) OR (ConsolidEndDate = 0D) THEN
                    ERROR(Text001);

                  IF NOT
                     CONFIRM(
                       Text023,
                       FALSE,ConsolidStartDate,ConsolidEndDate)
                  THEN
                    CurrReport.QUIT;

                  BusUnit.SETCURRENTKEY("Company Name");
                  BusUnit.SETRANGE("Company Name",BusUnit."Company Name");
                  BusUnit.FIND('-');
                  IF BusUnit.NEXT <> 0 THEN
                    ERROR(
                      Text005 +
                      Text006,
                      BusUnit.FIELDCAPTION("Company Name"),BusUnit."Company Name");
                  BusUnit.TESTFIELD(Consolidate,TRUE);

                  BusUnit2."File Format" := FileFormat;
                  IF BusUnit."File Format" <> FileFormat THEN
                    IF NOT CONFIRM(
                      Text037 + Text038,FALSE,
                      BusUnit.FIELDCAPTION("File Format"),BusUnit2."File Format",BusUnit.TABLECAPTION,BusUnit."File Format")
                    THEN
                      CurrReport.QUIT;

                  IF FileFormat = FileFormat::"Version 4.00 or Later (.xml)" THEN BEGIN
                    IF SubsidCurrencyCode = '' THEN
                      SubsidCurrencyCode := BusUnit."Currency Code";
                    GLSetup.GET;
                    IF (SubsidCurrencyCode <> BusUnit."Currency Code") AND
                       (SubsidCurrencyCode <> GLSetup."LCY Code") AND
                       NOT ((BusUnit."Currency Code" = '') AND (GLSetup."LCY Code" = ''))
                    THEN
                      ERROR(
                        Text002,
                        BusUnit.FIELDCAPTION("Currency Code"),SubsidCurrencyCode,
                        BusUnit.TABLECAPTION,BusUnit."Currency Code");
                  END ELSE BEGIN
                    SubsidCurrencyCode := BusUnit."Currency Code";
                    Window.OPEN(
                      '#1###############################\\' +
                      Text024 +
                      Text025 +
                      Text026);
                    Window.UPDATE(1,Text027);
                    Window.UPDATE(2,BusUnit.Code);
                    Window.UPDATE(3,'');
                  END;
                END;

    OnPostReport=BEGIN
                   IF FileFormat = FileFormat::"Version 3.70 or Earlier (.txt)" THEN
                     Consolidate.SetGlobals(
                       '','',BusUnit."Company Name",
                       SubsidCurrencyCode,AdditionalCurrencyCode,ParentCurrencyCode,
                       0,ConsolidStartDate,ConsolidEndDate);
                   Consolidate.SetDocNo(GLDocNo);
                   Consolidate.RUN(BusUnit);
                 END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table15;
        DataItemTableView=SORTING(No.)
                          WHERE(Account Type=CONST(Posting));
        OnPreDataItem=BEGIN
                        IF FileFormat = FileFormat::"Version 4.00 or Later (.xml)" THEN
                          CurrReport.BREAK;
                      END;

        OnAfterGetRecord=BEGIN
                           "Consol. Debit Acc." := "No.";
                           "Consol. Credit Acc." := "No.";
                           "Consol. Translation Method" := "Consol. Translation Method"::"Average Rate (Manual)";
                           Consolidate.InsertGLAccount("G/L Account");
                         END;

        OnPostDataItem=BEGIN
                         IF FileFormat = FileFormat::"Version 4.00 or Later (.xml)" THEN
                           CurrReport.BREAK;

                         // Import G/L entries
                         WHILE GLEntryFile.POS <> GLEntryFile.LEN DO BEGIN
                           GLEntryFile.READ(TextLine);
                           CASE COPYSTR(TextLine,1,4) OF
                             '<02>':
                               BEGIN
                                 TempGLEntry.INIT;
                                 EVALUATE(TempGLEntry."G/L Account No.",COPYSTR(TextLine,5,20));
                                 EVALUATE(TempGLEntry."Posting Date",COPYSTR(TextLine,26,9));
                                 EVALUATE(TempGLEntry.Amount,COPYSTR(TextLine,36,22));
                                 IF TempGLEntry.Amount > 0 THEN
                                   TempGLEntry."Debit Amount" := TempGLEntry.Amount
                                 ELSE
                                   TempGLEntry."Credit Amount" := -TempGLEntry.Amount;
                                 TempGLEntry."Entry No." := Consolidate.InsertGLEntry(TempGLEntry);
                               END;
                             '<03>':
                               BEGIN
                                 TempEntryDim."Entry No." := TempGLEntry."Entry No.";
                                 TempEntryDim."Dimension Code" := COPYSTR(TextLine,5,20);
                                 TempEntryDim."Dimension Value Code" := COPYSTR(TextLine,26,20);
                                 Consolidate.InsertEntryDim(TempEntryDim);
                               END;
                           END;
                         END;

                         Consolidate.SelectAllImportedDimensions;
                       END;

      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=8910;
      Height=2090;
      SaveValues=Yes;
      OnOpenForm=BEGIN
                   RequestOptionsForm.FileName.VISIBLE := NOT ISSERVICETIER;
                 END;

    }
    CONTROLS
    {
      { 1   ;TextBox      ;3410 ;550  ;5500 ;440  ;Name=FileName;
                                                   CaptionML=[DEU=Dateiname;
                                                              ENU=File Name];
                                                   SourceExpr=FileName;
                                                   OnAssistEdit=BEGIN
                                                                  ReadFromFile(FileName);
                                                                END;
                                                                 }
      { 21  ;Label        ;0    ;550  ;3300 ;440  ;ParentControl=1 }
      { 2   ;TextBox      ;3410 ;1100 ;2750 ;440  ;CaptionML=[DEU=Belegnr.;
                                                              ENU=Document No.];
                                                   SourceExpr=GLDocNo }
      { 22  ;Label        ;0    ;1100 ;3300 ;440  ;ParentControl=2 }
      { 3   ;TextBox      ;3410 ;0    ;2750 ;440  ;CaptionML=[DEU=Dateiformat;
                                                              ENU=File Format];
                                                   OptionCaptionML=[DEU=Version 4.00 oder hîher (.xml),Version 3.70 oder frÅher (.txt);
                                                                    ENU=Version 4.00 or Later (.xml),Version 3.70 or Earlier (.txt)];
                                                   SourceExpr=FileFormat }
      { 4   ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=3 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnInit=BEGIN
               FileNameVisible := TRUE;
             END;

      OnOpenPage=BEGIN
                   FileNameVisible := NOT ISSERVICETIER;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options] }

      { 3   ;2   ;Field     ;
                  CaptionML=[DEU=Dateiformat;
                             ENU=File Format];
                  OptionCaptionML=[DEU=Version 4.00 oder hîher (.xml),Version 3.70 oder frÅher (.txt);
                                   ENU=Version 4.00 or Later (.xml),Version 3.70 or Earlier (.txt)];
                  SourceExpr=FileFormat }

      { 1   ;2   ;Field     ;
                  Name=FileName;
                  CaptionML=[DEU=Dateiname;
                             ENU=File Name];
                  SourceExpr=FileName;
                  Visible=FileNameVisible;
                  OnAssistEdit=BEGIN
                                 ReadFromFile(FileName);
                               END;
                                }

      { 2   ;2   ;Field     ;
                  CaptionML=[DEU=Belegnr.;
                             ENU=Document No.];
                  SourceExpr=GLDocNo }

    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Geben Sie den Dateinamen ein.;ENU=Enter the file name.';
      Text001@1001 : TextConst 'DEU=Die Importdatei hat ein unbekanntes Format.;ENU=The file to be imported has an unknown format.';
      Text002@1067 : TextConst 'DEU=%1 in der Importdatei (%2) entspricht nicht %1 in %3 (%4).;ENU=The %1 in the file to be imported (%2) does not match the %1 in the %3 (%4).';
      Text005@1003 : TextConst 'DEU=Der Konzernmandant %1 %2 ist nicht eindeutig.\\;ENU=The business unit %1 %2 is not unique.\\';
      Text006@1004 : TextConst 'DEU=Lîschen Sie %1 in den zusÑtzlichen DatensÑtzen.;ENU=Delete %1 in the extra records.';
      Text015@1007 : TextConst 'DEU=Geben Sie eine Belegnummer ein.;ENU=Enter a document number.';
      Text023@1014 : TextConst 'DEU=Mîchten Sie die Periode vom %1 bis zum %2 konsolidieren?;ENU=Do you want to consolidate in the period from %1 to %2?';
      Text024@1015 : TextConst 'DEU=Konzernmandantencode #2##########\;ENU=Business Unit Code   #2##########\';
      Text025@1016 : TextConst 'DEU=Sachkontonr.         #3##########\;ENU=G/L Account No.      #3##########\';
      Text026@1017 : TextConst 'DEU=Datum                #4######;ENU=Date                 #4######';
      Text027@1018 : TextConst 'DEU=Datei wird gelesen ...;ENU=Reading File...';
      Text031@1022 : TextConst 'DEU=Aus Textdatei importieren;ENU=Import from Text File';
      BusUnit@1025 : Record 220;
      TempGLEntry@1032 : TEMPORARY Record 17;
      TempEntryDim@1075 : TEMPORARY Record 355;
      TempGLAcc@1076 : TEMPORARY Record 15;
      Consolidate@1064 : Codeunit 432;
      Window@1036 : Dialog;
      GLEntryFile@1037 : File;
      FileName@1038 : Text[250];
      FileFormat@1060 : 'Version 4.00 or Later (.xml),Version 3.70 or Earlier (.txt)';
      TextLine@1039 : Text[250];
      GLDocNo@1040 : Code[20];
      ConsolidStartDate@1041 : Date;
      ConsolidEndDate@1042 : Date;
      TransferPerDay@1043 : Boolean;
      SkipAllDimensions@1066 : Boolean;
      CheckSum@1065 : Decimal;
      CalculatedCheckSum@1073 : Decimal;
      Text034@1061 : TextConst 'DEU=Aus XML-Datei importieren;ENU=Import from XML File';
      Text035@1062 : TextConst 'DEU=XML-Dateien (*.xml)|*.xml|Alle Dateien (*.*)|*.*;ENU=XML Files (*.xml)|*.xml|All Files (*.*)|*.*';
      ParentCurrencyCode@1072 : Code[10];
      SubsidCurrencyCode@1071 : Code[10];
      AdditionalCurrencyCode@1070 : Code[10];
      ProductVersion@1068 : Code[10];
      FormatVersion@1069 : Code[10];
      Text036@1074 : TextConst 'DEU=Importierte PrÅfsumme (%1) stimmt nicht mit der berechneten PrÅfsumme (%2) Åberein. Die Datei ist mîglicherweise beschÑdigt.;ENU=Imported checksum (%1) does not equal the calculated checksum (%2). The file may be corrupt.';
      Text037@1005 : TextConst 'DEU=Der eingegebene %1, %2 stimmt nicht mit dem %1 in diesem %3, %4 Åberein.;ENU=The entered %1, %2, does not equal the %1 on this %3, %4.';
      Text038@1002 : TextConst 'DEU=\ OK zum Fortsetzen?;ENU=\ OK to continue?';
      Text039@1008 : TextConst 'DEU=Die Datei wurde erfolgreich auf den Server hochgeladen.;ENU=The file was successfully uploaded to server';
      FileNameVisible@19066691 : Boolean INDATASET;

    PROCEDURE ReadFromFile@4(VAR FileName2@1102601000 : Text[1024]);
    VAR
      CommonDialogMgt@1000 : Codeunit 412;
      NewFileName@1102601001 : Text[1024];
    BEGIN
      IF FileFormat = FileFormat::"Version 4.00 or Later (.xml)" THEN
        BEGIN
          IF NOT ISSERVICETIER THEN
            NewFileName := CommonDialogMgt.OpenFile(Text034,FileName2,4,Text035,0)
          ELSE
          IF NOT(UPLOAD(Text034, '', Text035, FileName2, NewFileName))
          THEN
            NewFileName := ''
        END
      ELSE
        BEGIN
          IF NOT ISSERVICETIER THEN
            NewFileName := CommonDialogMgt.OpenFile(Text031,FileName2,1,'',0)
          ELSE
          IF NOT(UPLOAD(Text031, '', Text035, FileName2, NewFileName))
          THEN
            NewFileName := '';
        END;

      IF NewFileName <> '' THEN
        FileName2 := NewFileName;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

