OBJECT Report 291 Delete Invd Blnkt Sales Orders
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00,NAVDACH6.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Erledigte Rahmenauftr. l”schen;
               ENU=Delete Invd Blnkt Sales Orders];
    ProcessingOnly=Yes;
  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table36;
        DataItemTableView=SORTING(Document Type,No.)
                          WHERE(Document Type=CONST(Blanket Order));
        ReqFilterHeadingML=[DEU=Rahmenauftrag;
                            ENU=Blanket Sales Order];
        OnPreDataItem=BEGIN
                        Window.OPEN(Text000);
                      END;

        OnAfterGetRecord=BEGIN
                           Window.UPDATE(1,"No.");

                           DocDim.RESET;
                           DocDim.SETRANGE("Document Type","Document Type");
                           DocDim.SETRANGE("Document No.","No.");
                           SalesLine.RESET;
                           SalesLine.SETRANGE("Document Type","Document Type");
                           SalesLine.SETRANGE("Document No.","No.");
                           SalesLine.SETFILTER("Quantity Invoiced",'<>0');
                           IF SalesLine.FIND('-') THEN BEGIN
                             SalesLine.SETRANGE("Quantity Invoiced");
                             SalesLine.SETFILTER("Outstanding Quantity",'<>0');
                             IF NOT SalesLine.FIND('-') THEN BEGIN
                               SalesLine.SETRANGE("Outstanding Quantity");
                               SalesLine.SETFILTER("Qty. Shipped Not Invoiced",'<>0');
                               IF NOT SalesLine.FIND('-') THEN BEGIN
                                 SalesLine.LOCKTABLE;
                                 IF NOT SalesLine.FIND('-') THEN BEGIN
                                   SalesLine.SETRANGE("Qty. Shipped Not Invoiced");
                                   SalesLine2.SETRANGE("Blanket Order No.","No.");
                                   IF NOT SalesLine2.FIND('-') THEN BEGIN

                                     SalesSetup.GET;
                                     IF SalesSetup."Archiving Blanket Sales Order" THEN
                                       ArchiveManagement.ArchSalesDocumentNoConfirm("Sales Header");

                                     DocDim.SETRANGE("Table ID",DATABASE::"Sales Line");
                                     IF SalesLine.FIND('-') THEN BEGIN
                                       REPEAT
                                         DocDim.SETRANGE("Line No.",SalesLine."Line No.");
                                         DocDim.DELETEALL;
                                       UNTIL SalesLine.NEXT = 0;
                                     END;

                                     SalesLine.DELETEALL;

                                     SalesCommentLine.SETRANGE("Document Type","Document Type");
                                     SalesCommentLine.SETRANGE("No.","No.");
                                     SalesCommentLine.DELETEALL;

                                     DocDim.SETRANGE("Table ID",DATABASE::"Sales Header");
                                     DocDim.SETRANGE("Line No.",0);
                                     DocDim.DELETEALL;

                                     DELETE;

                                     COMMIT;
                                   END;
                                 END;
                               END;
                             END;
                           END;
                         END;

        ReqFilterFields=No.,Sell-to Customer No.,Bill-to Customer No.;
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Verkauf Auftr„ge werden durchgelesen #1##########;ENU=Processing sales orders #1##########';
      SalesLine@1001 : Record 37;
      SalesLine2@1002 : Record 37;
      SalesCommentLine@1003 : Record 44;
      DocDim@1004 : Record 357;
      Window@1005 : Dialog;
      SalesSetup@1140000 : Record 311;
      ArchiveManagement@1140001 : Codeunit 5063;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

