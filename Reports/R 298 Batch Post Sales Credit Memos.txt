OBJECT Report 298 Batch Post Sales Credit Memos
{
  OBJECT-PROPERTIES
  {
    Date=14.08.09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Verk.Gutschriften stapelbuchen;
               ENU=Batch Post Sales Credit Memos];
    ProcessingOnly=Yes;
  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table36;
        DataItemTableView=SORTING(Document Type,No.)
                          WHERE(Document Type=CONST(Credit Memo));
        ReqFilterHeadingML=[DEU=Verkaufsgutschrift;
                            ENU=Sales Credit Memo];
        OnPreDataItem=BEGIN
                        IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                          ERROR(Text000);

                        Approvaltemplate.SETCURRENTKEY(Approvaltemplate."Table ID",Approvaltemplate."Document Type",Approvaltemplate.Enabled);
                        Approvaltemplate.SETRANGE("Table ID",DATABASE::"Sales Header");
                        Approvaltemplate.SETRANGE("Document Type","Sales Header"."Document Type"::"Credit Memo");
                        Approvaltemplate.SETRANGE(Enabled,TRUE);
                        IF Approvaltemplate.FINDFIRST THEN
                          "Sales Header".SETRANGE(Status,"Sales Header".Status::Released);

                        CounterTotal := COUNT;
                        Window.OPEN(Text001);
                      END;

        OnAfterGetRecord=BEGIN
                           IF CalcInvDisc THEN
                             CalculateInvoiceDiscount;

                           Counter := Counter + 1;
                           Window.UPDATE(1,"No.");
                           Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));
                           CLEAR(SalesPost);
                           SalesPost.SetPostingDate(ReplacePostingDate,ReplaceDocumentDate,PostingDateReq);
                           IF SalesPost.RUN("Sales Header") THEN BEGIN
                             CounterOK := CounterOK + 1;
                             IF MARKEDONLY THEN
                               MARK(FALSE);
                           END;
                         END;

        OnPostDataItem=BEGIN
                         Window.CLOSE;
                         MESSAGE(Text002,CounterOK,CounterTotal);
                       END;

        ReqFilterFields=No.,Status;
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=5060;
      Height=2090;
      SaveValues=Yes;
      OnOpenForm=BEGIN
                   SalesSetup.GET;
                   CalcInvDisc := SalesSetup."Calc. Inv. Discount";
                   ReplacePostingDate := FALSE;
                   ReplaceDocumentDate := FALSE;
                 END;

    }
    CONTROLS
    {
      { 1   ;TextBox      ;3410 ;0    ;1650 ;440  ;CaptionML=[DEU=Buchungsdatum;
                                                              ENU=Posting Date];
                                                   SourceExpr=PostingDateReq }
      { 21  ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=1 }
      { 2   ;CheckBox     ;3410 ;550  ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[DEU=Buchungsdatum ersetzen;
                                                              ENU=Replace Posting Date];
                                                   SourceExpr=ReplacePostingDate;
                                                   OnValidate=BEGIN
                                                                IF ReplacePostingDate THEN
                                                                  MESSAGE(Text003);
                                                              END;
                                                               }
      { 22  ;Label        ;0    ;550  ;3300 ;440  ;ParentControl=2 }
      { 3   ;CheckBox     ;3410 ;1100 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[DEU=Belegdatum ersetzen;
                                                              ENU=Replace Document Date];
                                                   SourceExpr=ReplaceDocumentDate }
      { 4   ;Label        ;0    ;1100 ;3300 ;440  ;ParentControl=3 }
      { 5   ;CheckBox     ;3410 ;1650 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[DEU=Rechnungsrab. berechnen;
                                                              ENU=Calc. Inv. Discount];
                                                   SourceExpr=CalcInvDisc;
                                                   OnValidate=BEGIN
                                                                SalesSetup.GET;
                                                                SalesSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                                                              END;
                                                               }
      { 6   ;Label        ;0    ;1650 ;3300 ;440  ;ParentControl=5 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   SalesSetup.GET;
                   CalcInvDisc := SalesSetup."Calc. Inv. Discount";
                   ReplacePostingDate := FALSE;
                   ReplaceDocumentDate := FALSE;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[DEU=Optionen;
                             ENU=Options] }

      { 1   ;2   ;Field     ;
                  CaptionML=[DEU=Buchungsdatum;
                             ENU=Posting Date];
                  SourceExpr=PostingDateReq }

      { 2   ;2   ;Field     ;
                  CaptionML=[DEU=Buchungsdatum ersetzen;
                             ENU=Replace Posting Date];
                  SourceExpr=ReplacePostingDate;
                  OnValidate=BEGIN
                               IF ReplacePostingDate THEN
                                 MESSAGE(Text003);
                             END;
                              }

      { 3   ;2   ;Field     ;
                  CaptionML=[DEU=Belegdatum ersetzen;
                             ENU=Replace Document Date];
                  SourceExpr=ReplaceDocumentDate }

      { 5   ;2   ;Field     ;
                  CaptionML=[DEU=Rechnungsrab. berechnen;
                             ENU=Calc. Inv. Discount];
                  SourceExpr=CalcInvDisc;
                  OnValidate=BEGIN
                               SalesSetup.GET;
                               SalesSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                             END;
                              }

    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Bitte das Buchungsdatum eingeben.;ENU=Please enter the posting date.';
      Text001@1001 : TextConst 'DEU=Gutschriften buchen  #1########## @2@@@@@@@@@@@@@;ENU=Posting credit memos  #1########## @2@@@@@@@@@@@@@';
      Text002@1002 : TextConst 'DEU=%1 Gutschriften von insgesamt %2 wurden gebucht.;ENU=%1 credit memos out of a total of %2 have now been posted.';
      Text003@1003 : TextConst 'DEU=Der zu dem neuen Buchungsdatum geh”rige Wechselkurs auf dem Verkaufskopf wird nicht auf die Verkaufszeilen angewendet.;ENU=The exchange rate associated with the new posting date on the sales header will not apply to the sales lines.';
      SalesLine@1005 : Record 37;
      SalesSetup@1006 : Record 311;
      Approvaltemplate@1004 : Record 464;
      SalesCalcDisc@1007 : Codeunit 60;
      SalesPost@1008 : Codeunit 80;
      Window@1009 : Dialog;
      PostingDateReq@1010 : Date;
      CounterTotal@1011 : Integer;
      Counter@1012 : Integer;
      CounterOK@1013 : Integer;
      ReplacePostingDate@1016 : Boolean;
      ReplaceDocumentDate@1017 : Boolean;
      CalcInvDisc@1018 : Boolean;

    PROCEDURE CalculateInvoiceDiscount@1();
    BEGIN
      SalesLine.RESET;
      SalesLine.SETRANGE("Document Type","Sales Header"."Document Type");
      SalesLine.SETRANGE("Document No.","Sales Header"."No.");
      IF SalesLine.FIND('-') THEN
        IF SalesCalcDisc.RUN(SalesLine) THEN BEGIN
          "Sales Header".GET("Sales Header"."Document Type","Sales Header"."No.");
          COMMIT;
        END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

