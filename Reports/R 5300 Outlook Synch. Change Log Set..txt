OBJECT Report 5300 Outlook Synch. Change Log Set.
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Outlook-Synch.-énderungsprotokolleinr.;
               ENU=Outlook Synch. Change Log Set.];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF NOT OSynchEntity.FIND('-') THEN
                    ERROR(Text004);
                END;

    OnPostReport=VAR
                   ChangeLogSetup@1000 : Record 402;
                 BEGIN
                   IF NOT ChangeLogSetup.GET THEN
                     ChangeLogSetup.INSERT;
                   IF NOT ChangeLogSetup."Change Log Activated" THEN BEGIN
                     ChangeLogSetup."Change Log Activated" := TRUE;
                     ChangeLogSetup.MODIFY;
                     MESSAGE(Text002);
                   END ELSE
                     MESSAGE(Text003);
                 END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table5300;
        DataItemTableView=SORTING(Code);
        DataItemVarName=OSynchEntity;
        OnAfterGetRecord=BEGIN
                           RegisterChangeLogPrimaryKey(OSynchEntity."Table No.");
                         END;

        ReqFilterFields=Code;
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table5301;
        DataItemTableView=SORTING(Synch. Entity Code,Element No.)
                          ORDER(Ascending)
                          WHERE(Element No.=FILTER(<>0));
        DataItemVarName=OSynchEntityElement;
        OnAfterGetRecord=BEGIN
                           RegisterChangeLogPrimaryKey(OSynchEntityElement."Table No.");
                         END;

        DataItemLink=Synch. Entity Code=FIELD(Code);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=2;
        DataItemTable=Table5303;
        DataItemTableView=SORTING(Record GUID,Filter Type,Line No.)
                          ORDER(Ascending)
                          WHERE(Filter Type=CONST(Table Relation));
        DataItemVarName=OSynchFilterElement;
        OnAfterGetRecord=BEGIN
                           RegisterChangeLogFilter(OSynchFilterElement);
                         END;

        DataItemLink=Record GUID=FIELD(Record GUID);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table5304;
        DataItemTableView=SORTING(Synch. Entity Code,Element No.,Line No.);
        DataItemVarName=OSynchField;
        OnAfterGetRecord=VAR
                           OSynchFilter@1000 : Record 5303;
                           OSynchEntityElement@1001 : Record 5301;
                         BEGIN
                           IF OSynchField."Table No." <> 0 THEN BEGIN
                             OSynchFilter.RESET;
                             OSynchFilter.SETRANGE("Record GUID",OSynchField."Record GUID");
                             OSynchFilter.SETRANGE("Filter Type",OSynchFilter."Filter Type"::"Table Relation");
                             OSynchFilter.SETRANGE(Type,OSynchFilter.Type::FIELD);
                             IF NOT OSynchFilter.FIND('-') THEN BEGIN
                               OSynchField.CALCFIELDS("Table Name");
                               IF OSynchField."Element No." = 0 THEN
                                 ERROR(Text001,OSynchField."Table Name",OSynchEntity.TABLECAPTION,OSynchEntity.Code)
                               ELSE BEGIN
                                 OSynchEntityElement.GET(OSynchField."Synch. Entity Code",OSynchField."Element No.");
                                 OSynchEntityElement.CALCFIELDS("Table Name");
                                 ERROR(
                                   Text005,
                                   OSynchField."Table Name",
                                   OSynchEntityElement."Table Name",
                                   OSynchEntityElement."Outlook Collection",
                                   OSynchEntity.Code);
                               END;
                             END;

                             Field.GET(OSynchField."Table No.",OSynchField."Field No.");
                             Field.TESTFIELD(Enabled,TRUE);
                             RegisterChangeLogField(OSynchField."Table No.",OSynchField."Field No.");

                             FieldID := OSynchFilter."Master Table Field No.";
                           END ELSE
                             FieldID := OSynchField."Field No.";

                           Field.GET(OSynchField."Master Table No.",FieldID);
                           Field.TESTFIELD(Enabled,TRUE);

                           RegisterChangeLogField(OSynchField."Master Table No.",FieldID);
                         END;

        DataItemLink=Synch. Entity Code=FIELD(Code);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemTable=Table5303;
        DataItemTableView=SORTING(Record GUID,Filter Type,Line No.)
                          ORDER(Ascending);
        DataItemVarName=OSynchFilterEntity;
        OnAfterGetRecord=BEGIN
                           RegisterChangeLogFilter(OSynchFilterEntity);
                         END;

        DataItemLinkReference=OSynchEntity;
        DataItemLink=Record GUID=FIELD(Record GUID);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemTable=Table5305;
        DataItemTableView=SORTING(User ID,Synch. Entity Code)
                          ORDER(Ascending);
        DataItemVarName=OSynchUserSetup;
        DataItemLinkReference=OSynchEntity;
        DataItemLink=Synch. Entity Code=FIELD(Code);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table5303;
        DataItemTableView=SORTING(Record GUID,Filter Type,Line No.)
                          ORDER(Ascending);
        DataItemVarName=OSynchFilterUserSetup;
        OnAfterGetRecord=BEGIN
                           RegisterChangeLogFilter(OSynchFilterUserSetup);
                         END;

        DataItemLink=Record GUID=FIELD(Record GUID);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemTable=Table5311;
        DataItemTableView=SORTING(Synch. Entity Code,Element No.,Depend. Synch. Entity Code)
                          ORDER(Ascending);
        DataItemVarName=OSynchDependency;
        DataItemLinkReference=OSynchEntity;
        DataItemLink=Synch. Entity Code=FIELD(Code);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table5303;
        DataItemTableView=SORTING(Record GUID,Filter Type,Line No.)
                          ORDER(Ascending);
        DataItemVarName=OSynchFilterDependency;
        OnAfterGetRecord=BEGIN
                           RegisterChangeLogFilter(OSynchFilterDependency);
                         END;

        DataItemLink=Record GUID=FIELD(Record GUID);
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=0;
      Height=0;
    }
    CONTROLS
    {
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      Field@1004 : Record 2000000041;
      FieldID@1000 : Integer;
      Text001@1002 : TextConst 'DEU=Die Relation zwischen der %1-Tabelle und der %2-Tabelle in der %3-Einheit kann nicht bestimmt werden. öberprÅfen Sie die Synchronisierungseinstellungen fÅr diese Einheit.;ENU=The relation between the %1 table and %2 table in the %3 entity cannot be determined. Please verify your synchronization settings for this entity.';
      Text002@1001 : TextConst 'DEU=Die énderungsprotokolleinstellungen wurden erfolgreich registriert. Sie mÅssen den Mandanten schlie·en und neu îffnen, damit die neuen Einstellungen des énderungsprotokolls wirksam werden.;ENU=The change log settings have been registered successfully. You must close and reopen the company for the new change log settings to take effect.';
      Text003@1003 : TextConst 'DEU=Die énderungsprotokolleinstellungen wurden erfolgreich registriert.;ENU=The change log settings have been registered successfully.';
      Text004@1005 : TextConst 'DEU=Die Einheit wurde nicht gefunden. Stellen Sie sicher, dass Sie den Namen richtig eingegeben haben.;ENU=The entity cannot be found. Make sure you have typed its name correctly.';
      Text005@1006 : TextConst 'DEU=Die Relation zwischen der %1-Tabelle und der %2-Tabelle in der %3-Sammlung der %4-Einheit kann nicht bestimmt werden. öberprÅfen Sie die Synchronisierungseinstellungen fÅr diese Einheit.;ENU=The relation between the %1 table and %2 table in the %3 collection from the %4 entity cannot be determined. Please verify your synchronization settings for this entity.';

    PROCEDURE RegisterChangeLogField@10(TableID@1000 : Integer;FieldID@1001 : Integer);
    VAR
      ChangeLogSetupTable@1002 : Record 403;
      ChangeLogSetupField@1003 : Record 404;
      NeedToBeUpdated@1004 : Boolean;
    BEGIN
      WITH ChangeLogSetupTable DO BEGIN
        RESET;
        IF NOT GET(TableID) THEN BEGIN
          INIT;
          "Table No." := TableID;
          VALIDATE("Log Insertion","Log Insertion"::"Some Fields");
          VALIDATE("Log Modification","Log Modification"::"Some Fields");
          VALIDATE("Log Deletion","Log Modification"::"Some Fields");
          INSERT;
          NeedToBeUpdated := TRUE;
        END ELSE BEGIN
          NeedToBeUpdated :=
            ("Log Insertion" <> "Log Insertion"::"All Fields") OR
            ("Log Modification" <> "Log Modification"::"All Fields") OR
            ("Log Deletion" <> "Log Deletion"::"All Fields");

          IF "Log Insertion" <> "Log Insertion"::"All Fields" THEN
            "Log Insertion" := "Log Insertion"::"Some Fields";

          IF "Log Modification" <> "Log Modification"::"All Fields" THEN
            "Log Modification" := "Log Insertion"::"Some Fields";

          IF "Log Deletion" <> "Log Deletion"::"All Fields" THEN
            "Log Deletion" := "Log Deletion"::"Some Fields";

          IF NeedToBeUpdated THEN
            MODIFY;
        END;
      END;

      IF NOT NeedToBeUpdated THEN
        EXIT;

      WITH ChangeLogSetupField DO BEGIN
        RESET;
        IF NOT GET(TableID,FieldID) THEN BEGIN
          INIT;
          "Table No." := TableID;
          "Field No." := FieldID;
          "Log Insertion" := TRUE;
          "Log Modification" := TRUE;
          "Log Deletion" := TRUE;
          INSERT;
        END ELSE BEGIN
          "Log Insertion" := TRUE;
          "Log Modification" := TRUE;
          "Log Deletion" := TRUE;
          MODIFY;
        END;
      END;
    END;

    PROCEDURE RegisterChangeLogPrimaryKey@1(TableID@1000 : Integer);
    VAR
      RecRef@1001 : RecordRef;
      I@1002 : Integer;
    BEGIN
      RecRef.OPEN(TableID,TRUE);
      FOR I := 1 TO RecRef.KEYINDEX(1).FIELDCOUNT DO
        RegisterChangeLogField(TableID,RecRef.KEYINDEX(1).FIELDINDEX(I).NUMBER);
      RecRef.CLOSE;
    END;

    PROCEDURE RegisterChangeLogFilter@8(OSynchFilter1@1000 : Record 5303);
    BEGIN
      IF Field.GET(OSynchFilter1."Table No.",OSynchFilter1."Field No.") THEN BEGIN
        Field.TESTFIELD(Enabled,TRUE);
        RegisterChangeLogField(OSynchFilter1."Table No.",OSynchFilter1."Field No.");
      END;
      IF Field.GET(OSynchFilter1."Master Table No.",OSynchFilter1."Master Table Field No.") THEN BEGIN
        Field.TESTFIELD(Enabled,TRUE);
        RegisterChangeLogField(OSynchFilter1."Master Table No.",OSynchFilter1."Master Table Field No.");
      END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

