OBJECT Codeunit 1003 Job Task-Indent
{
  OBJECT-PROPERTIES
  {
    Date=13.02.07;
    Time=12:00:00;
    Version List=NAVW15.00;
  }
  PROPERTIES
  {
    TableNo=1001;
    OnRun=BEGIN
            TESTFIELD("Job No.");
            IF NOT
               CONFIRM(
                 Text000 +
                 Text001 +
                 Text002 +
                 Text003,TRUE)
            THEN
              EXIT;
            JT := Rec;
            Indent("Job No.");
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Mit dieser Funktion kann die EinrÅckung fÅr alle Projektaufgaben aktualisiert werden.;ENU=This function updates the indentation of all the Job Tasks.';
      Text001@1001 : TextConst 'DEU="Alle Projektaufgaben zwischen einer Von-Summe und der entsprechenden Bis-Summe werden um eine Ebene eingerÅckt. ";ENU="All Job Tasks between a Begin-Total and the matching End-Total are indented one level. "';
      Text002@1002 : TextConst 'DEU=Die ZusammenzÑhlung fÅr Bis-Summen ist ebenfalls aktualisiert.;ENU=The Totaling for each End-total is also updated.';
      Text003@1003 : TextConst 'DEU=\\Mîchten Sie die Projektaufgaben einrÅcken?;ENU=\\Do you want to indent the Job Tasks?';
      Text004@1004 : TextConst 'DEU=EinrÅcken der Projektaufgaben #1##########.;ENU=Indenting the Job Tasks #1##########.';
      Text005@1005 : TextConst 'DEU=FÅr die Bis-Summe %1 fehlt eine entsprechende Von-Summe.;ENU=End-Total %1 is missing a matching Begin-Total.';
      JT@1006 : Record 1001;
      Window@1007 : Dialog;
      JTNo@1008 : ARRAY [10] OF Code[20];
      i@1009 : Integer;

    PROCEDURE Indent@1(JobNo@1000 : Code[20]);
    BEGIN
      Window.OPEN(Text004);
      JT.SETRANGE("Job No.",JobNo);
      WITH JT DO
        IF FIND('-') THEN
          REPEAT
            Window.UPDATE(1,"Job Task No.");

            IF "Job Task Type" = "Job Task Type"::"End-Total" THEN BEGIN
              IF i < 1 THEN
                ERROR(
                  Text005,
                  "Job Task No.");
              Totaling := JTNo[i] + '..' + "Job Task No.";
              i := i - 1;
            END;

            Indentation := i;
            MODIFY;

            IF "Job Task Type" = "Job Task Type"::"Begin-Total" THEN BEGIN
              i := i + 1;
              JTNo[i] := "Job Task No.";
            END;
          UNTIL NEXT = 0;

      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}

