OBJECT Codeunit 1011 Job Jnl.-Check Line
{
  OBJECT-PROPERTIES
  {
    Date=14.08.09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    TableNo=210;
    OnRun=VAR
            TempJnlLineDim@1000 : TEMPORARY Record 356;
          BEGIN
            GLSetup.GET;
            IF "Shortcut Dimension 1 Code" <> '' THEN BEGIN
              TempJnlLineDim."Table ID" := DATABASE::"Job Journal Line";
              TempJnlLineDim."Journal Template Name" := "Journal Template Name";
              TempJnlLineDim."Journal Batch Name" := "Journal Batch Name";
              TempJnlLineDim."Journal Line No." := "Line No.";
              TempJnlLineDim."Dimension Code" := GLSetup."Global Dimension 1 Code";
              TempJnlLineDim."Dimension Value Code" := "Shortcut Dimension 1 Code";
              TempJnlLineDim.INSERT;
            END;
            IF "Shortcut Dimension 2 Code" <> '' THEN BEGIN
              TempJnlLineDim."Table ID" := DATABASE::"Job Journal Line";
              TempJnlLineDim."Journal Template Name" := "Journal Template Name";
              TempJnlLineDim."Journal Batch Name" := "Journal Batch Name";
              TempJnlLineDim."Journal Line No." := "Line No.";
              TempJnlLineDim."Dimension Code" := GLSetup."Global Dimension 2 Code";
              TempJnlLineDim."Dimension Value Code" := "Shortcut Dimension 2 Code";
              TempJnlLineDim.INSERT;
            END;
            RunCheck(Rec,TempJnlLineDim);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=kann kein Ultimodatum sein.;ENU=cannot be a closing date.';
      Text001@1001 : TextConst 'DEU=liegt nicht im Bereich der fÅr Sie zugelassenen Buchungsdaten.;ENU=is not within your range of allowed posting dates.';
      Text002@1002 : TextConst 'DEU=Die Kombination der im %1 %2, %3, %4 verwendeten Dimensionen ist gesperrt. %5;ENU=The combination of dimensions used in %1 %2, %3, %4 is blocked. %5';
      Text003@1003 : TextConst 'DEU=Eine im %1 %2, %3, %4 verwendete Dimension hat einen Fehler hervorgerufen. %5;ENU=A dimension used in %1 %2, %3, %4 has caused an error. %5';
      Location@1006 : Record 14;
      GLSetup@1004 : Record 98;
      UserSetup@1005 : Record 91;
      DimMgt@1007 : Codeunit 408;
      AllowPostingFrom@1008 : Date;
      AllowPostingTo@1009 : Date;
      Text004@1010 : TextConst 'DEU="Sie mÅssen mehr Verbrauch buchen oder den Verkauf von %1 %2 in %3 %4 gutschreiben, bevor Sie das Projekt Buch.-Blatt %5 %6 = %7 buchen kînnen.";ENU="You must post more usage or credit the sale of %1 %2 in %3 %4 before you can post job journal %5 %6 = %7."';

    PROCEDURE RunCheck@1(VAR JobJnlLine@1000 : Record 210;VAR JnlLineDim@1001 : Record 356);
    VAR
      Job@1004 : Record 167;
      TableID@1002 : ARRAY [10] OF Integer;
      No@1003 : ARRAY [10] OF Code[20];
    BEGIN
      WITH JobJnlLine DO BEGIN
        IF EmptyLine THEN
          EXIT;
        TESTFIELD("Job No.");
        TESTFIELD("Job Task No.");
        TESTFIELD("No.");
        TESTFIELD("Posting Date");
        TESTFIELD(Quantity);

        Job.GET("Job No.");
        Job.TESTFIELD(Status,Job.Status::Order);

        IF NORMALDATE("Posting Date") <> "Posting Date" THEN
          FIELDERROR("Posting Date",Text000);

        IF ("Document Date" <> 0D) AND ("Document Date" <> NORMALDATE("Document Date")) THEN
          FIELDERROR("Document Date",Text000);

        IF DateNotAllowed("Posting Date") THEN
          FIELDERROR("Posting Date",Text001);

        IF NOT DimMgt.CheckJnlLineDimComb(JnlLineDim) THEN
          ERROR(
            Text002,
            TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
            DimMgt.GetDimCombErr);

        TableID[1] := DATABASE::Job;
        No[1] := "Job No.";
        TableID[2] := DimMgt.TypeToTableID2(Type);
        No[2] := "No.";
        TableID[3] := DATABASE::"Resource Group";
        No[3] := "Resource Group No.";
        IF NOT DimMgt.CheckJnlLineDimValuePosting(JnlLineDim,TableID,No) THEN BEGIN
          IF "Line No." <> 0 THEN
            ERROR(
              Text003,
              TABLECAPTION,"Journal Template Name","Journal Batch Name","Line No.",
              DimMgt.GetDimValuePostingErr);
          ERROR(DimMgt.GetDimValuePostingErr);
        END;

        IF Type = Type::Item THEN BEGIN
          IF ("Quantity (Base)" < 0) AND ("Entry Type" = "Entry Type"::Usage) THEN
            CheckItemQuantityJobJnl(JobJnlLine);
          GetLocation("Location Code");
          IF Location."Directed Put-away and Pick" THEN
            TESTFIELD("Bin Code",'')
          ELSE
            IF Location."Bin Mandatory" THEN
              TESTFIELD("Bin Code");
        END;
        IF "Line Type" IN ["Line Type"::Contract,"Line Type"::"Both Schedule and Contract"] THEN
          TESTFIELD(Chargeable,TRUE);
      END;
    END;

    LOCAL PROCEDURE GetLocation@7300(LocationCode@1000 : Code[10]);
    BEGIN
      IF LocationCode = '' THEN
        CLEAR(Location)
      ELSE
        IF Location.Code <> LocationCode THEN
          Location.GET(LocationCode);
    END;

    PROCEDURE DateNotAllowed@2(PostingDate@1000 : Date) : Boolean;
    BEGIN
      IF (AllowPostingFrom = 0D) AND (AllowPostingTo = 0D) THEN BEGIN
        IF USERID <> '' THEN
          IF UserSetup.GET(USERID) THEN BEGIN
            AllowPostingFrom := UserSetup."Allow Posting From";
            AllowPostingTo := UserSetup."Allow Posting To";
          END;
        IF (AllowPostingFrom = 0D) AND (AllowPostingTo = 0D) THEN BEGIN
          GLSetup.GET;
          AllowPostingFrom := GLSetup."Allow Posting From";
          AllowPostingTo := GLSetup."Allow Posting To";
        END;
        IF AllowPostingTo = 0D THEN
          AllowPostingTo := 31129999D;
      END;
      EXIT((PostingDate < AllowPostingFrom) OR (PostingDate > AllowPostingTo));
    END;

    LOCAL PROCEDURE CheckItemQuantityJobJnl@15(VAR JobJnlline@1001 : Record 210);
    VAR
      Item@1003 : Record 27;
      Job@1007 : Record 167;
    BEGIN
      Job.GET(JobJnlline."Job No.");
      IF (Job.GetQuantityAvailable(JobJnlline."No.",JobJnlline."Location Code",JobJnlline."Variant Code",0,2) +
          JobJnlline."Quantity (Base)") < 0 THEN
        ERROR(
          Text004,Item.TABLECAPTION,JobJnlline."No.",Job.TABLECAPTION,
          JobJnlline."Job No.",JobJnlline."Journal Batch Name",
          JobJnlline.FIELDCAPTION("Line No."),JobJnlline."Line No.");
    END;

    BEGIN
    END.
  }
}

