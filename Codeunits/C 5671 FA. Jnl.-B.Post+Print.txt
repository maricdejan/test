OBJECT Codeunit 5671 FA. Jnl.-B.Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=08.06.01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    TableNo=5620;
    OnRun=BEGIN
            FAJnlBatch.COPY(Rec);
            Code;
            Rec.COPY(FAJnlBatch);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Mchten Sie die Buch.-Bltter buchen und den Buchungsbericht drucken?;ENU=Do you want to post the journals and print the posting report?';
      Text001@1001 : TextConst 'DEU=Die Buch.-Bltter sind gebucht worden.;ENU=The journals were successfully posted.';
      Text002@1002 : TextConst 'DEU="Es konnten nicht alle Buch.-Bltter gebucht werden. ";ENU="It was not possible to post all of the journals. "';
      Text003@1003 : TextConst 'DEU=Die nicht erfolgreich gebuchten Buch.-Bltter sind jetzt markiert.;ENU=The journals that were not successfully posted are now marked.';
      FAJnlTemplate@1004 : Record 5619;
      FAJnlBatch@1005 : Record 5620;
      FAJnlLine@1006 : Record 5621;
      FAReg@1007 : Record 5617;
      FAJnlPostBatch@1008 : Codeunit 5633;
      JournalWithErrors@1009 : Boolean;

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH FAJnlBatch DO BEGIN
        FAJnlTemplate.GET("Journal Template Name");
        FAJnlTemplate.TESTFIELD("Posting Report ID");
        FAJnlTemplate.TESTFIELD("Maint. Posting Report ID");

        IF NOT CONFIRM(Text000,FALSE) THEN
          EXIT;

        FIND('-');
        REPEAT
          FAJnlLine."Journal Template Name" := "Journal Template Name";
          FAJnlLine."Journal Batch Name" := Name;
          FAJnlLine."Line No." := 1;
          CLEAR(FAJnlPostBatch);
          IF FAJnlPostBatch.RUN(FAJnlLine) THEN BEGIN
            IF FAReg.GET(FAJnlLine."Line No.") THEN BEGIN
              FAReg.SETRECFILTER;
              IF FAReg."From Entry No." > 0 THEN
                REPORT.RUN(FAJnlTemplate."Posting Report ID",FALSE,FALSE,FAReg);
              IF FAReg."From Maintenance Entry No." > 0 THEN
                REPORT.RUN(FAJnlTemplate."Maint. Posting Report ID",FALSE,FALSE,FAReg);
            END;
            MARK(FALSE);
          END
          ELSE BEGIN
            MARK(TRUE);
            JournalWithErrors := TRUE;
          END;
        UNTIL NEXT = 0;

        IF NOT JournalWithErrors THEN
          MESSAGE(Text001)
        ELSE
          MESSAGE(
            Text002 +
            Text003);

        IF NOT FIND('=><') THEN BEGIN
          RESET;
          FILTERGROUP := 2;
          SETRANGE("Journal Template Name","Journal Template Name");
          FILTERGROUP := 0;
          Name := '';
        END;
      END;
    END;

    BEGIN
    END.
  }
}

