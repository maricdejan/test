OBJECT Codeunit 5005359 Phys. Invt. Order Post
{
  OBJECT-PROPERTIES
  {
    Date=14.08.09;
    Time=12:00:00;
    Version List=NAVDACH6.00.01;
  }
  PROPERTIES
  {
    TableNo=5005350;
    Permissions=TableData 6507=ri,
                TableData 5005354=rimd,
                TableData 5005355=rimd,
                TableData 5005356=rimd,
                TableData 5005357=rimd,
                TableData 5005359=rimd,
                TableData 5005362=rimd;
    OnRun=BEGIN
            PhysInvtOrderHeader.COPY(Rec);
            Code;
            Rec := PhysInvtOrderHeader;
          END;

  }
  CODE
  {
    VAR
      Text1140000@1140000 : TextConst 'DEU=Zeilen prfen         #2######\;ENU=Checking lines        #2######\';
      Text1140001@1140001 : TextConst 'DEU=Buche Zeilen          #3######;ENU=Posting lines         #3######';
      Text1140002@1140002 : TextConst 'DEU=Die Kombination der im Inventurauftrag %1 verwendeten Dimensionen ist gesperrt. %2;ENU=The combination of dimensions used in phys. inventory order %1 is blocked. %2';
      Text1140003@1140003 : TextConst 'DEU=Die Kombination der im Inventurauftrag %1, Zeilennr. %2, verwendeten Dimensionen ist gesperrt. %3;ENU=The combination of dimensions used in  phys. inventory order %1, line no. %2 is blocked. %3';
      Text1140004@1140004 : TextConst 'DEU=Die Dimensionen, die im Inventurauftrag %1, Zeilennr. %2, verwendet werden, sind ungltig. %3;ENU=The dimensions used in phys. inventory order %1, line no. %2 are invalid. %3';
      Text1140005@1140005 : TextConst 'DEU=Es gibt eine Differenz zwischen den Werten der Felder %1, %2 und %3. \;ENU=There is a difference between the values of the fields %1, %2 and %3. \';
      Text1140006@1140006 : TextConst 'DEU=Identifizierende Werte der Zeile: %4 %5 %6 %7;ENU=Identified values in the line: %4 %5 %6 %7';
      Text1140007@1140007 : TextConst 'DEU=Es gibt nichts zu buchen.;ENU=There is nothing to post.';
      Text1140008@1140008 : TextConst 'DEU=%1 %2 -> %3 %4;ENU=%1 %2 -> %3 %4';
      PhysInvtOrderHeader@1140009 : Record 5005350;
      PhysInvtOrderLine@1140010 : Record 5005351;
      PhysInvtOrderLine2@1140011 : Record 5005351;
      PostedPhysInvtOrderHeader@1140012 : Record 5005354;
      PostedPhysInvtOrderLine@1140013 : Record 5005355;
      ExpectPhysInvtTrackLine@1140014 : Record 5005361;
      PostedExpectPhysInvtTrackLine@1140015 : Record 5005362;
      PhysInvtRecordingHeader@1140016 : Record 5005352;
      PhysInvtRecordingLine@1140017 : Record 5005353;
      PostedPhysInvtRecHeader@1140018 : Record 5005356;
      PostedPhysInvtRecLine@1140019 : Record 5005357;
      PhysInvtCommentLine@1140020 : Record 5005358;
      PhysInvtCommentLinePosted@1140021 : Record 5005358;
      DocDim@1140022 : Record 357;
      TempDocDim@1140023 : TEMPORARY Record 357;
      Item@11140024 : Record 27;
      ItemJnlLine@1140025 : Record 83;
      Location@1140026 : Record 14;
      TempWhseSplitSpecification@1140027 : TEMPORARY Record 336;
      DimManagement@1140028 : Codeunit 408;
      ItemTrkgMgtPhysInvtLine@1140029 : Codeunit 5005364;
      NoSeriesMgt@1140030 : Codeunit 396;
      ItemJnlPostLine@1140031 : Codeunit 22;
      Window@1140032 : Dialog;
      ErrorText@1140033 : Text[250];
      SourceCode@1140034 : Code[10];
      LineCount@1140035 : Integer;
      ModifyHeader@1140036 : Boolean;
      LinesToPost@1140037 : Boolean;
      WhsePosting@1140038 : Boolean;
      OriginalQuantityBase@1140039 : Decimal;

    PROCEDURE Code@1140000();
    VAR
      SourceCodeSetup@1140000 : Record 242;
      InventorySetup@1140001 : Record 313;
    BEGIN
      WITH PhysInvtOrderHeader DO BEGIN
        TESTFIELD(Status, Status::Finished);
        TESTFIELD("Posting Date");

        Window.OPEN(
          '#1################################\\' +
          Text1140000 +
          Text1140001);
        Window.UPDATE(1,STRSUBSTNO('%1 %2',TABLECAPTION, "No."));

        PhysInvtOrderHeader.LOCKTABLE;
        PhysInvtOrderLine.LOCKTABLE;

        ModifyHeader := FALSE;
        IF "Posting No." = '' THEN BEGIN
          IF "No. Series" <> '' THEN
            TESTFIELD("Posting No. Series");
          IF "No. Series" <> "Posting No. Series" THEN BEGIN
            "Posting No." := NoSeriesMgt.GetNextNo("Posting No. Series", "Posting Date", TRUE);
            ModifyHeader := TRUE;
          END;
        END;

        IF ModifyHeader THEN BEGIN
          MODIFY;
          COMMIT;
        END;

        CopyAndCheckDocDimToTempDocDim;

        // Check phys. inventory order lines
        LinesToPost := FALSE;
        LineCount := 0;
        PhysInvtOrderLine.RESET;
        PhysInvtOrderLine.SETRANGE("Document No.","No.");
        IF PhysInvtOrderLine.FIND('-') THEN REPEAT
          LineCount := LineCount + 1;
          Window.UPDATE(2, LineCount);
          IF NOT PhysInvtOrderLine.EmptyLine THEN BEGIN
            PhysInvtOrderLine.TESTFIELD("Item No.");
            Item.GET(PhysInvtOrderLine."Item No.");
            Item.TESTFIELD(Blocked, FALSE);

            PhysInvtOrderLine.TESTFIELD("Qty. Exp. Calculated", TRUE);
            PhysInvtOrderLine.TestQtyExpected;

            PhysInvtOrderLine.TESTFIELD("In Recording Lines", TRUE);
            PhysInvtOrderLine.TestQtyRecorded;

            IF GetSamePhysInvtOrderLine(
              PhysInvtOrderLine."Item No.", PhysInvtOrderLine."Variant Code",
              PhysInvtOrderLine."Location Code", PhysInvtOrderLine."Bin Code",
              ErrorText,
              PhysInvtOrderLine2) > 1
            THEN
              ERROR(
                ErrorText);

            PhysInvtOrderLine.TESTFIELD("Entry Type");
            IF ((PhysInvtOrderLine."Entry Type" = PhysInvtOrderLine."Entry Type"::"Positive Adjmt.") AND
                (PhysInvtOrderLine."Quantity (Base)" <>
                  PhysInvtOrderLine."Pos. Qty. (Base)" - PhysInvtOrderLine."Neg. Qty. (Base)")) OR
               ((PhysInvtOrderLine."Entry Type" = PhysInvtOrderLine."Entry Type"::"Negative Adjmt.") AND
                (- PhysInvtOrderLine."Quantity (Base)" <>
                  PhysInvtOrderLine."Pos. Qty. (Base)" - PhysInvtOrderLine."Neg. Qty. (Base)"))
            THEN
              ERROR(
                Text1140005 +
                Text1140006,
                PhysInvtOrderLine.FIELDCAPTION("Pos. Qty. (Base)"),
                PhysInvtOrderLine.FIELDCAPTION("Neg. Qty. (Base)"),
                PhysInvtOrderLine.FIELDCAPTION("Quantity (Base)"),
                PhysInvtOrderLine."Item No.",
                PhysInvtOrderLine."Variant Code",
                PhysInvtOrderLine."Location Code",
                PhysInvtOrderLine."Bin Code");

            IF NOT LinesToPost THEN
              LinesToPost := TRUE;
          END;
        UNTIL PhysInvtOrderLine.NEXT = 0;
        IF NOT LinesToPost THEN
          ERROR(Text1140007);

        SourceCodeSetup.GET;
        SourceCode := SourceCodeSetup."Phys. Invt. Order";

        InventorySetup.GET;

        // Insert posted order header
        PostedPhysInvtOrderHeader.LOCKTABLE;
        PostedPhysInvtOrderHeader.INIT;
        PostedPhysInvtOrderHeader.TRANSFERFIELDS(PhysInvtOrderHeader);
        PostedPhysInvtOrderHeader."Pre-Assigned No." := "No.";
        IF "Posting No." <> '' THEN BEGIN
          PostedPhysInvtOrderHeader."No." := "Posting No.";
          Window.UPDATE(
            1,
            STRSUBSTNO(
              Text1140008,
              TABLECAPTION, "No.",
              PostedPhysInvtOrderHeader.TABLECAPTION, PostedPhysInvtOrderHeader."No."));
        END;
        PostedPhysInvtOrderHeader."Source Code" := SourceCode;
        PostedPhysInvtOrderHeader."User ID" := USERID;
        PostedPhysInvtOrderHeader.INSERT;

         // DimManagement.MoveOneDocDimToPostedDocDim(
         //  DocDim, DATABASE::"Phys. Inventory Order Header" ,DocDim."Document Type"::" " , "No.", 0,
         //  DATABASE::"Phys. Inventory Order Header", PostedPhysInvtOrderHeader."No.");
        DimManagement.MoveOneDocDimToPostedDocDim(
          DocDim, DATABASE::"Phys. Inventory Order Header" ,DocDim."Document Type"::" " , "No.", 0,
          DATABASE::"Post. Phys. Invt. Order Header", PostedPhysInvtOrderHeader."No.");

        DimManagement.DeleteDocDim(
          DATABASE::"Phys. Inventory Order Header",DocDim."Document Type"::" ","No.",0);
        // DACH003.END

        // Insert posted order lines
        LineCount := 0;
        PhysInvtOrderLine.RESET;
        PhysInvtOrderLine.SETRANGE("Document No.", "No.");
        IF PhysInvtOrderLine.FIND('-') THEN REPEAT
          LineCount := LineCount + 1;
          Window.UPDATE(3, LineCount);

          PostedPhysInvtOrderLine.INIT;
          PostedPhysInvtOrderLine.TRANSFERFIELDS(PhysInvtOrderLine);
          PostedPhysInvtOrderLine."Document No." := PostedPhysInvtOrderHeader."No.";
          PostedPhysInvtOrderLine.INSERT;

          IF NOT PhysInvtOrderLine.EmptyLine THEN BEGIN

            IF (PhysInvtOrderLine."Location Code" = '') OR
               ((PhysInvtOrderLine."Pos. Qty. (Base)" = 0) AND (PhysInvtOrderLine."Neg. Qty. (Base)" = 0))
            THEN BEGIN
              WhsePosting := FALSE
            END ELSE BEGIN
              Location.GET(PhysInvtOrderLine."Location Code");
              Location.TESTFIELD("Directed Put-away and Pick", FALSE);
              WhsePosting := Location."Bin Mandatory";
            END;

            IF (PhysInvtOrderLine."Pos. Qty. (Base)" <> 0) OR
               (PhysInvtOrderLine."Neg. Qty. (Base)" = 0)
            THEN BEGIN
              OriginalQuantityBase := PhysInvtOrderLine."Pos. Qty. (Base)";
              PostItemJnlLine(
                TRUE, // Positive
                PhysInvtOrderLine."Pos. Qty. (Base)");
              InsertEntryRelation();
              IF WhsePosting THEN BEGIN
                PostWhseJnlLine(
                  ItemJnlLine,
                  OriginalQuantityBase, OriginalQuantityBase,
                  TRUE); // Positive
              END;
            END;

            IF PhysInvtOrderLine."Neg. Qty. (Base)" <> 0 THEN BEGIN
              OriginalQuantityBase := PhysInvtOrderLine."Neg. Qty. (Base)";
              PostItemJnlLine(
                FALSE, // Negative
                PhysInvtOrderLine."Neg. Qty. (Base)");
              InsertEntryRelation();
              IF WhsePosting THEN BEGIN
                PostWhseJnlLine(
                  ItemJnlLine,
                  OriginalQuantityBase, OriginalQuantityBase,
                  FALSE); // Negative
              END;
            END;

          END;

          DimManagement.MoveOneDocDimToPostedDocDim(
            DocDim,
            DATABASE::"Phys. Inventory Order Line",
            DocDim."Document Type"::" ",
            "No.",
            PhysInvtOrderLine."Line No.",
            DATABASE::"Posted Phys. Invt. Order Line",
            PostedPhysInvtOrderHeader."No.");

          DimManagement.DeleteDocDim(
            DATABASE::"Phys. Inventory Order Line",DocDim."Document Type"::" ","No.",PhysInvtOrderLine."Line No.");
        UNTIL PhysInvtOrderLine.NEXT = 0;

        // Insert posted expected phys. invt. tracking Lines
        ExpectPhysInvtTrackLine.RESET;
        ExpectPhysInvtTrackLine.SETRANGE("Order No", "No.");
        IF ExpectPhysInvtTrackLine.FIND('-') THEN REPEAT
          PostedExpectPhysInvtTrackLine.INIT;
          PostedExpectPhysInvtTrackLine.TRANSFERFIELDS(ExpectPhysInvtTrackLine);
          PostedExpectPhysInvtTrackLine."Order No" :=  PostedPhysInvtOrderHeader."No.";
          PostedExpectPhysInvtTrackLine.INSERT;
        UNTIL ExpectPhysInvtTrackLine.NEXT = 0;

        // Insert posted recording header and lines
        PhysInvtRecordingHeader.RESET;
        PhysInvtRecordingHeader.SETRANGE("Order No.", "No.");
        IF PhysInvtRecordingHeader.FIND('-') THEN REPEAT
          PostedPhysInvtRecHeader.INIT;
          PostedPhysInvtRecHeader.TRANSFERFIELDS(PhysInvtRecordingHeader);
          PostedPhysInvtRecHeader."Order No." := PostedPhysInvtOrderHeader."No.";
          PostedPhysInvtRecHeader.INSERT;
          PhysInvtRecordingLine.RESET;
          PhysInvtRecordingLine.SETRANGE("Order No.", PhysInvtRecordingHeader."Order No.");
          PhysInvtRecordingLine.SETRANGE("Recording No.", PhysInvtRecordingHeader."Recording No.");
          IF PhysInvtRecordingLine.FIND('-') THEN REPEAT
            PostedPhysInvtRecLine.INIT;
            PostedPhysInvtRecLine.TRANSFERFIELDS(PhysInvtRecordingLine);
            PostedPhysInvtRecLine."Order No." := PostedPhysInvtRecHeader."Order No.";
            PostedPhysInvtRecLine.INSERT;
          UNTIL PhysInvtRecordingLine.NEXT = 0;
          PhysInvtRecordingLine.DELETEALL;
        UNTIL PhysInvtRecordingHeader.NEXT = 0;
        PhysInvtRecordingHeader.DELETEALL;

        // Insert posted comment Lines
        PhysInvtCommentLine.RESET;
        PhysInvtCommentLine.SETRANGE("Document Type", PhysInvtCommentLine."Document Type"::Order);
        PhysInvtCommentLine.SETRANGE("Order No.", "No.");
        PhysInvtCommentLine.SETRANGE("Recording No.", 0);
        IF PhysInvtCommentLine.FIND('-') THEN REPEAT
          PhysInvtCommentLinePosted.INIT;
          PhysInvtCommentLinePosted.TRANSFERFIELDS(PhysInvtCommentLine);
          PhysInvtCommentLinePosted."Document Type" :=
            PhysInvtCommentLinePosted."Document Type"::"Posted Order";
          PhysInvtCommentLinePosted."Order No." := PostedPhysInvtOrderHeader."No.";
          PhysInvtCommentLinePosted.INSERT;
        UNTIL PhysInvtCommentLine.NEXT = 0;
        PhysInvtCommentLine.DELETEALL;

        PhysInvtCommentLine.RESET;
        PhysInvtCommentLine.SETRANGE("Document Type", PhysInvtCommentLine."Document Type"::Recording);
        PhysInvtCommentLine.SETRANGE("Order No.", "No.");
        IF PhysInvtCommentLine.FIND('-') THEN REPEAT
          PhysInvtCommentLinePosted.INIT;
          PhysInvtCommentLinePosted.TRANSFERFIELDS(PhysInvtCommentLine);
          PhysInvtCommentLinePosted."Document Type" :=
            PhysInvtCommentLinePosted."Document Type"::"Posted Recording";
          PhysInvtCommentLinePosted."Order No." := PostedPhysInvtOrderHeader."No.";
          PhysInvtCommentLinePosted.INSERT;
        UNTIL PhysInvtCommentLine.NEXT = 0;
        PhysInvtCommentLine.DELETEALL;

        // Delete phys. inventory order
        "Last Posting No." := "Posting No.";
        ExpectPhysInvtTrackLine.RESET;
        ExpectPhysInvtTrackLine.SETRANGE("Order No", "No.");
        ExpectPhysInvtTrackLine.DELETEALL;
        PhysInvtOrderLine.RESET;
        PhysInvtOrderLine.SETRANGE("Document No.", "No.");
        PhysInvtOrderLine.DELETEALL;
        DELETE;
      END;
    END;

    LOCAL PROCEDURE CopyAndCheckDocDimToTempDocDim@1140001();
    VAR
      CurrLineNo@1140000 : Integer;
    BEGIN
      TempDocDim.RESET;
      TempDocDim.DELETEALL;
      DocDim.SETFILTER("Table ID",'%1|%2',DATABASE::"Phys. Inventory Order Header",DATABASE::"Phys. Inventory Order Line");
      DocDim.SETRANGE("Document Type", DocDim."Document Type"::" ");
      DocDim.SETRANGE("Document No.",PhysInvtOrderHeader."No.");
      IF DocDim.FIND('-') THEN BEGIN
        CurrLineNo := DocDim."Line No.";
        REPEAT
          IF CurrLineNo <> DocDim."Line No." THEN BEGIN
            TempDocDim.SETRANGE("Line No.",CurrLineNo);
            CheckDimComb(CurrLineNo);
            CheckDimValuePosting(CurrLineNo);
            CurrLineNo := DocDim."Line No.";
          END;
          TempDocDim.INIT;
          TempDocDim := DocDim;
          TempDocDim.INSERT;
        UNTIL DocDim.NEXT = 0;
        TempDocDim.SETRANGE("Line No.",CurrLineNo);
        CheckDimComb(CurrLineNo);
        CheckDimValuePosting(CurrLineNo);
      END;
      TempDocDim.RESET;
    END;

    LOCAL PROCEDURE CheckDimComb@1140002(LineNo@1140000 : Integer);
    BEGIN
      IF NOT DimManagement.CheckDocDimComb(TempDocDim) THEN
        IF LineNo = 0 THEN
          ERROR(
            Text1140002,
            PhysInvtOrderHeader."No.",DimManagement.GetDimCombErr)
        ELSE
          ERROR(
            Text1140003,
            PhysInvtOrderHeader."No.", LineNo, DimManagement.GetDimCombErr);
    END;

    LOCAL PROCEDURE CheckDimValuePosting@1140003(LineNo@1140000 : Integer);
    VAR
      PhysInvtOrderLine2@1140001 : Record 5741;
      TableIDArr@1140002 : ARRAY [10] OF Integer;
      NumberArr@1140003 : ARRAY [10] OF Code[20];
    BEGIN
      IF PhysInvtOrderLine2.GET(PhysInvtOrderHeader."No.", LineNo) THEN BEGIN
        TableIDArr[1] := DATABASE::Item;
        NumberArr[1] := PhysInvtOrderLine2."Item No.";
        IF NOT DimManagement.CheckDocDimValuePosting(TempDocDim,TableIDArr,NumberArr) THEN
          ERROR(
            Text1140004,
            PhysInvtOrderHeader."No.", LineNo, DimManagement.GetDimValuePostingErr);
      END;
    END;

    LOCAL PROCEDURE PostItemJnlLine@1140004(Positive@1140000 : Boolean;Qty@1140001 : Decimal);
    VAR
      TempJnlLineDim@11400003 : TEMPORARY Record 356;
    BEGIN
      ItemJnlLine.INIT;
      ItemJnlLine."Posting Date" := PostedPhysInvtOrderHeader."Posting Date";
      ItemJnlLine."Document Date" := PostedPhysInvtOrderHeader."Posting Date";
      ItemJnlLine."Document No." := PostedPhysInvtOrderHeader."No.";

      IF Positive THEN
        ItemJnlLine."Entry Type" := ItemJnlLine."Entry Type"::"Positive Adjmt."
      ELSE
        ItemJnlLine."Entry Type" := ItemJnlLine."Entry Type"::"Negative Adjmt.";

      ItemJnlLine."Item No." := PhysInvtOrderLine."Item No.";
      ItemJnlLine."Variant Code":= PhysInvtOrderLine."Variant Code";
      ItemJnlLine.Description := PhysInvtOrderLine.Description;
      ItemJnlLine."Item Category Code" := PhysInvtOrderLine."Item Category Code";
      ItemJnlLine."Product Group Code" := PhysInvtOrderLine."Product Group Code";

      ItemJnlLine."Location Code" := PhysInvtOrderLine."Location Code";
      ItemJnlLine."Bin Code" := PhysInvtOrderLine."Bin Code";

      ItemJnlLine."Shortcut Dimension 1 Code" := PhysInvtOrderLine."Shortcut Dimension 1 Code";
      ItemJnlLine."Shortcut Dimension 2 Code" := PhysInvtOrderLine."Shortcut Dimension 2 Code";

      ItemJnlLine.Quantity := Qty;
      ItemJnlLine."Invoiced Quantity" := ItemJnlLine.Quantity;
      ItemJnlLine."Quantity (Base)" := ItemJnlLine.Quantity;
      ItemJnlLine."Invoiced Qty. (Base)" := ItemJnlLine.Quantity;
      ItemJnlLine."Unit of Measure Code" := PhysInvtOrderLine."Base Unit of Measure Code";
      ItemJnlLine."Qty. per Unit of Measure" := 1;

      ItemJnlLine."Source Code" := SourceCode;

      ItemJnlLine."Gen. Prod. Posting Group" := PhysInvtOrderLine."Gen. Prod. Posting Group";
      ItemJnlLine."Gen. Bus. Posting Group" := PhysInvtOrderLine."Gen. Bus. Posting Group";
      ItemJnlLine."Inventory Posting Group" := PhysInvtOrderLine."Inventory Posting Group";

      ItemJnlLine."Qty. (Calculated)" := PhysInvtOrderLine."Qty. Expected (Base)";
      IF Positive THEN
        ItemJnlLine."Qty. (Phys. Inventory)" :=
          PhysInvtOrderLine."Qty. Recorded (Base)" + PhysInvtOrderLine."Neg. Qty. (Base)"
      ELSE
        ItemJnlLine."Qty. (Phys. Inventory)" :=
          PhysInvtOrderLine."Qty. Recorded (Base)" - PhysInvtOrderLine."Pos. Qty. (Base)";

      ItemJnlLine."Last Item Ledger Entry No." := PhysInvtOrderLine."Last Item Ledger Entry No.";
      ItemJnlLine."Phys. Inventory" := TRUE;

      ItemJnlLine.VALIDATE("Unit Amount", PhysInvtOrderLine."Unit Amount");
      ItemJnlLine.VALIDATE("Unit Cost", PhysInvtOrderLine."Unit Cost");

      ItemJnlLine."Phys Invt Counting Period Code" := PhysInvtOrderLine."Phys Invt Counting Period Code";
      ItemJnlLine."Phys Invt Counting Period Type" := PhysInvtOrderLine."Phys Invt Counting Period Type";

      ItemTrkgMgtPhysInvtLine.TransferResEntryToItemJnlLine(
        PhysInvtOrderLine,
        ItemJnlLine,
        Qty,
        Positive);

      TempDocDim.RESET;
      TempDocDim.SETRANGE("Table ID", DATABASE::"Phys. Inventory Order Line");
      TempDocDim.SETRANGE("Line No.", PhysInvtOrderLine."Line No.");
      DimManagement.CopyDocDimToJnlLineDim(TempDocDim, TempJnlLineDim);
      IF TempJnlLineDim.FIND('-') THEN
        REPEAT
          TempJnlLineDim."New Dimension Value Code" := TempJnlLineDim."Dimension Value Code";
          TempJnlLineDim.MODIFY;
        UNTIL TempJnlLineDim.NEXT = 0;

      ItemJnlPostLine.RunWithCheck(ItemJnlLine, TempJnlLineDim);
    END;

    LOCAL PROCEDURE InsertEntryRelation@1140005();
    VAR
      TempHandlingSpecification2@1140000 : TEMPORARY Record 336;
      ItemEntryRelation@1140001 : Record 6507;
      ItemTrackingMgt@1140002 : Codeunit 6500;
      WhseSNRequired@1140003 : Boolean;
      WhseLNRequired@1140004 : Boolean;
    BEGIN
      IF WhsePosting THEN BEGIN
        TempWhseSplitSpecification.RESET;
        TempWhseSplitSpecification.DELETEALL;
      END;

      TempHandlingSpecification2.RESET;
      IF ItemJnlPostLine.CollectTrackingSpecification(TempHandlingSpecification2) THEN BEGIN
        IF TempHandlingSpecification2.FIND('-') THEN BEGIN
          REPEAT
            IF WhsePosting THEN BEGIN
              TempWhseSplitSpecification := TempHandlingSpecification2;
              TempWhseSplitSpecification."Source Type" := DATABASE::"Phys. Inventory Order Line";
              TempWhseSplitSpecification."Source Subtype" := 0;
              TempWhseSplitSpecification."Source ID" := PhysInvtOrderLine."Document No.";
              TempWhseSplitSpecification."Source Ref. No." := PhysInvtOrderLine."Line No.";
              TempWhseSplitSpecification.INSERT;
            END;

            ItemEntryRelation.INIT;
            ItemEntryRelation."Item Entry No." := TempHandlingSpecification2."Entry No.";
            ItemEntryRelation."Serial No." := TempHandlingSpecification2."Serial No.";
            ItemEntryRelation."Lot No." := TempHandlingSpecification2."Lot No.";
            ItemEntryRelation."Source Type" := DATABASE::"Posted Phys. Invt. Order Line";
            ItemEntryRelation."Source Subtype" := 0;
            ItemEntryRelation."Source ID" := PostedPhysInvtOrderLine."Document No.";
            ItemEntryRelation."Source Batch Name" := '';
            ItemEntryRelation."Source Prod. Order Line" := 0;
            ItemEntryRelation."Source Ref. No." := PostedPhysInvtOrderLine."Line No.";
            ItemEntryRelation."Order No." :=  PostedPhysInvtOrderLine."Document No.";
            ItemEntryRelation."Order Line No." := PostedPhysInvtOrderLine."Line No.";
            ItemEntryRelation.INSERT;
          UNTIL TempHandlingSpecification2.NEXT = 0;
        END;
      END;
      TempHandlingSpecification2.DELETEALL;
    END;

    LOCAL PROCEDURE PostWhseJnlLine@1140006(ItemJnlLine@1140000 : Record 83;OriginalQuantity@1140001 : Decimal;OriginalQuantityBase@1140002 : Decimal;Positive@1140003 : Boolean);
    VAR
      WhseJnlLine@1140004 : Record 7311;
      TempWhseJnlLine2@1140005 : TEMPORARY Record 7311;
      ItemTrackingMgt@1140006 : Codeunit 6500;
      WMSMgmt@1140007 : Codeunit 7302;
      WhseJnlPostLine@1140008 : Codeunit 7301;
    BEGIN
      WITH ItemJnlLine DO BEGIN
        Quantity := OriginalQuantity;
        "Quantity (Base)" := OriginalQuantityBase;
        IF WMSMgmt.CreateWhseJnlLine(ItemJnlLine,1,WhseJnlLine,FALSE,FALSE) THEN BEGIN

          WhseJnlLine."Source Type" := DATABASE::"Phys. Inventory Order Line";
          WhseJnlLine."Source Subtype" := 0;
          WhseJnlLine."Source No." := PhysInvtOrderLine."Document No.";
          WhseJnlLine."Source Line No." := PhysInvtOrderLine."Line No.";
          WhseJnlLine."Reference No." := PostedPhysInvtOrderHeader."No.";
          IF Positive THEN
            WhseJnlLine."Entry Type" :=  WhseJnlLine."Entry Type"::"Positive Adjmt."
          ELSE
            WhseJnlLine."Entry Type" :=  WhseJnlLine."Entry Type"::"Negative Adjmt.";


          ItemTrackingMgt.SplitWhseJnlLine(
            WhseJnlLine,TempWhseJnlLine2,TempWhseSplitSpecification,TRUE);
          IF TempWhseJnlLine2.FIND('-') THEN
            REPEAT
              WMSMgmt.CheckWhseJnlLine(TempWhseJnlLine2,1,0,FALSE);
              WhseJnlPostLine.RUN(TempWhseJnlLine2);
            UNTIL TempWhseJnlLine2.NEXT = 0;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

