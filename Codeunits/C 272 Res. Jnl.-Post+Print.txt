OBJECT Codeunit 272 Res. Jnl.-Post+Print
{
  OBJECT-PROPERTIES
  {
    Date=08.06.01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    TableNo=207;
    OnRun=BEGIN
            ResJnlLine.COPY(Rec);
            Code;
            Rec.COPY(ResJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=kann nicht gefiltert werden, wenn wiederkehrende Buch.-Bl„tter gebucht werden.;ENU=cannot be filtered when posting recurring journals';
      Text001@1001 : TextConst 'DEU=M”chten Sie die Buch.-Blattzeilen buchen und den Buchungsbericht drucken?;ENU=Do you want to post the journal lines and print the posting report?';
      Text002@1002 : TextConst 'DEU=Es gibt nichts zu buchen.;ENU=There is nothing to post.';
      Text003@1003 : TextConst 'DEU=Die Buch.-Blattzeilen sind gebucht worden.;ENU=The journal lines were successfully posted.';
      Text004@1004 : TextConst 'DEU="Die Buch.-Blattzeilen sind gebucht worden. ";ENU="The journal lines were successfully posted. "';
      Text005@1005 : TextConst 'DEU=Sie arbeiten im Buch.-Blatt %1.;ENU=You are now in the %1 journal.';
      ResJnlTemplate@1006 : Record 206;
      ResJnlLine@1007 : Record 207;
      ResReg@1008 : Record 240;
      ResJnlPostBatch@1009 : Codeunit 213;
      TempJnlBatchName@1010 : Code[10];

    LOCAL PROCEDURE Code@1();
    BEGIN
      WITH ResJnlLine DO BEGIN
        ResJnlTemplate.GET("Journal Template Name");
        ResJnlTemplate.TESTFIELD("Posting Report ID");
        IF ResJnlTemplate.Recurring AND (GETFILTER("Posting Date") <> '') THEN
          FIELDERROR("Posting Date",Text000);

        IF NOT CONFIRM(Text001) THEN
          EXIT;

        TempJnlBatchName := "Journal Batch Name";

        ResJnlPostBatch.RUN(ResJnlLine);

        IF ResReg.GET("Line No.") THEN BEGIN
          ResReg.SETRECFILTER;
          REPORT.RUN(ResJnlTemplate."Posting Report ID",FALSE,FALSE,ResReg);
        END;

        IF "Line No." = 0 THEN
          MESSAGE(Text002)
        ELSE
          IF TempJnlBatchName = "Journal Batch Name" THEN
            MESSAGE(Text003)
          ELSE
            MESSAGE(
              Text004 +
              Text005,
              "Journal Batch Name");

        IF NOT FIND('=><') OR (TempJnlBatchName <> "Journal Batch Name") THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

