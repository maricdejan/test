OBJECT Codeunit 412 Common Dialog Management
{
  OBJECT-PROPERTIES
  {
    Date=25.09.15;
    Time=12:26:36;
    Modified=Yes;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text003@1003 : TextConst 'DEU=Textdateien (*.txt)|*.txt|Alle Dateien (*.*)|*.*;ENU=Text Files (*.txt)|*.txt|All Files (*.*)|*.*';
      Text004@1004 : TextConst 'DEU=Microsoft Excel-Dateien (*.xl*)|*.xl*|Alle Dateien (*.*)|*.*;ENU=Microsoft Excel Files (*.xl*)|*.xl*|All Files (*.*)|*.*';
      Text005@1005 : TextConst 'DEU=Word-Dokumente (*.doc)|*.doc|Alle Dateien (*.*)|*.*;ENU=Word Documents (*.doc)|*.doc|All Files (*.*)|*.*';
      Text006@1006 : TextConst 'DEU=XML-Dateien (*.xml)|*.xml|Alle Dateien (*.*)|*.*;ENU=XML files (*.xml)|*.xml|All Files (*.*)|*.*';
      Text007@1007 : TextConst 'DEU=HTM-Dateien (*.htm)|*.htm|Alle Dateien (*.*)|*.*;ENU=HTM files (*.htm)|*.htm|All Files (*.*)|*.*';
      Text008@1008 : TextConst 'DEU=XML-Schemadateien (*.xsd)|*.xsd|Alle Dateien (*.*)|*.*;ENU=XML Schema Files (*.xsd)|*.xsd|All Files (*.*)|*.*';
      Text009@1009 : TextConst 'DEU=XSL-Transformationsdateien(*.xslt)|*.xslt|Alle Dateien (*.*)|*.*;ENU=XSL Transform Files(*.xslt)|*.xslt|All Files (*.*)|*.*';

    PROCEDURE OpenFile@1(WindowTitle@1000 : Text[50];DefaultFileName@1001 : Text[1024];DefaultFileType@1002 : ' ,Text,Excel,Word,Custom,Xml,Htm,Xsd,Xslt';FilterString@1003 : Text[250];Action@1004 : 'Open,Save') : Text[260];
    VAR
      CommonDialogControl@1005 : OCX "{F9043C85-F6F2-101A-A3C9-08002B2F49FB}:'Microsoft Common Dialog Control, version 6.0'";
      Filter@1007 : Text[255];
    BEGIN
      IF DefaultFileType = DefaultFileType::Custom THEN BEGIN
        GetDefaultFileType(DefaultFileName,DefaultFileType);
        Filter := FilterString;
      END ELSE
        Filter := GetFilterString(DefaultFileType);

      CommonDialogControl.MaxFileSize := 2048;
      CommonDialogControl.FileName := DefaultFileName;
      CommonDialogControl.DialogTitle := WindowTitle;
      CommonDialogControl.Filter := Filter;
      CommonDialogControl.InitDir := DefaultFileName;

      IF Action = Action::Open THEN
        CommonDialogControl.ShowOpen
      ELSE
        CommonDialogControl.ShowSave;
      EXIT(CommonDialogControl.FileName);
    END;

    PROCEDURE OpenFileWithName@2(DefaultFileName@1001 : Text[1024]) : Text[260];
    VAR
      DefaultFileType@1002 : ' ,Text,Excel,Word,Custom,Xml,Htm,Xsd,Xslt';
      FilterString@1003 : Text[250];
      Action@1004 : 'Open,Save';
    BEGIN
      GetDefaultFileType(DefaultFileName,DefaultFileType);
      IF DefaultFileType = DefaultFileType::Custom THEN
        FilterString := Text003;

      EXIT(OpenFile('',DefaultFileName,DefaultFileType,FilterString,Action::Open));
    END;

    LOCAL PROCEDURE GetDefaultFileType@3(DefaultFileName@1001 : Text[1024];VAR DefaultFileType@1002 : ' ,Text,Excel,Word,Custom,Xml,Htm,Xsd,Xslt');
    BEGIN
      CASE TRUE OF
        CheckFileNameForFileType(DefaultFileName,'.DOC'):
          DefaultFileType := DefaultFileType::Word;
        CheckFileNameForFileType(DefaultFileName,'.XLS'):
          DefaultFileType := DefaultFileType::Excel;
        CheckFileNameForFileType(DefaultFileName,'.TXT'):
          DefaultFileType := DefaultFileType::Custom;
        CheckFileNameForFileType(DefaultFileName,'.XML'):
          DefaultFileType := DefaultFileType::Xml;
        CheckFileNameForFileType(DefaultFileName,'.HTM'):
          DefaultFileType := DefaultFileType::Htm;
        CheckFileNameForFileType(DefaultFileName,'.XSD'):
          DefaultFileType := DefaultFileType::Xsd;
        CheckFileNameForFileType(DefaultFileName,'.XSLT'):
          DefaultFileType := DefaultFileType::Xslt;
        ELSE
          DefaultFileType := DefaultFileType::Custom;
      END;
    END;

    LOCAL PROCEDURE CheckFileNameForFileType@4(DefaultFileName@1001 : Text[1024];FileExtension@1002 : Text[5]) : Boolean;
    VAR
      Position@1003 : Integer;
    BEGIN
      Position := STRPOS(UPPERCASE(DefaultFileName),FileExtension);
      EXIT((Position > 0) AND (Position - 1 = STRLEN(DefaultFileName) - STRLEN(FileExtension)));
    END;

    PROCEDURE GetFilterString@5(FileType@1000 : ' ,Text,Excel,Word,Custom,Xml,Htm,Xsd,Xslt') : Text[255];
    BEGIN
      CASE FileType OF
        FileType::Text:
          EXIT(Text003);
        FileType::Excel:
          EXIT(Text004);
        FileType::Word:
          EXIT(Text005);
        FileType::Xml:
          EXIT(Text006);
        FileType::Htm:
          EXIT(Text007);
        FileType::Xsd:
          EXIT(Text008);
        FileType::Xslt:
          EXIT(Text009);
        FileType::Custom:
          EXIT('');
      END;
    END;

    BEGIN
    END.
  }
}

