OBJECT Codeunit 409 Dimension Value-Indent
{
  OBJECT-PROPERTIES
  {
    Date=08.06.01;
    Time=12:00:00;
    Version List=NAVW13.00;
  }
  PROPERTIES
  {
    TableNo=349;
    OnRun=BEGIN
            IF NOT
               CONFIRM(
                 STRSUBSTNO(
                   Text000 +
                   Text001 +
                   Text002 +
                   Text003,"Dimension Code"),TRUE)
            THEN
              EXIT;

            DimVal.SETRANGE("Dimension Code","Dimension Code");
            Indent;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU="Diese Funktion aktualisiert die EinrÅckung aller Dimensionswerte fÅr Dimension %1. ";ENU="This function updates the indentation of all the dimension values for dimension %1. "';
      Text001@1001 : TextConst 'DEU="Alle Dimensionen zwischen Von-Summe und Bis-Summe werden um eine Stufe eingerÅckt. ";ENU="All dimension values between a Begin-Total and the matching End-Total are indented by one level. "';
      Text002@1002 : TextConst 'DEU=Die ZusammenzÑhlung fÅr Bis-Summen ist ebenfalls aktualisiert.\\;ENU=The Totaling field for each End-Total is also updated.\\';
      Text003@1003 : TextConst 'DEU=Mîchten Sie die Dimensionswerte einrÅcken?;ENU=Do you want to indent the dimension values?';
      Text004@1004 : TextConst 'DEU=Dimensionswerte werden eingerÅckt @1@@@@@@@@@@@@@@@@@@;ENU=Indenting Dimension Values @1@@@@@@@@@@@@@@@@@@';
      Text005@1005 : TextConst 'DEU=Bis-Summe %1 hat keine passende Von-Summe.;ENU=End-Total %1 is missing a matching Begin-Total.';
      DimVal@1006 : Record 349;
      Window@1007 : Dialog;
      DimValCode@1008 : ARRAY [10] OF Code[20];
      i@1009 : Integer;

    PROCEDURE Indent@1();
    VAR
      NoOfDimVals@1000 : Integer;
      Progress@1001 : Integer;
    BEGIN
      Window.OPEN(Text004);

      NoOfDimVals := DimVal.COUNTAPPROX;
      IF NoOfDimVals = 0 THEN
        NoOfDimVals := 1;
      WITH DimVal DO
        IF FIND('-') THEN
          REPEAT
            Progress := Progress + 1;
            Window.UPDATE(1,10000 * Progress DIV NoOfDimVals);

            IF "Dimension Value Type" = "Dimension Value Type"::"End-Total" THEN BEGIN
              IF i < 1 THEN
                ERROR(
                  Text005,
                  Code);
              Totaling := DimValCode[i] + '..' + Code;
              i := i - 1;
            END;

            Indentation := i;
            MODIFY;

            IF "Dimension Value Type" = "Dimension Value Type"::"Begin-Total" THEN BEGIN
              i := i + 1;
              DimValCode[i] := Code;
            END;
          UNTIL NEXT = 0;

      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}

