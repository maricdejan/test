OBJECT Table 325 VAT Posting Setup
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00,NAVDACH4.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "VAT %" = 0 THEN
                 "VAT %" := GetVATPtc;
             END;

    CaptionML=[DEU=MwSt.-Buchungsmatrix Einr.;
               ENU=VAT Posting Setup];
    LookupFormID=Form472;
    DrillDownFormID=Form472;
  }
  FIELDS
  {
    { 1   ;   ;VAT Bus. Posting Group;Code10      ;TableRelation="VAT Business Posting Group";
                                                   CaptionML=[DEU=MwSt.-GeschÑftsbuchungsgruppe;
                                                              ENU=VAT Bus. Posting Group] }
    { 2   ;   ;VAT Prod. Posting Group;Code10     ;TableRelation="VAT Product Posting Group";
                                                   CaptionML=[DEU=MwSt.-Produktbuchungsgruppe;
                                                              ENU=VAT Prod. Posting Group] }
    { 3   ;   ;VAT Calculation Type;Option        ;CaptionML=[DEU=MwSt.-Berechnungsart;
                                                              ENU=VAT Calculation Type];
                                                   OptionCaptionML=[DEU=Normale MwSt.,Erwerbsbesteuerung,Nur MwSt.,Verkaufssteuer;
                                                                    ENU=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax];
                                                   OptionString=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax }
    { 4   ;   ;VAT %               ;Decimal       ;OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("VAT %"));
                                                                CheckVATIdentifier;
                                                              END;

                                                   CaptionML=[DEU=MwSt. %;
                                                              ENU=VAT %];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 5   ;   ;Unrealized VAT Type ;Option        ;OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Unrealized VAT Type"));

                                                                IF "Unrealized VAT Type" > 0 THEN BEGIN
                                                                  GLSetup.GET;
                                                                  IF NOT GLSetup."Unrealized VAT" AND NOT GLSetup."Prepayment Unrealized VAT" THEN
                                                                    GLSetup.TESTFIELD("Unrealized VAT",TRUE)
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Unreal. MwSt.-Art;
                                                              ENU=Unrealized VAT Type];
                                                   OptionCaptionML=[DEU=" ,Prozentsatz,Erste,Letzte,Erste (ganz bezahlt),Letzte (ganz bezahlt)";
                                                                    ENU=" ,Percentage,First,Last,First (Fully Paid),Last (Fully Paid)"];
                                                   OptionString=[ ,Percentage,First,Last,First (Fully Paid),Last (Fully Paid)] }
    { 6   ;   ;Adjust for Payment Discount;Boolean;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Adjust for Payment Discount"));

                                                                IF "Adjust for Payment Discount" THEN BEGIN
                                                                  GLSetup.GET;
                                                                  GLSetup.TESTFIELD("Adjust for Payment Disc.",TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Skonto berichtigen;
                                                              ENU=Adjust for Payment Discount] }
    { 7   ;   ;Sales VAT Account   ;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Sales VAT Account"));

                                                                CheckGLAcc("Sales VAT Account");
                                                              END;

                                                   CaptionML=[DEU=Umsatzsteuerkonto;
                                                              ENU=Sales VAT Account] }
    { 8   ;   ;Sales VAT Unreal. Account;Code20   ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Sales VAT Unreal. Account"));

                                                                CheckGLAcc("Sales VAT Unreal. Account");
                                                              END;

                                                   CaptionML=[DEU=Unreal. Umsatzsteuerkonto;
                                                              ENU=Sales VAT Unreal. Account] }
    { 9   ;   ;Purchase VAT Account;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Purchase VAT Account"));

                                                                CheckGLAcc("Purchase VAT Account");
                                                              END;

                                                   CaptionML=[DEU=Vorsteuerkonto;
                                                              ENU=Purchase VAT Account] }
    { 10  ;   ;Purch. VAT Unreal. Account;Code20  ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Purch. VAT Unreal. Account"));

                                                                CheckGLAcc("Purch. VAT Unreal. Account");
                                                              END;

                                                   CaptionML=[DEU=Unreal. Vorsteuerkonto;
                                                              ENU=Purch. VAT Unreal. Account] }
    { 11  ;   ;Reverse Chrg. VAT Acc.;Code20      ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Reverse Chrg. VAT Acc."));

                                                                CheckGLAcc("Reverse Chrg. VAT Acc.");
                                                              END;

                                                   CaptionML=[DEU=Erwerbssteuerkonto;
                                                              ENU=Reverse Chrg. VAT Acc.] }
    { 12  ;   ;Reverse Chrg. VAT Unreal. Acc.;Code20;
                                                   TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                TestNotSalesTax(FIELDCAPTION("Reverse Chrg. VAT Unreal. Acc."));

                                                                CheckGLAcc("Reverse Chrg. VAT Unreal. Acc.");
                                                              END;

                                                   CaptionML=[DEU=Unreal. Erwerbssteuerkonto;
                                                              ENU=Reverse Chrg. VAT Unreal. Acc.] }
    { 13  ;   ;VAT Identifier      ;Code10        ;OnValidate=BEGIN
                                                                "VAT %" := GetVATPtc;
                                                              END;

                                                   CaptionML=[DEU=MwSt.-Kennzeichen;
                                                              ENU=VAT Identifier] }
  }
  KEYS
  {
    {    ;VAT Bus. Posting Group,VAT Prod. Posting Group;
                                                   Clustered=Yes }
    {    ;VAT Prod. Posting Group,VAT Bus. Posting Group }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=%1 muss auf der SteuerzustÑndigkeitszeile angegeben werden, wenn die %2 %3 ist.;ENU=%1 must be entered on the tax jurisdiction line when %2 is %3.';
      Text001@1001 : TextConst 'DEU="Das %1 = %2 wurde bereits fÅr %3 = %4 in der %5 fÅr die %6 = %7 und die %8 = %9 verwendet.";ENU="%1 = %2 has already been used for %3 = %4 in %5 for %6 = %7 and %8 = %9."';
      GLSetup@1002 : Record 98;

    LOCAL PROCEDURE CheckGLAcc@2(AccNo@1000 : Code[20]);
    VAR
      GLAcc@1001 : Record 15;
    BEGIN
      IF AccNo <> '' THEN BEGIN
        GLAcc.GET(AccNo);
        GLAcc.CheckGLAcc;
      END;
    END;

    PROCEDURE TestNotSalesTax@1(FromFieldName@1000 : Text[100]);
    BEGIN
      IF "VAT Calculation Type" = "VAT Calculation Type"::"Sales Tax" THEN
        ERROR(
          Text000,
          FromFieldName,FIELDCAPTION("VAT Calculation Type"),
          "VAT Calculation Type");
    END;

    LOCAL PROCEDURE CheckVATIdentifier@14();
    VAR
      VATPostingSetup@1000 : Record 325;
    BEGIN
      VATPostingSetup.SETRANGE("VAT Bus. Posting Group","VAT Bus. Posting Group");
      VATPostingSetup.SETFILTER("VAT Prod. Posting Group",'<>%1',"VAT Prod. Posting Group");
      VATPostingSetup.SETFILTER("VAT %",'<>%1',"VAT %");
      VATPostingSetup.SETRANGE("VAT Identifier","VAT Identifier");
      IF VATPostingSetup.FIND('-') THEN
        ERROR(
        Text001,
        FIELDCAPTION("VAT Identifier"),VATPostingSetup."VAT Identifier",
        FIELDCAPTION("VAT %"),VATPostingSetup."VAT %",TABLECAPTION,
        FIELDCAPTION("VAT Bus. Posting Group"),VATPostingSetup."VAT Bus. Posting Group",
        FIELDCAPTION("VAT Prod. Posting Group"),VATPostingSetup."VAT Prod. Posting Group");
    END;

    LOCAL PROCEDURE GetVATPtc@3() : Decimal;
    VAR
      VATPostingSetup@1000 : Record 325;
    BEGIN
      VATPostingSetup.SETRANGE("VAT Bus. Posting Group","VAT Bus. Posting Group");
      VATPostingSetup.SETFILTER("VAT Prod. Posting Group",'<>%1',"VAT Prod. Posting Group");
      VATPostingSetup.SETRANGE("VAT Identifier","VAT Identifier");
      IF NOT VATPostingSetup.FIND('-') THEN
        VATPostingSetup."VAT %" := "VAT %";
      EXIT(VATPostingSetup."VAT %");
    END;

    BEGIN
    END.
  }
}

