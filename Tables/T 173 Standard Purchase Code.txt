OBJECT Table 173 Standard Purchase Code
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Description;
    OnDelete=BEGIN
               StdPurchLine.RESET;
               StdPurchLine.SETRANGE("Standard Purchase Code",Code);
               StdPurchLine.DELETEALL(TRUE);
             END;

    OnRename=VAR
               DimDoc@1000 : Record 357;
               DimDoc2@1001 : Record 357;
             BEGIN
               DimDoc.SETRANGE("Table ID",DATABASE::"Standard Purchase Line");
               DimDoc.SETRANGE("Document Type",DimDoc."Document Type"::" ");
               DimDoc.SETRANGE("Document No.",xRec.Code);
               IF DimDoc.FIND('-') THEN
                 REPEAT
                   DimDoc2 := DimDoc;
                   DimDoc2."Document No." := Code;
                   IF DimDoc2.INSERT THEN;
                 UNTIL DimDoc.NEXT = 0;
               DimDoc.DELETEALL;
             END;

    CaptionML=[DEU=Standardeinkaufscode;
               ENU=Standard Purchase Code];
    PasteIsValid=No;
    LookupFormID=Form177;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[DEU=Code;
                                                              ENU=Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text50        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description] }
    { 3   ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   OnValidate=VAR
                                                                Currency@1000 : Record 4;
                                                                Currency2@1001 : Record 4;
                                                              BEGIN
                                                                IF NOT Currency.GET("Currency Code") THEN
                                                                  Currency.InitRoundingPrecision;
                                                                IF NOT Currency2.GET(xRec."Currency Code") THEN
                                                                  Currency2.InitRoundingPrecision;

                                                                IF Currency."Amount Rounding Precision" <> Currency2."Amount Rounding Precision" THEN BEGIN
                                                                  StdPurchLine.RESET;
                                                                  StdPurchLine.SETRANGE("Standard Purchase Code",Code);
                                                                  StdPurchLine.SETRANGE(Type,StdPurchLine.Type::"G/L Account");
                                                                  StdPurchLine.SETFILTER("Amount Excl. VAT",'<>%1',0);
                                                                  IF StdPurchLine.FIND('-') THEN BEGIN
                                                                    IF NOT CONFIRM(
                                                                      STRSUBSTNO(
                                                                        Text001,FIELDCAPTION("Currency Code"),StdPurchLine.FIELDCAPTION("Amount Excl. VAT"),
                                                                        FIELDCAPTION("Currency Code")),TRUE)
                                                                    THEN
                                                                      ERROR(Text002);
                                                                    REPEAT
                                                                      StdPurchLine."Amount Excl. VAT" :=
                                                                        ROUND(StdPurchLine."Amount Excl. VAT",Currency."Amount Rounding Precision");
                                                                      StdPurchLine.MODIFY;
                                                                    UNTIL StdPurchLine.NEXT = 0;
                                                                  END;
                                                                END;
                                                                //MODIFY;
                                                              END;

                                                   CaptionML=[DEU=WÑhrungscode;
                                                              ENU=Currency Code] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      StdPurchLine@1000 : Record 174;
      Text001@1003 : TextConst 'DEU=Wenn Sie den %1 Ñndern, wird der %2 entsprechend des neuen %3 gerundet.;ENU=If you change the %1, the %2 will be rounded according to the new %3.';
      Text002@1002 : TextConst 'DEU=Die Verarbeitung wurde unterbrochen, um die Warnung zu berÅcksichtigen.;ENU=The update has been interrupted to respect the warning.';

    BEGIN
    END.
  }
}

