OBJECT Table 413 IC Partner
{
  OBJECT-PROPERTIES
  {
    Date=14.08.09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnDelete=VAR
               GLEntry@1007 : Record 17;
               GLSetup@1006 : Record 98;
               AccountingPeriod@1004 : Record 50;
               Cust@1003 : Record 18;
               Vend@1002 : Record 23;
               ICInbox@1001 : Record 418;
               ICOutbox@1000 : Record 414;
             BEGIN
               GLEntry.LOCKTABLE;

               GLEntry.SETCURRENTKEY("IC Partner Code");
               GLEntry.SETRANGE("IC Partner Code",Code);
               AccountingPeriod.SETRANGE(Closed,FALSE);
               IF AccountingPeriod.FIND('-') THEN
                 GLEntry.SETFILTER("Posting Date",'>=%1',AccountingPeriod."Starting Date");
               IF GLEntry.FIND('-') THEN
                 ERROR(Text000,xRec.Code);

               GLSetup.GET;
               IF GLSetup."Allow G/L Acc. Deletion Before" <> 0D THEN BEGIN
                 GLEntry.SETFILTER("Posting Date",'>=%1',GLSetup."Allow G/L Acc. Deletion Before");
                 IF GLEntry.FIND('-') THEN
                   ERROR(Text001,Code,GLSetup."Allow G/L Acc. Deletion Before");
               END;

               IF "Customer No." <> '' THEN
                 IF Cust.GET("Customer No.") THEN
                   ERROR(Text002,Code,Cust.TABLECAPTION,Cust."No.");

               IF "Vendor No." <> '' THEN
                 IF Vend.GET("Customer No.") THEN
                   ERROR(Text002,Code,Vend.TABLECAPTION,Vend."No.");

               ICInbox.SETRANGE("IC Partner Code",Code);
               IF ICInbox.FIND('-') THEN
                 ERROR(Text003,Code,ICInbox.TABLECAPTION);

               ICOutbox.SETRANGE("IC Partner Code",Code);
               IF ICOutbox.FIND('-') THEN
                 ERROR(Text003,Code,ICOutbox.TABLECAPTION);

               GLEntry.RESET;
               GLEntry.SETCURRENTKEY("IC Partner Code");
               GLEntry.SETRANGE("IC Partner Code",Code);
               GLEntry.MODIFYALL("IC Partner Code",'');

               CommentLine.SETRANGE("Table Name",CommentLine."Table Name"::"IC Partner");
               CommentLine.SETRANGE("No.",Code);
               CommentLine.DELETEALL;
             END;

    CaptionML=[DEU=IC-Partner;
               ENU=IC Partner];
    LookupFormID=Form608;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;CaptionML=[DEU=Code;
                                                              ENU=Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Text50        ;CaptionML=[DEU=Name;
                                                              ENU=Name] }
    { 3   ;   ;Currency Code       ;Code10        ;TableRelation=Currency.Code;
                                                   CaptionML=[DEU=WÑhrungscode;
                                                              ENU=Currency Code] }
    { 4   ;   ;Inbox Type          ;Option        ;OnValidate=VAR
                                                                Cust@1000 : Record 18;
                                                                Vend@1001 : Record 23;
                                                              BEGIN
                                                                IF "Inbox Type" <> xRec."Inbox Type" THEN
                                                                  "Inbox Details" := '';
                                                                IF "Inbox Type" = "Inbox Type"::"E-Mail" THEN BEGIN
                                                                  IF "Customer No." <> '' THEN BEGIN
                                                                    IF Cust.GET("Customer No.") THEN
                                                                      "Inbox Details" := Cust."E-Mail";
                                                                  END ELSE
                                                                    IF "Vendor No." <> '' THEN
                                                                      IF Vend.GET("Vendor No.") THEN
                                                                        "Inbox Details" := Vend."E-Mail";
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Eingangsart;
                                                              ENU=Inbox Type];
                                                   OptionCaptionML=[DEU=Dateispeicherort,Datenbank,E-Mail,Keine IC-öbertragung;
                                                                    ENU=File Location,Database,E-Mail,No IC Transfer];
                                                   OptionString=File Location,Database,E-Mail,No IC Transfer }
    { 5   ;   ;Inbox Details       ;Text250       ;TableRelation=IF (Inbox Type=CONST(Database)) Company.Name;
                                                   OnLookup=VAR
                                                              Company@1000 : Record 2000000006;
                                                              Companies@1001 : Form 357;
                                                              CmDlgMgt@1002 : Codeunit 412;
                                                              i@1003 : Integer;
                                                              InboxDetails@1004 : Text[250];
                                                              InboxDetails2@1005 : Text[250];
                                                            BEGIN
                                                              CASE "Inbox Type" OF
                                                                "Inbox Type"::Database:
                                                                  BEGIN
                                                                    Company.SETFILTER(Name,'<>%1',COMPANYNAME);
                                                                    Companies.SETTABLEVIEW(Company);
                                                                    Companies.LOOKUPMODE := TRUE;
                                                                    IF Companies.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                      Companies.GETRECORD(Company);
                                                                      "Inbox Details" := Company.Name;
                                                                    END;
                                                                  END;
                                                                "Inbox Type"::"File Location":
                                                                  BEGIN
                                                                    IF "Inbox Details" = '' THEN
                                                                      InboxDetails := STRSUBSTNO('%1.xml',Code)
                                                                    ELSE
                                                                      InboxDetails := "Inbox Details" + STRSUBSTNO('%1.xml',Code);
                                                                    InboxDetails2 := CmDlgMgt.OpenFile(Text005,InboxDetails,4,'*.xml|*.xml',1);
                                                                    IF InboxDetails <> InboxDetails2 THEN BEGIN
                                                                      "Inbox Details" := InboxDetails2;
                                                                      IF STRLEN("Inbox Details") > 4 THEN BEGIN
                                                                        i := STRLEN("Inbox Details") - 4;
                                                                        WHILE (i > 1) AND ("Inbox Details"[i] <> '\') DO
                                                                          i := i - 1;
                                                                        IF i > 1 THEN
                                                                          "Inbox Details" := COPYSTR("Inbox Details",1,i - 1);
                                                                      END;
                                                                    END;
                                                                  END;
                                                              END;
                                                            END;

                                                   CaptionML=[DEU=Eingangsdetails;
                                                              ENU=Inbox Details] }
    { 6   ;   ;Receivables Account ;Code20        ;TableRelation="G/L Account".No.;
                                                   CaptionML=[DEU=Debitorensammelkonto;
                                                              ENU=Receivables Account] }
    { 7   ;   ;Payables Account    ;Code20        ;TableRelation="G/L Account".No.;
                                                   CaptionML=[DEU=Kreditorensammelkonto;
                                                              ENU=Payables Account] }
    { 10  ;   ;Blocked             ;Boolean       ;CaptionML=[DEU=Gesperrt;
                                                              ENU=Blocked] }
    { 11  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Comment Line" WHERE (Table Name=CONST(IC Partner),
                                                                                           No.=FIELD(Code)));
                                                   CaptionML=[DEU=Bemerkung;
                                                              ENU=Comment];
                                                   Editable=No }
    { 12  ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   CaptionML=[DEU=Debitorennr.;
                                                              ENU=Customer No.] }
    { 13  ;   ;Vendor No.          ;Code20        ;TableRelation=Vendor;
                                                   CaptionML=[DEU=Kreditorennr.;
                                                              ENU=Vendor No.] }
    { 14  ;   ;Outbound Sales Item No. Type;Option;CaptionML=[DEU=Ausgehende VK-Artikelnr.-Art;
                                                              ENU=Outbound Sales Item No. Type];
                                                   OptionCaptionML=[DEU=Interne Nr.,Gemeinsame Artikelnr.,Referenz;
                                                                    ENU=Internal No.,Common Item No.,Cross Reference];
                                                   OptionString=Internal No.,Common Item No.,Cross Reference }
    { 15  ;   ;Outbound Purch. Item No. Type;Option;
                                                   CaptionML=[DEU=Ausgehende EK-Artikelnr.-Art;
                                                              ENU=Outbound Purch. Item No. Type];
                                                   OptionCaptionML=[DEU=Interne Nr.,Gemeinsame Artikelnr.,Referenz,Kred.-Artikelnr.;
                                                                    ENU=Internal No.,Common Item No.,Cross Reference,Vendor Item No.];
                                                   OptionString=Internal No.,Common Item No.,Cross Reference,Vendor Item No. }
    { 16  ;   ;Cost Distribution in LCY;Boolean   ;CaptionML=[DEU=Kostenverteilung in MW;
                                                              ENU=Cost Distribution in LCY] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      CommentLine@1001 : Record 97;
      Text000@1005 : TextConst 'DEU=Sie kînnen IC-Partner %1 nicht lîschen, weil in einem noch nicht abgeschlossenen GeschÑftsjahr Posten vorliegen.;ENU=You cannot delete IC Partner %1 because it has ledger entries in a fiscal year that has not been closed yet.';
      Text001@1004 : TextConst 'DEU=Sie kînnen IC-Partner %1 nicht lîschen, weil nach %2 Posten vorliegen.;ENU=You cannot delete IC Partner %1 because it has ledger entries after %2.';
      Text002@1003 : TextConst 'DEU=Sie kînnen IC-Partner %1 nicht lîschen, weil er fÅr %2 %3 verwendet wird.;ENU=You cannot delete IC Partner %1 because it is used for %2 %3';
      Text003@1002 : TextConst 'DEU=Sie kînnen IC-Partner %1 nicht lîschen, weil er in %2 verwendet wird.;ENU=You cannot delete IC Partner %1 because it is used in %2';
      Text004@1006 : TextConst 'DEU=%1 %2 ist mit einem gesperrten IC-Partner verknÅpft.;ENU=%1 %2 is linked to a blocked IC Partner.';
      Text005@1000 : TextConst 'DEU=Speicherort fÅr IC-Dateien;ENU=File Location for IC files';

    PROCEDURE CheckICPartner@1();
    BEGIN
      TESTFIELD(Blocked,FALSE);
    END;

    PROCEDURE CheckICPartnerIndirect@2(AccountType@1000 : Text[250];AccountNo@1001 : Code[20]);
    BEGIN
      IF Blocked THEN
        ERROR(Text004,AccountType,AccountNo);
    END;

    BEGIN
    END.
  }
}

