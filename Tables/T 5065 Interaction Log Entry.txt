OBJECT Table 5065 Interaction Log Entry
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               InteractionCommentLine@1000 : Record 5123;
               CampaignMgt@1001 : Codeunit 7030;
             BEGIN
               InteractionCommentLine.SETRANGE("Entry No.","Entry No.");
               InteractionCommentLine.DELETEALL;

               CampaignMgt.DeleteContfromTargetGr(Rec);
             END;

    CaptionML=[DEU=AktivitÑtenprotokollposten;
               ENU=Interaction Log Entry];
    LookupFormID=Form5076;
    DrillDownFormID=Form5076;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[DEU=Lfd. Nr.;
                                                              ENU=Entry No.] }
    { 2   ;   ;Contact No.         ;Code20        ;TableRelation=Contact;
                                                   CaptionML=[DEU=Kontaktnr.;
                                                              ENU=Contact No.] }
    { 3   ;   ;Contact Company No. ;Code20        ;TableRelation=Contact WHERE (Type=CONST(Company));
                                                   CaptionML=[DEU=Kontakt Unternehmensnr.;
                                                              ENU=Contact Company No.] }
    { 4   ;   ;Date                ;Date          ;CaptionML=[DEU=Datum;
                                                              ENU=Date] }
    { 5   ;   ;Description         ;Text50        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description] }
    { 6   ;   ;Information Flow    ;Option        ;CaptionML=[DEU=Informationsfluss;
                                                              ENU=Information Flow];
                                                   OptionCaptionML=[DEU=" ,Ausgehend,Eingehend";
                                                                    ENU=" ,Outbound,Inbound"];
                                                   OptionString=[ ,Outbound,Inbound] }
    { 7   ;   ;Initiated By        ;Option        ;CaptionML=[DEU=Veranlasst durch;
                                                              ENU=Initiated By];
                                                   OptionCaptionML=[DEU=" ,Uns,Kontakt";
                                                                    ENU=" ,Us,Them"];
                                                   OptionString=[ ,Us,Them] }
    { 8   ;   ;Attachment No.      ;Integer       ;TableRelation=Attachment;
                                                   CaptionML=[DEU=Dateianhangsnr.;
                                                              ENU=Attachment No.] }
    { 9   ;   ;Cost (LCY)          ;Decimal       ;CaptionML=[DEU=Einstandsbetrag (MW);
                                                              ENU=Cost (LCY)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 10  ;   ;Duration (Min.)     ;Decimal       ;CaptionML=[DEU=Dauer (Min.);
                                                              ENU=Duration (Min.)];
                                                   DecimalPlaces=0:0;
                                                   Editable=No }
    { 11  ;   ;User ID             ;Code20        ;TableRelation=User;
                                                   OnLookup=VAR
                                                              LoginMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              LoginMgt.LookupUserID("User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[DEU=Benutzer-ID;
                                                              ENU=User ID] }
    { 12  ;   ;Interaction Group Code;Code10      ;TableRelation="Interaction Group";
                                                   CaptionML=[DEU=AktivitÑtengruppencode;
                                                              ENU=Interaction Group Code] }
    { 13  ;   ;Interaction Template Code;Code10   ;TableRelation="Interaction Template";
                                                   CaptionML=[DEU=AktivitÑtenvorlagencode;
                                                              ENU=Interaction Template Code] }
    { 14  ;   ;Campaign No.        ;Code20        ;TableRelation=Campaign;
                                                   CaptionML=[DEU=Kampagnennr.;
                                                              ENU=Campaign No.] }
    { 15  ;   ;Campaign Entry No.  ;Integer       ;TableRelation="Campaign Entry" WHERE (Campaign No.=FIELD(Campaign No.));
                                                   CaptionML=[DEU=Kampagnenposten Lfd. Nr.;
                                                              ENU=Campaign Entry No.] }
    { 16  ;   ;Campaign Response   ;Boolean       ;CaptionML=[DEU=Kampagnenreaktion;
                                                              ENU=Campaign Response] }
    { 17  ;   ;Campaign Target     ;Boolean       ;CaptionML=[DEU=Kampagnenziel;
                                                              ENU=Campaign Target] }
    { 18  ;   ;Segment No.         ;Code20        ;CaptionML=[DEU=Segmentnr.;
                                                              ENU=Segment No.] }
    { 19  ;   ;Evaluation          ;Option        ;CaptionML=[DEU=Bewertung;
                                                              ENU=Evaluation];
                                                   OptionCaptionML=[DEU=" ,Sehr positiv,Positiv,Neutral,Negativ,Sehr negativ";
                                                                    ENU=" ,Very Positive,Positive,Neutral,Negative,Very Negative"];
                                                   OptionString=[ ,Very Positive,Positive,Neutral,Negative,Very Negative];
                                                   Editable=No }
    { 20  ;   ;Time of Interaction ;Time          ;CaptionML=[DEU=Uhrzeit;
                                                              ENU=Time of Interaction] }
    { 21  ;   ;Attempt Failed      ;Boolean       ;CaptionML=[DEU=Nicht erreicht;
                                                              ENU=Attempt Failed] }
    { 23  ;   ;To-do No.           ;Code20        ;TableRelation=To-do;
                                                   CaptionML=[DEU=Aufgabennr.;
                                                              ENU=To-do No.] }
    { 24  ;   ;Salesperson Code    ;Code10        ;TableRelation=Salesperson/Purchaser;
                                                   CaptionML=[DEU=VerkÑufercode;
                                                              ENU=Salesperson Code] }
    { 25  ;   ;Delivery Status     ;Option        ;CaptionML=[DEU=öbermittlungsstatus;
                                                              ENU=Delivery Status];
                                                   OptionCaptionML=[DEU=" ,In Bearbeitung,Fehler";
                                                                    ENU=" ,In Progress,Error"];
                                                   OptionString=[ ,In Progress,Error] }
    { 26  ;   ;Canceled            ;Boolean       ;CaptionML=[DEU=Storniert;
                                                              ENU=Canceled] }
    { 27  ;   ;Correspondence Type ;Option        ;CaptionML=[DEU=Korrespondenzart;
                                                              ENU=Correspondence Type];
                                                   OptionCaptionML=[DEU=" ,Brief,E-Mail,Fax";
                                                                    ENU=" ,Hard Copy,E-Mail,Fax"];
                                                   OptionString=[ ,Hard Copy,E-Mail,Fax] }
    { 28  ;   ;Contact Alt. Address Code;Code10   ;TableRelation="Contact Alt. Address".Code WHERE (Contact No.=FIELD(Contact No.));
                                                   CaptionML=[DEU=Alt. Kontaktadresscode;
                                                              ENU=Contact Alt. Address Code] }
    { 29  ;   ;Logged Segment Entry No.;Integer   ;TableRelation="Logged Segment";
                                                   CaptionML=[DEU=Protok. Segmentposten Lfd. Nr.;
                                                              ENU=Logged Segment Entry No.] }
    { 30  ;   ;Document Type       ;Option        ;CaptionML=[DEU=Belegart;
                                                              ENU=Document Type];
                                                   OptionCaptionML=[DEU=" ,Angebot,Rahmenauftrag,Auftragsbest.,VK-Rech.,VK-Liefersch.,VK-Gutschr.,Kontoausz.,Mahnung,Serv.-Auftr. erst.,Serv.-Auftr. buchen,Anfrage,Rahmenbestellung,Bestellung,EK-Rech.,EK-Liefersch.,EK-Gutschr.,Deckblatt,Verkaufsreklamation,Zinsrechnung,RÅcksendung,RÅcklieferung,Eink.-ReklamationsbestÑtigung,Servicevertrag,Servicevertragsangebot,Serviceangebot";
                                                                    ENU=" ,Sales Qte.,Sales Blnkt. Ord,Sales Ord. Cnfrmn.,Sales Inv.,Sales Shpt. Note,Sales Cr. Memo,Sales Stmnt.,Sales Rmdr.,Serv. Ord. Create,Serv. Ord. Post,Purch.Qte.,Purch. Blnkt. Ord.,Purch. Ord.,Purch. Inv.,Purch. Rcpt.,Purch. Cr. Memo,Cover Sheet,Sales Return Order,Sales Finance Charge Memo,Sales Return Receipt,Purch. Return Shipment,Purch. Return Ord. Cnfrmn.,Service Contract,Service Contract Quote,Service Quote"];
                                                   OptionString=[ ,Sales Qte.,Sales Blnkt. Ord,Sales Ord. Cnfrmn.,Sales Inv.,Sales Shpt. Note,Sales Cr. Memo,Sales Stmnt.,Sales Rmdr.,Serv. Ord. Create,Serv. Ord. Post,Purch.Qte.,Purch. Blnkt. Ord.,Purch. Ord.,Purch. Inv.,Purch. Rcpt.,Purch. Cr. Memo,Cover Sheet,Sales Return Order,Sales Finance Charge Memo,Sales Return Receipt,Purch. Return Shipment,Purch. Return Ord. Cnfrmn.,Service Contract,Service Contract Quote,Service Quote] }
    { 31  ;   ;Document No.        ;Code20        ;TestTableRelation=No;
                                                   CaptionML=[DEU=Belegnr.;
                                                              ENU=Document No.] }
    { 32  ;   ;Version No.         ;Integer       ;CaptionML=[DEU=Versionsnr.;
                                                              ENU=Version No.] }
    { 33  ;   ;Doc. No. Occurrence ;Integer       ;CaptionML=[DEU=Belegnr.-HÑufigkeit;
                                                              ENU=Doc. No. Occurrence] }
    { 34  ;   ;Contact Via         ;Text80        ;CaptionML=[DEU=Nummer/Adresse;
                                                              ENU=Contact Via] }
    { 35  ;   ;Send Word Docs. as Attmt.;Boolean  ;CaptionML=[DEU=Word-Dokumente als Anh. senden;
                                                              ENU=Send Word Docs. as Attmt.] }
    { 36  ;   ;Interaction Language Code;Code10   ;TableRelation=Language;
                                                   CaptionML=[DEU=AktivitÑtensprachcode;
                                                              ENU=Interaction Language Code] }
    { 37  ;   ;E-Mail Logged       ;Boolean       ;CaptionML=[DEU=E-Mail protokolliert;
                                                              ENU=E-Mail Logged] }
    { 38  ;   ;Subject             ;Text50        ;CaptionML=[DEU=Betreff;
                                                              ENU=Subject] }
    { 39  ;   ;Contact Name        ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact No.)));
                                                   CaptionML=[DEU=Kontaktname;
                                                              ENU=Contact Name];
                                                   Editable=No }
    { 40  ;   ;Contact Company Name;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Contact.Name WHERE (No.=FIELD(Contact Company No.),
                                                                                          Type=CONST(Company)));
                                                   CaptionML=[DEU=Kontakt Unternehmensname;
                                                              ENU=Contact Company Name];
                                                   Editable=No }
    { 43  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Inter. Log Entry Comment Line" WHERE (Entry No.=FIELD(Entry No.)));
                                                   CaptionML=[DEU=Bemerkung;
                                                              ENU=Comment];
                                                   Editable=No }
    { 44  ;   ;Opportunity No.     ;Code20        ;TableRelation=Opportunity;
                                                   CaptionML=[DEU=Verkaufschancennr.;
                                                              ENU=Opportunity No.] }
    { 45  ;   ;Postponed           ;Boolean       ;CaptionML=[DEU=ZurÅckgestellt;
                                                              ENU=Postponed] }
    { 46  ;   ;Mail GUID           ;GUID          ;CaptionML=[DEU=E-Mail-GUID;
                                                              ENU=Mail GUID] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Contact Company No.,Contact No.,Date,Postponed;
                                                   SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Contact Company No.,Date,Contact No.,Canceled,Initiated By,Attempt Failed,Postponed;
                                                   SumIndexFields=Cost (LCY),Duration (Min.);
                                                   MaintainSIFTIndex=No }
    {    ;Interaction Group Code,Date              }
    {    ;Interaction Group Code,Canceled,Date,Postponed;
                                                   SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Interaction Template Code,Date           }
    {    ;Interaction Template Code,Canceled,Date,Postponed;
                                                   SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Canceled,Campaign No.,Campaign Entry No.,Date,Postponed;
                                                   SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Campaign No.,Campaign Entry No.,Date,Postponed;
                                                   SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Salesperson Code,Date,Postponed         ;SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Canceled,Salesperson Code,Date,Postponed;SumIndexFields=Cost (LCY),Duration (Min.) }
    {    ;Logged Segment Entry No.,Postponed       }
    {    ;Attachment No.                           }
    {    ;To-do No.,Date                           }
    {    ;Contact No.,Correspondence Type,E-Mail Logged,Subject,Postponed }
    {    ;Campaign No.,Campaign Target             }
    {    ;Campaign No.,Contact Company No.,Campaign Target,Postponed }
    {    ;Opportunity No.,Date                     }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Entry No.,Description,Contact No.,Date   }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=%1 %2 ist als %3 gekennzeichnet.\;ENU=%1 %2 is marked %3.\';
      Text001@1001 : TextConst 'DEU=Mîchten Sie das Kennzeichen Storniert entfernen?;ENU=Do you wish to remove the checkmark?';
      Text002@1002 : TextConst 'DEU=Mîchten Sie den %1 %2 als %3 kennzeichnen?;ENU=Do you wish to mark %1 %2 as %3?';
      Text003@1003 : TextConst 'DEU=Es ist nicht mîglich KontoauszÅge einzusehen, nachdem sie gedruckt wurden.;ENU=It is not possible to view sales statements after they have been printed.';
      Text004@1004 : TextConst 'DEU=Es ist nicht mîglich DeckblÑtter einzusehen, nachdem sie gedruckt wurden.;ENU=It is not possible to show cover sheets after they have been printed.';
      Text005@1005 : TextConst 'DEU=Mîchten Sie die Markierung von den gewÑhlten %1 entfernen?;ENU=Do you wish to remove the checkmark from the selected %1 lines?';
      Text006@1006 : TextConst 'DEU=Mîchten Sie die ausgewÑhlten %1 als %2 kennzeichnen?;ENU=Do you wish to mark the selected %1 lines as %2?';
      Text009@1007 : TextConst 'DEU=Mîchten Sie den Anhang entfernen?;ENU=Do you want to remove Attachment?';
      Text010@1008 : TextConst 'DEU=Mîchten Sie die eindeutigen AnhÑnge fÅr die gewÑhlten Zeilen entfernen?;ENU=Do you want to remove unique Attachments for the selected lines?';
      Text011@1009 : TextConst 'DEU=Sehr positiv,Positiv,Neutral,Negativ,Sehr negativ;ENU=Very Positive,Positive,Neutral,Negative,Very Negative';

    PROCEDURE CreateInteraction@10();
    VAR
      SegLine@1000 : TEMPORARY Record 5077;
    BEGIN
      SegLine.CreateInteractionFromIntLogEnt(Rec);
    END;

    PROCEDURE CreateTodo@8();
    VAR
      TempTodo@1000 : TEMPORARY Record 5080;
    BEGIN
      TempTodo.CreateToDoFromInteractLogEntry(Rec)
    END;

    PROCEDURE OpenAttachment@5();
    VAR
      Attachment@1000 : Record 5062;
      SegLine@1001 : Record 5077;
    BEGIN
      IF "Attachment No." = 0 THEN
        EXIT;
      Attachment.GET("Attachment No.");

      IF Attachment."Storage Type" <> Attachment."Storage Type"::"Exchange Storage" THEN BEGIN
        SegLine."Contact No." := "Contact No.";
        SegLine."Salesperson Code" := "Salesperson Code";
        SegLine."Contact Alt. Address Code" := "Contact Alt. Address Code";
        SegLine.Date := Date;
        SegLine."Campaign No." := "Campaign No.";
        SegLine."Segment No." := "Segment No.";
        SegLine."Line No." := "Entry No.";
        SegLine.Description := Description;
        SegLine.Subject := Subject;
        SegLine."Language Code" := "Interaction Language Code";
        Attachment.ShowAttachment(SegLine,FORMAT("Entry No.") + ' ' + Description,FALSE);
      END ELSE
        Attachment.DisplayInOutlook;
    END;

    PROCEDURE ToggleCanceledCheckmark@7();
    VAR
      InteractionLogEntry@1003 : Record 5065;
      Attachment@1002 : Record 5062;
      ErrorTxt@1004 : Text[80];
      MasterCanceledCheckmark@1001 : Boolean;
      RemoveUniqueAttachment@1000 : Boolean;
    BEGIN
      IF FIND('-') THEN BEGIN
        IF ConfirmToggleCanceledCheckmark(COUNT,ErrorTxt) THEN BEGIN
          MasterCanceledCheckmark := NOT Canceled;
          IF FindUniqueAttachment AND MasterCanceledCheckmark THEN
            RemoveUniqueAttachment := CONFIRM(ErrorTxt,FALSE);
          SETCURRENTKEY("Entry No.");
          IF FIND('-') THEN
            REPEAT
              SetCanceledCheckmark(MasterCanceledCheckmark,RemoveUniqueAttachment);
            UNTIL NEXT = 0
          END;
      END
    END;

    PROCEDURE SetCanceledCheckmark@2(CanceledCheckmark@1003 : Boolean;RemoveUniqueAttachment@1004 : Boolean);
    VAR
      CampaignEntry@1000 : Record 5072;
      LoggedSeg@1001 : Record 5075;
      Attachment@1002 : Record 5062;
    BEGIN
      IF Canceled AND NOT CanceledCheckmark THEN BEGIN
        IF "Logged Segment Entry No." <> 0 THEN BEGIN
          LoggedSeg.GET("Logged Segment Entry No.");
          LoggedSeg.TESTFIELD(Canceled,FALSE);
        END;
        IF "Campaign Entry No." <> 0 THEN BEGIN
          CampaignEntry.GET("Campaign Entry No.");
          CampaignEntry.TESTFIELD(Canceled,FALSE);
        END;
      END;

      IF NOT Canceled AND CanceledCheckmark THEN BEGIN
        IF UniqueAttachment AND RemoveUniqueAttachment THEN BEGIN
          Attachment.GET("Attachment No.");
          IF Attachment.RemoveAttachment(FALSE) THEN
            "Attachment No." := 0;
        END;
      END;

      Canceled := CanceledCheckmark;
      MODIFY;
    END;

    PROCEDURE ConfirmToggleCanceledCheckmark@3(NumberOfSelectedLines@1000 : Integer;VAR ErrorTxt@1001 : Text[80]) : Boolean;
    BEGIN
      IF NumberOfSelectedLines = 1 THEN BEGIN
        ErrorTxt := Text009;
        IF Canceled THEN BEGIN
          EXIT(CONFIRM(
            Text000 +
            Text001,TRUE,TABLECAPTION,"Entry No.",FIELDCAPTION(Canceled)));
        END ELSE BEGIN
          EXIT(CONFIRM(
            Text002,TRUE,TABLECAPTION,"Entry No.",FIELDCAPTION(Canceled)));
        END;
      END ELSE BEGIN
        ErrorTxt := Text010;
        IF Canceled THEN BEGIN
          EXIT(CONFIRM(
            Text005,TRUE,TABLECAPTION));
        END ELSE BEGIN
          EXIT(CONFIRM(
            Text006,TRUE,TABLECAPTION,FIELDCAPTION(Canceled)));
        END;
      END
    END;

    PROCEDURE UniqueAttachment@4() IsUnique@1000 : Boolean;
    VAR
      InteractLogEntry@1001 : Record 5065;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        InteractLogEntry.SETCURRENTKEY("Attachment No.");
        InteractLogEntry.SETRANGE("Attachment No.","Attachment No.");
        InteractLogEntry.SETFILTER("Entry No.",'<>%1',"Entry No.");
        IsUnique := NOT InteractLogEntry.FIND('-');
      END;
    END;

    PROCEDURE FindUniqueAttachment@6() IsUnique@1000 : Boolean;
    BEGIN
      IF FIND('-') THEN
        REPEAT
          IsUnique := UniqueAttachment;
        UNTIL (NEXT = 0) OR IsUnique
    END;

    PROCEDURE ShowDocument@1();
    VAR
      SalesHeader@1000 : Record 36;
      SalesHeaderArchive@1011 : Record 5107;
      SalesInvHeader@1001 : Record 112;
      SalesShptHeader@1002 : Record 110;
      SalesCrMemoHeader@1003 : Record 114;
      IssuedReminderHeader@1004 : Record 297;
      PurchHeader@1005 : Record 38;
      PurchHeaderArchive@1012 : Record 5109;
      PurchInvHeader@1006 : Record 122;
      PurchRcptHeader@1007 : Record 120;
      PurchCrMemoHeader@1008 : Record 124;
      ServHeader@1009 : Record 5900;
      ReturnRcptHeader@1013 : Record 6660;
      IssuedFinChargeMemoHeader@1015 : Record 304;
      ReturnReceiptHeader@1014 : Record 6660;
      ReturnShipmentHeader@1016 : Record 6650;
      ServiceContractHeader@1017 : Record 5965;
    BEGIN
      CASE "Document Type" OF
        "Document Type"::"Sales Qte.":
          IF "Version No." <> 0 THEN BEGIN
            SalesHeaderArchive.GET(
              SalesHeaderArchive."Document Type"::Quote,"Document No.",
              "Doc. No. Occurrence","Version No.");
            SalesHeaderArchive.SETRANGE("Document Type",SalesHeaderArchive."Document Type"::Quote);
            SalesHeaderArchive.SETRANGE("No.","Document No.");
            SalesHeaderArchive.SETRANGE("Doc. No. Occurrence","Doc. No. Occurrence");
            FORM.RUN(FORM::"Sales Quote Archive",SalesHeaderArchive);
          END ELSE BEGIN
            SalesHeader.GET(SalesHeader."Document Type"::Quote,"Document No.");
            FORM.RUN(FORM::"Sales Quote",SalesHeader);
          END;
        "Document Type"::"Sales Blnkt. Ord":
          BEGIN
            SalesHeader.GET(SalesHeader."Document Type"::"Blanket Order","Document No.");
            FORM.RUN(FORM::"Blanket Sales Order",SalesHeader);
          END;
        "Document Type"::"Sales Ord. Cnfrmn.":
          IF "Version No." <> 0 THEN BEGIN
            SalesHeaderArchive.GET(
              SalesHeaderArchive."Document Type"::Order,"Document No.",
              "Doc. No. Occurrence","Version No.");
            SalesHeaderArchive.SETRANGE("Document Type",SalesHeaderArchive."Document Type"::Order);
            SalesHeaderArchive.SETRANGE("No.","Document No.");
            SalesHeaderArchive.SETRANGE("Doc. No. Occurrence","Doc. No. Occurrence");
            FORM.RUN(FORM::"Sales Order Archive",SalesHeaderArchive);
          END ELSE BEGIN
            SalesHeader.GET(SalesHeader."Document Type"::Order,"Document No.");
            FORM.RUN(FORM::"Sales Order",SalesHeader);
          END;
        "Document Type"::"Sales Inv.":
          BEGIN
            SalesInvHeader.GET("Document No.");
            FORM.RUN(FORM::"Posted Sales Invoice",SalesInvHeader);
          END;
        "Document Type"::"Sales Shpt. Note":
          BEGIN
            SalesShptHeader.GET("Document No.");
            FORM.RUN(FORM::"Posted Sales Shipment",SalesShptHeader);
          END;
        "Document Type"::"Sales Cr. Memo":
          BEGIN
            SalesCrMemoHeader.GET("Document No.");
            FORM.RUN(FORM::"Posted Sales Credit Memo",SalesCrMemoHeader);
          END;
        "Document Type"::"Sales Stmnt.":
          ERROR(Text003);
        "Document Type"::"Sales Rmdr.":
          BEGIN
            IssuedReminderHeader.GET("Document No.");
            FORM.RUN(FORM::"Issued Reminder",IssuedReminderHeader);
          END;
        "Document Type"::"Serv. Ord. Create":
          BEGIN
            ServHeader.GET(ServHeader."Document Type"::Order,"Document No.");
            FORM.RUN(FORM::"Service Order",ServHeader)
          END;
        "Document Type"::"Purch.Qte.":
          IF "Version No." <> 0 THEN BEGIN
            PurchHeaderArchive.GET(
              PurchHeaderArchive."Document Type"::Quote,"Document No.",
              "Doc. No. Occurrence","Version No.");
            PurchHeaderArchive.SETRANGE("Document Type",PurchHeaderArchive."Document Type"::Quote);
            PurchHeaderArchive.SETRANGE("No.","Document No.");
            PurchHeaderArchive.SETRANGE("Doc. No. Occurrence","Doc. No. Occurrence");
            FORM.RUN(FORM::"Purchase Quote Archive",PurchHeaderArchive);
          END ELSE BEGIN
            PurchHeader.GET(PurchHeader."Document Type"::Quote,"Document No.");
            FORM.RUN(FORM::"Purchase Quote",PurchHeader);
          END;
        "Document Type"::"Purch. Blnkt. Ord.":
          BEGIN
            PurchHeader.GET(PurchHeader."Document Type"::"Blanket Order","Document No.");
            FORM.RUN(FORM::"Blanket Purchase Order",PurchHeader);
          END;
        "Document Type"::"Purch. Ord.":
          IF "Version No." <> 0 THEN BEGIN
            PurchHeaderArchive.GET(
              PurchHeaderArchive."Document Type"::Order,"Document No.",
              "Doc. No. Occurrence","Version No.");
            PurchHeaderArchive.SETRANGE("Document Type",PurchHeaderArchive."Document Type"::Order);
            PurchHeaderArchive.SETRANGE("No.","Document No.");
            PurchHeaderArchive.SETRANGE("Doc. No. Occurrence","Doc. No. Occurrence");
            FORM.RUN(FORM::"Purchase Order Archive",PurchHeaderArchive);
          END ELSE BEGIN
            PurchHeader.GET(PurchHeader."Document Type"::Order,"Document No.");
            FORM.RUN(FORM::"Purchase Order",PurchHeader);
          END;
        "Document Type"::"Purch. Inv.":
          BEGIN
            PurchInvHeader.GET("Document No.");
            FORM.RUN(FORM::"Posted Purchase Invoice",PurchInvHeader);
          END;
        "Document Type"::"Purch. Rcpt.":
          BEGIN
            PurchRcptHeader.GET("Document No.");
            FORM.RUN(FORM::"Posted Purchase Receipt",PurchRcptHeader);
          END;
        "Document Type"::"Purch. Cr. Memo":
          BEGIN
            PurchCrMemoHeader.GET("Document No.");
            FORM.RUN(FORM::"Posted Purchase Credit Memo",PurchCrMemoHeader);
          END;
        "Document Type"::"Cover Sheet":
          ERROR(Text004);
        "Document Type"::"Sales Return Order":
          IF SalesHeader.GET(SalesHeader."Document Type"::"Return Order","Document No.") THEN
            FORM.RUN(FORM::"Sales Return Order",SalesHeader)
          ELSE BEGIN
            ReturnRcptHeader.SETRANGE(ReturnRcptHeader."Return Order No.","Document No.");
            FORM.RUN(FORM::"Posted Return Receipt",ReturnRcptHeader);
          END;
        "Document Type"::"Sales Finance Charge Memo":
          BEGIN
            IssuedFinChargeMemoHeader.GET("Document No.");
            FORM.RUN(FORM::"Issued Finance Charge Memo",IssuedFinChargeMemoHeader);
          END;
        "Document Type"::"Sales Return Receipt":
          BEGIN
            ReturnReceiptHeader.GET("Document No.");
            FORM.RUN(FORM::"Posted Return Receipt",ReturnReceiptHeader);
          END;
        "Document Type"::"Purch. Return Shipment":
          BEGIN
            ReturnShipmentHeader.GET("Document No.");
            FORM.RUN(FORM::"Posted Return Shipment",ReturnShipmentHeader);
          END;
        "Document Type"::"Purch. Return Ord. Cnfrmn.":
          IF PurchHeader.GET(PurchHeader."Document Type"::"Return Order","Document No.") THEN
            FORM.RUN(FORM::"Purchase Return Order",PurchHeader)
          ELSE BEGIN
            ReturnShipmentHeader.SETRANGE(ReturnShipmentHeader."Return Order No.","Document No.");
            FORM.RUN(FORM::"Posted Return Shipment",ReturnShipmentHeader);
          END;
        "Document Type"::"Service Contract":
          BEGIN
            ServiceContractHeader.GET(ServiceContractHeader."Contract Type"::Contract,"Document No.");
            FORM.RUN(FORM::"Service Contract",ServiceContractHeader);
          END;
        "Document Type"::"Service Contract Quote":
          BEGIN
            ServiceContractHeader.GET(ServiceContractHeader."Contract Type"::Quote,"Document No.");
            FORM.RUN(FORM::"Service Contract Quote",ServiceContractHeader);
          END;
        "Document Type"::"Service Quote":
          BEGIN
            ServHeader.GET(ServHeader."Document Type"::Quote,"Document No.");
            FORM.RUN(FORM::"Service Quote",ServHeader);
          END;
      END;
    END;

    PROCEDURE EvaluateInteraction@9();
    VAR
      Selected@1000 : Integer;
    BEGIN
      IF FIND('-') THEN BEGIN
        Selected := DIALOG.STRMENU(Text011);
        IF Selected <> 0 THEN
          REPEAT
            Evaluation := Selected;
            MODIFY;
          UNTIL NEXT = 0
      END;
    END;

    PROCEDURE ResumeInteraction@11();
    VAR
      SegLine@1000 : TEMPORARY Record 5077;
      Cont@1006 : Record 5050;
      Salesperson@1005 : Record 13;
      Campaign@1004 : Record 5071;
      Todo@1003 : Record 5080;
      Opportunity@1002 : Record 5092;
      SegManagement@1001 : Codeunit 5051;
    BEGIN
      SegLine.DELETEALL;
      SegLine.INIT;
      SegManagement.CopyFieldsFromInteractLogEntry(SegLine,Rec);
      SegLine.VALIDATE(Date,WORKDATE);

      IF Todo.GET(GETFILTER("To-do No.")) THEN
        SegLine.SETRANGE("To-do No.",Todo."No.");
      IF Cont.GET(GETFILTER("Contact Company No.")) THEN
        SegLine.SETRANGE("Contact Company No.",Cont."No.");
      IF Cont.GET(GETFILTER("Contact No.")) THEN
        SegLine.SETRANGE("Contact No.",Cont."No.");
      IF Salesperson.GET(GETFILTER("Salesperson Code")) THEN
        SegLine.SETRANGE("Salesperson Code",Salesperson.Code);
      IF Campaign.GET(GETFILTER("Campaign No.")) THEN
        SegLine.SETRANGE("Campaign No.",Campaign."No.");
      IF Opportunity.GET(GETFILTER("Opportunity No.")) THEN
        SegLine.SETRANGE("Opportunity No.",Opportunity."No.");

      SegLine.StartWizard;
    END;

    BEGIN
    END.
  }
}

