OBJECT Table 5307 Outlook Synch. Option Correl.
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Outlook Value");
               TESTFIELD("Field Value");

               CheckDuplicatedRecords;
             END;

    OnModify=BEGIN
               CheckDuplicatedRecords;
             END;

    CaptionML=[DEU=Outlook-Synch.-Optionskorrel.;
               ENU=Outlook Synch. Option Correl.];
  }
  FIELDS
  {
    { 1   ;   ;Synch. Entity Code  ;Code10        ;TableRelation="Outlook Synch. Entity".Code;
                                                   OnValidate=BEGIN
                                                                SetDefaults;
                                                              END;

                                                   CaptionML=[DEU=Synch.-Einheitscode;
                                                              ENU=Synch. Entity Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Element No.         ;Integer       ;CaptionML=[DEU=Elementnr.;
                                                              ENU=Element No.] }
    { 3   ;   ;Field Line No.      ;Integer       ;CaptionML=[DEU=Feldzeilennr.;
                                                              ENU=Field Line No.] }
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=[DEU=Zeilennr.;
                                                              ENU=Line No.] }
    { 5   ;   ;Outlook Object      ;Text80        ;CaptionML=[DEU=Outlook-Objekt;
                                                              ENU=Outlook Object] }
    { 6   ;   ;Outlook Property    ;Text80        ;CaptionML=[DEU=Outlook-Eigenschaft;
                                                              ENU=Outlook Property] }
    { 7   ;   ;Outlook Value       ;Text80        ;OnValidate=VAR
                                                                IntVar@1000 : Integer;
                                                              BEGIN
                                                                OSynchField.RESET;
                                                                OSynchField.GET("Synch. Entity Code","Element No.","Field Line No.");

                                                                IF OSynchSetupMgt.CheckOEnumeration(OSynchField) THEN BEGIN
                                                                  IF "Element No." = 0 THEN
                                                                    OSynchSetupMgt.ValidateEnumerationValue(
                                                                      "Outlook Value",
                                                                      "Enumeration No.",
                                                                      OSynchField."Outlook Object",
                                                                      '',
                                                                      OSynchField."Outlook Property")
                                                                  ELSE BEGIN
                                                                    OSynchEntity.GET("Synch. Entity Code");
                                                                    OSynchSetupMgt.ValidateEnumerationValue(
                                                                      "Outlook Value",
                                                                      "Enumeration No.",
                                                                      OSynchEntity."Outlook Item",
                                                                      OSynchField."Outlook Object",
                                                                      OSynchField."Outlook Property");
                                                                  END;
                                                                END ELSE BEGIN
                                                                  IF NOT EVALUATE(IntVar,"Outlook Value") THEN
                                                                    ERROR(Text002);

                                                                  "Enumeration No." := IntVar;
                                                                END;
                                                              END;

                                                   OnLookup=VAR
                                                              OutlookValue@1000 : Text[80];
                                                              EnumerationNo@1001 : Integer;
                                                            BEGIN
                                                              OSynchField.RESET;
                                                              OSynchField.GET("Synch. Entity Code","Element No.","Field Line No.");
                                                              IF NOT OSynchSetupMgt.CheckOEnumeration(OSynchField) THEN
                                                                ERROR(Text003);

                                                              IF "Element No." = 0 THEN
                                                                OutlookValue := OSynchSetupMgt.ShowEnumerationsLookup("Outlook Object",'',"Outlook Property",EnumerationNo)
                                                              ELSE BEGIN
                                                                OSynchEntity.GET("Synch. Entity Code");
                                                                OutlookValue :=
                                                                  OSynchSetupMgt.ShowEnumerationsLookup(
                                                                    OSynchEntity."Outlook Item",
                                                                    "Outlook Object",
                                                                    "Outlook Property",
                                                                    EnumerationNo);
                                                              END;

                                                              IF OutlookValue = '' THEN
                                                                EXIT;

                                                              "Outlook Value" := OutlookValue;
                                                              "Enumeration No." := EnumerationNo;
                                                            END;

                                                   CaptionML=[DEU=Outlook-Wert;
                                                              ENU=Outlook Value] }
    { 8   ;   ;Table No.           ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[DEU=Tabellennr.;
                                                              ENU=Table No.] }
    { 9   ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Table No.));
                                                   CaptionML=[DEU=Feldnr.;
                                                              ENU=Field No.] }
    { 10  ;   ;Field Value         ;Text80        ;OnLookup=VAR
                                                              LookupRecRef@1000 : RecordRef;
                                                              LookupFieldRef@1001 : FieldRef;
                                                              OptionID@1002 : Integer;
                                                            BEGIN
                                                              LookupRecRef.OPEN("Table No.",TRUE);
                                                              LookupFieldRef := LookupRecRef.FIELD("Field No.");

                                                              OptionID := OSynchSetupMgt.ShowOptionsLookup(LookupFieldRef.OPTIONCAPTION);

                                                              IF OptionID <> 0 THEN
                                                                VALIDATE("Option No.",OptionID - 1);

                                                              LookupRecRef.CLOSE;
                                                            END;

                                                   CaptionML=[DEU=Feldwert;
                                                              ENU=Field Value];
                                                   Editable=No }
    { 11  ;   ;Option No.          ;Integer       ;OnValidate=VAR
                                                                LookupRecRef@1000 : RecordRef;
                                                                LookupFieldRef@1001 : FieldRef;
                                                              BEGIN
                                                                LookupRecRef.OPEN("Table No.",TRUE);
                                                                LookupFieldRef := LookupRecRef.FIELD("Field No.");
                                                                "Field Value" := SELECTSTR("Option No." + 1,LookupFieldRef.OPTIONCAPTION);
                                                                LookupRecRef.CLOSE;
                                                              END;

                                                   CaptionML=[DEU=Optionsnr.;
                                                              ENU=Option No.];
                                                   Editable=No }
    { 12  ;   ;Enumeration No.     ;Integer       ;CaptionML=[DEU=AufzÑhlungsnr.;
                                                              ENU=Enumeration No.];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Synch. Entity Code,Element No.,Field Line No.,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      OSynchEntity@1000 : Record 5300;
      OSynchEntityElement@1001 : Record 5301;
      OSynchField@1002 : Record 5304;
      OSynchSetupMgt@1003 : Codeunit 5300;
      Text001@1006 : TextConst 'DEU=Die Zeile, die Sie zu erstellen versuchen, ist bereits vorhanden.;ENU=The line you are trying to create already exists.';
      Text002@1007 : TextConst 'DEU=Dieser Wert ist ungÅltig. Er muss entweder eine ganze Zahl oder ein AufzÑhlungselement sein.;ENU=This value is not valid. It must be either an integer or an enumeration element.';
      Text003@1008 : TextConst 'DEU=Die Lookupfenstertabelle kann nicht geîffnet werden, weil diese Outlook-Eigenschaft nicht dem AufzÑhlungstyp angehîrt.;ENU=The look up window table cannot be opened because this Outlook property is not of the enumeration type.';

    PROCEDURE SetDefaults@1();
    BEGIN
      IF "Element No." = 0 THEN BEGIN
        OSynchEntity.GET("Synch. Entity Code");
        "Outlook Object" := OSynchEntity."Outlook Item";
      END ELSE BEGIN
        OSynchEntityElement.GET("Synch. Entity Code","Element No.");
        "Outlook Object" := OSynchEntityElement."Outlook Collection";
      END;

      OSynchField.GET("Synch. Entity Code","Element No.","Field Line No.");
      "Outlook Property" := OSynchField."Outlook Property";
      "Field No." := OSynchField."Field No.";

      IF OSynchField."Table No." = 0 THEN
        "Table No." := OSynchField."Master Table No."
      ELSE
        "Table No." := OSynchField."Table No.";
    END;

    LOCAL PROCEDURE CheckDuplicatedRecords@2();
    VAR
      OSynchOptionCorrel@1000 : Record 5307;
    BEGIN
      OSynchOptionCorrel.RESET;
      OSynchOptionCorrel.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      OSynchOptionCorrel.SETRANGE("Element No.","Element No.");
      OSynchOptionCorrel.SETRANGE("Field Line No.","Field Line No.");
      OSynchOptionCorrel.SETFILTER("Line No.",'<>%1',"Line No.");
      OSynchOptionCorrel.SETRANGE("Option No.","Option No.");
      OSynchOptionCorrel.SETRANGE("Enumeration No.","Enumeration No.");
      IF OSynchOptionCorrel.FIND('-') THEN
        ERROR(Text001);
    END;

    BEGIN
    END.
  }
}

