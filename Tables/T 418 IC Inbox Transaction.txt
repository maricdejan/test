OBJECT Table 418 IC Inbox Transaction
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ICInboxJnlLine@1000 : Record 419;
               ICInboxPurchHdr@1001 : Record 436;
               ICInboxSalesHdr@1002 : Record 434;
             BEGIN
               CASE "Source Type" OF
                 "Source Type"::Journal:
                   BEGIN
                     ICInboxJnlLine.SETRANGE("Transaction No.","Transaction No.");
                     ICInboxJnlLine.SETRANGE("IC Partner Code","IC Partner Code");
                     ICInboxJnlLine.SETRANGE("Transaction Source","Transaction Source");
                     IF ICInboxJnlLine.FIND('-') THEN
                       ICInboxJnlLine.DELETEALL(TRUE);
                   END;
                 "Source Type"::"Sales Document":
                   BEGIN
                     ICInboxSalesHdr.SETRANGE("IC Transaction No.","Transaction No.");
                     ICInboxSalesHdr.SETRANGE("IC Partner Code","IC Partner Code");
                     ICInboxSalesHdr.SETRANGE("Transaction Source","Transaction Source");
                     IF ICInboxSalesHdr.FIND('-') THEN
                       ICInboxSalesHdr.DELETE(TRUE);
                   END;
                 "Source Type"::"Purchase Document":
                   BEGIN
                     ICInboxPurchHdr.SETRANGE("IC Transaction No.","Transaction No.");
                     ICInboxPurchHdr.SETRANGE("IC Partner Code","IC Partner Code");
                     ICInboxPurchHdr.SETRANGE("Transaction Source","Transaction Source");
                     IF ICInboxPurchHdr.FIND('-') THEN
                       ICInboxPurchHdr.DELETE(TRUE);
                   END;
               END;
             END;

    CaptionML=[DEU=IC-Eingangstransaktion;
               ENU=IC Inbox Transaction];
  }
  FIELDS
  {
    { 1   ;   ;Transaction No.     ;Integer       ;CaptionML=[DEU=Transaktionsnr.;
                                                              ENU=Transaction No.];
                                                   Editable=No }
    { 2   ;   ;IC Partner Code     ;Code20        ;TableRelation="IC Partner";
                                                   CaptionML=[DEU=IC-Partnercode;
                                                              ENU=IC Partner Code];
                                                   Editable=No }
    { 3   ;   ;Source Type         ;Option        ;CaptionML=[DEU=Herkunftsart;
                                                              ENU=Source Type];
                                                   OptionCaptionML=[DEU=Buch.-Blatt,Verkaufsbeleg,Einkaufsbeleg;
                                                                    ENU=Journal,Sales Document,Purchase Document];
                                                   OptionString=Journal,Sales Document,Purchase Document;
                                                   Editable=No }
    { 5   ;   ;Document Type       ;Option        ;CaptionML=[DEU=Belegart;
                                                              ENU=Document Type];
                                                   OptionCaptionML=[DEU=" ,Zahlung,Rechnung,Gutschrift,Erstattung,Auftrag,Reklamation";
                                                                    ENU=" ,Payment,Invoice,Credit Memo,Refund,Order,Return Order"];
                                                   OptionString=[ ,Payment,Invoice,Credit Memo,Refund,Order,Return Order];
                                                   Editable=No }
    { 6   ;   ;Document No.        ;Code20        ;CaptionML=[DEU=Belegnr.;
                                                              ENU=Document No.];
                                                   Editable=No }
    { 7   ;   ;Posting Date        ;Date          ;CaptionML=[DEU=Buchungsdatum;
                                                              ENU=Posting Date];
                                                   Editable=No }
    { 8   ;   ;Transaction Source  ;Option        ;CaptionML=[DEU=Transaktionsursprung;
                                                              ENU=Transaction Source];
                                                   OptionCaptionML=[DEU=ZurÅckgesendet durch Partner,Erstellt durch Partner;
                                                                    ENU=Returned by Partner,Created by Partner];
                                                   OptionString=Returned by Partner,Created by Partner;
                                                   Editable=No }
    { 9   ;   ;Document Date       ;Date          ;CaptionML=[DEU=Belegdatum;
                                                              ENU=Document Date];
                                                   Editable=No }
    { 10  ;   ;Line Action         ;Option        ;OnValidate=BEGIN
                                                                IF (("Line Action" = "Line Action"::"Return to IC Partner") OR ("Line Action" = "Line Action"::Accept))  AND
                                                                   ("Transaction Source" = "Transaction Source"::"Returned by Partner")
                                                                THEN
                                                                  ERROR(Text000,"Transaction No.","IC Partner Code");

                                                                IF "Line Action" = "Line Action"::Accept THEN
                                                                  InboxCheckAccept;
                                                              END;

                                                   CaptionML=[DEU=Zeilenaktion;
                                                              ENU=Line Action];
                                                   OptionCaptionML=[DEU=Keine Aktion,Akzeptieren,An IC-Partner zurÅck,Stornieren;
                                                                    ENU=No Action,Accept,Return to IC Partner,Cancel];
                                                   OptionString=No Action,Accept,Return to IC Partner,Cancel }
    { 11  ;   ;Original Document No.;Code20       ;CaptionML=[DEU=Originalbelegnr.;
                                                              ENU=Original Document No.] }
  }
  KEYS
  {
    {    ;Transaction No.,IC Partner Code,Transaction Source,Document Type;
                                                   Clustered=Yes }
    {    ;IC Partner Code                          }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Transaktionsnr. %1 wurde vom IC-Partner %2 zurÅckgesendet.\Sie kînnen nur zurÅckgesendete Transaktionen stornieren.;ENU=Transaction No. %1 has been returned by IC Partner %2.\You can only cancel returned transactions.';

    PROCEDURE ShowDetails@1();
    VAR
      ICInBoxJnlLine@1005 : Record 419;
      ICInboxSalesHeader@1004 : Record 434;
      ICInboxPurchHeader@1003 : Record 436;
      FrmICInBoxJnlLine@1002 : Form 616;
      FrmICInboxSalesHeader@1001 : Form 644;
      FrmICInboxPurchHeader@1000 : Form 646;
    BEGIN
      CASE "Source Type" OF
        "Source Type"::Journal:
          BEGIN
            ICInBoxJnlLine.SETRANGE("Transaction No.","Transaction No.");
            ICInBoxJnlLine.SETRANGE("IC Partner Code","IC Partner Code");
            ICInBoxJnlLine.SETRANGE("Transaction Source","Transaction Source");
            FrmICInBoxJnlLine.SETTABLEVIEW(ICInBoxJnlLine);
            FrmICInBoxJnlLine.RUNMODAL;
          END;
        "Source Type"::"Sales Document":
          BEGIN
            ICInboxSalesHeader.SETRANGE("IC Transaction No.","Transaction No.");
            ICInboxSalesHeader.SETRANGE("IC Partner Code","IC Partner Code");
            ICInboxSalesHeader.SETRANGE("Transaction Source","Transaction Source");
            FrmICInboxSalesHeader.SETTABLEVIEW(ICInboxSalesHeader);
            FrmICInboxSalesHeader.RUNMODAL;
          END;
        "Source Type"::"Purchase Document":
          BEGIN
            ICInboxPurchHeader.SETRANGE("IC Partner Code","IC Partner Code");
            ICInboxPurchHeader.SETRANGE("IC Transaction No.","Transaction No.");
            ICInboxPurchHeader.SETRANGE("Transaction Source","Transaction Source");
            FrmICInboxPurchHeader.SETTABLEVIEW(ICInboxPurchHeader);
            FrmICInboxPurchHeader.RUNMODAL;
          END;
      END;
    END;

    PROCEDURE InboxCheckAccept@40();
    VAR
      ICInboxTransaction2@1001 : Record 418;
      HandledICInboxTrans@1002 : Record 420;
      Text001@1003 : TextConst 'DEU=Transaktion Nr. %2 ist eine Kopie von Transaktion Nr. %1, die bereits an ''Akzeptieren'' gesendet wurde.\Mîchten Sie Transaktion Nr. %2 ebenfalls akzeptieren?;ENU=Transaction No. %2 is a copy of Transaction No. %1, which has already been set to Accept.\Do you also want to accept Transaction No. %2?';
      Text002@1000 : TextConst 'DEU=Eine Kopie von Transaktion Nr. %1 wurde bereits akzeptiert und befindet sich jetzt im Fenster ''Bearbeitete IC-Eingangstrans.''.\Mîchten Sie Transaktion Nr. %1 ebenfalls akzeptieren?;ENU=A copy of Transaction No. %1 has already been accepted and is now in the Handled IC Inbox Transactions window.\Do you also want to accept Transaction No. %1?';
      ICInboxPurchHeader@1004 : Record 436;
      PurchHeader@1005 : Record 38;
      PurchInvHeader@1006 : Record 122;
      Text003@1007 : TextConst 'DEU=FÅr Transaktion %1 ist bereits eine Bestellung vorhanden. Wenn Sie diesen Beleg annehmen und buchen, sollten Sie die ursprÅngliche Bestellung %2 lîschen, um doppelte Buchungen zu vermeiden.;ENU=A purchase order already exists for transaction %1. If you accept and post this document, you should delete the original purchase order %2 to avoid duplicate postings.';
      Text004@1008 : TextConst 'DEU=Die Einkaufsrechnung %1 wurde bereits fÅr Transaktion %2 gebucht. Wenn Sie diesen Beleg akzeptieren und buchen, liegt eine doppelte Buchung vor.\Mîchten Sie die Transaktion wirklich annehmen?;ENU=Purchase invoice %1 has already been posted for transaction %2. If you accept and post this document, you will have duplicate postings.\Are you sure you want to accept the transaction?';
    BEGIN
      HandledICInboxTrans.SETRANGE("IC Partner Code","IC Partner Code");
      HandledICInboxTrans.SETRANGE("Document Type","Document Type");
      HandledICInboxTrans.SETRANGE("Source Type","Source Type");
      HandledICInboxTrans.SETRANGE("Document No.","Document No.");
      IF HandledICInboxTrans.FINDFIRST THEN
        IF NOT CONFIRM(STRSUBSTNO(Text002,"Transaction No."),FALSE) THEN
          ERROR('');

      ICInboxTransaction2.SETRANGE("IC Partner Code","IC Partner Code");
      ICInboxTransaction2.SETRANGE("Document Type","Document Type");
      ICInboxTransaction2.SETRANGE("Source Type","Source Type");
      ICInboxTransaction2.SETRANGE("Document No.","Document No.");
      ICInboxTransaction2.SETFILTER("Transaction No.",'<>%1',"Transaction No.");
      ICInboxTransaction2.SETRANGE("Line Action","Line Action"::Accept);
      IF ICInboxTransaction2.FINDFIRST THEN
        IF NOT CONFIRM(STRSUBSTNO(Text001,ICInboxTransaction2."Transaction No.","Transaction No."),FALSE) THEN
          ERROR('');
      IF ("Source Type" = "Source Type":: "Purchase Document") AND
         ("Document Type" = "Document Type"::Invoice)
      THEN BEGIN
        ICInboxPurchHeader.GET("Transaction No.","IC Partner Code","Transaction Source");
        IF ICInboxPurchHeader."Your Reference" <> '' THEN BEGIN
          PurchHeader.SETRANGE("No.",ICInboxPurchHeader."Your Reference");
          IF PurchHeader.FINDFIRST THEN
            MESSAGE(Text003,ICInboxPurchHeader."IC Transaction No.",ICInboxPurchHeader."Your Reference")
          ELSE BEGIN
            PurchInvHeader.SETCURRENTKEY("Order No.");
            PurchInvHeader.SETRANGE("Order No.",ICInboxPurchHeader."Your Reference");
            IF PurchInvHeader.FINDFIRST THEN
              IF NOT CONFIRM(Text004,TRUE,ICInboxPurchHeader."Your Reference",ICInboxPurchHeader."IC Transaction No.") THEN
                "Line Action" := xRec."Line Action";
          END;
        END;
      END;
    END;

    BEGIN
    END.
  }
}

