OBJECT Table 221 Gen. Jnl. Allocation
{
  OBJECT-PROPERTIES
  {
    Date=14.08.09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               LOCKTABLE;
               GenJnlLine.GET("Journal Template Name","Journal Batch Name","Journal Line No.");

               ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
               ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");

               DimMgt.InsertJnlLineDim(
                 DATABASE::"Gen. Jnl. Allocation",
                 "Journal Template Name","Journal Batch Name","Journal Line No.","Line No.",
                 "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
             END;

    OnDelete=BEGIN
               VALIDATE(Amount,0);

               DimMgt.DeleteJnlLineDim(
                 DATABASE::"Gen. Jnl. Allocation",
                 "Journal Template Name","Journal Batch Name","Journal Line No.","Line No.");
             END;

    CaptionML=[DEU=Fibu Buch.-Blatt Verteilung;
               ENU=Gen. Jnl. Allocation];
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Gen. Journal Template";
                                                   CaptionML=[DEU=Buch.-Blattvorlagenname;
                                                              ENU=Journal Template Name] }
    { 2   ;   ;Journal Batch Name  ;Code10        ;TableRelation="Gen. Journal Batch".Name WHERE (Journal Template Name=FIELD(Journal Template Name));
                                                   CaptionML=[DEU=Buch.-Blattname;
                                                              ENU=Journal Batch Name] }
    { 3   ;   ;Journal Line No.    ;Integer       ;TableRelation="Gen. Journal Line"."Line No." WHERE (Journal Template Name=FIELD(Journal Template Name),
                                                                                                       Journal Batch Name=FIELD(Journal Batch Name));
                                                   CaptionML=[DEU=Buch.-Blattzeilennr.;
                                                              ENU=Journal Line No.] }
    { 4   ;   ;Line No.            ;Integer       ;CaptionML=[DEU=Zeilennr.;
                                                              ENU=Line No.] }
    { 5   ;   ;Account No.         ;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                IF "Account No." = '' THEN BEGIN
                                                                  GLAcc.INIT;
                                                                  CreateDim(DATABASE::"G/L Account","Account No.");
                                                                END ELSE BEGIN
                                                                  GLAcc.GET("Account No.");
                                                                  GLAcc.CheckGLAcc;
                                                                  GLAcc.TESTFIELD("Direct Posting",TRUE);
                                                                END;
                                                                "Account Name" := GLAcc.Name;
                                                                "Gen. Posting Type" := GLAcc."Gen. Posting Type";
                                                                "Gen. Bus. Posting Group" := GLAcc."Gen. Bus. Posting Group";
                                                                "Gen. Prod. Posting Group" := GLAcc."Gen. Prod. Posting Group";
                                                                "VAT Bus. Posting Group" := GLAcc."VAT Bus. Posting Group";
                                                                "VAT Prod. Posting Group" := GLAcc."VAT Prod. Posting Group";
                                                                VALIDATE("VAT Prod. Posting Group");

                                                                CreateDim(DATABASE::"G/L Account","Account No.");
                                                              END;

                                                   CaptionML=[DEU=Kontonr.;
                                                              ENU=Account No.] }
    { 6   ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[DEU=Shortcutdimensionscode 1;
                                                              ENU=Shortcut Dimension 1 Code];
                                                   CaptionClass='1,2,1' }
    { 7   ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[DEU=Shortcutdimensionscode 2;
                                                              ENU=Shortcut Dimension 2 Code];
                                                   CaptionClass='1,2,2' }
    { 8   ;   ;Allocation Quantity ;Decimal       ;OnValidate=BEGIN
                                                                IF "Allocation Quantity" = 0 THEN BEGIN
                                                                  "Allocation %" := 0;
                                                                  Amount := 0;
                                                                END;
                                                                UpdateAllocations(GenJnlLine);
                                                              END;

                                                   CaptionML=[DEU=Anzahl Verteilungen;
                                                              ENU=Allocation Quantity];
                                                   DecimalPlaces=0:5 }
    { 9   ;   ;Allocation %        ;Decimal       ;OnValidate=BEGIN
                                                                "Allocation Quantity" := 0;
                                                                IF "Allocation %" = 0 THEN
                                                                  Amount := 0;
                                                                UpdateAllocations(GenJnlLine);
                                                              END;

                                                   CaptionML=[DEU=Verteilung %;
                                                              ENU=Allocation %];
                                                   DecimalPlaces=2:2 }
    { 10  ;   ;Amount              ;Decimal       ;OnValidate=BEGIN
                                                                IF ("Allocation Quantity" <> 0) OR ("Allocation %" <> 0) THEN BEGIN
                                                                  "Allocation Quantity" := 0;
                                                                  "Allocation %" := 0;
                                                                  UpdateAllocations(GenJnlLine);
                                                                END ELSE BEGIN
                                                                  VALIDATE("VAT Prod. Posting Group");
                                                                  MODIFY;
                                                                  UpdateJnlBalance(GenJnlLine);
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Betrag;
                                                              ENU=Amount];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 11  ;   ;Gen. Posting Type   ;Option        ;OnValidate=BEGIN
                                                                VALIDATE("VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[DEU=Buchungsart;
                                                              ENU=Gen. Posting Type];
                                                   OptionCaptionML=[DEU=" ,Einkauf,Verkauf";
                                                                    ENU=" ,Purchase,Sale"];
                                                   OptionString=[ ,Purchase,Sale] }
    { 12  ;   ;Gen. Bus. Posting Group;Code10     ;TableRelation="Gen. Business Posting Group";
                                                   OnValidate=BEGIN
                                                                IF xRec."Gen. Bus. Posting Group" <> "Gen. Bus. Posting Group" THEN
                                                                  IF GenBusPostingGrp.ValidateVatBusPostingGroup(GenBusPostingGrp,"Gen. Bus. Posting Group") THEN
                                                                    VALIDATE("VAT Bus. Posting Group",GenBusPostingGrp."Def. VAT Bus. Posting Group");
                                                              END;

                                                   CaptionML=[DEU=Gesch„ftsbuchungsgruppe;
                                                              ENU=Gen. Bus. Posting Group] }
    { 13  ;   ;Gen. Prod. Posting Group;Code10    ;TableRelation="Gen. Product Posting Group";
                                                   OnValidate=BEGIN
                                                                IF xRec."Gen. Prod. Posting Group" <> "Gen. Prod. Posting Group" THEN
                                                                  IF GenProdPostingGrp.ValidateVatProdPostingGroup(GenProdPostingGrp,"Gen. Prod. Posting Group") THEN
                                                                    VALIDATE("VAT Prod. Posting Group",GenProdPostingGrp."Def. VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[DEU=Produktbuchungsgruppe;
                                                              ENU=Gen. Prod. Posting Group] }
    { 14  ;   ;VAT Calculation Type;Option        ;CaptionML=[DEU=MwSt.-Berechnungsart;
                                                              ENU=VAT Calculation Type];
                                                   OptionCaptionML=[DEU=Normale MwSt.,Erwerbsbesteuerung,Nur MwSt.,Verkaufssteuer;
                                                                    ENU=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax];
                                                   OptionString=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                   Editable=No }
    { 15  ;   ;VAT Amount          ;Decimal       ;CaptionML=[DEU=MwSt.-Betrag;
                                                              ENU=VAT Amount];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 16  ;   ;VAT %               ;Decimal       ;CaptionML=[DEU=MwSt. %;
                                                              ENU=VAT %];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   Editable=No }
    { 17  ;   ;Account Name        ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("G/L Account".Name WHERE (No.=FIELD(Account No.)));
                                                   CaptionML=[DEU=Kontoname;
                                                              ENU=Account Name];
                                                   Editable=No }
    { 18  ;   ;Tax Area Code       ;Code20        ;TableRelation="Tax Area";
                                                   OnValidate=BEGIN
                                                                VALIDATE("VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[DEU=Steuergebietscode;
                                                              ENU=Tax Area Code] }
    { 19  ;   ;Tax Liable          ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE("VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[DEU=Steuerpflichtig;
                                                              ENU=Tax Liable] }
    { 20  ;   ;Tax Group Code      ;Code10        ;TableRelation="Tax Group";
                                                   OnValidate=BEGIN
                                                                VALIDATE("VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[DEU=Steuergruppencode;
                                                              ENU=Tax Group Code] }
    { 21  ;   ;Use Tax             ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE("VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[DEU=Verbrauchssteuer;
                                                              ENU=Use Tax] }
    { 22  ;   ;VAT Bus. Posting Group;Code10      ;TableRelation="VAT Business Posting Group";
                                                   OnValidate=BEGIN
                                                                VALIDATE("VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[DEU=MwSt.-Gesch„ftsbuchungsgruppe;
                                                              ENU=VAT Bus. Posting Group] }
    { 23  ;   ;VAT Prod. Posting Group;Code10     ;TableRelation="VAT Product Posting Group";
                                                   OnValidate=BEGIN
                                                                GenJnlLine.GET("Journal Template Name","Journal Batch Name","Journal Line No.");
                                                                CheckVAT(GenJnlLine);
                                                                UpdateVAT(GenJnlLine);
                                                              END;

                                                   CaptionML=[DEU=MwSt.-Produktbuchungsgruppe;
                                                              ENU=VAT Prod. Posting Group] }
    { 24  ;   ;Additional-Currency Amount;Decimal ;CaptionML=[DEU=Betrag (BW);
                                                              ENU=Additional-Currency Amount];
                                                   Editable=No;
                                                   AutoFormatType=1 }
  }
  KEYS
  {
    {    ;Journal Template Name,Journal Batch Name,Journal Line No.,Line No.;
                                                   SumIndexFields=Amount;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=%1 kann in der Verteilung nicht verwendet werden, wenn diese in der Fibu Buch.-Blattzeile abgeschlossen sind.;ENU=%1 cannot be used in allocations when they are completed on the general journal line.';
      GLAcc@1001 : Record 15;
      GenJnlLine@1002 : Record 81;
      GenBusPostingGrp@1003 : Record 250;
      GenProdPostingGrp@1004 : Record 251;
      DimMgt@1005 : Codeunit 408;

    PROCEDURE UpdateAllocations@2(VAR GenJnlLine@1000 : Record 81);
    VAR
      GenJnlAlloc@1001 : Record 221;
      GenJnlAlloc2@1002 : Record 221;
      FromAllocations@1003 : Boolean;
      TotalQty@1004 : Decimal;
      TotalPct@1005 : Decimal;
      TotalPctRnded@1006 : Decimal;
      TotalAmountLCY@1007 : Decimal;
      TotalAmountLCY2@1008 : Decimal;
      TotalAmountLCYRnded@1009 : Decimal;
      TotalAmountLCYRnded2@1010 : Decimal;
      UpdateGenJnlLine@1011 : Boolean;
    BEGIN
      IF "Line No." <> 0 THEN BEGIN
        FromAllocations := TRUE;
        GenJnlAlloc.UpdateVAT(GenJnlLine);
        MODIFY;
        GenJnlLine.GET("Journal Template Name","Journal Batch Name","Journal Line No.");
        CheckVAT(GenJnlLine);
      END;

      GenJnlAlloc.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
      GenJnlAlloc.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
      GenJnlAlloc.SETRANGE("Journal Line No.",GenJnlLine."Line No.");
      IF FromAllocations THEN
        UpdateGenJnlLine := TRUE
      ELSE
        IF NOT GenJnlAlloc.ISEMPTY THEN BEGIN
          GenJnlAlloc.LOCKTABLE;
          UpdateGenJnlLine := TRUE;
        END;
      IF GenJnlAlloc.FINDSET THEN
        REPEAT
          IF (GenJnlAlloc."Allocation Quantity" <> 0) OR (GenJnlAlloc."Allocation %" <> 0) THEN BEGIN
            IF NOT FromAllocations THEN
              GenJnlAlloc.CheckVAT(GenJnlLine);
            IF GenJnlAlloc."Allocation Quantity" = 0 THEN BEGIN
              TotalAmountLCY2 := TotalAmountLCY2 - GenJnlLine."Amount (LCY)" * GenJnlAlloc."Allocation %" / 100;
              GenJnlAlloc.Amount := ROUND(TotalAmountLCY2) - TotalAmountLCYRnded2;
              TotalAmountLCYRnded2 := TotalAmountLCYRnded2 + GenJnlAlloc.Amount;
            END ELSE BEGIN
              IF TotalQty = 0 THEN BEGIN
                GenJnlAlloc2.COPY(GenJnlAlloc);
                GenJnlAlloc2.SETFILTER("Allocation Quantity",'<>0');
                REPEAT
                  TotalQty := TotalQty + GenJnlAlloc2."Allocation Quantity";
                UNTIL GenJnlAlloc2.NEXT = 0;
                IF TotalQty = 0 THEN
                  TotalQty := 1;
              END;
              TotalPct := TotalPct + GenJnlAlloc."Allocation Quantity" / TotalQty * 100;
              GenJnlAlloc."Allocation %" := ROUND(TotalPct,0.01) - TotalPctRnded;
              TotalPctRnded := TotalPctRnded + GenJnlAlloc."Allocation %";
              TotalAmountLCY := TotalAmountLCY - GenJnlLine."Amount (LCY)" * GenJnlAlloc."Allocation Quantity" / TotalQty;
              GenJnlAlloc.Amount := ROUND(TotalAmountLCY) - TotalAmountLCYRnded;
              TotalAmountLCYRnded := TotalAmountLCYRnded + GenJnlAlloc.Amount;
            END;
            GenJnlAlloc.UpdateVAT(GenJnlLine);
            GenJnlAlloc.MODIFY;
          END;
        UNTIL GenJnlAlloc.NEXT = 0;

      IF UpdateGenJnlLine THEN
        UpdateJnlBalance(GenJnlLine);

      IF FromAllocations THEN
        FIND;
    END;

    PROCEDURE UpdateAllocationsAddCurr@6(VAR GenJnlLine@1000 : Record 81;AddCurrAmount@1001 : Decimal);
    VAR
      GenJnlAlloc@1002 : Record 221;
      GenJnlAlloc2@1003 : Record 221;
      GLSetup@1004 : Record 98;
      Currency@1005 : Record 4;
      TotalQty@1006 : Decimal;
      TotalPct@1007 : Decimal;
      TotalPctRnded@1008 : Decimal;
      TotalAmountAddCurr@1009 : Decimal;
      TotalAmountAddCurr2@1010 : Decimal;
      TotalAmountAddCurrRnded@1011 : Decimal;
      TotalAmountAddCurrRnded2@1012 : Decimal;
    BEGIN
      GenJnlAlloc.SETRANGE("Journal Template Name",GenJnlLine."Journal Template Name");
      GenJnlAlloc.SETRANGE("Journal Batch Name",GenJnlLine."Journal Batch Name");
      GenJnlAlloc.SETRANGE("Journal Line No.",GenJnlLine."Line No.");
      GenJnlAlloc.LOCKTABLE;
      IF GenJnlAlloc.FINDSET THEN BEGIN
        GLSetup.GET;
        Currency.GET(GLSetup."Additional Reporting Currency");
        Currency.TESTFIELD("Amount Rounding Precision");
        REPEAT
          IF (GenJnlAlloc."Allocation Quantity" <> 0) OR (GenJnlAlloc."Allocation %" <> 0) THEN BEGIN
            IF GenJnlAlloc."Allocation Quantity" = 0 THEN BEGIN
              TotalAmountAddCurr2 :=
                TotalAmountAddCurr2 - AddCurrAmount * GenJnlAlloc."Allocation %" / 100;
              GenJnlAlloc."Additional-Currency Amount" :=
                ROUND(TotalAmountAddCurr2,Currency."Amount Rounding Precision") -
                TotalAmountAddCurrRnded2;
              TotalAmountAddCurrRnded2 :=
                TotalAmountAddCurrRnded2 + GenJnlAlloc."Additional-Currency Amount";
            END ELSE BEGIN
              IF TotalQty = 0 THEN BEGIN
                GenJnlAlloc2.COPY(GenJnlAlloc);
                GenJnlAlloc2.SETFILTER("Allocation Quantity",'<>0');
                REPEAT
                  TotalQty := TotalQty + GenJnlAlloc2."Allocation Quantity";
                UNTIL GenJnlAlloc2.NEXT = 0;
                IF TotalQty = 0 THEN
                  TotalQty := 1;
              END;
              TotalPct := TotalPct + GenJnlAlloc."Allocation Quantity" / TotalQty * 100;
              GenJnlAlloc."Allocation %" := ROUND(TotalPct,0.01) - TotalPctRnded;
              TotalPctRnded := TotalPctRnded + GenJnlAlloc."Allocation %";
              TotalAmountAddCurr :=
                TotalAmountAddCurr -
                AddCurrAmount * GenJnlAlloc."Allocation Quantity" / TotalQty;
              GenJnlAlloc."Additional-Currency Amount" :=
                ROUND(TotalAmountAddCurr,Currency."Amount Rounding Precision") -
                TotalAmountAddCurrRnded;
              TotalAmountAddCurrRnded :=
                TotalAmountAddCurrRnded + GenJnlAlloc."Additional-Currency Amount";
            END;
            GenJnlAlloc.MODIFY;
          END;
        UNTIL GenJnlAlloc.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE UpdateJnlBalance@4(VAR GenJnlLine@1000 : Record 81);
    BEGIN
      GenJnlLine.CALCFIELDS("Allocated Amt. (LCY)");
      IF GenJnlLine."Bal. Account No." = '' THEN
        GenJnlLine."Balance (LCY)" := GenJnlLine."Amount (LCY)" + GenJnlLine."Allocated Amt. (LCY)"
      ELSE
        GenJnlLine."Balance (LCY)" := GenJnlLine."Allocated Amt. (LCY)";
      GenJnlLine.MODIFY;
    END;

    PROCEDURE CheckVAT@1(VAR GenJnlLine@1000 : Record 81);
    BEGIN
      IF ("Gen. Posting Type" <> 0) AND (GenJnlLine."Gen. Posting Type" <> 0) THEN
        ERROR(
          Text000,
          GenJnlLine.FIELDCAPTION("Gen. Posting Type"));
    END;

    PROCEDURE UpdateVAT@3(VAR GenJnlLine@1000 : Record 81);
    VAR
      GenJnlLine2@1001 : Record 81;
    BEGIN
      GenJnlLine2."Account No." := "Account No.";
      GenJnlLine2.Amount := Amount;
      GenJnlLine2."Amount (LCY)" := Amount;
      GenJnlLine2."Gen. Posting Type" := "Gen. Posting Type";
      GenJnlLine2."Gen. Bus. Posting Group" := "Gen. Bus. Posting Group";
      GenJnlLine2."Gen. Prod. Posting Group" := "Gen. Prod. Posting Group";
      GenJnlLine2."VAT Bus. Posting Group" := "VAT Bus. Posting Group";
      GenJnlLine2."VAT Prod. Posting Group" := "VAT Prod. Posting Group";
      GenJnlLine2."Tax Area Code" := "Tax Area Code";
      GenJnlLine2."Tax Liable" := "Tax Liable";
      GenJnlLine2."Tax Group Code" := "Tax Group Code";
      GenJnlLine2."Use Tax" := "Use Tax";
      GenJnlLine2."VAT Calculation Type" := "VAT Calculation Type";
      GenJnlLine2."VAT Amount" := "VAT Amount";
      GenJnlLine2."VAT %" := "VAT %";
      GenJnlLine2."Posting Date" := GenJnlLine."Posting Date";
      GenJnlLine2.VALIDATE("VAT Prod. Posting Group");
      Amount := GenJnlLine2."Amount (LCY)";
      "VAT Calculation Type" := GenJnlLine2."VAT Calculation Type";
      "VAT Amount" := GenJnlLine2."VAT Amount";
      "VAT %" := GenJnlLine2."VAT %";
    END;

    PROCEDURE GetCurrencyCode@5() : Code[10];
    VAR
      GenJnlLine3@1000 : Record 81;
    BEGIN
      GenJnlLine3.SETRANGE("Journal Template Name","Journal Template Name");
      GenJnlLine3.SETRANGE("Journal Batch Name","Journal Batch Name");
      GenJnlLine3.SETRANGE("Line No.","Journal Line No.");
      IF GenJnlLine3.FINDFIRST THEN
        EXIT(GenJnlLine3."Currency Code")
      ELSE
        EXIT('');
    END;

    PROCEDURE CreateDim@13(Type1@1000 : Integer;No1@1001 : Code[20]);
    VAR
      TableID@1002 : ARRAY [10] OF Integer;
      No@1003 : ARRAY [10] OF Code[20];
    BEGIN
      TableID[1] := Type1;
      No[1] := No1;
      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
      DimMgt.GetDefaultDim(
        TableID,No,'',
        "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
      IF "Line No." <> 0 THEN
        DimMgt.UpdateJnlLineDefaultDim(
          DATABASE::"Gen. Jnl. Allocation",
          "Journal Template Name","Journal Batch Name","Journal Line No.","Line No.",
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    PROCEDURE ValidateShortcutDimCode@14(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      IF "Line No." <> 0 THEN BEGIN
        DimMgt.SaveJnlLineDim(
          DATABASE::"Gen. Jnl. Allocation",
          "Journal Template Name","Journal Batch Name","Journal Line No.","Line No.",
          FieldNumber,ShortcutDimCode);
        IF MODIFY THEN;
      END ELSE
        DimMgt.SaveTempDim(FieldNumber,ShortcutDimCode);
    END;

    PROCEDURE LookupShortcutDimCode@18(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.LookupDimValueCode(FieldNumber,ShortcutDimCode);
      IF "Line No." <> 0 THEN BEGIN
        DimMgt.SaveJnlLineDim(
          DATABASE::"Gen. Jnl. Allocation","Journal Template Name",
          "Journal Batch Name","Journal Line No.","Line No.",FieldNumber,ShortcutDimCode);
        MODIFY;
      END ELSE
        DimMgt.SaveTempDim(FieldNumber,ShortcutDimCode);
    END;

    PROCEDURE ShowShortcutDimCode@15(VAR ShortcutDimCode@1000 : ARRAY [8] OF Code[20]);
    BEGIN
      IF "Line No." <> 0 THEN
        DimMgt.ShowJnlLineDim(
          DATABASE::"Gen. Jnl. Allocation","Journal Template Name",
          "Journal Batch Name","Journal Line No.","Line No.",ShortcutDimCode)
      ELSE
        DimMgt.ShowTempDim(ShortcutDimCode);
    END;

    BEGIN
    END.
  }
}

