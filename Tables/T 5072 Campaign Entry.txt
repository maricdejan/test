OBJECT Table 5072 Campaign Entry
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Campaign No.;
    OnDelete=VAR
               InteractLogEntry@1000 : Record 5065;
             BEGIN
               InteractLogEntry.SETCURRENTKEY("Campaign No.","Campaign Entry No.");
               InteractLogEntry.SETRANGE("Campaign No.","Campaign No.");
               InteractLogEntry.SETRANGE("Campaign Entry No.","Entry No.");
               InteractLogEntry.DELETEALL;
             END;

    CaptionML=[DEU=Kampagnenposten;
               ENU=Campaign Entry];
    LookupFormID=Form5089;
    DrillDownFormID=Form5089;
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[DEU=Lfd. Nr.;
                                                              ENU=Entry No.] }
    { 2   ;   ;Campaign No.        ;Code20        ;TableRelation=Campaign;
                                                   CaptionML=[DEU=Kampagnennr.;
                                                              ENU=Campaign No.] }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description] }
    { 4   ;   ;Date                ;Date          ;CaptionML=[DEU=Datum;
                                                              ENU=Date] }
    { 5   ;   ;User ID             ;Code20        ;TableRelation=User;
                                                   OnLookup=VAR
                                                              LoginMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              LoginMgt.LookupUserID("User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[DEU=Benutzer-ID;
                                                              ENU=User ID];
                                                   Editable=No }
    { 6   ;   ;Segment No.         ;Code20        ;TableRelation="Segment Header";
                                                   CaptionML=[DEU=Segmentnr.;
                                                              ENU=Segment No.] }
    { 7   ;   ;Canceled            ;Boolean       ;CaptionML=[DEU=Storniert;
                                                              ENU=Canceled];
                                                   BlankZero=Yes }
    { 8   ;   ;No. of Interactions ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Interaction Log Entry" WHERE (Campaign No.=FIELD(Campaign No.),
                                                                                                    Campaign Entry No.=FIELD(Entry No.),
                                                                                                    Canceled=FIELD(Canceled)));
                                                   CaptionML=[DEU=Anzahl Aktivit„ten;
                                                              ENU=No. of Interactions];
                                                   Editable=No }
    { 10  ;   ;Cost (LCY)          ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Interaction Log Entry"."Cost (LCY)" WHERE (Campaign No.=FIELD(Campaign No.),
                                                                                                               Campaign Entry No.=FIELD(Entry No.),
                                                                                                               Canceled=FIELD(Canceled)));
                                                   CaptionML=[DEU=Einstandsbetrag (MW);
                                                              ENU=Cost (LCY)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 11  ;   ;Duration (Min.)     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Interaction Log Entry"."Duration (Min.)" WHERE (Campaign No.=FIELD(Campaign No.),
                                                                                                                    Campaign Entry No.=FIELD(Entry No.),
                                                                                                                    Canceled=FIELD(Canceled)));
                                                   CaptionML=[DEU=Dauer (Min.);
                                                              ENU=Duration (Min.)];
                                                   DecimalPlaces=0:0;
                                                   Editable=No }
    { 12  ;   ;Salesperson Code    ;Code10        ;TableRelation=Salesperson/Purchaser;
                                                   CaptionML=[DEU=Verk„ufercode;
                                                              ENU=Salesperson Code] }
    { 13  ;   ;Register No.        ;Integer       ;TableRelation="Logged Segment";
                                                   CaptionML=[DEU=Journalnr.;
                                                              ENU=Register No.] }
    { 14  ;   ;Document Type       ;Option        ;CaptionML=[DEU=Belegart;
                                                              ENU=Document Type];
                                                   OptionCaptionML=[DEU=" ,Angebot,Rahmenauftrag,Auftragsbest.,VK-Rech.,VK-Liefersch.,VK-Gutschr.,Kontoausz.,Mahnung,Serv.-Auftr. erst.,Serv.-Auftr. buchen,Anfrage,Rahmenbestellung,Bestellung,EK-Rech.,EK-Liefersch.,EK-Gutschr.,Deckblatt";
                                                                    ENU=" ,Sales Qte.,Sales Blnkt. Ord,Sales Ord. Cnfrmn.,Sales Inv.,Sales Shpt. Note,Sales Cr. Memo,Sales Stmnt.,Sales Rmdr.,Serv. Ord. Create,Serv. Ord. Post,Purch.Qte.,Purch. Blnkt. Ord.,Purch. Ord.,Purch. Inv.,Purch. Rcpt.,Purch. Cr. Memo,Cover Sheet"];
                                                   OptionString=[ ,Sales Qte.,Sales Blnkt. Ord,Sales Ord. Cnfrmn.,Sales Inv.,Sales Shpt. Note,Sales Cr. Memo,Sales Stmnt.,Sales Rmdr.,Serv. Ord. Create,Serv. Ord. Post,Purch.Qte.,Purch. Blnkt. Ord.,Purch. Ord.,Purch. Inv.,Purch. Rcpt.,Purch. Cr. Memo,Cover Sheet] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Campaign No.,Date,Document Type          }
    {    ;Register No.                             }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Entry No.,Campaign No.,Description,Date,Document Type }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=%1 %2 ist als %3 gekennzeichnet.\;ENU=%1 %2 is marked %3.\';
      Text001@1001 : TextConst 'DEU=M”chten Sie das Kennzeichen Storniert entfernen?;ENU=Do you wish to remove the checkmark?';
      Text002@1002 : TextConst 'DEU=M”chten Sie den %1 %2 als %3 kennzeichnen?;ENU=Do you wish to mark %1 %2 as %3?';

    PROCEDURE ToggleCanceledCheckmark@2();
    VAR
      MasterCanceledCheckmark@1000 : Boolean;
    BEGIN
      IF ConfirmToggleCanceledCheckmark THEN BEGIN
        MasterCanceledCheckmark := NOT Canceled;
        SetCanceledCheckmark(MasterCanceledCheckmark);
      END;
    END;

    PROCEDURE SetCanceledCheckmark@1(CanceledCheckmark@1001 : Boolean);
    VAR
      InteractLogEntry@1000 : Record 5065;
    BEGIN
      Canceled := CanceledCheckmark;
      MODIFY;

      InteractLogEntry.SETCURRENTKEY("Campaign No.","Campaign Entry No.");
      InteractLogEntry.SETRANGE("Campaign No.","Campaign No.");
      InteractLogEntry.SETRANGE("Campaign Entry No.","Entry No.");
      InteractLogEntry.MODIFYALL(Canceled,Canceled);
    END;

    PROCEDURE ConfirmToggleCanceledCheckmark@3() : Boolean;
    BEGIN
      IF Canceled THEN BEGIN
        EXIT(CONFIRM(
          Text000+
          Text001,TRUE,TABLECAPTION,"Entry No.",FIELDCAPTION(Canceled)));
      END ELSE BEGIN
        EXIT(CONFIRM(
          Text002,TRUE,TABLECAPTION,"Entry No.",FIELDCAPTION(Canceled)));
      END;
    END;

    BEGIN
    END.
  }
}

