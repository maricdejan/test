OBJECT Table 5071 Campaign
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Description;
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 RMSetup.GET;
                 RMSetup.TESTFIELD("Campaign Nos.");
                 NoSeriesMgt.InitSeries(RMSetup."Campaign Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;

               DimMgt.UpdateDefaultDim(
                 DATABASE::Campaign,"No.",
                 "Global Dimension 1 Code","Global Dimension 2 Code");
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;

               IF ("Starting Date" <> xRec."Starting Date") OR
                  ("Ending Date" <> xRec."Ending Date")
               THEN
                 UpdateDates;
             END;

    OnDelete=VAR
               SalesPrice@1001 : Record 7002;
               SalesLineDisc@1000 : Record 7004;
             BEGIN
               DimMgt.DeleteDefaultDim(DATABASE::Campaign,"No.");

               RMCommentLine.SETRANGE("Table Name",RMCommentLine."Table Name"::Campaign);
               RMCommentLine.SETRANGE("No.","No.");
               RMCommentLine.DELETEALL;

               CampaignEntry.SETCURRENTKEY("Campaign No.");
               CampaignEntry.SETRANGE("Campaign No.","No.");
               CampaignEntry.DELETEALL;

               CampaignMgmt.DeactivateCampaign(Rec,FALSE);

               SalesPrice.SETRANGE("Sales Type",SalesPrice."Sales Type"::Campaign);
               SalesPrice.SETRANGE("Sales Code","No.");
               SalesPrice.DELETEALL;

               SalesLineDisc.SETRANGE("Sales Type",SalesLineDisc."Sales Type"::Campaign);
               SalesLineDisc.SETRANGE("Sales Code","No.");
               SalesLineDisc.DELETEALL;
             END;

    OnRename=BEGIN
               "Last Date Modified" := TODAY;
             END;

    CaptionML=[DEU=Kampagne;
               ENU=Campaign];
    LookupFormID=Form5087;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  RMSetup.GET;
                                                                  NoSeriesMgt.TestManual(RMSetup."Campaign Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Nr.;
                                                              ENU=No.] }
    { 2   ;   ;Description         ;Text50        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description] }
    { 3   ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                IF ("Starting Date" > "Ending Date") AND ("Ending Date" > 0D) THEN
                                                                  ERROR(Text000,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"));
                                                              END;

                                                   CaptionML=[DEU=Startdatum;
                                                              ENU=Starting Date] }
    { 4   ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                IF ("Ending Date" < "Starting Date") AND ("Ending Date" > 0D) THEN
                                                                  ERROR(Text001,FIELDCAPTION("Ending Date"),FIELDCAPTION("Starting Date"));
                                                              END;

                                                   CaptionML=[DEU=Enddatum;
                                                              ENU=Ending Date] }
    { 5   ;   ;Salesperson Code    ;Code10        ;TableRelation=Salesperson/Purchaser;
                                                   CaptionML=[DEU=Verk„ufercode;
                                                              ENU=Salesperson Code] }
    { 6   ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Rlshp. Mgt. Comment Line" WHERE (Table Name=CONST(Campaign),
                                                                                                       No.=FIELD(No.)));
                                                   CaptionML=[DEU=Bemerkung;
                                                              ENU=Comment];
                                                   Editable=No }
    { 7   ;   ;Last Date Modified  ;Date          ;CaptionML=[DEU=Korrigiert am;
                                                              ENU=Last Date Modified];
                                                   Editable=No }
    { 8   ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[DEU=Nummernserie;
                                                              ENU=No. Series];
                                                   Editable=No }
    { 9   ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Global Dimension 1 Code");
                                                              END;

                                                   CaptionML=[DEU=Globaler Dimensionscode 1;
                                                              ENU=Global Dimension 1 Code];
                                                   CaptionClass='1,1,1' }
    { 10  ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Global Dimension 2 Code");
                                                              END;

                                                   CaptionML=[DEU=Globaler Dimensionscode 2;
                                                              ENU=Global Dimension 2 Code];
                                                   CaptionClass='1,1,2' }
    { 11  ;   ;Status Code         ;Code10        ;TableRelation="Campaign Status";
                                                   CaptionML=[DEU=Statuscode;
                                                              ENU=Status Code] }
    { 12  ;   ;Target Contacts Contacted;Integer  ;FieldClass=FlowField;
                                                   CalcFormula=Count("Interaction Log Entry" WHERE (Campaign No.=FIELD(No.),
                                                                                                    Campaign Target=CONST(Yes),
                                                                                                    Canceled=CONST(No),
                                                                                                    Date=FIELD(Date Filter),
                                                                                                    Postponed=CONST(No)));
                                                   CaptionML=[DEU=Anzahl Kontaktaufnahmen;
                                                              ENU=Target Contacts Contacted];
                                                   Editable=No }
    { 13  ;   ;Contacts Responded  ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Interaction Log Entry" WHERE (Campaign No.=FIELD(No.),
                                                                                                    Campaign Response=CONST(Yes),
                                                                                                    Canceled=CONST(No),
                                                                                                    Date=FIELD(Date Filter),
                                                                                                    Postponed=CONST(No)));
                                                   CaptionML=[DEU=Anzahl Kontaktantworten;
                                                              ENU=Contacts Responded];
                                                   Editable=No }
    { 14  ;   ;Duration (Min.)     ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Interaction Log Entry"."Duration (Min.)" WHERE (Campaign No.=FIELD(No.),
                                                                                                                    Canceled=CONST(No),
                                                                                                                    Date=FIELD(Date Filter),
                                                                                                                    Postponed=CONST(No)));
                                                   CaptionML=[DEU=Dauer (Min.);
                                                              ENU=Duration (Min.)];
                                                   DecimalPlaces=0:0;
                                                   Editable=No }
    { 16  ;   ;Cost (LCY)          ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Interaction Log Entry"."Cost (LCY)" WHERE (Campaign No.=FIELD(No.),
                                                                                                               Canceled=CONST(No),
                                                                                                               Date=FIELD(Date Filter),
                                                                                                               Postponed=CONST(No)));
                                                   CaptionML=[DEU=Einstandsbetrag (MW);
                                                              ENU=Cost (LCY)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 17  ;   ;No. of Opportunities;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Opportunity Entry" WHERE (Campaign No.=FIELD(No.),
                                                                                                Active=CONST(Yes)));
                                                   CaptionML=[DEU=Anzahl Verkaufschancen;
                                                              ENU=No. of Opportunities];
                                                   Editable=No }
    { 18  ;   ;Estimated Value (LCY);Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Opportunity Entry"."Estimated Value (LCY)" WHERE (Campaign No.=FIELD(No.),
                                                                                                                      Active=CONST(Yes)));
                                                   CaptionML=[DEU=Erwarteter Wert (MW);
                                                              ENU=Estimated Value (LCY)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 19  ;   ;Calcd. Current Value (LCY);Decimal ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Opportunity Entry"."Calcd. Current Value (LCY)" WHERE (Campaign No.=FIELD(No.),
                                                                                                                           Active=CONST(Yes)));
                                                   CaptionML=[DEU=Berech. aktueller Wert (MW);
                                                              ENU=Calcd. Current Value (LCY)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 20  ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[DEU=Datumsfilter;
                                                              ENU=Date Filter] }
    { 23  ;   ;Action Taken Filter ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=[DEU=Kriterienaktionsfilter;
                                                              ENU=Action Taken Filter];
                                                   OptionCaptionML=[DEU=" ,N„chster Schritt,Vorheriger Schritt,Aktualisiert,Schritt bersprungen,Gewonnen,Verloren";
                                                                    ENU=" ,Next,Previous,Updated,Jumped,Won,Lost"];
                                                   OptionString=[ ,Next,Previous,Updated,Jumped,Won,Lost] }
    { 24  ;   ;Sales Cycle Filter  ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation="Sales Cycle";
                                                   CaptionML=[DEU=Verkaufsprozessfilter;
                                                              ENU=Sales Cycle Filter] }
    { 25  ;   ;Sales Cycle Stage Filter;Integer   ;FieldClass=FlowFilter;
                                                   TableRelation="Sales Cycle Stage".Stage WHERE (Sales Cycle Code=FIELD(Sales Cycle Filter));
                                                   CaptionML=[DEU=Verkaufsprozess-Stufenfilter;
                                                              ENU=Sales Cycle Stage Filter] }
    { 26  ;   ;Probability % Filter;Decimal       ;FieldClass=FlowFilter;
                                                   CaptionML=[DEU=Wahrscheinlichkeit % Filter;
                                                              ENU=Probability % Filter];
                                                   DecimalPlaces=1:1;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 27  ;   ;Completed % Filter  ;Decimal       ;FieldClass=FlowFilter;
                                                   CaptionML=[DEU=Abgeschlossen % Filter;
                                                              ENU=Completed % Filter];
                                                   DecimalPlaces=1:1;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 28  ;   ;Contact Filter      ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation=Contact;
                                                   CaptionML=[DEU=Kontaktfilter;
                                                              ENU=Contact Filter] }
    { 29  ;   ;Contact Company Filter;Code20      ;FieldClass=FlowFilter;
                                                   TableRelation=Contact WHERE (Type=CONST(Company));
                                                   CaptionML=[DEU=Kontakt Unternehmensfilter;
                                                              ENU=Contact Company Filter] }
    { 30  ;   ;Estimated Value Filter;Decimal     ;FieldClass=FlowFilter;
                                                   CaptionML=[DEU=Erwarteter Wert Filter;
                                                              ENU=Estimated Value Filter];
                                                   AutoFormatType=1 }
    { 31  ;   ;Calcd. Current Value Filter;Decimal;FieldClass=FlowFilter;
                                                   CaptionML=[DEU=Berech. aktueller Wert Filter;
                                                              ENU=Calcd. Current Value Filter];
                                                   AutoFormatType=1 }
    { 32  ;   ;Chances of Success % Filter;Decimal;FieldClass=FlowFilter;
                                                   CaptionML=[DEU=Erfolgschancen % Filter;
                                                              ENU=Chances of Success % Filter];
                                                   DecimalPlaces=0:0;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 33  ;   ;To-do Status Filter ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=[DEU=Aufgabenstatusfilter;
                                                              ENU=To-do Status Filter];
                                                   OptionCaptionML=[DEU=Nicht begonnen,In Bearbeitung,Erledigt,Warten,Zurckgestellt;
                                                                    ENU=Not Started,In Progress,Completed,Waiting,Postponed];
                                                   OptionString=Not Started,In Progress,Completed,Waiting,Postponed }
    { 34  ;   ;To-do Closed Filter ;Boolean       ;FieldClass=FlowFilter;
                                                   CaptionML=[DEU=Abgeschlossene Aufgaben Filter;
                                                              ENU=To-do Closed Filter] }
    { 35  ;   ;Priority Filter     ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=[DEU=Priorit„tenfilter;
                                                              ENU=Priority Filter];
                                                   OptionCaptionML=[DEU=Niedrig,Normal,Hoch;
                                                                    ENU=Low,Normal,High];
                                                   OptionString=Low,Normal,High }
    { 36  ;   ;Team Filter         ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation=Team;
                                                   CaptionML=[DEU=Teamfilter;
                                                              ENU=Team Filter] }
    { 37  ;   ;Salesperson Filter  ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation=Salesperson/Purchaser;
                                                   CaptionML=[DEU=Verk„uferfilter;
                                                              ENU=Salesperson Filter] }
    { 38  ;   ;Opportunity Entry Exists;Boolean   ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Opportunity Entry" WHERE (Campaign No.=FIELD(No.),
                                                                                                Active=CONST(Yes),
                                                                                                Salesperson Code=FIELD(Salesperson Filter),
                                                                                                Contact No.=FIELD(Contact Filter),
                                                                                                Contact Company No.=FIELD(Contact Company Filter),
                                                                                                Sales Cycle Code=FIELD(Sales Cycle Filter),
                                                                                                Sales Cycle Stage=FIELD(Sales Cycle Stage Filter),
                                                                                                Action Taken=FIELD(Action Taken Filter),
                                                                                                Estimated Value (LCY)=FIELD(Estimated Value Filter),
                                                                                                Calcd. Current Value (LCY)=FIELD(Calcd. Current Value Filter),
                                                                                                Completed %=FIELD(Completed % Filter),
                                                                                                Chances of Success %=FIELD(Chances of Success % Filter),
                                                                                                Probability %=FIELD(Probability % Filter),
                                                                                                Estimated Close Date=FIELD(Date Filter),
                                                                                                Close Opportunity Code=FIELD(Close Opportunity Filter)));
                                                   CaptionML=[DEU=Verkaufschancenposten vorh.;
                                                              ENU=Opportunity Entry Exists];
                                                   Editable=No }
    { 39  ;   ;To-do Entry Exists  ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist(To-do WHERE (Campaign No.=FIELD(No.),
                                                                                  Contact No.=FIELD(Contact Filter),
                                                                                  Contact Company No.=FIELD(Contact Company Filter),
                                                                                  Salesperson Code=FIELD(Salesperson Filter),
                                                                                  Team Code=FIELD(Team Filter),
                                                                                  Status=FIELD(To-do Status Filter),
                                                                                  Closed=FIELD(To-do Closed Filter),
                                                                                  Priority=FIELD(Priority Filter),
                                                                                  Date=FIELD(Date Filter)));
                                                   CaptionML=[DEU=Aufgabenposten vorhanden;
                                                              ENU=To-do Entry Exists];
                                                   Editable=No }
    { 40  ;   ;Close Opportunity Filter;Code10    ;FieldClass=FlowFilter;
                                                   TableRelation="Close Opportunity Code";
                                                   CaptionML=[DEU=Verkaufschancenabschlussfilter;
                                                              ENU=Close Opportunity Filter] }
    { 41  ;   ;Activated           ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Campaign Target Group" WHERE (Campaign No.=FIELD(No.)));
                                                   CaptionML=[DEU=Aktiviert;
                                                              ENU=Activated];
                                                   Editable=No }
    { 8725;   ;Selling-in Start Date;Date         ;OnValidate=BEGIN
                                                                IF ("Selling-in Start Date" > "Selling-in End Date") AND ("Selling-in End Date" > 0D) THEN
                                                                  ERROR(Text000,FIELDCAPTION("Selling-in Start Date"),FIELDCAPTION("Selling-in End Date"));
                                                              END;

                                                   CaptionML=[DEU=Verkauf Startdatum;
                                                              ENU=Selling-in Start Date] }
    { 8726;   ;Selling-in End Date ;Date          ;OnValidate=BEGIN
                                                                IF ("Selling-in End Date" < "Selling-in Start Date") AND ("Selling-in End Date" > 0D) THEN
                                                                  ERROR(Text001,FIELDCAPTION("Selling-in End Date"),FIELDCAPTION("Selling-in Start Date"));
                                                              END;

                                                   CaptionML=[DEU=Verkauf Enddatum;
                                                              ENU=Selling-in End Date] }
    { 8727;   ;Visible Date        ;Date          ;CaptionML=[DEU=Sichtbares Datum;
                                                              ENU=Visible Date] }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Salesperson Code                         }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.,Description,Starting Date,Ending Date,Status Code }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=%1 muss vor dem %2 sein.;ENU=%1 must be before %2.';
      Text001@1001 : TextConst 'DEU=%1 muss nach dem %2 sein.;ENU=%1 must be after %2.';
      RMSetup@1002 : Record 5079;
      Campaign@1003 : Record 5071;
      RMCommentLine@1004 : Record 5061;
      CampaignEntry@1005 : Record 5072;
      NoSeriesMgt@1006 : Codeunit 396;
      DimMgt@1007 : Codeunit 408;
      CampaignMgmt@1008 : Codeunit 7030;
      SalesPrice@1009 : Integer;

    PROCEDURE AssistEdit@2(OldCampaign@1000 : Record 5071) : Boolean;
    BEGIN
      WITH Campaign DO BEGIN
        Campaign := Rec;
        RMSetup.GET;
        RMSetup.TESTFIELD("Campaign Nos.");
        IF NoSeriesMgt.SelectSeries(RMSetup."Campaign Nos.",OldCampaign."No. Series","No. Series") THEN BEGIN
          RMSetup.GET;
          RMSetup.TESTFIELD("Campaign Nos.");
          NoSeriesMgt.SetSeries("No.");
          Rec := Campaign;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      DimMgt.SaveDefaultDim(DATABASE::Campaign,"No.",FieldNumber,ShortcutDimCode);
      MODIFY;
    END;

    PROCEDURE UpdateDates@1();
    VAR
      SalesPrice@1002 : Record 7002;
      SalesLineDisc@1001 : Record 7004;
      SalesPrice2@1003 : Record 7002;
      SalesLineDisc2@1004 : Record 7004;
    BEGIN
      MODIFY;
      SalesPrice.SETRANGE(SalesPrice."Sales Type",SalesPrice."Sales Type"::Campaign);
      SalesPrice.SETRANGE(SalesPrice."Sales Code","No.");
      SalesPrice.LOCKTABLE;
      IF SalesPrice.FIND('-') THEN
        REPEAT
          SalesPrice2 := SalesPrice;
          SalesPrice.DELETE;
          SalesPrice2.VALIDATE("Starting Date","Starting Date");
          SalesPrice2.INSERT(TRUE);
          SalesPrice2.VALIDATE("Ending Date","Ending Date");
          SalesPrice2.MODIFY;
        UNTIL SalesPrice.NEXT = 0;

      SalesLineDisc.SETRANGE(SalesLineDisc."Sales Type",SalesLineDisc."Sales Type"::Campaign);
      SalesLineDisc.SETRANGE(SalesLineDisc."Sales Code","No.");
      SalesLineDisc.LOCKTABLE;
      IF SalesLineDisc.FIND('-') THEN
        REPEAT
          SalesLineDisc2 := SalesLineDisc;
          SalesLineDisc.DELETE;
          SalesLineDisc2.VALIDATE("Starting Date","Starting Date");
          SalesLineDisc2.INSERT(TRUE);
          SalesLineDisc2.VALIDATE("Ending Date","Ending Date");
          SalesLineDisc2.MODIFY;
        UNTIL SalesLineDisc.NEXT = 0;
    END;

    BEGIN
    END.
  }
}

