OBJECT Table 80 Gen. Journal Template
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               VALIDATE("Form ID");
             END;

    OnDelete=BEGIN
               GenJnlAlloc.SETRANGE("Journal Template Name",Name);
               GenJnlAlloc.DELETEALL;
               GenJnlLine.SETRANGE("Journal Template Name",Name);
               GenJnlLine.DELETEALL(TRUE);
               GenJnlBatch.SETRANGE("Journal Template Name",Name);
               GenJnlBatch.DELETEALL;
             END;

    CaptionML=[DEU=Fibu Buch.-Blattvorlage;
               ENU=Gen. Journal Template];
    LookupFormID=Form250;
  }
  FIELDS
  {
    { 1   ;   ;Name                ;Code10        ;CaptionML=[DEU=Name;
                                                              ENU=Name];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text80        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description] }
    { 5   ;   ;Test Report ID      ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Report));
                                                   CaptionML=[DEU=Testberichts-ID;
                                                              ENU=Test Report ID] }
    { 6   ;   ;Form ID             ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Form));
                                                   OnValidate=BEGIN
                                                                IF "Form ID" = 0 THEN
                                                                  VALIDATE(Type);
                                                              END;

                                                   CaptionML=[DEU=Formular-ID;
                                                              ENU=Form ID] }
    { 7   ;   ;Posting Report ID   ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Report));
                                                   CaptionML=[DEU=Buchungsberichts-ID;
                                                              ENU=Posting Report ID] }
    { 8   ;   ;Force Posting Report;Boolean       ;CaptionML=[DEU=Buchungsbericht immer drucken;
                                                              ENU=Force Posting Report] }
    { 9   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                "Test Report ID" := REPORT::"General Journal - Test";
                                                                "Posting Report ID" := REPORT::"G/L Register";
                                                                SourceCodeSetup.GET;
                                                                CASE Type OF
                                                                  Type::General:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."General Journal";
                                                                      "Form ID" :=  FORM::"General Journal";
                                                                    END;
                                                                  Type::Sales:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Sales Journal";
                                                                      "Form ID" := FORM::"Sales Journal";
                                                                    END;
                                                                  Type::Purchases:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Purchase Journal";
                                                                      "Form ID" := FORM::"Purchase Journal";
                                                                    END;
                                                                  Type::"Cash Receipts":
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Cash Receipt Journal";
                                                                      "Form ID" := FORM::"Cash Receipt Journal";
                                                                    END;
                                                                  Type::Payments:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Payment Journal";
                                                                      "Form ID" := FORM::"Payment Journal";
                                                                    END;
                                                                  Type::Assets:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Fixed Asset G/L Journal";
                                                                      "Form ID" := FORM::"Fixed Asset G/L Journal";
                                                                    END;
                                                                  Type::Intercompany:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."IC General Journal";
                                                                      "Form ID" := FORM::"IC General Journal";
                                                                    END;
                                                                  Type::Jobs:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Job G/L Journal";
                                                                      "Form ID" := FORM::"Job G/L Journal";
                                                                    END;
                                                                END;

                                                                IF Recurring THEN
                                                                  "Form ID" := FORM::"Recurring General Journal";
                                                              END;

                                                   CaptionML=[DEU=Art;
                                                              ENU=Type];
                                                   OptionCaptionML=[DEU=Allgemein,VerkÑufe,EinkÑufe,ZahlungseingÑnge,ZahlungsausgÑnge,Anlagen,Intercompany,Projekte;
                                                                    ENU=General,Sales,Purchases,Cash Receipts,Payments,Assets,Intercompany,Jobs];
                                                   OptionString=General,Sales,Purchases,Cash Receipts,Payments,Assets,Intercompany,Jobs }
    { 10  ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   OnValidate=BEGIN
                                                                GenJnlLine.SETRANGE("Journal Template Name",Name);
                                                                GenJnlLine.MODIFYALL("Source Code","Source Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[DEU=Herkunftscode;
                                                              ENU=Source Code] }
    { 11  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=[DEU=Ursachencode;
                                                              ENU=Reason Code] }
    { 12  ;   ;Recurring           ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE(Type);
                                                                IF Recurring THEN
                                                                  TESTFIELD("No. Series",'');
                                                              END;

                                                   CaptionML=[DEU=Wiederkehrend;
                                                              ENU=Recurring] }
    { 15  ;   ;Test Report Name    ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Test Report ID)));
                                                   CaptionML=[DEU=Testberichtsname;
                                                              ENU=Test Report Name];
                                                   Editable=No }
    { 16  ;   ;Form Name           ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Form),
                                                                                                                Object ID=FIELD(Form ID)));
                                                   CaptionML=[DEU=Formularname;
                                                              ENU=Form Name];
                                                   Editable=No }
    { 17  ;   ;Posting Report Name ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Posting Report ID)));
                                                   CaptionML=[DEU=Buchungsberichtsname;
                                                              ENU=Posting Report Name];
                                                   Editable=No }
    { 18  ;   ;Force Doc. Balance  ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[DEU=Saldo pro Beleg abstimmen;
                                                              ENU=Force Doc. Balance] }
    { 19  ;   ;Bal. Account Type   ;Option        ;OnValidate=BEGIN
                                                                "Bal. Account No." := '';
                                                              END;

                                                   CaptionML=[DEU=Gegenkontoart;
                                                              ENU=Bal. Account Type];
                                                   OptionCaptionML=[DEU=Sachkonto,Debitor,Kreditor,Bankkonto,Anlage;
                                                                    ENU=G/L Account,Customer,Vendor,Bank Account,Fixed Asset];
                                                   OptionString=G/L Account,Customer,Vendor,Bank Account,Fixed Asset }
    { 20  ;   ;Bal. Account No.    ;Code20        ;TableRelation=IF (Bal. Account Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Customer)) Customer
                                                                 ELSE IF (Bal. Account Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Bal. Account Type=CONST(Bank Account)) "Bank Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Fixed Asset)) "Fixed Asset";
                                                   OnValidate=BEGIN
                                                                IF "Bal. Account Type" = "Bal. Account Type"::"G/L Account" THEN
                                                                  CheckGLAcc("Bal. Account No.");
                                                              END;

                                                   CaptionML=[DEU=Gegenkontonr.;
                                                              ENU=Bal. Account No.] }
    { 21  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF "No. Series" <> '' THEN BEGIN
                                                                  IF Recurring THEN
                                                                    ERROR(
                                                                      Text000,
                                                                      FIELDCAPTION("Posting No. Series"));
                                                                  IF "No. Series" = "Posting No. Series" THEN
                                                                    "Posting No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Nummernserie;
                                                              ENU=No. Series] }
    { 22  ;   ;Posting No. Series  ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF ("Posting No. Series" = "No. Series") AND ("Posting No. Series" <> '') THEN
                                                                  FIELDERROR("Posting No. Series",STRSUBSTNO(Text001,"Posting No. Series"));
                                                              END;

                                                   CaptionML=[DEU=Buchungsnr.-Serie;
                                                              ENU=Posting No. Series] }
    { 23  ;   ;Copy VAT Setup to Jnl. Lines;Boolean;
                                                   InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF CONFIRM(
                                                                     Text002,
                                                                     TRUE, GenJnlBatch.FIELDCAPTION("Copy VAT Setup to Jnl. Lines"),GenJnlBatch.TABLECAPTION)
                                                                THEN BEGIN
                                                                  GenJnlBatch.SETRANGE("Journal Template Name",Name);
                                                                  GenJnlBatch.MODIFYALL("Copy VAT Setup to Jnl. Lines","Copy VAT Setup to Jnl. Lines");
                                                                  MODIFY;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=MwSt.Einr. in Bu.Bl.Zeile kop.;
                                                              ENU=Copy VAT Setup to Jnl. Lines] }
    { 24  ;   ;Allow VAT Difference;Boolean       ;OnValidate=VAR
                                                                Ok@1000 : Boolean;
                                                              BEGIN
                                                                IF "Allow VAT Difference" <> xRec."Allow VAT Difference" THEN
                                                                  IF CONFIRM(
                                                                       Text002,
                                                                       TRUE, GenJnlBatch.FIELDCAPTION("Allow VAT Difference"),GenJnlBatch.TABLECAPTION)
                                                                  THEN BEGIN
                                                                    GenJnlBatch.SETRANGE("Journal Template Name",Name);
                                                                    GenJnlBatch.MODIFYALL("Allow VAT Difference","Allow VAT Difference");
                                                                  END;
                                                              END;

                                                   CaptionML=[DEU=MwSt.-Differenz zulassen;
                                                              ENU=Allow VAT Difference] }
    { 25  ;   ;Cust. Receipt Report ID;Integer    ;TableRelation=Object.ID WHERE (Type=CONST(Report));
                                                   CaptionML=[DEU=Deb.-BestÑtigung Berichts-ID;
                                                              ENU=Cust. Receipt Report ID] }
    { 26  ;   ;Cust. Receipt Report Name;Text80   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Cust. Receipt Report ID)));
                                                   CaptionML=[DEU=Deb.-BestÑtigung Berichtsname;
                                                              ENU=Cust. Receipt Report Name];
                                                   Editable=No }
    { 27  ;   ;Vendor Receipt Report ID;Integer   ;TableRelation=Object.ID WHERE (Type=CONST(Report));
                                                   CaptionML=[DEU=Kred.-BestÑtigung Berichts-ID;
                                                              ENU=Vendor Receipt Report ID] }
    { 28  ;   ;Vendor Receipt Report Name;Text80  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Vendor Receipt Report ID)));
                                                   CaptionML=[DEU=Kred.-BestÑtigung Berichtsname;
                                                              ENU=Vendor Receipt Report Name];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Name                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Name,Description,Type                    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Nur das %1 Feld kann in wiederk. Buch.-Bl. ausgefÅllt werden.;ENU=Only the %1 field can be filled in on recurring journals.';
      Text001@1001 : TextConst 'DEU=darf nicht %1 sein;ENU=must not be %1';
      Text002@1002 : TextConst 'DEU=Mîchten Sie das Feld %1 fÅr jeden %2 aktualisieren?;ENU=Do you want to update the %1 field on all %2es?';
      GenJnlBatch@1003 : Record 232;
      GenJnlLine@1004 : Record 81;
      GenJnlAlloc@1005 : Record 221;
      SourceCodeSetup@1006 : Record 242;

    LOCAL PROCEDURE CheckGLAcc@2(AccNo@1000 : Code[20]);
    VAR
      GLAcc@1001 : Record 15;
    BEGIN
      IF AccNo <> '' THEN BEGIN
        GLAcc.GET(AccNo);
        GLAcc.CheckGLAcc;
        GLAcc.TESTFIELD("Direct Posting",TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

