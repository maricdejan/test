OBJECT Table 234 BOM Journal Batch
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Name,Description;
    OnInsert=BEGIN
               LOCKTABLE;
               BOMJnlTemplate.GET("Journal Template Name");
             END;

    OnDelete=BEGIN
               BOMJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
               BOMJnlLine.SETRANGE("Journal Batch Name",Name);
               BOMJnlLine.DELETEALL(TRUE);
             END;

    OnRename=BEGIN
               BOMJnlLine.SETRANGE("Journal Template Name",xRec."Journal Template Name");
               BOMJnlLine.SETRANGE("Journal Batch Name",xRec.Name);
               WHILE BOMJnlLine.FIND('-') DO
                 BOMJnlLine.RENAME("Journal Template Name",Name,BOMJnlLine."Line No.");
             END;

    CaptionML=[DEU=StÅckliste Buch.-Blattname;
               ENU=BOM Journal Batch];
    LookupFormID=Form265;
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="BOM Journal Template";
                                                   CaptionML=[DEU=Buch.-Blattvorlagenname;
                                                              ENU=Journal Template Name];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Code10        ;CaptionML=[DEU=Name;
                                                              ENU=Name];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description] }
    { 4   ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   OnValidate=BEGIN
                                                                IF "Reason Code" <> xRec."Reason Code" THEN BEGIN
                                                                  BOMJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                  BOMJnlLine.SETRANGE("Journal Batch Name",Name);
                                                                  BOMJnlLine.MODIFYALL("Reason Code","Reason Code");
                                                                  MODIFY;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Ursachencode;
                                                              ENU=Reason Code] }
    { 5   ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF "No. Series" <> '' THEN BEGIN
                                                                  BOMJnlTemplate.GET("Journal Template Name");
                                                                  IF BOMJnlTemplate.Recurring THEN
                                                                    ERROR(
                                                                      Text000,
                                                                      FIELDCAPTION("Posting No. Series"));
                                                                  IF "No. Series" = "Posting No. Series" THEN
                                                                    VALIDATE("Posting No. Series",'');
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Nummernserie;
                                                              ENU=No. Series] }
    { 6   ;   ;Posting No. Series  ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF ("Posting No. Series" = "No. Series") AND ("Posting No. Series" <> '') THEN
                                                                  FIELDERROR("Posting No. Series",STRSUBSTNO(Text001,"Posting No. Series"));
                                                                BOMJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                                                BOMJnlLine.SETRANGE("Journal Batch Name",Name);
                                                                BOMJnlLine.MODIFYALL("Posting No. Series","Posting No. Series");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[DEU=Buchungsnr.-Serie;
                                                              ENU=Posting No. Series] }
    { 22  ;   ;Recurring           ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("BOM Journal Template".Recurring WHERE (Name=FIELD(Journal Template Name)));
                                                   CaptionML=[DEU=Wiederkehrend;
                                                              ENU=Recurring];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Journal Template Name,Name              ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Nur das %1 Feld kann in wiederk. Buch.-Bl. ausgefÅllt werden.;ENU=Only the %1 field can be filled in on recurring journals.';
      Text001@1001 : TextConst 'DEU=darf nicht %1 sein;ENU=must not be %1';
      BOMJnlTemplate@1002 : Record 88;
      BOMJnlLine@1003 : Record 89;

    PROCEDURE SetupNewBatch@3();
    BEGIN
      BOMJnlTemplate.GET("Journal Template Name");
      "No. Series" := BOMJnlTemplate."No. Series";
      "Posting No. Series" := BOMJnlTemplate."Posting No. Series";
      "Reason Code" := BOMJnlTemplate."Reason Code";
    END;

    BEGIN
    END.
  }
}

