OBJECT Table 7023 Sales Price Worksheet
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Sales Type" = "Sales Type"::"All Customers" THEN
                 "Sales Code" := ''
               ELSE
                 TESTFIELD("Sales Code");
               TESTFIELD("Item No.");
             END;

    OnRename=BEGIN
               IF "Sales Type" <> "Sales Type"::"All Customers" THEN
                 TESTFIELD("Sales Code");
               TESTFIELD("Item No.");
             END;

    CaptionML=[DEU=VK-Preisvorschlag;
               ENU=Sales Price Worksheet];
  }
  FIELDS
  {
    { 1   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                IF "Item No." <> xRec."Item No." THEN BEGIN
                                                                  "Unit of Measure Code" := '';
                                                                  "Variant Code" := '';
                                                                END;

                                                                IF "Sales Type" = "Sales Type"::"Customer Price Group" THEN
                                                                  IF CustPriceGr.GET("Sales Code") AND
                                                                     (CustPriceGr."Allow Invoice Disc." <> "Allow Invoice Disc.")
                                                                  THEN
                                                                    IF Item.GET("Item No.") THEN
                                                                      "Allow Invoice Disc." := Item."Allow Invoice Disc.";

                                                                CalcCurrentPrice(PriceAlreadyExists);
                                                              END;

                                                   CaptionML=[DEU=Artikelnr.;
                                                              ENU=Item No.];
                                                   NotBlank=Yes }
    { 2   ;   ;Sales Code          ;Code20        ;TableRelation=IF (Sales Type=CONST(Customer Price Group)) "Customer Price Group"
                                                                 ELSE IF (Sales Type=CONST(Customer)) Customer
                                                                 ELSE IF (Sales Type=CONST(Campaign)) Campaign;
                                                   OnValidate=BEGIN
                                                                IF ("Sales Code" <> '') AND ("Sales Type" = "Sales Type"::"All Customers") THEN
                                                                  ERROR(Text001,FIELDCAPTION("Sales Code"));

                                                                CalcCurrentPrice(PriceAlreadyExists);

                                                                IF ("Sales Code" = '') AND ("Sales Type" <> "Sales Type"::"All Customers") THEN
                                                                  EXIT;

                                                                IF NOT PriceAlreadyExists AND ("Sales Code" <> '') THEN
                                                                  CASE "Sales Type" OF
                                                                    "Sales Type"::"Customer Price Group":
                                                                      BEGIN
                                                                        CustPriceGr.GET("Sales Code");
                                                                        "Price Includes VAT" := CustPriceGr."Price Includes VAT";
                                                                        "VAT Bus. Posting Gr. (Price)" := CustPriceGr."VAT Bus. Posting Gr. (Price)";
                                                                        "Allow Line Disc." := CustPriceGr."Allow Line Disc.";
                                                                        "Allow Invoice Disc." := CustPriceGr."Allow Invoice Disc.";
                                                                      END;
                                                                    "Sales Type"::Customer:
                                                                      BEGIN
                                                                        Cust.GET("Sales Code");
                                                                        "Currency Code" := Cust."Currency Code";
                                                                        "Price Includes VAT" := Cust."Prices Including VAT";
                                                                        "Allow Line Disc." := Cust."Allow Line Disc.";
                                                                      END;
                                                                    "Sales Type"::Campaign:
                                                                      BEGIN
                                                                        Campaign.GET("Sales Code");
                                                                        "Starting Date" := Campaign."Starting Date";
                                                                        "Ending Date" := Campaign."Ending Date";
                                                                      END;
                                                                  END;
                                                              END;

                                                   CaptionML=[DEU=Verkaufscode;
                                                              ENU=Sales Code] }
    { 3   ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   OnValidate=BEGIN
                                                                CalcCurrentPrice(PriceAlreadyExists);
                                                              END;

                                                   CaptionML=[DEU=W„hrungscode;
                                                              ENU=Currency Code] }
    { 4   ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                IF ("Starting Date" > "Ending Date") AND ("Ending Date" <> 0D) THEN
                                                                  ERROR(Text000,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"));

                                                                IF CurrFieldNo <> 0 THEN
                                                                  IF "Sales Type" = "Sales Type"::Campaign THEN
                                                                    ERROR(Text002,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"),FIELDCAPTION("Sales Type"),("Sales Type"));

                                                                CalcCurrentPrice(PriceAlreadyExists);
                                                              END;

                                                   CaptionML=[DEU=Startdatum;
                                                              ENU=Starting Date] }
    { 5   ;   ;Current Unit Price  ;Decimal       ;CaptionML=[DEU=Aktueller VK-Preis;
                                                              ENU=Current Unit Price];
                                                   MinValue=0;
                                                   Editable=No;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Currency Code" }
    { 6   ;   ;New Unit Price      ;Decimal       ;CaptionML=[DEU=Neuer VK-Preis;
                                                              ENU=New Unit Price];
                                                   MinValue=0;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Currency Code" }
    { 7   ;   ;Price Includes VAT  ;Boolean       ;CaptionML=[DEU=VK-Preis inkl. MwSt.;
                                                              ENU=Price Includes VAT] }
    { 10  ;   ;Allow Invoice Disc. ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[DEU=Rech.-Rabatt zulassen;
                                                              ENU=Allow Invoice Disc.] }
    { 11  ;   ;VAT Bus. Posting Gr. (Price);Code10;TableRelation="VAT Business Posting Group";
                                                   CaptionML=[DEU=MwSt.-Gesch„ftsbuch.-G.(Preis);
                                                              ENU=VAT Bus. Posting Gr. (Price)] }
    { 13  ;   ;Sales Type          ;Option        ;OnValidate=BEGIN
                                                                IF "Sales Type" <> xRec."Sales Type" THEN
                                                                  VALIDATE("Sales Code",'');
                                                              END;

                                                   CaptionML=[DEU=Verkaufsart;
                                                              ENU=Sales Type];
                                                   OptionCaptionML=[DEU=Debitor,Debitorenpreisgruppe,Alle Debitoren,Kampagne;
                                                                    ENU=Customer,Customer Price Group,All Customers,Campaign];
                                                   OptionString=Customer,Customer Price Group,All Customers,Campaign }
    { 14  ;   ;Minimum Quantity    ;Decimal       ;OnValidate=BEGIN
                                                                CalcCurrentPrice(PriceAlreadyExists);
                                                              END;

                                                   CaptionML=[DEU=Mindestmenge;
                                                              ENU=Minimum Quantity];
                                                   MinValue=0 }
    { 15  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                VALIDATE("Starting Date");

                                                                IF CurrFieldNo <> 0 THEN
                                                                  IF "Sales Type" = "Sales Type"::Campaign THEN
                                                                    ERROR(Text002,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"),FIELDCAPTION("Sales Type"),("Sales Type"));
                                                              END;

                                                   CaptionML=[DEU=Enddatum;
                                                              ENU=Ending Date] }
    { 5400;   ;Unit of Measure Code;Code10        ;TableRelation="Item Unit of Measure".Code WHERE (Item No.=FIELD(Item No.));
                                                   OnValidate=BEGIN
                                                                CalcCurrentPrice(PriceAlreadyExists);
                                                              END;

                                                   CaptionML=[DEU=Einheitencode;
                                                              ENU=Unit of Measure Code] }
    { 5700;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   OnValidate=BEGIN
                                                                CalcCurrentPrice(PriceAlreadyExists);
                                                              END;

                                                   CaptionML=[DEU=Variantencode;
                                                              ENU=Variant Code] }
    { 7001;   ;Allow Line Disc.    ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[DEU=Zeilenrabatt zulassen;
                                                              ENU=Allow Line Disc.] }
  }
  KEYS
  {
    {    ;Starting Date,Ending Date,Sales Type,Sales Code,Currency Code,Item No.,Variant Code,Unit of Measure Code,Minimum Quantity;
                                                   Clustered=Yes }
    {    ;Item No.,Variant Code,Unit of Measure Code,Minimum Quantity,Starting Date,Ending Date,Sales Type,Sales Code,Currency Code }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      CustPriceGr@1000 : Record 6;
      Text000@1001 : TextConst 'DEU=Das %1 kann nicht nach dem %2 sein.;ENU=%1 cannot be after %2';
      Cust@1002 : Record 18;
      Text001@1003 : TextConst 'DEU=%1 muss leer sein.;ENU=%1 must be blank.';
      Campaign@1006 : Record 5071;
      Item@1005 : Record 27;
      PriceAlreadyExists@1004 : Boolean;
      Text002@1007 : TextConst 'DEU="Sie k”nnen das %1 und %2 nur in der Kampagnenkarte „ndern, wenn die %3 = %4 ist.";ENU="%1 and %2 can only be altered from the Campaign Card when %3 = %4"';

    PROCEDURE CalcCurrentPrice@1(VAR PriceAlreadyExists@1000 : Boolean);
    VAR
      SalesPrice@1001 : Record 7002;
    BEGIN
      SalesPrice.SETRANGE("Item No.","Item No.");
      SalesPrice.SETRANGE("Sales Type","Sales Type");
      SalesPrice.SETRANGE("Sales Code","Sales Code");
      SalesPrice.SETRANGE("Currency Code","Currency Code");
      SalesPrice.SETRANGE("Unit of Measure Code","Unit of Measure Code");
      SalesPrice.SETRANGE("Starting Date",0D,"Starting Date");
      SalesPrice.SETRANGE("Minimum Quantity",0,"Minimum Quantity");
      SalesPrice.SETRANGE("Variant Code","Variant Code");
      IF SalesPrice.FIND('+') THEN BEGIN
        "Current Unit Price" := SalesPrice."Unit Price";
        "Price Includes VAT" := SalesPrice."Price Includes VAT";
        "Allow Line Disc." := SalesPrice."Allow Line Disc.";
        "Allow Invoice Disc." := SalesPrice."Allow Invoice Disc.";
        "VAT Bus. Posting Gr. (Price)" := SalesPrice."VAT Bus. Posting Gr. (Price)";
        PriceAlreadyExists := SalesPrice."Starting Date" = "Starting Date";
      END ELSE BEGIN
        "Current Unit Price" := 0;
        PriceAlreadyExists := FALSE;
      END;
    END;

    BEGIN
    END.
  }
}

