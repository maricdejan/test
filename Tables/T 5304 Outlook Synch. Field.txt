OBJECT Table 5304 Outlook Synch. Field
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Synch. Entity Code;
    OnInsert=BEGIN
               TESTFIELD("Field No.");

               IF "Table No." <> 0 THEN
                 TESTFIELD("Table Relation");

               CheckDuplicatedRecords;

               IF ISNULLGUID("Record GUID") THEN
                 "Record GUID" := CREATEGUID;
             END;

    OnModify=BEGIN
               TESTFIELD("Field No.");
               CheckDuplicatedRecords;

               IF "Table No." <> 0 THEN
                 TESTFIELD("Table Relation");
             END;

    OnDelete=VAR
               OSynchFilter@1000 : Record 5303;
               OSynchOptionCorrel@1001 : Record 5307;
             BEGIN
               OSynchFilter.RESET;
               OSynchFilter.SETRANGE("Record GUID","Record GUID");
               OSynchFilter.DELETEALL;

               OSynchOptionCorrel.RESET;
               OSynchOptionCorrel.SETRANGE("Synch. Entity Code","Synch. Entity Code");
               OSynchOptionCorrel.SETRANGE("Element No.","Element No.");
               OSynchOptionCorrel.SETRANGE("Field Line No.","Line No.");
               OSynchOptionCorrel.DELETEALL;
             END;

    CaptionML=[DEU=Outlook-Synch.-Feld;
               ENU=Outlook Synch. Field];
    PasteIsValid=No;
  }
  FIELDS
  {
    { 1   ;   ;Synch. Entity Code  ;Code10        ;TableRelation="Outlook Synch. Entity".Code;
                                                   OnValidate=BEGIN
                                                                GetMasterInformation;
                                                              END;

                                                   CaptionML=[DEU=Synch.-Einheitscode;
                                                              ENU=Synch. Entity Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Element No.         ;Integer       ;TableRelation="Outlook Synch. Entity Element"."Element No." WHERE (Synch. Entity Code=FIELD(Synch. Entity Code));
                                                   CaptionML=[DEU=Elementnr.;
                                                              ENU=Element No.] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[DEU=Zeilennr.;
                                                              ENU=Line No.] }
    { 4   ;   ;Master Table No.    ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[DEU=Mastertabellennr.;
                                                              ENU=Master Table No.];
                                                   Editable=No }
    { 5   ;   ;Outlook Object      ;Text80        ;CaptionML=[DEU=Outlook-Objekt;
                                                              ENU=Outlook Object];
                                                   Editable=No }
    { 6   ;   ;Outlook Property    ;Text80        ;OnValidate=BEGIN
                                                                CheckReadOnlyStatus;

                                                                IF "Outlook Property" = xRec."Outlook Property" THEN
                                                                  EXIT;

                                                                IF NOT "User-Defined" AND ("Outlook Object" = '') THEN BEGIN
                                                                  IF "Element No." = 0 THEN
                                                                    ERROR(
                                                                      Text009,
                                                                      FIELDCAPTION("Outlook Property"),
                                                                      OSynchEntity.FIELDCAPTION("Outlook Item"));
                                                                  ERROR(
                                                                    Text009,
                                                                    FIELDCAPTION("Outlook Property"),
                                                                    OSynchEntityElement.FIELDCAPTION("Outlook Collection"));
                                                                END;

                                                                OSynchOptionCorrel.RESET;
                                                                OSynchOptionCorrel.SETRANGE("Synch. Entity Code","Synch. Entity Code");
                                                                OSynchOptionCorrel.SETRANGE("Element No.","Element No.");
                                                                OSynchOptionCorrel.SETRANGE("Field Line No.","Line No.");

                                                                IF OSynchOptionCorrel.FIND('-') THEN BEGIN
                                                                  IF NOT CONFIRM(STRSUBSTNO(Text008,OSynchOptionCorrel.TABLECAPTION,OSynchFilter.TABLECAPTION)) THEN BEGIN
                                                                    "Outlook Property" := xRec."Outlook Property";
                                                                    "User-Defined" := xRec."User-Defined";
                                                                    EXIT;
                                                                  END;

                                                                  OSynchOptionCorrel.DELETEALL;
                                                                END;

                                                                IF "Outlook Property" <> '' THEN
                                                                  "Field Default Value" := '';
                                                              END;

                                                   OnLookup=VAR
                                                              PropertyName@1000 : Text[80];
                                                            BEGIN
                                                              IF "Outlook Object" = '' THEN BEGIN
                                                                IF "Element No." = 0 THEN
                                                                  ERROR(
                                                                    Text009,
                                                                    FIELDCAPTION("Outlook Property"),
                                                                    OSynchEntity.FIELDCAPTION("Outlook Item"));
                                                                ERROR(
                                                                  Text009,
                                                                  FIELDCAPTION("Outlook Property"),
                                                                  OSynchEntityElement.FIELDCAPTION("Outlook Collection"));
                                                              END;

                                                              IF "User-Defined" THEN
                                                                ERROR(Text001);

                                                              IF "Element No." = 0 THEN
                                                                PropertyName := OSynchSetupMgt.ShowOItemProperties("Outlook Object")
                                                              ELSE BEGIN
                                                                OSynchEntity.GET("Synch. Entity Code");
                                                                PropertyName := OSynchSetupMgt.ShowOCollectionProperties(OSynchEntity."Outlook Item","Outlook Object");
                                                              END;

                                                              IF PropertyName <> '' THEN
                                                                VALIDATE("Outlook Property",PropertyName);
                                                            END;

                                                   CaptionML=[DEU=Outlook-Eigenschaft;
                                                              ENU=Outlook Property] }
    { 7   ;   ;User-Defined        ;Boolean       ;OnValidate=BEGIN
                                                                "Outlook Property" := '';
                                                                IF NOT "User-Defined" THEN
                                                                  EXIT;

                                                                VALIDATE("Outlook Property","Field Name");
                                                              END;

                                                   CaptionML=[DEU=Benutzerdefiniert;
                                                              ENU=User-Defined] }
    { 8   ;   ;Search Field        ;Boolean       ;CaptionML=[DEU=Suchfeld;
                                                              ENU=Search Field] }
    { 9   ;   ;Condition           ;Text250       ;CaptionML=[DEU=Bedingung;
                                                              ENU=Condition];
                                                   Editable=No }
    { 10  ;   ;Table No.           ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                CALCFIELDS("Table Name");

                                                                IF "Table No." = xRec."Table No." THEN
                                                                  EXIT;

                                                                IF NOT OSynchSetupMgt.CheckPKFieldsQuantity("Table No.") THEN
                                                                  EXIT;

                                                                OSynchOptionCorrel.RESET;
                                                                OSynchOptionCorrel.SETRANGE("Synch. Entity Code","Synch. Entity Code");
                                                                OSynchOptionCorrel.SETRANGE("Element No.","Element No.");
                                                                OSynchOptionCorrel.SETRANGE("Field Line No.","Line No.");

                                                                IF ("Table Relation" <> '') OR OSynchOptionCorrel.FIND('-') THEN BEGIN
                                                                  IF NOT CONFIRM(STRSUBSTNO(Text008,OSynchOptionCorrel.TABLECAPTION,OSynchFilter.TABLECAPTION)) THEN BEGIN
                                                                    "Table No." := xRec."Table No.";
                                                                    EXIT;
                                                                  END;

                                                                  OSynchOptionCorrel.DELETEALL;

                                                                  OSynchFilter.RESET;
                                                                  OSynchFilter.SETRANGE("Record GUID","Record GUID");
                                                                  OSynchFilter.SETRANGE("Filter Type",OSynchFilter."Filter Type"::"Table Relation");
                                                                  OSynchFilter.DELETEALL;

                                                                  "Table Relation" := '';
                                                                END;

                                                                "Field No." := 0;
                                                                "Field Name" := '';
                                                                "Field Default Value" := '';
                                                              END;

                                                   OnLookup=VAR
                                                              TableNo@1000 : Integer;
                                                            BEGIN
                                                              TableNo := OSynchSetupMgt.ShowTablesList;

                                                              IF TableNo <> 0 THEN
                                                                VALIDATE("Table No.",TableNo);
                                                            END;

                                                   CaptionML=[DEU=Tabellennr.;
                                                              ENU=Table No.];
                                                   BlankZero=Yes }
    { 11  ;   ;Table Name          ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table No.)));
                                                   CaptionML=[DEU=Tabellenname;
                                                              ENU=Table Name];
                                                   Editable=No }
    { 12  ;   ;Table Relation      ;Text250       ;OnValidate=BEGIN
                                                                TESTFIELD("Table Relation");
                                                                CheckReadOnlyStatus;
                                                              END;

                                                   CaptionML=[DEU=Tabellenrelation;
                                                              ENU=Table Relation];
                                                   Editable=No }
    { 13  ;   ;Field No.           ;Integer       ;OnValidate=BEGIN
                                                                TESTFIELD("Field No.");
                                                                VALIDATE("Field Name");
                                                                CheckReadOnlyStatus;

                                                                IF "Field No." = xRec."Field No." THEN
                                                                  EXIT;

                                                                IF "Table No." = 0 THEN
                                                                  Field.GET("Master Table No.","Field No.")
                                                                ELSE
                                                                  Field.GET("Table No.","Field No.");

                                                                IF Field.Class = Field.Class::FlowFilter THEN
                                                                  ERROR(Text002,Field.Class);

                                                                IF NOT Field.Enabled THEN
                                                                  ERROR(Text012,Field.FieldName);

                                                                VALIDATE("Field Name",Field."Field Caption");

                                                                IF "User-Defined" THEN
                                                                  VALIDATE("Outlook Property",Field."Field Caption");

                                                                OSynchOptionCorrel.RESET;
                                                                OSynchOptionCorrel.SETRANGE("Synch. Entity Code","Synch. Entity Code");
                                                                OSynchOptionCorrel.SETRANGE("Element No.","Element No.");
                                                                OSynchOptionCorrel.SETRANGE("Field Line No.","Line No.");

                                                                IF OSynchOptionCorrel.FIND('-') THEN BEGIN
                                                                  IF NOT CONFIRM(STRSUBSTNO(Text008,OSynchOptionCorrel.TABLECAPTION,OSynchFilter.TABLECAPTION)) THEN BEGIN
                                                                    "Field No." := xRec."Field No.";
                                                                    EXIT;
                                                                  END;

                                                                  OSynchOptionCorrel.DELETEALL;
                                                                END;

                                                                "Field Default Value" := '';
                                                              END;

                                                   OnLookup=VAR
                                                              FieldNo@1000 : Integer;
                                                            BEGIN
                                                              IF "Table No." = 0 THEN
                                                                FieldNo := OSynchSetupMgt.ShowTableFieldsList("Master Table No.")
                                                              ELSE
                                                                FieldNo := OSynchSetupMgt.ShowTableFieldsList("Table No.");

                                                              IF FieldNo <> 0 THEN
                                                                VALIDATE("Field No.",FieldNo);
                                                            END;

                                                   CaptionML=[DEU=Feldnr.;
                                                              ENU=Field No.];
                                                   BlankZero=Yes }
    { 14  ;   ;Field Name          ;Text80        ;OnValidate=BEGIN
                                                                "Field Name" := '';
                                                                IF "Field No." = 0 THEN
                                                                  EXIT;

                                                                IF "Table No." = 0 THEN
                                                                  Field.GET("Master Table No.","Field No.")
                                                                ELSE
                                                                  Field.GET("Table No.","Field No.");

                                                                "Field Name" := Field."Field Caption";
                                                              END;

                                                   CaptionML=[DEU=Feldname;
                                                              ENU=Field Name];
                                                   Editable=No }
    { 15  ;   ;Read-Only Status    ;Option        ;OnValidate=BEGIN
                                                                CheckReadOnlyStatus;
                                                              END;

                                                   CaptionML=[DEU=Status 'SchreibgeschÅtzt';
                                                              ENU=Read-Only Status];
                                                   OptionCaptionML=[DEU=" ,SchreibgeschÅtzt in Microsoft Dynamics NAV,SchreibgeschÅtzt in Outlook";
                                                                    ENU=" ,Read-Only in Microsoft Dynamics NAV,Read-Only in Outlook"];
                                                   OptionString=[ ,Read-Only in Microsoft Dynamics NAV,Read-Only in Outlook];
                                                   Editable=No }
    { 16  ;   ;Field Default Value ;Text250       ;OnValidate=VAR
                                                                RecRef@1000 : RecordRef;
                                                                FldRef@1001 : FieldRef;
                                                              BEGIN
                                                                TESTFIELD("Master Table No.");
                                                                TESTFIELD("Field No.");

                                                                IF "Field Default Value" = xRec."Field Default Value" THEN
                                                                  EXIT;

                                                                IF "Outlook Property" <> '' THEN
                                                                  ERROR(Text005,FIELDCAPTION("Field Default Value"),FIELDCAPTION("Outlook Property"));

                                                                CLEAR(RecRef);
                                                                CLEAR(FldRef);

                                                                IF "Table No." = 0 THEN BEGIN
                                                                  Field.GET("Master Table No.","Field No.");
                                                                  RecRef.OPEN("Master Table No.",TRUE);
                                                                END ELSE BEGIN
                                                                  Field.GET("Table No.","Field No.");
                                                                  RecRef.OPEN("Table No.",TRUE);
                                                                END;

                                                                IF Field.Class = Field.Class::FlowField THEN
                                                                  ERROR(Text010,0,Field.Class);

                                                                FldRef := RecRef.FIELD("Field No.");

                                                                IF Field.Type = Field.Type::Option THEN BEGIN
                                                                  IF NOT OSynchTypeConversion.EvaluateOptionField(FldRef,"Field Default Value") THEN
                                                                    ERROR(Text004,"Field Default Value",FldRef.TYPE,FldRef.OPTIONSTRING);
                                                                END ELSE
                                                                  IF NOT EVALUATE(FldRef,"Field Default Value") THEN
                                                                    ERROR(Text003,FIELDCAPTION("Field Default Value"),FldRef.TYPE);

                                                                RecRef.CLOSE;
                                                              END;

                                                   CaptionML=[DEU=Feldstandardwert;
                                                              ENU=Field Default Value] }
    { 17  ;   ;Record GUID         ;GUID          ;CaptionML=[DEU=Datensatz-GUID;
                                                              ENU=Record GUID];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Synch. Entity Code,Element No.,Line No. ;Clustered=Yes }
    {    ;Record GUID                              }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Field@1001 : Record 2000000041;
      OSynchEntity@1003 : Record 5300;
      OSynchEntityElement@1004 : Record 5301;
      OSynchFilter@1019 : Record 5303;
      OSynchOptionCorrel@1013 : Record 5307;
      OSynchSetupMgt@1000 : Codeunit 5300;
      OSynchTypeConversion@1008 : Codeunit 5302;
      Text001@1015 : TextConst 'DEU=FÅr benutzerdefinierte Felder kann keine Auswahl aus einer Liste der Outlook-Eintragssammlungen vorgenommen werden.;ENU=You cannot choose from a list of Outlook item collections for user-defined fields.';
      Text002@1012 : TextConst 'DEU=Ein %1-Feld darf nicht fÅr die Synchronisierung verwendet werden.;ENU=You cannot use a %1 field for synchronization.';
      Text003@1011 : TextConst 'DEU=Der Wert des %1-Felds kann nicht in den %2-Datentyp konvertiert werden.;ENU=The value of the %1 field cannot be converted to the %2 datatype.';
      Text004@1010 : TextConst 'DEU=Der Wert des %1-Felds kann nicht in den %2-Datentyp konvertiert werden.\Die mîglichen Werte fÅr die Option sind: ''%3''.;ENU=The value of the %1 field cannot be converted to the %2 datatype.\The possible option values are: ''%3''.';
      Text005@1009 : TextConst 'DEU=Das %1-Feld muss leer sein, wenn das %2-Feld verwendet wird.;ENU=The %1 field should be blank when the %2 field is used.';
      Text006@1006 : TextConst 'DEU=Dies ist kein gÅltiger Outlook-Eigenschaftsname.;ENU=This is not a valid Outlook property name.';
      Text007@1005 : TextConst 'DEU=Die %1- und die %2-Felder kînnen nicht synchronisiert werden, weil beide schreibgeschÅtzt sind.;ENU=You cannot synchronize the %1 and the %2 fields because they are both write protected.';
      Text008@1016 : TextConst 'DEU=Wenn Sie den Wert dieses Felds Ñndern, werden die diesem Eintrag zugeordneten %1- und %2-DatensÑtze entfernt.\Mîchten Sie das Feld trotzdem Ñndern?;ENU=If you change the value of this field, %1 and %2 records related to this entry will be removed.\Do you want to change this field anyway?';
      Text009@1018 : TextConst 'DEU=Sie kînnen das %1-Feld nicht Ñndern, wenn %2 fÅr diese Einheit nicht angegeben ist.;ENU=You cannot change the %1 field if the %2 is not specified for this entity.';
      Text010@1020 : TextConst 'DEU=Dieses Feld kann nicht fÅr %2-Felder verwendet werden.;ENU=You cannot use this field for %2 fields.';
      Text011@1007 : TextConst 'DEU=Die %1-Tabelle darf nicht geîffnet sein, weil die %2- oder %3-Felder leer sind.\Setzen Sie die entsprechenden Werte in diese Felder ein, und wiederholen Sie den Vorgang.;ENU=The %1 table cannot be open, because the %2 or %3 fields are empty.\Fill in these fields with the appropriate values and try again.';
      Text012@1021 : TextConst 'DEU=Das %1-Feld kann nicht ausgewÑhlt werden, weil es deaktiviert ist.;ENU=You cannot select the %1 field because it is disabled.';
      Text013@1002 : TextConst 'DEU=Sie kînnen diesen Wert nicht verwenden, weil eine Outlook-Eigenschaft mit diesem Namen vorhanden ist.;ENU=You cannot use this value because an Outlook property with this name exists.';
      Text014@1022 : TextConst 'DEU=Der Eintrag, den Sie zu erstellen versuchen, ist bereits vorhanden.;ENU=The entry you are trying to create already exists.';

    LOCAL PROCEDURE GetMasterInformation@2();
    BEGIN
      IF "Element No." = 0 THEN BEGIN
        OSynchEntity.GET("Synch. Entity Code");
        "Master Table No." := OSynchEntity."Table No.";
        "Outlook Object" := OSynchEntity."Outlook Item";
      END ELSE BEGIN
        OSynchEntityElement.GET("Synch. Entity Code","Element No.");
        "Master Table No." := OSynchEntityElement."Table No.";
        "Outlook Object" := OSynchEntityElement."Outlook Collection";
      END;
    END;

    LOCAL PROCEDURE CheckReadOnlyStatus@9();
    VAR
      OSynchProcessLine@1002 : Codeunit 5305;
      IsReadOnlyOutlook@1000 : Boolean;
      IsReadOnlyNavision@1001 : Boolean;
    BEGIN
      IsReadOnlyOutlook := CheckOtlookPropertyName;

      IF ("Outlook Property" <> '') AND ("Field No." <> 0) THEN BEGIN
        IF "Table No." = 0 THEN BEGIN
          Field.GET("Master Table No.","Field No.");
          IF OSynchProcessLine.CheckKeyField("Master Table No.","Field No.") OR (Field.Class = Field.Class::FlowField) THEN
            IsReadOnlyNavision := TRUE;
        END ELSE BEGIN
          OSynchFilter.RESET;
          OSynchFilter.SETRANGE("Record GUID","Record GUID");
          OSynchFilter.SETRANGE("Filter Type",OSynchFilter."Filter Type"::"Table Relation");
          OSynchFilter.SETRANGE(Type,OSynchFilter.Type::FIELD);
          IF OSynchFilter.FIND('-') THEN
            REPEAT
              Field.GET(OSynchFilter."Master Table No.",OSynchFilter."Master Table Field No.");
              IF OSynchProcessLine.CheckKeyField("Master Table No.",OSynchFilter."Master Table Field No.") OR
                 (Field.Class = Field.Class::FlowField)
              THEN
                IsReadOnlyNavision := TRUE;
            UNTIL OSynchFilter.NEXT = 0;
        END;

        IF IsReadOnlyOutlook THEN BEGIN
          IF IsReadOnlyNavision THEN
            ERROR(Text007,"Outlook Property","Field Name");
          "Read-Only Status" := "Read-Only Status"::"Read-Only in Outlook";
        END ELSE BEGIN
          IF IsReadOnlyNavision THEN
            "Read-Only Status" := "Read-Only Status"::"Read-Only in Microsoft Dynamics NAV"
          ELSE
            "Read-Only Status" := "Read-Only Status"::" ";
        END;
      END ELSE BEGIN
        IF "Field No." = 0 THEN
          "Read-Only Status" := "Read-Only Status"::"Read-Only in Outlook"
        ELSE
          "Read-Only Status" := "Read-Only Status"::"Read-Only in Microsoft Dynamics NAV";
      END;
    END;

    LOCAL PROCEDURE CheckOtlookPropertyName@3() : Boolean;
    VAR
      IsReadOnly@1000 : Boolean;
    BEGIN
      IF "Outlook Property" = '' THEN
        EXIT(FALSE);

      IF "User-Defined" THEN
        IF "Element No." = 0 THEN BEGIN
          IF OSynchSetupMgt.ValidateOItemPropertyName("Outlook Property","Outlook Object",IsReadOnly,TRUE) THEN
            ERROR(Text013);
        END ELSE BEGIN
          OSynchEntity.GET("Synch. Entity Code");
          IF OSynchSetupMgt.ValidateOCollectPropertyName(
               "Outlook Property",
               OSynchEntity."Outlook Item",
               "Outlook Object",
               IsReadOnly,
               TRUE)
          THEN
            ERROR(Text013);
        END
      ELSE BEGIN
        IF "Element No." = 0 THEN BEGIN
          IF NOT OSynchSetupMgt.ValidateOItemPropertyName("Outlook Property","Outlook Object",IsReadOnly,FALSE) THEN
            ERROR(Text006);
        END ELSE BEGIN
          OSynchEntity.GET("Synch. Entity Code");
          IF NOT
             OSynchSetupMgt.ValidateOCollectPropertyName(
               "Outlook Property",
               OSynchEntity."Outlook Item",
               "Outlook Object",
               IsReadOnly,
               FALSE)
          THEN
            ERROR(Text006);
        END;
      END;

      EXIT(IsReadOnly);
    END;

    LOCAL PROCEDURE CheckDuplicatedRecords@4();
    VAR
      OSynchField@1000 : Record 5304;
    BEGIN
      OSynchField.RESET;
      OSynchField.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      OSynchField.SETRANGE("Element No.","Element No.");
      OSynchField.SETFILTER("Line No.",'<>%1',"Line No.");
      OSynchField.SETRANGE("Outlook Property","Outlook Property");
      OSynchField.SETRANGE("Table No.","Table No.");
      OSynchField.SETRANGE("Field No.","Field No.");
      IF OSynchField.FIND('-') THEN
        ERROR(Text014);
    END;

    PROCEDURE ShowOOptionCorrelForm@1();
    BEGIN
      IF ("Field No." = 0) OR ("Outlook Property" = '') THEN
        ERROR(Text011,
          OSynchOptionCorrel.TABLECAPTION,
          FIELDCAPTION("Field No."),
          FIELDCAPTION("Outlook Property"));
      OSynchSetupMgt.ShowOOptionCorrelForm(Rec);
    END;

    BEGIN
    END.
  }
}

