OBJECT Table 5301 Outlook Synch. Entity Element
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Table No.");

               IF ISNULLGUID("Record GUID") THEN
                 "Record GUID" := CREATEGUID;
             END;

    OnDelete=BEGIN
               OSynchSetupDetail.RESET;
               OSynchSetupDetail.SETRANGE("Synch. Entity Code","Synch. Entity Code");
               OSynchSetupDetail.SETRANGE("Element No.","Element No.");
               IF OSynchSetupDetail.FIND('-') THEN
                 ERROR(Text001);

               OSynchSetupDetail.DELETEALL(TRUE);

               OSynchFilter.RESET;
               OSynchFilter.SETRANGE("Record GUID","Record GUID");
               OSynchFilter.DELETEALL;

               OSynchField.RESET;
               OSynchField.SETRANGE("Synch. Entity Code","Synch. Entity Code");
               OSynchField.SETRANGE("Element No.","Element No.");
               OSynchField.DELETEALL(TRUE);

               OSynchDependency.RESET;
               OSynchDependency.SETRANGE("Synch. Entity Code","Synch. Entity Code");
               OSynchDependency.SETRANGE("Element No.","Element No.");
               OSynchDependency.DELETEALL(TRUE);
             END;

    CaptionML=[DEU=Outlook-Synch.-Einheitselement;
               ENU=Outlook Synch. Entity Element];
    PasteIsValid=No;
  }
  FIELDS
  {
    { 1   ;   ;Synch. Entity Code  ;Code10        ;TableRelation="Outlook Synch. Entity".Code;
                                                   CaptionML=[DEU=Synch.-Einheitscode;
                                                              ENU=Synch. Entity Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Element No.         ;Integer       ;CaptionML=[DEU=Elementnr.;
                                                              ENU=Element No.] }
    { 3   ;   ;Table No.           ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   OnValidate=BEGIN
                                                                IF "Table No." <> xRec."Table No." THEN BEGIN
                                                                  CheckUserSetup;
                                                                  CheckMasterTableNo;
                                                                  TESTFIELD("Table No.");

                                                                  IF NOT OSynchSetupMgt.CheckPKFieldsQuantity("Table No.") THEN
                                                                    EXIT;

                                                                  IF "Element No." <> 0 THEN BEGIN
                                                                    IF NOT
                                                                       CONFIRM(
                                                                         STRSUBSTNO(
                                                                           Text003,
                                                                           OSynchField.TABLECAPTION,
                                                                           OSynchFilter.TABLECAPTION,
                                                                           OSynchDependency.TABLECAPTION))
                                                                    THEN BEGIN
                                                                      "Table No." := xRec."Table No.";
                                                                      EXIT;
                                                                    END;

                                                                    OSynchField.SETRANGE("Synch. Entity Code","Synch. Entity Code");
                                                                    OSynchField.SETRANGE("Element No.","Element No.");
                                                                    OSynchField.DELETEALL(TRUE);

                                                                    OSynchFilter.RESET;
                                                                    OSynchFilter.SETRANGE("Record GUID","Record GUID");
                                                                    OSynchFilter.DELETEALL;

                                                                    OSynchDependency.RESET;
                                                                    OSynchDependency.SETRANGE("Synch. Entity Code","Synch. Entity Code");
                                                                    OSynchDependency.SETRANGE("Element No.","Element No.");
                                                                    OSynchDependency.DELETEALL(TRUE);

                                                                    "Table Relation" := '';
                                                                  END;
                                                                END;

                                                                CALCFIELDS("Table Name","No. of Dependencies");
                                                              END;

                                                   OnLookup=VAR
                                                              TableNo@1000 : Integer;
                                                            BEGIN
                                                              CheckMasterTableNo;
                                                              TableNo := OSynchSetupMgt.ShowTablesList;

                                                              IF TableNo <> 0 THEN
                                                                VALIDATE("Table No.",TableNo);
                                                            END;

                                                   CaptionML=[DEU=Tabellennr.;
                                                              ENU=Table No.];
                                                   BlankZero=Yes }
    { 4   ;   ;Table Name          ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table No.)));
                                                   CaptionML=[DEU=Tabellenname;
                                                              ENU=Table Name];
                                                   Editable=No }
    { 5   ;   ;Table Relation      ;Text250       ;OnValidate=BEGIN
                                                                TESTFIELD("Table Relation");
                                                              END;

                                                   CaptionML=[DEU=Tabellenrelation;
                                                              ENU=Table Relation];
                                                   Editable=No }
    { 6   ;   ;Outlook Collection  ;Text80        ;OnValidate=BEGIN
                                                                IF "Outlook Collection" <> '' THEN BEGIN
                                                                  OSynchEntity.GET("Synch. Entity Code");
                                                                  IF NOT OSynchSetupMgt.ValidateOutlookCollectionName("Outlook Collection",OSynchEntity."Outlook Item") THEN
                                                                    ERROR(Text002);
                                                                  CheckCollectionName;
                                                                END;

                                                                IF "Outlook Collection" = xRec."Outlook Collection" THEN
                                                                  EXIT;

                                                                CheckUserSetup;
                                                                CheckMasterTableNo;

                                                                IF "Element No." = 0 THEN
                                                                  EXIT;

                                                                IF xRec."Outlook Collection" <> '' THEN
                                                                  IF NOT
                                                                     CONFIRM(
                                                                       STRSUBSTNO(
                                                                         Text003,
                                                                         OSynchField.TABLECAPTION,
                                                                         OSynchFilter.TABLECAPTION,
                                                                         OSynchDependency.TABLECAPTION))
                                                                  THEN BEGIN
                                                                    "Outlook Collection" := xRec."Outlook Collection";
                                                                    EXIT;
                                                                  END;

                                                                OSynchField.RESET;
                                                                OSynchField.SETRANGE("Synch. Entity Code","Synch. Entity Code");
                                                                OSynchField.SETRANGE("Element No.","Element No.");
                                                                OSynchField.DELETEALL(TRUE);

                                                                OSynchDependency.RESET;
                                                                OSynchDependency.SETRANGE("Synch. Entity Code","Synch. Entity Code");
                                                                OSynchDependency.SETRANGE("Element No.","Element No.");
                                                                OSynchDependency.DELETEALL(TRUE);
                                                              END;

                                                   OnLookup=VAR
                                                              CollectionName@1000 : Text[80];
                                                            BEGIN
                                                              CheckMasterTableNo;
                                                              OSynchEntity.GET("Synch. Entity Code");

                                                              OSynchEntity.TESTFIELD("Outlook Item");

                                                              CollectionName := OSynchSetupMgt.ShowOCollectionsList(OSynchEntity."Outlook Item");

                                                              IF CollectionName <> '' THEN
                                                                VALIDATE("Outlook Collection",CollectionName);
                                                            END;

                                                   CaptionML=[DEU=Outlook-Sammlung;
                                                              ENU=Outlook Collection] }
    { 7   ;   ;Master Table No.    ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Outlook Synch. Entity"."Table No." WHERE (Code=FIELD(Synch. Entity Code)));
                                                   CaptionML=[DEU=Mastertabellennr.;
                                                              ENU=Master Table No.];
                                                   Editable=No }
    { 8   ;   ;Record GUID         ;GUID          ;CaptionML=[DEU=Datensatz-GUID;
                                                              ENU=Record GUID];
                                                   Editable=No }
    { 9   ;   ;No. of Dependencies ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Outlook Synch. Dependency" WHERE (Synch. Entity Code=FIELD(Synch. Entity Code),
                                                                                                        Element No.=FIELD(Element No.)));
                                                   CaptionML=[DEU=Anzahl Abh„ngigkeiten;
                                                              ENU=No. of Dependencies];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Synch. Entity Code,Element No.          ;Clustered=Yes }
    {    ;Table No.                                }
    {    ;Record GUID                              }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      OSynchEntity@1003 : Record 5300;
      OSynchFilter@1002 : Record 5303;
      OSynchField@1001 : Record 5304;
      OSynchDependency@1012 : Record 5311;
      OSynchSetupDetail@1011 : Record 5310;
      OSynchSetupMgt@1000 : Codeunit 5300;
      Text001@1005 : TextConst 'DEU=Diese Sammlung kann nicht gel”scht werden, weil sie mit Synchronisierung verwendet wird.;ENU=You cannot delete this collection because it is used with synchronization.';
      Text002@1007 : TextConst 'DEU=Es ist keine Outlook-Eintragssammlung mit diesem Namen vorhanden.\Klicken Sie auf die AssistButton, um eine Liste der gltigen Sammlungen fr diesen Outlook-Eintrag anzuzeigen.;ENU=The Outlook item collection with this name does not exist.\Click the AssistButton to see a list of valid collections for this Outlook item.';
      Text003@1004 : TextConst 'DEU=Wenn Sie den Wert in diesem Feld „ndern, werden die dieser Sammlung zugeordneten %1-, %2- und %3-Datens„tze gel”scht.\M”chten Sie den Wert trotzdem „ndern?;ENU=If you change the value in this field, the %1, %2, and %3 records related to this collection will be deleted.\Do you want to change it anyway?';
      Text004@1006 : TextConst 'DEU=Diese Sammlung kann nicht ge„ndert werden, weil sie mit Synchronisierung fr den Benutzer %1 verwendet wird.;ENU=You cannot change this collection because it is used with synchronization for user %1.';
      Text005@1008 : TextConst 'DEU="Es ist bereits eine Sammlung von Outlook-Eintr„gen mit diesem Namen vorhanden.\Identifikationsfelder und -werte:\%1=''''%2'''',%3=''''%4''''.";ENU="An Outlook item collection with this name already exists.\Identification fields and values:\%1=''''%2'''',%3=''''%4''''."';

    PROCEDURE ShowElementFields@1();
    BEGIN
      TESTFIELD("Synch. Entity Code");
      TESTFIELD("Element No.");
      TESTFIELD("Table No.");
      TESTFIELD("Table Relation");
      TESTFIELD("Outlook Collection");

      OSynchField.RESET;
      OSynchField.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      OSynchField.SETRANGE("Element No.","Element No.");

      FORM.RUNMODAL(FORM::"Outlook Synch. Fields",OSynchField);
    END;

    PROCEDURE ShowDependencies@3();
    BEGIN
      TESTFIELD("Synch. Entity Code");
      TESTFIELD("Element No.");
      TESTFIELD("Table No.");
      TESTFIELD("Table Relation");
      TESTFIELD("Outlook Collection");

      OSynchDependency.RESET;
      OSynchDependency.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      OSynchDependency.SETRANGE("Element No.","Element No.");

      FORM.RUNMODAL(FORM::"Outlook Synch. Dependencies",OSynchDependency);
      CALCFIELDS("No. of Dependencies");
    END;

    PROCEDURE CheckMasterTableNo@2();
    BEGIN
      CALCFIELDS("Master Table No.");
      IF "Master Table No." = 0 THEN BEGIN
        OSynchEntity.GET("Synch. Entity Code");
        OSynchEntity.TESTFIELD("Table No.");
      END;
    END;

    PROCEDURE CheckUserSetup@4();
    VAR
      OSynchUserSetup@1000 : Record 5305;
    BEGIN
      OSynchUserSetup.RESET;
      OSynchUserSetup.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      IF NOT OSynchUserSetup.FIND('-') THEN
        EXIT;

      REPEAT
        OSynchUserSetup.CALCFIELDS("No. of Elements");
        IF OSynchUserSetup."No. of Elements" > 0 THEN
          IF OSynchSetupDetail.GET(OSynchUserSetup."User ID","Synch. Entity Code","Element No.") THEN
            ERROR(Text004,OSynchUserSetup."User ID");
      UNTIL OSynchUserSetup.NEXT = 0;
    END;

    PROCEDURE CheckCollectionName@5();
    VAR
      OSynchEntityElement@1000 : Record 5301;
    BEGIN
      OSynchEntityElement.RESET;
      OSynchEntityElement.SETRANGE("Synch. Entity Code","Synch. Entity Code");
      OSynchEntityElement.SETRANGE("Outlook Collection","Outlook Collection");
      IF OSynchEntityElement.FIND('-') THEN
        ERROR(
          Text005,
          OSynchEntityElement.FIELDCAPTION("Synch. Entity Code"),
          OSynchEntityElement."Synch. Entity Code",
          OSynchEntityElement.FIELDCAPTION("Element No."),
          OSynchEntityElement."Element No.");
    END;

    BEGIN
    END.
  }
}

