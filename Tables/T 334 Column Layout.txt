OBJECT Table 334 Column Layout
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Spaltenlayout;
               ENU=Column Layout];
  }
  FIELDS
  {
    { 1   ;   ;Column Layout Name  ;Code10        ;TableRelation="Column Layout Name";
                                                   CaptionML=[DEU=Spaltenlayoutname;
                                                              ENU=Column Layout Name] }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[DEU=Zeilennr.;
                                                              ENU=Line No.] }
    { 3   ;   ;Column No.          ;Code10        ;CaptionML=[DEU=Spaltennr.;
                                                              ENU=Column No.] }
    { 4   ;   ;Column Header       ;Text30        ;CaptionML=[DEU=SpaltenÅberschrift;
                                                              ENU=Column Header] }
    { 5   ;   ;Column Type         ;Option        ;InitValue=Net Change;
                                                   CaptionML=[DEU=Spaltenart;
                                                              ENU=Column Type];
                                                   OptionCaptionML=[DEU=Formel,Bewegung,Saldo bis Datum,Saldovortrag,Jahr bis Datum,Rest des GeschÑftsjahres,Gesamtes GeschÑftsjahr;
                                                                    ENU=Formula,Net Change,Balance at Date,Beginning Balance,Year to Date,Rest of Fiscal Year,Entire Fiscal Year];
                                                   OptionString=Formula,Net Change,Balance at Date,Beginning Balance,Year to Date,Rest of Fiscal Year,Entire Fiscal Year }
    { 6   ;   ;Ledger Entry Type   ;Option        ;CaptionML=[DEU=Postenart;
                                                              ENU=Ledger Entry Type];
                                                   OptionCaptionML=[DEU=Sachposten,Finanzbudgetposten;
                                                                    ENU=G/L Entries,G/L Budget Entries];
                                                   OptionString=G/L Entries,G/L Budget Entries }
    { 7   ;   ;Amount Type         ;Option        ;CaptionML=[DEU=Betragsart;
                                                              ENU=Amount Type];
                                                   OptionCaptionML=[DEU=Nettobetrag,Sollbetrag,Habenbetrag;
                                                                    ENU=Net Amount,Debit Amount,Credit Amount];
                                                   OptionString=Net Amount,Debit Amount,Credit Amount }
    { 8   ;   ;Formula             ;Code80        ;OnValidate=BEGIN
                                                                AccSchedLine.CheckFormula(Formula);
                                                              END;

                                                   CaptionML=[DEU=Formel;
                                                              ENU=Formula] }
    { 9   ;   ;Comparison Date Formula;DateFormula;OnValidate=BEGIN
                                                                IF FORMAT("Comparison Date Formula") <> '' THEN
                                                                  VALIDATE("Comparison Period Formula",'');
                                                              END;

                                                   CaptionML=[DEU=Vergleichsdatumsformel;
                                                              ENU=Comparison Date Formula] }
    { 10  ;   ;Show Opposite Sign  ;Boolean       ;CaptionML=[DEU=Umgekehrtes Vorzeichen anz.;
                                                              ENU=Show Opposite Sign] }
    { 11  ;   ;Show                ;Option        ;InitValue=Always;
                                                   CaptionML=[DEU=Anzeigen;
                                                              ENU=Show];
                                                   OptionCaptionML=[DEU=Immer,Nie,Wenn positiv,Wenn negativ;
                                                                    ENU=Always,Never,When Positive,When Negative];
                                                   OptionString=Always,Never,When Positive,When Negative;
                                                   NotBlank=Yes }
    { 12  ;   ;Rounding Factor     ;Option        ;CaptionML=[DEU=Rundungsfaktor;
                                                              ENU=Rounding Factor];
                                                   OptionCaptionML=[DEU=Kein,1,1000,1000000;
                                                                    ENU=None,1,1000,1000000];
                                                   OptionString=None,1,1000,1000000 }
    { 14  ;   ;Comparison Period Formula;Code20   ;OnValidate=VAR
                                                                Steps@1001 : Integer;
                                                                Type@1000 : ' ,Period,Fiscal year,Fiscal Halfyear,Fiscal Quarter';
                                                                RangeFromType@1007 : 'Int,CP,LP';
                                                                RangeToType@1006 : 'Int,CP,LP';
                                                                RangeFromInt@1005 : Integer;
                                                                RangeToInt@1004 : Integer;
                                                              BEGIN
                                                                ParsePeriodFormula(
                                                                  "Comparison Period Formula",
                                                                  Steps,Type,RangeFromType,RangeToType,RangeFromInt,RangeToInt);
                                                                IF "Comparison Period Formula" <> '' THEN
                                                                  CLEAR("Comparison Date Formula");
                                                              END;

                                                   CaptionML=[DEU=Vergleichsperiodendatumsformel;
                                                              ENU=Comparison Period Formula] }
    { 15  ;   ;Business Unit Totaling;Text80      ;TableRelation="Business Unit";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[DEU=ZusammenzÑhlung Konzernmandant;
                                                              ENU=Business Unit Totaling] }
    { 16  ;   ;Dimension 1 Totaling;Text80        ;ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[DEU=Dimensionssumme 1;
                                                              ENU=Dimension 1 Totaling];
                                                   CaptionClass=GetCaptionClass(5) }
    { 17  ;   ;Dimension 2 Totaling;Text80        ;ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[DEU=Dimensionssumme 2;
                                                              ENU=Dimension 2 Totaling];
                                                   CaptionClass=GetCaptionClass(6) }
    { 18  ;   ;Dimension 3 Totaling;Text80        ;ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[DEU=Dimensionssumme 3;
                                                              ENU=Dimension 3 Totaling];
                                                   CaptionClass=GetCaptionClass(7) }
    { 19  ;   ;Dimension 4 Totaling;Text80        ;ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[DEU=Dimensionssumme 4;
                                                              ENU=Dimension 4 Totaling];
                                                   CaptionClass=GetCaptionClass(8) }
  }
  KEYS
  {
    {    ;Column Layout Name,Line No.             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      AccSchedLine@1000 : TEMPORARY Record 85;
      Text001@1003 : TextConst 'DEU=%1 ist keine gÅltige Periodendatumsformel;ENU=%1 is not a valid Period Formula';
      Text002@1002 : TextConst 'DEU=P;ENU=P';
      Text003@1001 : TextConst 'DEU=GJ;ENU=FY';
      Text004@1005 : TextConst 'DEU=LP;ENU=CP';
      Text005@1004 : TextConst 'DEU=EP;ENU=LP';
      Text006@1014 : TextConst 'DEU=1,6,,Dimensionsfilter 1;ENU=1,6,,Dimension 1 Filter';
      Text007@1013 : TextConst 'DEU=1,6,,Dimensionsfilter 2;ENU=1,6,,Dimension 2 Filter';
      Text008@1012 : TextConst 'DEU=1,6,,Dimensionsfilter 3;ENU=1,6,,Dimension 3 Filter';
      Text009@1011 : TextConst 'DEU=1,6,,Dimensionsfilter 4;ENU=1,6,,Dimension 4 Filter';
      Text010@1010 : TextConst 'DEU=,, ZusammenzÑhlung;ENU=,, Totaling';
      Text011@1009 : TextConst 'DEU=1,5,,Dimensionssumme 1;ENU=1,5,,Dimension 1 Totaling';
      Text012@1008 : TextConst 'DEU=1,5,,Dimensionssumme 2;ENU=1,5,,Dimension 2 Totaling';
      Text013@1007 : TextConst 'DEU=1,5,,Dimensionssumme 3;ENU=1,5,,Dimension 3 Totaling';
      Text014@1006 : TextConst 'DEU=1,5,,Dimensionssumme 4;ENU=1,5,,Dimension 4 Totaling';
      Text015@1015 : TextConst 'DEU=Der %1 bezieht sich auf die %2 %3, die nicht vorhanden ist. Das Feld %4 in der Tabelle %5 wurde jetzt gelîscht.;ENU=The %1 refers to %2 %3, which does not exist. The field %4 on table %5 has now been deleted.';
      ColumnLayoutName@1016 : Record 333;
      AnalysisView@1017 : Record 363;
      GLSetup@1018 : Record 98;
      HasGLSetup@1019 : Boolean;

    PROCEDURE ParsePeriodFormula@26(Formula@1002 : Code[20];VAR Steps@1001 : Integer;VAR Type@1006 : ' ,Period,Fiscal Year';VAR RangeFromType@1009 : 'Int,CP,LP';VAR RangeToType@1008 : 'Int,CP,LP';VAR RangeFromInt@1003 : Integer;VAR RangeToInt@1000 : Integer);
    VAR
      OriginalFormula@1004 : Code[20];
    BEGIN
      // <PeriodFormula> ::= <signed integer> <formula> | blank
      // <signed integer> ::= <sign> <positive integer> | blank
      // <sign> ::= + | - | blank
      // <positive integer> ::= <digit 1-9> <digits>
      // <digit 1-9> ::= 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
      // <digits> ::= 0 <digits> | <digit 1-9> <digits> | blank
      // <formula> ::= P | FY <range> | FH <range> | FQ <range>
      // <range> ::= blank | [<range2>]
      // <range2> ::= <index> .. <index> | <index>
      // <index> ::= <positive integer> | CP | LP

      OriginalFormula := Formula;
      Formula := DELCHR(Formula);

      IF NOT ParseFormula(Formula,Steps,Type) THEN
        ERROR(Text001,OriginalFormula);

      IF Type = Type::"Fiscal Year" THEN
        IF NOT ParseRange(Formula,RangeFromType,RangeFromInt,RangeToType,RangeToInt) THEN
          ERROR(Text001,OriginalFormula);

      IF Formula <> '' THEN
        ERROR(Text001,OriginalFormula);
    END;

    LOCAL PROCEDURE ParseFormula@5(VAR Formula@1002 : Code[20];VAR Steps@1001 : Integer;VAR Type@1006 : ' ,Period,Fiscal year,Fiscal Halfyear,Fiscal Quarter') : Boolean;
    BEGIN
      Steps := 0;
      Type := Type::" ";

      IF Formula = '' THEN
        EXIT(TRUE);

      IF NOT ParseSignedInteger(Formula,Steps) THEN
        EXIT(FALSE);

      IF Formula = '' THEN
        EXIT(FALSE);

      IF NOT ParseType(Formula,Type) THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ParseSignedInteger@1(VAR Formula@1000 : Code[20];VAR Int@1001 : Integer) : Boolean;
    BEGIN
      Int := 0;

      CASE COPYSTR(Formula,1,1) OF
        '-':
          BEGIN
            Formula := COPYSTR(Formula,2);
            IF NOT ParseInt(Formula,Int,FALSE) THEN
              EXIT(FALSE);
            Int := -Int;
          END;
        '+':
          BEGIN
            Formula := COPYSTR(Formula,2);
            IF NOT ParseInt(Formula,Int,FALSE) THEN
              EXIT(FALSE);
          END;
        ELSE
          BEGIN
            IF NOT ParseInt(Formula,Int,TRUE) THEN
              EXIT(FALSE);
          END;
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ParseInt@2(VAR Formula@1001 : Code[20];VAR Int@1000 : Integer;AllowNotInt@1003 : Boolean) : Boolean;
    VAR
      IntegerStr@1002 : Code[20];
    BEGIN
      IF COPYSTR(Formula,1,1) IN ['1'..'9'] THEN
        REPEAT
          IntegerStr := IntegerStr + COPYSTR(Formula,1,1);
          Formula := COPYSTR(Formula,2);
          IF Formula = '' THEN
            EXIT(FALSE);
        UNTIL NOT (COPYSTR(Formula,1,1) IN ['0'..'9'])
      ELSE
        EXIT(AllowNotInt);
      EVALUATE(Int,IntegerStr);
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ParseType@3(VAR Formula@1001 : Code[20];VAR Type@1000 : ' ,Period,Fiscal Year') : Boolean;
    BEGIN
      CASE ReadToken(Formula) OF
        Text002:
          Type := Type::Period;
        Text003:
          Type := Type::"Fiscal Year";
        ELSE
          EXIT(FALSE);
      END;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ParseRange@6(VAR Formula@1002 : Code[20];VAR FromType@1001 : 'Int,CP,LP';VAR FromInt@1004 : Integer;VAR ToType@1006 : 'Int,CP,LP';VAR ToInt@1005 : Integer) : Boolean;
    BEGIN
      FromType := FromType::CP;
      ToType := ToType::CP;

      IF Formula = '' THEN
        EXIT(TRUE);

      IF NOT ParseToken(Formula,'[') THEN
        EXIT(FALSE);

      IF NOT ParseIndex(Formula,FromType,FromInt) THEN
        EXIT(FALSE);
      IF Formula = '' THEN
        EXIT(FALSE);

      IF COPYSTR(Formula,1,1) = '.' THEN BEGIN
        IF NOT ParseToken(Formula,'..') THEN
          EXIT(FALSE);
        IF NOT ParseIndex(Formula,ToType,ToInt) THEN
          EXIT(FALSE);
      END ELSE BEGIN
        ToType := FromType;
        ToInt := FromInt;
      END;

      IF NOT ParseToken(Formula,']') THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ParseIndex@11(VAR Formula@1002 : Code[20];VAR IndexType@1001 : 'Int,CP,LP';VAR Index@1004 : Integer) : Boolean;
    BEGIN
      IF Formula = '' THEN
        EXIT(FALSE);

      IF ParseInt(Formula,Index,FALSE) THEN
        IndexType := IndexType::Int
      ELSE
        CASE ReadToken(Formula) OF
          Text004:
            IndexType := IndexType::CP;
          Text005:
            IndexType := IndexType::LP;
          ELSE
            EXIT(FALSE);
        END;

      EXIT(TRUE);
    END;

    LOCAL PROCEDURE ParseToken@13(VAR Formula@1000 : Code[20];Token@1001 : Code[20]) : Boolean;
    BEGIN
      IF COPYSTR(Formula,1,STRLEN(Token)) <> Token THEN
        EXIT(FALSE);
      Formula := COPYSTR(Formula,STRLEN(Token) + 1);
      EXIT(TRUE)
    END;

    LOCAL PROCEDURE ReadToken@7(VAR Formula@1000 : Code[20]) : Code[20];
    VAR
      Token@1001 : Code[20];
      p@1002 : Integer;
    BEGIN
      FOR p := 1 TO STRLEN(Formula) DO BEGIN
        IF COPYSTR(Formula,p,1) IN ['[',']','.'] THEN BEGIN
          Formula := COPYSTR(Formula,STRLEN(Token) + 1);
          EXIT(Token);
        END;
        Token := Token + COPYSTR(Formula,p,1);
      END;

      Formula := '';
      EXIT(Token);
    END;

    PROCEDURE LookUpDimFilter@57(DimNo@1000 : Integer;VAR Text@1001 : Text[250]) : Boolean;
    VAR
      DimVal@1002 : Record 349;
      DimValList@1003 : Form 560;
    BEGIN
      GetColLayoutSetup;
      CASE DimNo OF
        1: DimVal.SETRANGE("Dimension Code",AnalysisView."Dimension 1 Code");
        2: DimVal.SETRANGE("Dimension Code",AnalysisView."Dimension 2 Code");
        3: DimVal.SETRANGE("Dimension Code",AnalysisView."Dimension 3 Code");
        4: DimVal.SETRANGE("Dimension Code",AnalysisView."Dimension 4 Code");
      END;
      DimValList.LOOKUPMODE(TRUE);
      DimValList.SETTABLEVIEW(DimVal);
      IF DimValList.RUNMODAL = ACTION::LookupOK THEN BEGIN
        DimValList.GETRECORD(DimVal);
        Text := Text + DimValList.GetSelectionFilter;
        EXIT(TRUE);
      END ELSE
        EXIT(FALSE)
    END;

    PROCEDURE GetCaptionClass@4(AnalysisViewDimType@1000 : Integer) : Text[250];
    BEGIN
      GetColLayoutSetup;
      CASE AnalysisViewDimType OF
        1:
          BEGIN
            IF AnalysisView."Dimension 1 Code" <> '' THEN
              EXIT('1,6,' + AnalysisView."Dimension 1 Code")
            ELSE
              EXIT(Text006);
          END;
        2:
          BEGIN
            IF AnalysisView."Dimension 2 Code" <> '' THEN
              EXIT('1,6,' + AnalysisView."Dimension 2 Code")
            ELSE
              EXIT(Text007);
          END;
        3:
          BEGIN
            IF AnalysisView."Dimension 3 Code" <> '' THEN
              EXIT('1,6,' + AnalysisView."Dimension 3 Code")
            ELSE
              EXIT(Text008);
          END;
        4:
          BEGIN
            IF AnalysisView."Dimension 4 Code" <> '' THEN
              EXIT('1,6,' + AnalysisView."Dimension 4 Code")
            ELSE
              EXIT(Text009);
          END;
        5:
          BEGIN
            IF AnalysisView."Dimension 1 Code" <> '' THEN
              EXIT('1,5,' + AnalysisView."Dimension 1 Code" + Text010)
            ELSE
              EXIT(Text011);
          END;
        6:
          BEGIN
            IF AnalysisView."Dimension 2 Code" <> '' THEN
              EXIT('1,5,' + AnalysisView."Dimension 2 Code" + Text010)
            ELSE
              EXIT(Text012);
          END;
        7:
          BEGIN
            IF AnalysisView."Dimension 3 Code" <> '' THEN
              EXIT('1,5,' + AnalysisView."Dimension 3 Code" + Text010)
            ELSE
              EXIT(Text013);
          END;
        8:
          BEGIN
            IF AnalysisView."Dimension 4 Code" <> '' THEN
              EXIT('1,5,' + AnalysisView."Dimension 4 Code" + Text010)
            ELSE
              EXIT(Text014);
          END;
      END;
    END;

    LOCAL PROCEDURE GetColLayoutSetup@8();
    BEGIN
      IF "Column Layout Name" <> ColumnLayoutName.Name THEN
        ColumnLayoutName.GET("Column Layout Name");
      IF ColumnLayoutName."Analysis View Name" <> '' THEN BEGIN
        IF ColumnLayoutName."Analysis View Name" <> AnalysisView.Code THEN
          IF NOT AnalysisView.GET(ColumnLayoutName."Analysis View Name") THEN BEGIN
            MESSAGE(
              Text015,
              ColumnLayoutName.TABLECAPTION,AnalysisView.TABLECAPTION,ColumnLayoutName."Analysis View Name",
              ColumnLayoutName.FIELDCAPTION("Analysis View Name"),ColumnLayoutName.TABLECAPTION);
            ColumnLayoutName."Analysis View Name" := '';
            ColumnLayoutName.MODIFY;
          END;
      END;

      IF ColumnLayoutName."Analysis View Name" = '' THEN BEGIN
        IF NOT HasGLSetup THEN BEGIN
          GLSetup.GET;
          HasGLSetup := TRUE;
        END;
        CLEAR(AnalysisView);
        AnalysisView."Dimension 1 Code" := GLSetup."Global Dimension 1 Code";
        AnalysisView."Dimension 2 Code" := GLSetup."Global Dimension 2 Code";
      END;
    END;

    BEGIN
    END.
  }
}

