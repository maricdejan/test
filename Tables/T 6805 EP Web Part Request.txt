OBJECT Table 6805 EP Web Part Request
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               EPFunctions@1000 : Codeunit 6800;
             BEGIN
               IF "Group Code" <> '' THEN
                 EPFunctions.InsertGroupReqTypeTabs(Rec);
             END;

    OnModify=BEGIN
               IF "Defined by System" THEN
                 ERROR(Text004,TABLECAPTION);
             END;

    OnDelete=VAR
               EPGrpReqTypeTables@1000000000 : Record 6811;
               EPSearchConfig@1000 : Record 6839;
             BEGIN
               IF "Defined by System" THEN
                 ERROR(Text005,TABLECAPTION);

               EPGrpReqTypeTables.SETRANGE("Group Code","Group Code");
               EPGrpReqTypeTables.SETRANGE("WP Request Code",Code);
               IF EPGrpReqTypeTables.FIND('-') THEN
                 REPEAT
                   EPGrpReqTypeTables.DELETE(TRUE);
                 UNTIL EPGrpReqTypeTables.NEXT = 0;

               EPSearchConfig.SETCURRENTKEY("Group Code","WP Request Code");
               EPSearchConfig.SETRANGE("Group Code","Group Code");
               EPSearchConfig.SETRANGE("WP Request Code",Code);
               IF EPSearchConfig.FIND('-') THEN
                 REPEAT
                   EPSearchConfig.DELETE(TRUE);
                 UNTIL EPSearchConfig.NEXT = 0;
             END;

    OnRename=BEGIN
               IF "Defined by System" THEN
                 ERROR(Text006,TABLECAPTION);
             END;

    CaptionML=[DEU=EP Webpartanforderung;
               ENU=EP Web Part Request];
    LookupFormID=Form6845;
  }
  FIELDS
  {
    { 1   ;   ;Group Code          ;Code20        ;TableRelation="EP Group".Code;
                                                   CaptionML=[DEU=Gruppencode;
                                                              ENU=Group Code] }
    { 2   ;   ;Code                ;Code20        ;OnValidate=VAR
                                                                EPWebPartRequest@1000 : Record 6805;
                                                              BEGIN
                                                                IF "Group Code" <> '' THEN
                                                                  IF NOT EPWebPartRequest.GET('',Code) THEN
                                                                    ERROR(Text003,FIELDCAPTION(Code),Code);
                                                              END;

                                                   CaptionML=[DEU=Code;
                                                              ENU=Code];
                                                   NotBlank=Yes }
    { 4   ;   ;Description         ;Text30        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description];
                                                   Editable=Yes }
    { 5   ;   ;Editable            ;Boolean       ;OnValidate=BEGIN
                                                                IF Editable THEN
                                                                  IF (Type = Type::List) OR (Type = Type::System) THEN
                                                                    ERROR(Text007,TABLECAPTION,FIELDCAPTION(Type),Type)
                                                              END;

                                                   CaptionML=[DEU=Editierbar;
                                                              ENU=Editable] }
    { 8   ;   ;Type                ;Option        ;OnValidate=VAR
                                                                EPWPRTable@1000 : Record 6811;
                                                              BEGIN
                                                                EPWPRTable.SETRANGE("Group Code",Rec."Group Code");
                                                                EPWPRTable.SETRANGE("WP Request Code",Rec.Code);
                                                                CASE Type OF
                                                                  Type::List:
                                                                    BEGIN
                                                                      TESTFIELD(Editable,FALSE);
                                                                      EPWPRTable.SETRANGE("Header or Line",EPWPRTable."Header or Line"::Header);
                                                                      IF EPWPRTable.FIND('-') THEN BEGIN
                                                                        EPWPRTable."Header or Line" := EPWPRTable."Header or Line"::Line;
                                                                        ERROR(Text001,EPWPRTable."Header or Line",
                                                                          TABLECAPTION,FIELDCAPTION(Type),Type);
                                                                      END;
                                                                    END;
                                                                  Type::Card:
                                                                    BEGIN
                                                                      EPWPRTable.SETRANGE("Header or Line",EPWPRTable."Header or Line"::Line);
                                                                      IF EPWPRTable.FIND('-') THEN BEGIN
                                                                        EPWPRTable."Header or Line" := EPWPRTable."Header or Line"::Header;
                                                                        ERROR(Text001,EPWPRTable."Header or Line",
                                                                          TABLECAPTION,FIELDCAPTION(Type),Type);
                                                                      END;
                                                                    END;
                                                                  Type::System:
                                                                    BEGIN
                                                                      TESTFIELD(Editable,FALSE);
                                                                      IF EPWPRTable.FIND('-') THEN
                                                                        ERROR(Text002,TABLECAPTION,FIELDCAPTION(Type),Type);
                                                                    END;
                                                                END
                                                              END;

                                                   CaptionML=[DEU=Art;
                                                              ENU=Type];
                                                   OptionCaptionML=[DEU=" ,šbersicht,Karte,Kopf + Zeile,System";
                                                                    ENU=" ,List,Card,Header + Line,System"];
                                                   OptionString=[ ,List,Card,Header + Line,System] }
    { 9   ;   ;Defined by System   ;Boolean       ;CaptionML=[DEU=Vom System definiert;
                                                              ENU=Defined by System];
                                                   Editable=No }
    { 10  ;   ;No. of Where-Used Groups;Integer   ;FieldClass=FlowField;
                                                   CalcFormula=Count("EP Web Part Request" WHERE (Group Code=FILTER(<>''),
                                                                                                  Code=FIELD(Code)));
                                                   CaptionML=[DEU=Anzahl Verwendungsgruppen;
                                                              ENU=No. of Where-Used Groups];
                                                   Editable=No }
    { 12  ;   ;Caption ID          ;Integer       ;CaptionML=[DEU=Caption-ID;
                                                              ENU=Caption ID];
                                                   Editable=No }
    { 13  ;   ;Caption             ;Text80        ;CaptionML=[DEU=Caption;
                                                              ENU=Caption];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Group Code,Code                         ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Code,Description                         }
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'DEU=%2 von %3 %4: Es kann nur eine %1-Tabelle definiert werden.;ENU=Only a %1 Table can be defined for a %2 of %3 %4.';
      Text002@1000 : TextConst 'DEU=%1 von %2 %3: Es k”nnen keine Tabellen definiert werden.;ENU=No Tables can be defined for a %1 of %2 %3.';
      Text003@1002 : TextConst 'DEU=Es wurde keine Vorlage von %1 %2 definiert.;ENU=No template of %1 %2 has been defined.';
      Text004@1003 : TextConst 'DEU=Vom System definiert: %1 kann nicht ge„ndert werden.;ENU=A %1 Defined By System cannot be modified.';
      Text005@1004 : TextConst 'DEU=Vom System definiert: %1 kann nicht gel”scht werden.;ENU=A %1 Defined By System cannot be deleted.';
      Text006@1005 : TextConst 'DEU=Vom System definiert: %1 kann nicht umbenannt werden.;ENU=A %1 Defined By System cannot be renamed.';
      Text007@1006 : TextConst 'DEU=Ein(e) %1 von %2 %3 kann nicht bearbeitbar sein.;ENU=A %1 of %2 %3 cannot be editable.';

    BEGIN
    END.
  }
}

