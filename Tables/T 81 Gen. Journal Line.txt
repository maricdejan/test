OBJECT Table 81 Gen. Journal Line
{
  OBJECT-PROPERTIES
  {
    Date=11.12.10;
    Time=12:00:00;
    Version List=NAVW16.00.10,NAVDACH5.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               JnlLineDim@1000 : Record 356;
             BEGIN
               JnlLineDim.LOCKTABLE;
               GenJnlAlloc.LOCKTABLE;
               LOCKTABLE;
               GenJnlTemplate.GET("Journal Template Name");
               GenJnlBatch.GET("Journal Template Name","Journal Batch Name");
               "Check Printed" := FALSE;

               ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
               ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
               DimMgt.InsertJnlLineDim(
                 DATABASE::"Gen. Journal Line",
                 "Journal Template Name","Journal Batch Name","Line No.",0,
                 "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
             END;

    OnModify=BEGIN
               TESTFIELD("Check Printed",FALSE);
               IF ("Applies-to ID" = '') AND (xRec."Applies-to ID" <> '') THEN
                 ClearCustVendApplnEntry;
             END;

    OnDelete=VAR
               JnlLineDim@1000 : Record 356;
             BEGIN
               CheckNoCardTransactEntryExist(Rec);

               TESTFIELD("Check Printed",FALSE);

               ClearCustVendApplnEntry;

               JnlLineDim.SETRANGE("Table ID",DATABASE::"Gen. Jnl. Allocation");
               JnlLineDim.SETRANGE("Journal Template Name","Journal Template Name");
               JnlLineDim.SETRANGE("Journal Batch Name","Journal Batch Name");
               JnlLineDim.SETRANGE("Journal Line No.","Line No.");
               JnlLineDim.DELETEALL;

               GenJnlAlloc.SETRANGE("Journal Template Name","Journal Template Name");
               GenJnlAlloc.SETRANGE("Journal Batch Name","Journal Batch Name");
               GenJnlAlloc.SETRANGE("Journal Line No.","Line No.");
               GenJnlAlloc.DELETEALL;

               DimMgt.DeleteJnlLineDim(
                 DATABASE::"Gen. Journal Line",
                 "Journal Template Name","Journal Batch Name","Line No.",0);
             END;

    OnRename=BEGIN
               CheckNoCardTransactEntryExist(Rec);
               TESTFIELD("Check Printed",FALSE);
             END;

    CaptionML=[DEU=Fibu Buch.-Blattzeile;
               ENU=Gen. Journal Line];
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Gen. Journal Template";
                                                   CaptionML=[DEU=Buch.-Blattvorlagenname;
                                                              ENU=Journal Template Name] }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[DEU=Zeilennr.;
                                                              ENU=Line No.] }
    { 3   ;   ;Account Type        ;Option        ;OnValidate=BEGIN
                                                                IF ("Account Type" IN ["Account Type"::Customer,"Account Type"::Vendor,"Account Type"::"Fixed Asset",
                                                                    "Account Type"::"IC Partner"]) AND
                                                                   ("Bal. Account Type" IN ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor,"Bal. Account Type"::"Fixed Asset",
                                                                    "Bal. Account Type"::"IC Partner"])
                                                                THEN
                                                                  ERROR(
                                                                    Text000,
                                                                    FIELDCAPTION("Account Type"),FIELDCAPTION("Bal. Account Type"));
                                                                VALIDATE("Account No.",'');
                                                                IF "Account Type" IN ["Account Type"::Customer,"Account Type"::Vendor,"Account Type"::"Bank Account"] THEN BEGIN
                                                                  VALIDATE("Gen. Posting Type","Gen. Posting Type"::" ");
                                                                  VALIDATE("Gen. Bus. Posting Group",'');
                                                                  VALIDATE("Gen. Prod. Posting Group",'');
                                                                END ELSE
                                                                  IF "Bal. Account Type" IN [
                                                                     "Bal. Account Type"::"G/L Account","Account Type"::"Bank Account","Bal. Account Type"::"Fixed Asset"]
                                                                  THEN
                                                                    VALIDATE("Payment Terms Code",'');
                                                                UpdateSource;

                                                                IF ("Account Type" <> "Account Type"::"Fixed Asset") AND
                                                                   ("Bal. Account Type" <> "Bal. Account Type"::"Fixed Asset")
                                                                THEN BEGIN
                                                                  "Depreciation Book Code" := '';
                                                                  VALIDATE("FA Posting Type","FA Posting Type"::" ");
                                                                END;
                                                                IF xRec."Account Type" IN
                                                                   [xRec."Account Type"::Customer,xRec."Account Type"::Vendor]
                                                                THEN BEGIN
                                                                  "Bill-to/Pay-to No." := '';
                                                                  "Ship-to/Order Address Code" := '';
                                                                  "Sell-to/Buy-from No." := '';
                                                                END;

                                                                IF ("Account Type" = "Account Type"::"IC Partner") AND
                                                                   ("Bal. Account Type" = "Bal. Account Type"::"G/L Account") AND
                                                                   GLAcc.GET("Bal. Account No.")
                                                                THEN
                                                                  "IC Partner G/L Acc. No." := GLAcc."Default IC Partner G/L Acc. No"
                                                                ELSE
                                                                  "IC Partner G/L Acc. No." := '';

                                                                IF "Journal Template Name" <> '' THEN
                                                                  IF ("Account Type" = "Account Type"::"IC Partner") THEN BEGIN
                                                                    GetTemplate;
                                                                    IF GenJnlTemplate.Type <> GenJnlTemplate.Type::Intercompany THEN
                                                                      FIELDERROR("Account Type");
                                                                  END;
                                                                IF "Account Type" <> "Account Type"::Customer THEN
                                                                  VALIDATE("Credit Card No.",'');
                                                              END;

                                                   CaptionML=[DEU=Kontoart;
                                                              ENU=Account Type];
                                                   OptionCaptionML=[DEU=Sachkonto,Debitor,Kreditor,Bankkonto,Anlage,IC-Partner;
                                                                    ENU=G/L Account,Customer,Vendor,Bank Account,Fixed Asset,IC Partner];
                                                   OptionString=G/L Account,Customer,Vendor,Bank Account,Fixed Asset,IC Partner }
    { 4   ;   ;Account No.         ;Code20        ;TableRelation=IF (Account Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Account Type=CONST(Customer)) Customer
                                                                 ELSE IF (Account Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Account Type=CONST(Bank Account)) "Bank Account"
                                                                 ELSE IF (Account Type=CONST(Fixed Asset)) "Fixed Asset"
                                                                 ELSE IF (Account Type=CONST(IC Partner)) "IC Partner";
                                                   OnValidate=BEGIN
                                                                VALIDATE("Job No.",'');
                                                                IF "Account No." = '' THEN BEGIN
                                                                  UpdateLineBalance;
                                                                  UpdateSource;
                                                                  CreateDim(
                                                                    DimMgt.TypeToTableID1("Account Type"),"Account No.",
                                                                    DimMgt.TypeToTableID1("Bal. Account Type"),"Bal. Account No.",
                                                                    DATABASE::Job,"Job No.",
                                                                    DATABASE::"Salesperson/Purchaser","Salespers./Purch. Code",
                                                                    DATABASE::Campaign,"Campaign No.");
                                                                  IF xRec."Account No." <> '' THEN BEGIN
                                                                    "Gen. Posting Type" := "Gen. Posting Type"::" ";
                                                                    "Gen. Bus. Posting Group" := '';
                                                                    "Gen. Prod. Posting Group" := '';
                                                                    "VAT Bus. Posting Group" := '';
                                                                    "VAT Prod. Posting Group" := '';
                                                                    "Tax Area Code" := '';
                                                                    "Tax Liable" := FALSE;
                                                                    "Tax Group Code" := '';
                                                                  END;
                                                                  EXIT;
                                                                END;

                                                                IF "Account Type" IN ["Account Type"::Customer,"Account Type"::Vendor,"Account Type"::"IC Partner"] THEN
                                                                  "IC Partner Code" := '';

                                                                CASE "Account Type" OF
                                                                  "Account Type"::"G/L Account":
                                                                    BEGIN
                                                                      GLAcc.GET("Account No.");
                                                                      CheckGLAcc;
                                                                      ReplaceInfo := "Bal. Account No." = '';
                                                                      IF NOT ReplaceInfo THEN BEGIN
                                                                        GenJnlBatch.GET("Journal Template Name","Journal Batch Name");
                                                                        ReplaceInfo := GenJnlBatch."Bal. Account No." <> '';
                                                                      END;
                                                                      IF ReplaceInfo THEN BEGIN
                                                                        Description := GLAcc.Name;
                                                                      END;
                                                                      IF ("Bal. Account No." = '') OR
                                                                         ("Bal. Account Type" IN
                                                                          ["Bal. Account Type"::"G/L Account","Bal. Account Type"::"Bank Account"])
                                                                      THEN BEGIN
                                                                        "Posting Group" := '';
                                                                        "Salespers./Purch. Code" := '';
                                                                        "Payment Terms Code" := '';
                                                                      END;
                                                                      IF "Bal. Account No." = '' THEN
                                                                        "Currency Code" := '';
                                                                      IF NOT GenJnlBatch.GET("Journal Template Name","Journal Batch Name") OR
                                                                         GenJnlBatch."Copy VAT Setup to Jnl. Lines"
                                                                      THEN BEGIN
                                                                        "Gen. Posting Type" := GLAcc."Gen. Posting Type";
                                                                        "Gen. Bus. Posting Group" := GLAcc."Gen. Bus. Posting Group";
                                                                        "Gen. Prod. Posting Group" := GLAcc."Gen. Prod. Posting Group";
                                                                        "VAT Bus. Posting Group" := GLAcc."VAT Bus. Posting Group";
                                                                        "VAT Prod. Posting Group" := GLAcc."VAT Prod. Posting Group";
                                                                      END;
                                                                      "Tax Area Code" := GLAcc."Tax Area Code";
                                                                      "Tax Liable" := GLAcc."Tax Liable";
                                                                      "Tax Group Code" := GLAcc."Tax Group Code";
                                                                      IF "Posting Date" <> 0D THEN
                                                                        IF "Posting Date" = CLOSINGDATE("Posting Date") THEN BEGIN
                                                                          "Gen. Posting Type" := 0;
                                                                          "Gen. Bus. Posting Group" := '';
                                                                          "Gen. Prod. Posting Group" := '';
                                                                          "VAT Bus. Posting Group" := '';
                                                                          "VAT Prod. Posting Group" := '';
                                                                        END;
                                                                    END;
                                                                  "Account Type"::Customer:
                                                                    BEGIN
                                                                      Cust.GET("Account No.");
                                                                      Cust.CheckBlockedCustOnJnls(Cust,"Document Type", FALSE);
                                                                      IF (Cust."IC Partner Code" <> '') THEN BEGIN
                                                                        IF GenJnlTemplate.GET("Journal Template Name") THEN;
                                                                        IF (Cust."IC Partner Code" <> '' ) AND (ICPartner.GET(Cust."IC Partner Code")) THEN BEGIN
                                                                          ICPartner.CheckICPartnerIndirect(FORMAT("Account Type"),"Account No.");
                                                                          "IC Partner Code" := Cust."IC Partner Code";
                                                                        END;
                                                                      END;
                                                                      Description := Cust.Name;
                                                                      "Posting Group" := Cust."Customer Posting Group";
                                                                      "Salespers./Purch. Code" := Cust."Salesperson Code";
                                                                      "Payment Terms Code" := Cust."Payment Terms Code";
                                                                      VALIDATE("Bill-to/Pay-to No.","Account No.");
                                                                      VALIDATE("Sell-to/Buy-from No.", "Account No.");
                                                                      IF SetCurrencyCode("Bal. Account Type","Bal. Account No.") THEN
                                                                        Cust.TESTFIELD("Currency Code","Currency Code")
                                                                      ELSE
                                                                        "Currency Code" := Cust."Currency Code";
                                                                      "Gen. Posting Type" := 0;
                                                                      "Gen. Bus. Posting Group" := '';
                                                                      "Gen. Prod. Posting Group" := '';
                                                                      "VAT Bus. Posting Group" := '';
                                                                      "VAT Prod. Posting Group" := '';
                                                                      IF (Cust."Bill-to Customer No." <> '') AND (Cust."Bill-to Customer No." <> "Account No.") THEN BEGIN
                                                                        OK := CONFIRM(Text014,FALSE,Cust.TABLECAPTION,Cust."No.",Cust.FIELDCAPTION("Bill-to Customer No."),
                                                                        Cust."Bill-to Customer No.");
                                                                        IF NOT OK THEN
                                                                          ERROR('');
                                                                      END;
                                                                      VALIDATE("Payment Terms Code");
                                                                    END;
                                                                  "Account Type"::Vendor:
                                                                    BEGIN
                                                                      Vend.GET("Account No.");
                                                                      Vend.CheckBlockedVendOnJnls(Vend,"Document Type",FALSE);
                                                                      IF (Vend."IC Partner Code" <> '') THEN BEGIN
                                                                        IF GenJnlTemplate.GET("Journal Template Name") THEN;
                                                                        IF (Vend."IC Partner Code" <> '') AND (ICPartner.GET(Vend."IC Partner Code")) THEN BEGIN
                                                                          ICPartner.CheckICPartnerIndirect(FORMAT("Account Type"),"Account No.");
                                                                          "IC Partner Code" := Vend."IC Partner Code";
                                                                        END;
                                                                      END;
                                                                      Description := Vend.Name;
                                                                      "Posting Group" := Vend."Vendor Posting Group";
                                                                      "Salespers./Purch. Code" := Vend."Purchaser Code";
                                                                      "Payment Terms Code" := Vend."Payment Terms Code";
                                                                      VALIDATE("Bill-to/Pay-to No.","Account No.");
                                                                      VALIDATE("Sell-to/Buy-from No.","Account No.");
                                                                      IF SetCurrencyCode("Bal. Account Type","Bal. Account No.") THEN
                                                                        Vend.TESTFIELD("Currency Code","Currency Code")
                                                                      ELSE
                                                                        "Currency Code" := Vend."Currency Code";
                                                                      "Gen. Posting Type" := 0;
                                                                      "Gen. Bus. Posting Group" := '';
                                                                      "Gen. Prod. Posting Group" := '';
                                                                      "VAT Bus. Posting Group" := '';
                                                                      "VAT Prod. Posting Group" := '';
                                                                      IF (Vend."Pay-to Vendor No." <> '') AND (Vend."Pay-to Vendor No." <> "Account No.")  THEN BEGIN
                                                                        OK := CONFIRM(Text014,FALSE,Vend.TABLECAPTION,Vend."No.",Vend.FIELDCAPTION("Pay-to Vendor No."),
                                                                        Vend."Pay-to Vendor No.");
                                                                        IF NOT OK THEN
                                                                          ERROR('');
                                                                      END;
                                                                      VALIDATE("Payment Terms Code");
                                                                    END;
                                                                  "Account Type"::"Bank Account":
                                                                    BEGIN
                                                                      BankAcc.GET("Account No.");
                                                                      BankAcc.TESTFIELD(Blocked,FALSE);
                                                                      ReplaceInfo := "Bal. Account No." = '';
                                                                      IF NOT ReplaceInfo THEN BEGIN
                                                                        GenJnlBatch.GET("Journal Template Name","Journal Batch Name");
                                                                        ReplaceInfo := GenJnlBatch."Bal. Account No." <> '';
                                                                      END;
                                                                      IF ReplaceInfo THEN BEGIN
                                                                        Description := BankAcc.Name;
                                                                      END;
                                                                      IF ("Bal. Account No." = '') OR
                                                                         ("Bal. Account Type" IN
                                                                          ["Bal. Account Type"::"G/L Account","Bal. Account Type"::"Bank Account"])
                                                                      THEN BEGIN
                                                                        "Posting Group" := '';
                                                                        "Salespers./Purch. Code" := '';
                                                                        "Payment Terms Code" := '';
                                                                      END;
                                                                      IF BankAcc."Currency Code" = '' THEN BEGIN
                                                                        IF "Bal. Account No." = '' THEN
                                                                          "Currency Code" := '';
                                                                      END ELSE
                                                                        IF SetCurrencyCode("Bal. Account Type","Bal. Account No.") THEN
                                                                          BankAcc.TESTFIELD("Currency Code","Currency Code")
                                                                        ELSE
                                                                          "Currency Code" := BankAcc."Currency Code";
                                                                      "Gen. Posting Type" := 0;
                                                                      "Gen. Bus. Posting Group" := '';
                                                                      "Gen. Prod. Posting Group" := '';
                                                                      "VAT Bus. Posting Group" := '';
                                                                      "VAT Prod. Posting Group" := '';
                                                                    END;
                                                                  "Account Type"::"Fixed Asset":
                                                                    BEGIN
                                                                      FA.GET("Account No.");
                                                                      FA.TESTFIELD(FA.Blocked,FALSE);
                                                                      FA.TESTFIELD(FA.Inactive,FALSE);
                                                                      FA.TESTFIELD(FA."Budgeted Asset",FALSE);
                                                                      Description := FA.Description;
                                                                      IF "Depreciation Book Code" = '' THEN BEGIN
                                                                        FASetup.GET;
                                                                        "Depreciation Book Code" := FASetup."Default Depr. Book";
                                                                        IF NOT FADeprBook.GET("Account No.","Depreciation Book Code") THEN
                                                                          "Depreciation Book Code" := '';
                                                                      END;
                                                                      IF "Depreciation Book Code" <> '' THEN BEGIN
                                                                        FADeprBook.GET("Account No.","Depreciation Book Code");
                                                                        "Posting Group" := FADeprBook."FA Posting Group";
                                                                      END;
                                                                      GetFAVATSetup;
                                                                      GetFAAddCurrExchRate;
                                                                    END;
                                                                  "Account Type"::"IC Partner":
                                                                    BEGIN
                                                                      ICPartner.GET("Account No.");
                                                                      ICPartner.CheckICPartner;
                                                                      Description := ICPartner.Name;
                                                                      IF ("Bal. Account No." = '') OR ("Bal. Account Type" = "Bal. Account Type"::"G/L Account") THEN
                                                                        "Currency Code" := ICPartner."Currency Code";
                                                                      IF ("Bal. Account Type" = "Bal. Account Type"::"Bank Account") AND ("Currency Code" = '') THEN
                                                                        "Currency Code" := ICPartner."Currency Code";
                                                                      "Gen. Posting Type" := 0;
                                                                      "Gen. Bus. Posting Group" := '';
                                                                      "Gen. Prod. Posting Group" := '';
                                                                      "VAT Bus. Posting Group" := '';
                                                                      "VAT Prod. Posting Group" := '';
                                                                      "IC Partner Code" := "Account No.";
                                                                    END;
                                                                END;

                                                                VALIDATE("Currency Code");
                                                                VALIDATE("VAT Prod. Posting Group");
                                                                UpdateLineBalance;
                                                                UpdateSource;
                                                                CreateDim(
                                                                  DimMgt.TypeToTableID1("Account Type"),"Account No.",
                                                                  DimMgt.TypeToTableID1("Bal. Account Type"),"Bal. Account No.",
                                                                  DATABASE::Job,"Job No.",
                                                                  DATABASE::"Salesperson/Purchaser","Salespers./Purch. Code",
                                                                  DATABASE::Campaign,"Campaign No.");
                                                              END;

                                                   CaptionML=[DEU=Kontonr.;
                                                              ENU=Account No.] }
    { 5   ;   ;Posting Date        ;Date          ;OnValidate=BEGIN
                                                                VALIDATE("Document Date","Posting Date");
                                                                VALIDATE("Currency Code");

                                                                IF ((Rec."Posting Date" <> xRec."Posting Date") AND (Amount <> 0))  THEN BEGIN
                                                                  ChangePostingDateWarning;
                                                                  PaymentToleranceMgt.PmtTolGenJnl(Rec);
                                                                END;

                                                                ValidateApplyRequirements(Rec);

                                                                IF JobTaskIsSet THEN BEGIN
                                                                  CreateTempJobJnlLine;
                                                                  UpdatePricesFromJobJnlLine;
                                                                END
                                                              END;

                                                   CaptionML=[DEU=Buchungsdatum;
                                                              ENU=Posting Date];
                                                   ClosingDates=Yes }
    { 6   ;   ;Document Type       ;Option        ;OnValidate=BEGIN
                                                                CheckNoCardTransactEntryExist(xRec);

                                                                VALIDATE("Payment Terms Code");
                                                                IF "Account No." <> '' THEN
                                                                  CASE "Account Type" OF
                                                                    "Account Type"::Customer:
                                                                       BEGIN
                                                                         Cust.GET("Account No.");
                                                                         Cust.CheckBlockedCustOnJnls(Cust,"Document Type",FALSE);
                                                                       END;
                                                                    "Account Type"::Vendor:
                                                                       BEGIN
                                                                         Vend.GET("Account No.");
                                                                         Vend.CheckBlockedVendOnJnls(Vend,"Document Type",FALSE);
                                                                       END;
                                                                  END;
                                                                IF "Bal. Account No." <> '' THEN
                                                                  CASE "Bal. Account Type" OF
                                                                    "Account Type"::Customer:
                                                                      BEGIN
                                                                        Cust.GET("Bal. Account No.");
                                                                        Cust.CheckBlockedCustOnJnls(Cust,"Document Type",FALSE);
                                                                      END;
                                                                    "Account Type"::Vendor:
                                                                      BEGIN
                                                                        Vend.GET("Bal. Account No.");
                                                                        Vend.CheckBlockedVendOnJnls(Vend,"Document Type",FALSE);
                                                                      END;
                                                                  END;
                                                                UpdateSalesPurchLCY;
                                                                ValidateApplyRequirements(Rec);
                                                                IF NOT ("Document Type" IN ["Document Type"::Payment,"Document Type"::Refund]) THEN
                                                                  VALIDATE("Credit Card No.",'');
                                                              END;

                                                   CaptionML=[DEU=Belegart;
                                                              ENU=Document Type];
                                                   OptionCaptionML=[DEU=" ,Zahlung,Rechnung,Gutschrift,Zinsrechnung,Mahnung,Erstattung";
                                                                    ENU=" ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund"];
                                                   OptionString=[ ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund] }
    { 7   ;   ;Document No.        ;Code20        ;OnValidate=BEGIN
                                                                CheckNoCardTransactEntryExist(xRec);
                                                              END;

                                                   CaptionML=[DEU=Belegnr.;
                                                              ENU=Document No.] }
    { 8   ;   ;Description         ;Text50        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description] }
    { 10  ;   ;VAT %               ;Decimal       ;OnValidate=BEGIN
                                                                GetCurrency;
                                                                CASE "VAT Calculation Type" OF
                                                                  "VAT Calculation Type"::"Normal VAT",
                                                                  "VAT Calculation Type"::"Reverse Charge VAT":
                                                                    BEGIN
                                                                      "VAT Amount" :=
                                                                        ROUND(Amount * "VAT %" / (100 + "VAT %"),Currency."Amount Rounding Precision",Currency.VATRoundingDirection);
                                                                      "VAT Base Amount" :=
                                                                        ROUND(Amount - "VAT Amount",Currency."Amount Rounding Precision");
                                                                    END;
                                                                  "VAT Calculation Type"::"Full VAT":
                                                                    "VAT Amount" := Amount;
                                                                  "VAT Calculation Type"::"Sales Tax":
                                                                    IF ("Gen. Posting Type" = "Gen. Posting Type"::Purchase) AND
                                                                       "Use Tax"
                                                                    THEN BEGIN
                                                                      "VAT Amount" := 0;
                                                                      "VAT %" := 0;
                                                                    END ELSE BEGIN
                                                                      "VAT Amount" :=
                                                                        Amount -
                                                                        SalesTaxCalculate.ReverseCalculateTax(
                                                                          "Tax Area Code","Tax Group Code","Tax Liable",
                                                                          "Posting Date",Amount,Quantity,"Currency Factor");
                                                                      IF Amount - "VAT Amount" <> 0 THEN
                                                                        "VAT %" := ROUND(100 * "VAT Amount" / (Amount - "VAT Amount"),0.00001)
                                                                      ELSE
                                                                        "VAT %" := 0;
                                                                      "VAT Amount" :=
                                                                        ROUND("VAT Amount",Currency."Amount Rounding Precision");
                                                                    END;
                                                                END;
                                                                "VAT Base Amount" := Amount - "VAT Amount";
                                                                "VAT Difference" := 0;

                                                                IF "Currency Code" = '' THEN
                                                                  "VAT Amount (LCY)" := "VAT Amount"
                                                                ELSE
                                                                  "VAT Amount (LCY)" :=
                                                                    ROUND(
                                                                      CurrExchRate.ExchangeAmtFCYToLCY(
                                                                        "Posting Date","Currency Code",
                                                                        "VAT Amount","Currency Factor"));
                                                                "VAT Base Amount (LCY)" := "Amount (LCY)" - "VAT Amount (LCY)";

                                                                UpdateSalesPurchLCY;
                                                              END;

                                                   CaptionML=[DEU=MwSt. %;
                                                              ENU=VAT %];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   Editable=No }
    { 11  ;   ;Bal. Account No.    ;Code20        ;TableRelation=IF (Bal. Account Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Customer)) Customer
                                                                 ELSE IF (Bal. Account Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Bal. Account Type=CONST(Bank Account)) "Bank Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Fixed Asset)) "Fixed Asset"
                                                                 ELSE IF (Bal. Account Type=CONST(IC Partner)) "IC Partner";
                                                   OnValidate=BEGIN
                                                                VALIDATE("Job No.",'');
                                                                IF "Bal. Account No." = '' THEN BEGIN
                                                                  UpdateLineBalance;
                                                                  UpdateSource;
                                                                  CreateDim(
                                                                    DimMgt.TypeToTableID1("Bal. Account Type"),"Bal. Account No.",
                                                                    DimMgt.TypeToTableID1("Account Type"),"Account No.",
                                                                    DATABASE::Job,"Job No.",
                                                                    DATABASE::"Salesperson/Purchaser","Salespers./Purch. Code",
                                                                    DATABASE::Campaign,"Campaign No.");
                                                                  IF xRec."Bal. Account No." <> '' THEN BEGIN
                                                                    "Bal. Gen. Posting Type" := "Bal. Gen. Posting Type"::" ";
                                                                    "Bal. Gen. Bus. Posting Group" := '';
                                                                    "Bal. Gen. Prod. Posting Group" := '';
                                                                    "Bal. VAT Bus. Posting Group" := '';
                                                                    "Bal. VAT Prod. Posting Group" := '';
                                                                    "Bal. Tax Area Code" := '';
                                                                    "Bal. Tax Liable" := FALSE;
                                                                    "Bal. Tax Group Code" := '';
                                                                  END;
                                                                  EXIT;
                                                                END;
                                                                IF xRec."Bal. Account Type" IN [xRec."Bal. Account Type"::Customer, xRec."Bal. Account Type"::Vendor,
                                                                   xRec."Bal. Account Type"::"IC Partner"]
                                                                THEN
                                                                  "IC Partner Code" := '';

                                                                CASE "Bal. Account Type" OF
                                                                  "Bal. Account Type"::"G/L Account":
                                                                    BEGIN
                                                                      GLAcc.GET("Bal. Account No.");
                                                                      CheckGLAcc;
                                                                      IF "Account No." = '' THEN BEGIN
                                                                        Description := GLAcc.Name;
                                                                        "Currency Code" := '';
                                                                      END;
                                                                      IF ("Account No." = '') OR
                                                                         ("Account Type" IN
                                                                          ["Account Type"::"G/L Account","Account Type"::"Bank Account"])
                                                                      THEN BEGIN
                                                                        "Posting Group" := '';
                                                                        "Salespers./Purch. Code" := '';
                                                                        "Payment Terms Code" := '';
                                                                      END;
                                                                      IF NOT GenJnlBatch.GET("Journal Template Name","Journal Batch Name") OR
                                                                         GenJnlBatch."Copy VAT Setup to Jnl. Lines"
                                                                      THEN BEGIN
                                                                        "Bal. Gen. Posting Type" := GLAcc."Gen. Posting Type";
                                                                        "Bal. Gen. Bus. Posting Group" := GLAcc."Gen. Bus. Posting Group";
                                                                        "Bal. Gen. Prod. Posting Group" := GLAcc."Gen. Prod. Posting Group";
                                                                        "Bal. VAT Bus. Posting Group" := GLAcc."VAT Bus. Posting Group";
                                                                        "Bal. VAT Prod. Posting Group" := GLAcc."VAT Prod. Posting Group";
                                                                      END;
                                                                      "Bal. Tax Area Code" := GLAcc."Tax Area Code";
                                                                      "Bal. Tax Liable" := GLAcc."Tax Liable";
                                                                      "Bal. Tax Group Code" := GLAcc."Tax Group Code";
                                                                      IF "Posting Date" <> 0D THEN
                                                                        IF "Posting Date" = CLOSINGDATE("Posting Date") THEN BEGIN
                                                                          "Bal. Gen. Bus. Posting Group" := '';
                                                                          "Bal. Gen. Prod. Posting Group" := '';
                                                                          "Bal. VAT Bus. Posting Group" := '';
                                                                          "Bal. VAT Prod. Posting Group" := '';
                                                                          "Bal. Gen. Posting Type" := 0;
                                                                        END;
                                                                    END;
                                                                  "Bal. Account Type"::Customer:
                                                                    BEGIN
                                                                      Cust.GET("Bal. Account No.");
                                                                      Cust.CheckBlockedCustOnJnls(Cust,"Document Type",FALSE);
                                                                      IF (Cust."IC Partner Code" <> '') THEN BEGIN
                                                                        IF GenJnlTemplate.GET("Journal Template Name") THEN;
                                                                        IF (Cust."IC Partner Code" <> '') AND (ICPartner.GET(Cust."IC Partner Code")) THEN BEGIN
                                                                          ICPartner.CheckICPartnerIndirect(FORMAT("Bal. Account Type"),"Bal. Account No.");
                                                                          "IC Partner Code" := Cust."IC Partner Code";
                                                                        END;
                                                                      END;

                                                                      IF "Account No." = '' THEN BEGIN
                                                                        Description := Cust.Name;
                                                                      END;
                                                                      "Posting Group" := Cust."Customer Posting Group";
                                                                      "Salespers./Purch. Code" := Cust."Salesperson Code";
                                                                      "Payment Terms Code" := Cust."Payment Terms Code";
                                                                      VALIDATE("Bill-to/Pay-to No.","Bal. Account No.");
                                                                      VALIDATE("Sell-to/Buy-from No.","Bal. Account No.");
                                                                      IF ("Account No." = '') OR ("Account Type" = "Account Type"::"G/L Account") THEN
                                                                        "Currency Code" := Cust."Currency Code";
                                                                      IF ("Account Type" = "Account Type"::"Bank Account") AND ("Currency Code" = '') THEN
                                                                        "Currency Code" := Cust."Currency Code";
                                                                      "Bal. Gen. Posting Type" := 0;
                                                                      "Bal. Gen. Bus. Posting Group" := '';
                                                                      "Bal. Gen. Prod. Posting Group" := '';
                                                                      "Bal. VAT Bus. Posting Group" := '';
                                                                      "Bal. VAT Prod. Posting Group" := '';
                                                                      IF (Cust."Bill-to Customer No." <> '') AND (Cust."Bill-to Customer No." <> "Bal. Account No.") THEN BEGIN
                                                                        OK := CONFIRM(Text014,FALSE,Cust.TABLECAPTION,Cust."No.",Cust.FIELDCAPTION("Bill-to Customer No."),
                                                                        Cust."Bill-to Customer No.");
                                                                        IF NOT OK THEN
                                                                          ERROR('');
                                                                      END;
                                                                      VALIDATE("Payment Terms Code");
                                                                    END;
                                                                  "Bal. Account Type"::Vendor:
                                                                    BEGIN
                                                                      Vend.GET("Bal. Account No.");
                                                                      Vend.CheckBlockedVendOnJnls(Vend,"Document Type",FALSE);
                                                                      IF (Vend."IC Partner Code" <> '') THEN BEGIN
                                                                        IF GenJnlTemplate.GET("Journal Template Name") THEN;
                                                                        IF (Vend."IC Partner Code" <> '') AND (ICPartner.GET(Vend."IC Partner Code")) THEN BEGIN
                                                                          ICPartner.CheckICPartnerIndirect(FORMAT("Bal. Account Type"),"Bal. Account No.");
                                                                          "IC Partner Code" := Vend."IC Partner Code";
                                                                        END;
                                                                      END;

                                                                      IF "Account No." = '' THEN BEGIN
                                                                        Description := Vend.Name;
                                                                      END;
                                                                      "Posting Group" := Vend."Vendor Posting Group";
                                                                      "Salespers./Purch. Code" := Vend."Purchaser Code";
                                                                      "Payment Terms Code" := Vend."Payment Terms Code";
                                                                      VALIDATE("Bill-to/Pay-to No.","Bal. Account No.");
                                                                      VALIDATE("Sell-to/Buy-from No.","Bal. Account No.");
                                                                      IF ("Account No." = '') OR ("Account Type" = "Account Type"::"G/L Account") THEN
                                                                        "Currency Code" := Vend."Currency Code";
                                                                      IF ("Account Type" = "Account Type"::"Bank Account") AND ("Currency Code" = '') THEN
                                                                        "Currency Code" := Vend."Currency Code";
                                                                      "Bal. Gen. Posting Type" := 0;
                                                                      "Bal. Gen. Bus. Posting Group" := '';
                                                                      "Bal. Gen. Prod. Posting Group" := '';
                                                                      "Bal. VAT Bus. Posting Group" := '';
                                                                      "Bal. VAT Prod. Posting Group" := '';
                                                                      IF (Vend."Pay-to Vendor No." <> '') AND (Vend."Pay-to Vendor No." <> "Bal. Account No.")  THEN BEGIN
                                                                        OK := CONFIRM(Text014,FALSE,Vend.TABLECAPTION,Vend."No.",Vend.FIELDCAPTION("Pay-to Vendor No."),
                                                                        Vend."Pay-to Vendor No.");
                                                                        IF NOT OK THEN
                                                                          ERROR('');
                                                                      END;
                                                                      VALIDATE("Payment Terms Code");
                                                                    END;
                                                                  "Bal. Account Type"::"Bank Account":
                                                                    BEGIN
                                                                      BankAcc.GET("Bal. Account No.");
                                                                      BankAcc.TESTFIELD(Blocked,FALSE);
                                                                      IF "Account No." = '' THEN BEGIN
                                                                        Description := BankAcc.Name;
                                                                      END;
                                                                      IF ("Account No." = '') OR
                                                                         ("Account Type" IN
                                                                          ["Account Type"::"G/L Account","Account Type"::"Bank Account"])
                                                                      THEN BEGIN
                                                                        "Posting Group" := '';
                                                                        "Salespers./Purch. Code" := '';
                                                                        "Payment Terms Code" := '';
                                                                      END;
                                                                      IF BankAcc."Currency Code" = '' THEN BEGIN
                                                                        IF "Account No." = '' THEN
                                                                          "Currency Code" := '';
                                                                      END ELSE
                                                                        IF SetCurrencyCode("Bal. Account Type","Bal. Account No.") THEN
                                                                          BankAcc.TESTFIELD("Currency Code","Currency Code")
                                                                        ELSE
                                                                          "Currency Code" := BankAcc."Currency Code";
                                                                      "Bal. Gen. Posting Type" := 0;
                                                                      "Bal. Gen. Bus. Posting Group" := '';
                                                                      "Bal. Gen. Prod. Posting Group" := '';
                                                                      "Bal. VAT Bus. Posting Group" := '';
                                                                      "Bal. VAT Prod. Posting Group" := '';
                                                                    END;
                                                                  "Bal. Account Type"::"Fixed Asset":
                                                                    BEGIN
                                                                      FA.GET("Bal. Account No.");
                                                                      FA.TESTFIELD(FA.Blocked,FALSE);
                                                                      FA.TESTFIELD(FA.Inactive,FALSE);
                                                                      FA.TESTFIELD(FA."Budgeted Asset",FALSE);
                                                                      IF "Account No." = '' THEN BEGIN
                                                                        Description := FA.Description;
                                                                      END;
                                                                      IF "Depreciation Book Code" = '' THEN BEGIN
                                                                        FASetup.GET;
                                                                        "Depreciation Book Code" := FASetup."Default Depr. Book";
                                                                        IF NOT FADeprBook.GET("Bal. Account No.","Depreciation Book Code") THEN
                                                                          "Depreciation Book Code" := '';
                                                                      END;
                                                                      IF "Depreciation Book Code" <> '' THEN BEGIN
                                                                        FADeprBook.GET("Bal. Account No.","Depreciation Book Code");
                                                                        "Posting Group" := FADeprBook."FA Posting Group";
                                                                      END;
                                                                      GetFAVATSetup;
                                                                      GetFAAddCurrExchRate;
                                                                    END;
                                                                  "Bal. Account Type"::"IC Partner":
                                                                    BEGIN
                                                                      ICPartner.GET("Bal. Account No.");
                                                                      IF "Account No." = '' THEN BEGIN
                                                                        Description := ICPartner.Name;
                                                                      END;
                                                                      IF ("Account No." = '') OR ("Account Type" = "Account Type"::"G/L Account") THEN
                                                                        "Currency Code" := ICPartner."Currency Code";
                                                                      IF ("Account Type" = "Account Type"::"Bank Account") AND ("Currency Code" = '') THEN
                                                                        "Currency Code" := ICPartner."Currency Code";
                                                                      "Bal. Gen. Posting Type" := 0;
                                                                      "Bal. Gen. Bus. Posting Group" := '';
                                                                      "Bal. Gen. Prod. Posting Group" := '';
                                                                      "Bal. VAT Bus. Posting Group" := '';
                                                                      "Bal. VAT Prod. Posting Group" := '';
                                                                      "IC Partner Code" := "Bal. Account No.";
                                                                    END;

                                                                END;

                                                                VALIDATE("Currency Code");
                                                                VALIDATE("Bal. VAT Prod. Posting Group");
                                                                UpdateLineBalance;
                                                                UpdateSource;
                                                                CreateDim(
                                                                  DimMgt.TypeToTableID1("Bal. Account Type"),"Bal. Account No.",
                                                                  DimMgt.TypeToTableID1("Account Type"),"Account No.",
                                                                  DATABASE::Job,"Job No.",
                                                                  DATABASE::"Salesperson/Purchaser","Salespers./Purch. Code",
                                                                  DATABASE::Campaign,"Campaign No.");

                                                                IF ("Account Type" = "Account Type"::"IC Partner") AND
                                                                   ("Bal. Account Type" = "Bal. Account Type"::"G/L Account")
                                                                THEN
                                                                  "IC Partner G/L Acc. No." := GLAcc."Default IC Partner G/L Acc. No";
                                                              END;

                                                   CaptionML=[DEU=Gegenkontonr.;
                                                              ENU=Bal. Account No.] }
    { 12  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   OnValidate=BEGIN
                                                                IF "Bal. Account Type" = "Bal. Account Type"::"Bank Account" THEN BEGIN
                                                                  IF BankAcc3.GET("Bal. Account No.") AND (BankAcc3."Currency Code" <> '')THEN
                                                                    BankAcc3.TESTFIELD("Currency Code","Currency Code");
                                                                END;
                                                                IF "Account Type" = "Account Type"::"Bank Account" THEN BEGIN
                                                                  IF BankAcc3.GET("Account No.") AND (BankAcc3."Currency Code" <> '') THEN
                                                                    BankAcc3.TESTFIELD("Currency Code","Currency Code");
                                                                END;
                                                                IF ("Recurring Method" IN
                                                                    ["Recurring Method"::"B  Balance","Recurring Method"::"RB Reversing Balance"]) AND
                                                                   ("Currency Code" <> '')
                                                                THEN
                                                                  ERROR(
                                                                    Text001,
                                                                    FIELDCAPTION("Currency Code"),FIELDCAPTION("Recurring Method"),"Recurring Method");

                                                                IF "Currency Code" <> '' THEN BEGIN
                                                                  GetCurrency;
                                                                  IF ("Currency Code" <> xRec."Currency Code") OR
                                                                     ("Posting Date" <> xRec."Posting Date") OR
                                                                     (CurrFieldNo = FIELDNO("Currency Code")) OR
                                                                     ("Currency Factor" = 0)
                                                                  THEN
                                                                    "Currency Factor" :=
                                                                      CurrExchRate.ExchangeRate("Posting Date","Currency Code");
                                                                END ELSE
                                                                  "Currency Factor" := 0;
                                                                VALIDATE("Currency Factor");

                                                                IF (("Currency Code" <> xRec."Currency Code") AND (Amount <> 0)) THEN
                                                                  PaymentToleranceMgt.PmtTolGenJnl(Rec);
                                                              END;

                                                   CaptionML=[DEU=Whrungscode;
                                                              ENU=Currency Code] }
    { 13  ;   ;Amount              ;Decimal       ;OnValidate=BEGIN
                                                                GetCurrency;
                                                                IF "Currency Code" = '' THEN
                                                                  "Amount (LCY)" := Amount
                                                                ELSE
                                                                  "Amount (LCY)" := ROUND(
                                                                    CurrExchRate.ExchangeAmtFCYToLCY(
                                                                      "Posting Date","Currency Code",
                                                                      Amount,"Currency Factor"));

                                                                Amount := ROUND(Amount,Currency."Amount Rounding Precision");
                                                                IF (CurrFieldNo <> 0) AND
                                                                   (CurrFieldNo <> FIELDNO("Applies-to Doc. No.")) AND
                                                                   ((("Account Type" = "Account Type"::Customer) AND
                                                                     ("Account No." <> '') AND (Amount > 0) AND
                                                                     (CurrFieldNo <> FIELDNO("Bal. Account No."))) OR
                                                                    (("Bal. Account Type" = "Bal. Account Type"::Customer) AND
                                                                     ("Bal. Account No." <> '') AND (Amount < 0) AND
                                                                     (CurrFieldNo <> FIELDNO("Account No."))))
                                                                THEN
                                                                  CustCheckCreditLimit.GenJnlLineCheck(Rec);

                                                                VALIDATE("VAT %");
                                                                VALIDATE("Bal. VAT %");
                                                                UpdateLineBalance;

                                                                IF ((Rec.Amount <> xRec.Amount))  THEN BEGIN
                                                                  IF ("Applies-to Doc. No." <> '') OR ("Applies-to ID" <> '') THEN
                                                                    SetApplyToAmount;
                                                                  PaymentToleranceMgt.PmtTolGenJnl(Rec);
                                                                END;

                                                                IF JobTaskIsSet THEN BEGIN
                                                                  CreateTempJobJnlLine;
                                                                  UpdatePricesFromJobJnlLine;
                                                                END
                                                              END;

                                                   CaptionML=[DEU=Betrag;
                                                              ENU=Amount];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 14  ;   ;Debit Amount        ;Decimal       ;OnValidate=BEGIN
                                                                GetCurrency;
                                                                "Debit Amount" := ROUND("Debit Amount",Currency."Amount Rounding Precision");
                                                                Correction := "Debit Amount" < 0;
                                                                Amount := "Debit Amount";
                                                                VALIDATE(Amount);
                                                              END;

                                                   CaptionML=[DEU=Sollbetrag;
                                                              ENU=Debit Amount];
                                                   BlankZero=Yes;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 15  ;   ;Credit Amount       ;Decimal       ;OnValidate=BEGIN
                                                                GetCurrency;
                                                                "Credit Amount" := ROUND("Credit Amount",Currency."Amount Rounding Precision");
                                                                Correction := "Credit Amount" < 0;
                                                                Amount := -"Credit Amount";
                                                                VALIDATE(Amount);
                                                              END;

                                                   CaptionML=[DEU=Habenbetrag;
                                                              ENU=Credit Amount];
                                                   BlankZero=Yes;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 16  ;   ;Amount (LCY)        ;Decimal       ;OnValidate=BEGIN
                                                                IF "Currency Code" = '' THEN BEGIN
                                                                  Amount := "Amount (LCY)";
                                                                  VALIDATE(Amount);
                                                                END ELSE BEGIN
                                                                  IF CheckFixedCurrency THEN BEGIN
                                                                    GetCurrency;
                                                                    Amount := ROUND(
                                                                      CurrExchRate.ExchangeAmtLCYToFCY(
                                                                        "Posting Date","Currency Code",
                                                                        "Amount (LCY)","Currency Factor"),
                                                                        Currency."Amount Rounding Precision")
                                                                  END ELSE BEGIN
                                                                    TESTFIELD("Amount (LCY)");
                                                                    TESTFIELD(Amount);
                                                                    "Currency Factor" := Amount / "Amount (LCY)";
                                                                  END;

                                                                  VALIDATE("VAT %");
                                                                  VALIDATE("Bal. VAT %");
                                                                  UpdateLineBalance;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Betrag (MW);
                                                              ENU=Amount (LCY)];
                                                   AutoFormatType=1 }
    { 17  ;   ;Balance (LCY)       ;Decimal       ;CaptionML=[DEU=Saldo (MW);
                                                              ENU=Balance (LCY)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 18  ;   ;Currency Factor     ;Decimal       ;OnValidate=BEGIN
                                                                IF ("Currency Code" = '') AND ("Currency Factor" <> 0) THEN
                                                                  FIELDERROR("Currency Factor",STRSUBSTNO(Text002,FIELDCAPTION("Currency Code")));
                                                                VALIDATE(Amount);
                                                              END;

                                                   CaptionML=[DEU=Whrungsfaktor;
                                                              ENU=Currency Factor];
                                                   DecimalPlaces=0:15;
                                                   MinValue=0;
                                                   Editable=No }
    { 19  ;   ;Sales/Purch. (LCY)  ;Decimal       ;CaptionML=[DEU=Verkauf/Einkauf (MW);
                                                              ENU=Sales/Purch. (LCY)];
                                                   AutoFormatType=1 }
    { 20  ;   ;Profit (LCY)        ;Decimal       ;CaptionML=[DEU=DB (MW);
                                                              ENU=Profit (LCY)];
                                                   AutoFormatType=1 }
    { 21  ;   ;Inv. Discount (LCY) ;Decimal       ;CaptionML=[DEU=Rechnungsrabatt (MW);
                                                              ENU=Inv. Discount (LCY)];
                                                   AutoFormatType=1 }
    { 22  ;   ;Bill-to/Pay-to No.  ;Code20        ;TableRelation=IF (Account Type=CONST(Customer)) Customer
                                                                 ELSE IF (Bal. Account Type=CONST(Customer)) Customer
                                                                 ELSE IF (Account Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Bal. Account Type=CONST(Vendor)) Vendor;
                                                   OnValidate=BEGIN
                                                                IF Rec."Bill-to/Pay-to No." <> xRec."Bill-to/Pay-to No." THEN
                                                                  "Ship-to/Order Address Code" := '';
                                                                GLSetup.GET;
                                                                IF GLSetup."Bill-to/Sell-to VAT Calc." = GLSetup."Bill-to/Sell-to VAT Calc."::"Bill-to/Pay-to No." THEN
                                                                  UpdateCountryCodeAndVATRegNo("Bill-to/Pay-to No.");
                                                              END;

                                                   CaptionML=[DEU=Rech. an/Zahlung an Nr.;
                                                              ENU=Bill-to/Pay-to No.] }
    { 23  ;   ;Posting Group       ;Code10        ;TableRelation=IF (Account Type=CONST(Customer)) "Customer Posting Group"
                                                                 ELSE IF (Account Type=CONST(Vendor)) "Vendor Posting Group"
                                                                 ELSE IF (Account Type=CONST(Fixed Asset)) "FA Posting Group";
                                                   CaptionML=[DEU=Buchungsgruppe;
                                                              ENU=Posting Group];
                                                   Editable=No }
    { 24  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                              END;

                                                   CaptionML=[DEU=Shortcutdimensionscode 1;
                                                              ENU=Shortcut Dimension 1 Code];
                                                   CaptionClass='1,2,1' }
    { 25  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                              END;

                                                   CaptionML=[DEU=Shortcutdimensionscode 2;
                                                              ENU=Shortcut Dimension 2 Code];
                                                   CaptionClass='1,2,2' }
    { 26  ;   ;Salespers./Purch. Code;Code10      ;TableRelation=Salesperson/Purchaser;
                                                   OnValidate=BEGIN
                                                                CreateDim(
                                                                  DATABASE::"Salesperson/Purchaser","Salespers./Purch. Code",
                                                                  DimMgt.TypeToTableID1("Account Type"),"Account No.",
                                                                  DimMgt.TypeToTableID1("Bal. Account Type"),"Bal. Account No.",
                                                                  DATABASE::Job,"Job No.",
                                                                  DATABASE::Campaign,"Campaign No.");
                                                              END;

                                                   CaptionML=[DEU=Verk.-/Einkufercode;
                                                              ENU=Salespers./Purch. Code] }
    { 29  ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   CaptionML=[DEU=Herkunftscode;
                                                              ENU=Source Code];
                                                   Editable=No }
    { 30  ;   ;System-Created Entry;Boolean       ;CaptionML=[DEU=Systembuchung;
                                                              ENU=System-Created Entry];
                                                   Editable=No }
    { 34  ;   ;On Hold             ;Code3         ;CaptionML=[DEU=Abwarten;
                                                              ENU=On Hold] }
    { 35  ;   ;Applies-to Doc. Type;Option        ;CaptionML=[DEU=Ausgleich mit Belegart;
                                                              ENU=Applies-to Doc. Type];
                                                   OptionCaptionML=[DEU=" ,Zahlung,Rechnung,Gutschrift,Zinsrechnung,Mahnung,Erstattung";
                                                                    ENU=" ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund"];
                                                   OptionString=[ ,Payment,Invoice,Credit Memo,Finance Charge Memo,Reminder,Refund] }
    { 36  ;   ;Applies-to Doc. No. ;Code20        ;OnValidate=VAR
                                                                CustLedgEntry@1000 : Record 21;
                                                                VendLedgEntry@1003 : Record 25;
                                                                TempGenJnlLine@1001 : TEMPORARY Record 81;
                                                              BEGIN
                                                                IF ("Applies-to Doc. No." = '') AND (xRec."Applies-to Doc. No." <> '') THEN BEGIN
                                                                  PaymentToleranceMgt.DelPmtTolApllnDocNo(Rec,xRec."Applies-to Doc. No.");

                                                                  TempGenJnlLine := Rec;
                                                                  IF (TempGenJnlLine."Bal. Account Type" = TempGenJnlLine."Bal. Account Type"::Customer) OR
                                                                    (TempGenJnlLine."Bal. Account Type" = TempGenJnlLine."Bal. Account Type"::Vendor)
                                                                  THEN
                                                                    CODEUNIT.RUN(CODEUNIT::"Exchange Acc. G/L Journal Line",TempGenJnlLine);

                                                                  IF TempGenJnlLine."Account Type" = TempGenJnlLine."Account Type"::Customer THEN BEGIN
                                                                    CustLedgEntry.SETCURRENTKEY("Document No.");
                                                                    CustLedgEntry.SETRANGE("Document No.",xRec."Applies-to Doc. No.");
                                                                    IF NOT(xRec."Applies-to Doc. Type" = "Document Type"::" ") THEN
                                                                      CustLedgEntry.SETRANGE("Document Type",xRec."Applies-to Doc. Type");
                                                                    CustLedgEntry.SETRANGE("Customer No.",TempGenJnlLine."Account No.");
                                                                    CustLedgEntry.SETRANGE(Open,TRUE);
                                                                    IF CustLedgEntry.FIND('-') THEN BEGIN
                                                                      IF CustLedgEntry."Amount to Apply" <> 0 THEN  BEGIN
                                                                        CustLedgEntry."Amount to Apply" := 0;
                                                                        CODEUNIT.RUN(CODEUNIT::"Cust. Entry-Edit",CustLedgEntry);
                                                                      END;
                                                                    END;
                                                                  END ELSE IF TempGenJnlLine."Account Type" = TempGenJnlLine."Account Type"::Vendor THEN BEGIN
                                                                    VendLedgEntry.SETCURRENTKEY("Document No.");
                                                                    VendLedgEntry.SETRANGE("Document No.",xRec."Applies-to Doc. No.");
                                                                    IF NOT(xRec."Applies-to Doc. Type" = "Document Type"::" ") THEN
                                                                      VendLedgEntry.SETRANGE("Document Type",xRec."Applies-to Doc. Type");
                                                                    VendLedgEntry.SETRANGE("Vendor No.",TempGenJnlLine."Account No.");
                                                                    VendLedgEntry.SETRANGE(Open,TRUE);
                                                                    IF VendLedgEntry.FIND('-') THEN BEGIN
                                                                      IF VendLedgEntry."Amount to Apply" <> 0 THEN  BEGIN
                                                                        VendLedgEntry."Amount to Apply" := 0;
                                                                        CODEUNIT.RUN(CODEUNIT::"Vend. Entry-Edit",VendLedgEntry);
                                                                      END;
                                                                    END;
                                                                  END;

                                                                END;

                                                                IF (("Applies-to Doc. No." <> xRec."Applies-to Doc. No.") AND (Amount <> 0)) THEN BEGIN
                                                                  IF xRec."Applies-to Doc. No." <> '' THEN
                                                                    PaymentToleranceMgt.DelPmtTolApllnDocNo(Rec,xRec."Applies-to Doc. No.");
                                                                  SetApplyToAmount;
                                                                  PaymentToleranceMgt.PmtTolGenJnl(Rec);
                                                                END;

                                                                GetCustLedgerEntry;
                                                                ValidateApplyRequirements(Rec);

                                                                GetCreditCard;
                                                              END;

                                                   OnLookup=VAR
                                                              GenJnlPostLine@1000 : Codeunit 12;
                                                              PaymentToleranceMgt@1001 : Codeunit 426;
                                                              OldAppliesToDocNo@1002 : Code[20];
                                                            BEGIN
                                                              IF xRec."Line No." = 0 THEN
                                                                xRec.Amount := Amount;

                                                              OldAppliesToDocNo := "Applies-to Doc. No.";
                                                              "Applies-to Doc. No." := '';

                                                              IF "Bal. Account Type" IN
                                                                ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor]
                                                              THEN BEGIN
                                                                AccNo := "Bal. Account No.";
                                                                AccType := "Bal. Account Type";
                                                                CLEAR(CustLedgEntry);
                                                                CLEAR(VendLedgEntry);
                                                              END ELSE BEGIN
                                                                AccNo := "Account No.";
                                                                AccType := "Account Type";
                                                                CLEAR(CustLedgEntry);
                                                                CLEAR(VendLedgEntry);
                                                              END;

                                                              xRec."Currency Code" := "Currency Code";
                                                              xRec."Posting Date" := "Posting Date";

                                                              CASE AccType OF
                                                                AccType::Customer:
                                                                  BEGIN
                                                                    CustLedgEntry.SETCURRENTKEY("Customer No.",Open,Positive,"Due Date");
                                                                    CustLedgEntry.SETRANGE("Customer No.",AccNo);
                                                                    CustLedgEntry.SETRANGE(Open,TRUE);
                                                                    IF "Applies-to Doc. No." <> '' THEN BEGIN
                                                                      CustLedgEntry.SETRANGE("Document Type","Applies-to Doc. Type");
                                                                      CustLedgEntry.SETRANGE("Document No.","Applies-to Doc. No.");
                                                                      IF NOT CustLedgEntry.FIND('-') THEN BEGIN
                                                                        CustLedgEntry.SETRANGE("Document Type");
                                                                        CustLedgEntry.SETRANGE("Document No.");
                                                                      END;
                                                                    END;
                                                                    IF "Applies-to ID" <> '' THEN BEGIN
                                                                      CustLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
                                                                      IF NOT CustLedgEntry.FIND('-') THEN
                                                                        CustLedgEntry.SETRANGE("Applies-to ID");
                                                                    END;
                                                                    IF "Applies-to Doc. Type" <> "Applies-to Doc. Type"::" " THEN BEGIN
                                                                      CustLedgEntry.SETRANGE("Document Type","Applies-to Doc. Type");
                                                                      IF NOT CustLedgEntry.FIND('-') THEN
                                                                        CustLedgEntry.SETRANGE("Document Type");
                                                                    END;
                                                                    IF  "Applies-to Doc. No." <>''THEN BEGIN
                                                                      CustLedgEntry.SETRANGE("Document No.","Applies-to Doc. No.");
                                                                      IF NOT CustLedgEntry.FIND('-') THEN
                                                                        CustLedgEntry.SETRANGE("Document No.");
                                                                    END;
                                                                    IF Amount <> 0 THEN BEGIN
                                                                      CustLedgEntry.SETRANGE(Positive,Amount < 0);
                                                                      IF CustLedgEntry.FIND('-') THEN;
                                                                      CustLedgEntry.SETRANGE(Positive);
                                                                    END;
                                                                    ApplyCustEntries.SetGenJnlLine(Rec,GenJnlLine.FIELDNO("Applies-to Doc. No."));
                                                                    ApplyCustEntries.SETTABLEVIEW(CustLedgEntry);
                                                                    ApplyCustEntries.SETRECORD(CustLedgEntry);
                                                                    ApplyCustEntries.LOOKUPMODE(TRUE);
                                                                    IF ApplyCustEntries.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                      ApplyCustEntries.GETRECORD(CustLedgEntry);
                                                                      CLEAR(ApplyCustEntries);
                                                                      IF "Currency Code" <> CustLedgEntry."Currency Code" THEN
                                                                        IF Amount = 0 THEN BEGIN
                                                                          FromCurrencyCode := GetShowCurrencyCode("Currency Code");
                                                                          ToCurrencyCode := GetShowCurrencyCode(CustLedgEntry."Currency Code");
                                                                          IF NOT
                                                                             CONFIRM(
                                                                               Text003 +
                                                                               Text004,TRUE,
                                                                               FIELDCAPTION("Currency Code"),TABLECAPTION,FromCurrencyCode,
                                                                               ToCurrencyCode)
                                                                          THEN
                                                                            ERROR(Text005);
                                                                          VALIDATE("Currency Code",CustLedgEntry."Currency Code");
                                                                        END ELSE
                                                                          GenJnlApply.CheckAgainstApplnCurrency(
                                                                            "Currency Code",CustLedgEntry."Currency Code",
                                                                            GenJnlLine."Account Type"::Customer,TRUE);
                                                                      IF Amount = 0 THEN BEGIN
                                                                        CustLedgEntry.CALCFIELDS("Remaining Amount");
                                                                        IF CustLedgEntry."Amount to Apply" <> 0 THEN BEGIN
                                                                          IF GenJnlPostLine.CheckCalcPmtDiscGenJnlCust(Rec,CustLedgEntry,0,FALSE)
                                                                          THEN BEGIN
                                                                            IF ABS(CustLedgEntry."Amount to Apply") >=
                                                                              ABS(CustLedgEntry."Remaining Amount" - CustLedgEntry."Remaining Pmt. Disc. Possible")
                                                                            THEN
                                                                              Amount := -(CustLedgEntry."Remaining Amount" -
                                                                                CustLedgEntry."Remaining Pmt. Disc. Possible")
                                                                            ELSE
                                                                              Amount := -CustLedgEntry."Amount to Apply";
                                                                          END ELSE
                                                                            Amount := -CustLedgEntry."Amount to Apply";
                                                                        END ELSE BEGIN
                                                                        IF GenJnlPostLine.CheckCalcPmtDiscGenJnlCust(Rec,CustLedgEntry,0,FALSE)
                                                                        THEN
                                                                          Amount := -(CustLedgEntry."Remaining Amount" -
                                                                            CustLedgEntry."Remaining Pmt. Disc. Possible")
                                                                        ELSE
                                                                          Amount := -CustLedgEntry."Remaining Amount";
                                                                        END;
                                                                        IF "Bal. Account Type" IN
                                                                          ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor]
                                                                        THEN
                                                                          Amount := -Amount;
                                                                        VALIDATE(Amount);
                                                                      END;
                                                                      "Applies-to Doc. Type" := CustLedgEntry."Document Type";
                                                                      "Applies-to Doc. No." := CustLedgEntry."Document No.";
                                                                      "Applies-to ID" := '';
                                                                    END ELSE BEGIN
                                                                      "Applies-to Doc. No." := OldAppliesToDocNo;
                                                                      CLEAR(ApplyCustEntries);
                                                                    END;

                                                                    GetCreditCard;
                                                                  END;
                                                                AccType::Vendor:
                                                                  BEGIN
                                                                    VendLedgEntry.SETCURRENTKEY("Vendor No.",Open,Positive,"Due Date");
                                                                    VendLedgEntry.SETRANGE("Vendor No.",AccNo);
                                                                    VendLedgEntry.SETRANGE(Open,TRUE);
                                                                    IF "Applies-to Doc. No." <> '' THEN BEGIN
                                                                      VendLedgEntry.SETRANGE("Document Type","Applies-to Doc. Type");
                                                                      VendLedgEntry.SETRANGE("Document No.","Applies-to Doc. No.");
                                                                      IF NOT VendLedgEntry.FIND('-') THEN BEGIN
                                                                        VendLedgEntry.SETRANGE("Document Type");
                                                                        VendLedgEntry.SETRANGE("Document No.");
                                                                      END;
                                                                    END;
                                                                    IF "Applies-to ID" <> '' THEN BEGIN
                                                                      VendLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
                                                                      IF NOT VendLedgEntry.FIND('-') THEN
                                                                        VendLedgEntry.SETRANGE("Applies-to ID");
                                                                    END;
                                                                    IF "Applies-to Doc. Type" <> "Applies-to Doc. Type"::" " THEN BEGIN
                                                                      VendLedgEntry.SETRANGE("Document Type","Applies-to Doc. Type");
                                                                      IF NOT VendLedgEntry.FIND('-') THEN
                                                                        VendLedgEntry.SETRANGE("Document Type");
                                                                    END;
                                                                    IF  "Applies-to Doc. No." <>''THEN BEGIN
                                                                      VendLedgEntry.SETRANGE("Document No.","Applies-to Doc. No.");
                                                                      IF NOT VendLedgEntry.FIND('-') THEN
                                                                        VendLedgEntry.SETRANGE("Document No.");
                                                                    END;
                                                                    IF Amount <> 0 THEN BEGIN
                                                                      VendLedgEntry.SETRANGE(Positive,Amount < 0);
                                                                      IF VendLedgEntry.FIND('-') THEN;
                                                                      VendLedgEntry.SETRANGE(Positive);
                                                                    END;
                                                                    ApplyVendEntries.SetGenJnlLine(Rec,GenJnlLine.FIELDNO("Applies-to Doc. No."));
                                                                    ApplyVendEntries.SETTABLEVIEW(VendLedgEntry);
                                                                    ApplyVendEntries.SETRECORD(VendLedgEntry);
                                                                    ApplyVendEntries.LOOKUPMODE(TRUE);
                                                                    IF ApplyVendEntries.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                      ApplyVendEntries.GETRECORD(VendLedgEntry);
                                                                      CLEAR(ApplyVendEntries);
                                                                      IF "Currency Code" <> VendLedgEntry."Currency Code" THEN
                                                                        IF Amount = 0 THEN BEGIN
                                                                          FromCurrencyCode := GetShowCurrencyCode("Currency Code");
                                                                          ToCurrencyCode := GetShowCurrencyCode(VendLedgEntry."Currency Code");
                                                                          IF NOT
                                                                             CONFIRM(
                                                                               Text003 +
                                                                               Text004,TRUE,
                                                                               FIELDCAPTION("Currency Code"),TABLECAPTION,FromCurrencyCode,
                                                                               ToCurrencyCode)
                                                                          THEN
                                                                            ERROR(Text005);
                                                                          VALIDATE("Currency Code",VendLedgEntry."Currency Code");
                                                                        END ELSE
                                                                          GenJnlApply.CheckAgainstApplnCurrency(
                                                                            "Currency Code",VendLedgEntry."Currency Code",GenJnlLine."Account Type"::Vendor,TRUE);
                                                                      IF Amount = 0 THEN BEGIN
                                                                        VendLedgEntry.CALCFIELDS("Remaining Amount");
                                                                        IF VendLedgEntry."Amount to Apply" <> 0 THEN BEGIN
                                                                          IF GenJnlPostLine.CheckCalcPmtDiscGenJnlVend(Rec,VendLedgEntry,0,FALSE)
                                                                          THEN BEGIN
                                                                            IF ABS(VendLedgEntry."Amount to Apply") >=
                                                                              ABS(VendLedgEntry."Remaining Amount" - VendLedgEntry."Remaining Pmt. Disc. Possible")
                                                                            THEN
                                                                              Amount := -(VendLedgEntry."Remaining Amount" -
                                                                                VendLedgEntry."Remaining Pmt. Disc. Possible")
                                                                            ELSE
                                                                              Amount := -VendLedgEntry."Amount to Apply";
                                                                          END ELSE
                                                                            Amount := -VendLedgEntry."Amount to Apply";
                                                                        END ELSE BEGIN
                                                                        IF GenJnlPostLine.CheckCalcPmtDiscGenJnlVend(Rec,VendLedgEntry,0,FALSE)
                                                                        THEN
                                                                          Amount := -(VendLedgEntry."Remaining Amount" -
                                                                            VendLedgEntry."Remaining Pmt. Disc. Possible")
                                                                        ELSE
                                                                          Amount := -VendLedgEntry."Remaining Amount";
                                                                        END;
                                                                        IF "Bal. Account Type" IN
                                                                          ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor]
                                                                        THEN
                                                                          Amount := -Amount;
                                                                        VALIDATE(Amount);
                                                                      END;
                                                                      "Applies-to Doc. Type" := VendLedgEntry."Document Type";
                                                                      "Applies-to Doc. No." := VendLedgEntry."Document No.";
                                                                      "Applies-to ID" := '';
                                                                    END ELSE BEGIN
                                                                      "Applies-to Doc. No." := OldAppliesToDocNo;
                                                                      CLEAR(ApplyVendEntries);
                                                                    END;
                                                                  END;
                                                                ELSE
                                                                  "Applies-to Doc. No." := OldAppliesToDocNo;
                                                              END;

                                                              IF (xRec.Amount <> 0) THEN
                                                                IF NOT PaymentToleranceMgt.PmtTolGenJnl(Rec) THEN
                                                                  EXIT;
                                                            END;

                                                   CaptionML=[DEU=Ausgleich mit Belegnr.;
                                                              ENU=Applies-to Doc. No.] }
    { 38  ;   ;Due Date            ;Date          ;CaptionML=[DEU=Flligkeitsdatum;
                                                              ENU=Due Date] }
    { 39  ;   ;Pmt. Discount Date  ;Date          ;CaptionML=[DEU=Skontodatum;
                                                              ENU=Pmt. Discount Date] }
    { 40  ;   ;Payment Discount %  ;Decimal       ;CaptionML=[DEU=Skonto %;
                                                              ENU=Payment Discount %];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 42  ;   ;Job No.             ;Code20        ;TableRelation=Job;
                                                   OnValidate=BEGIN
                                                                IF ("Job No." = xRec."Job No.") THEN
                                                                  EXIT;

                                                                SourceCodeSetup.GET;
                                                                IF "Source Code" <> SourceCodeSetup."Job G/L WIP" THEN
                                                                  VALIDATE("Job Task No.",'');
                                                                IF "Job No." = '' THEN BEGIN
                                                                  CreateDim(
                                                                    DATABASE::Job,"Job No.",
                                                                    DimMgt.TypeToTableID1("Account Type"),"Account No.",
                                                                    DimMgt.TypeToTableID1("Bal. Account Type"),"Bal. Account No.",
                                                                    DATABASE::"Salesperson/Purchaser","Salespers./Purch. Code",
                                                                    DATABASE::Campaign,"Campaign No.");
                                                                  EXIT;
                                                                END;

                                                                TESTFIELD("Account Type","Account Type"::"G/L Account");
                                                                IF "Bal. Account No." <> '' THEN
                                                                  TESTFIELD("Bal. Account Type","Bal. Account Type"::"G/L Account");
                                                                Job.GET("Job No.");
                                                                Job.TestBlocked;
                                                                "Job Currency Code" := Job."Currency Code";

                                                                CreateDim(
                                                                  DATABASE::Job,"Job No.",
                                                                  DimMgt.TypeToTableID1("Account Type"),"Account No.",
                                                                  DimMgt.TypeToTableID1("Bal. Account Type"),"Bal. Account No.",
                                                                  DATABASE::"Salesperson/Purchaser","Salespers./Purch. Code",
                                                                  DATABASE::Campaign,"Campaign No.");
                                                              END;

                                                   CaptionML=[DEU=Projektnr.;
                                                              ENU=Job No.] }
    { 43  ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE(Amount);
                                                              END;

                                                   CaptionML=[DEU=Menge;
                                                              ENU=Quantity];
                                                   DecimalPlaces=0:5 }
    { 44  ;   ;VAT Amount          ;Decimal       ;OnValidate=BEGIN
                                                                GenJnlBatch.GET("Journal Template Name","Journal Batch Name");
                                                                GenJnlBatch.TESTFIELD("Allow VAT Difference",TRUE);
                                                                IF NOT ("VAT Calculation Type" IN
                                                                  ["VAT Calculation Type"::"Normal VAT","VAT Calculation Type"::"Reverse Charge VAT"])
                                                                THEN
                                                                  ERROR(
                                                                    Text010,FIELDCAPTION("VAT Calculation Type"),
                                                                    "VAT Calculation Type"::"Normal VAT","VAT Calculation Type"::"Reverse Charge VAT");
                                                                IF "VAT Amount" <> 0 THEN BEGIN
                                                                  TESTFIELD("VAT %");
                                                                  TESTFIELD(Amount);
                                                                END;

                                                                GetCurrency;
                                                                "VAT Amount" := ROUND("VAT Amount",Currency."Amount Rounding Precision",Currency.VATRoundingDirection);

                                                                IF "VAT Amount" * Amount < 0 THEN
                                                                  IF "VAT Amount" > 0 THEN
                                                                    ERROR(Text011,FIELDCAPTION("VAT Amount"))
                                                                  ELSE
                                                                    ERROR(Text012,FIELDCAPTION("VAT Amount"));

                                                                "VAT Base Amount" := Amount - "VAT Amount";

                                                                "VAT Difference" :=
                                                                  "VAT Amount" -
                                                                  ROUND(
                                                                    Amount * "VAT %" / (100 + "VAT %"),
                                                                    Currency."Amount Rounding Precision",Currency.VATRoundingDirection);
                                                                IF ABS("VAT Difference") > Currency."Max. VAT Difference Allowed" THEN
                                                                  ERROR(Text013,FIELDCAPTION("VAT Difference"),Currency."Max. VAT Difference Allowed");

                                                                IF "Currency Code" = '' THEN
                                                                  "VAT Amount (LCY)" := "VAT Amount"
                                                                ELSE
                                                                  "VAT Amount (LCY)" :=
                                                                    ROUND(
                                                                      CurrExchRate.ExchangeAmtFCYToLCY(
                                                                        "Posting Date","Currency Code",
                                                                        "VAT Amount","Currency Factor"));
                                                                "VAT Base Amount (LCY)" := "Amount (LCY)" - "VAT Amount (LCY)";

                                                                UpdateSalesPurchLCY;
                                                              END;

                                                   CaptionML=[DEU=MwSt.-Betrag;
                                                              ENU=VAT Amount];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 45  ;   ;VAT Posting         ;Option        ;CaptionML=[DEU=MwSt.-Buchung;
                                                              ENU=VAT Posting];
                                                   OptionCaptionML=[DEU=Automatische MwSt.-Buch.,Manuelle MwSt.-Buch.;
                                                                    ENU=Automatic VAT Entry,Manual VAT Entry];
                                                   OptionString=Automatic VAT Entry,Manual VAT Entry;
                                                   Editable=No }
    { 47  ;   ;Payment Terms Code  ;Code10        ;TableRelation="Payment Terms";
                                                   OnValidate=BEGIN
                                                                "Due Date" := 0D;
                                                                "Pmt. Discount Date" := 0D;
                                                                "Payment Discount %" := 0;
                                                                IF ("Account Type" <> "Account Type"::"G/L Account") OR
                                                                   ("Bal. Account Type" <> "Bal. Account Type"::"G/L Account")
                                                                THEN
                                                                  CASE "Document Type" OF
                                                                    "Document Type"::Invoice:
                                                                      IF ("Payment Terms Code" <> '') AND ("Document Date" <> 0D) THEN BEGIN
                                                                        PaymentTerms.GET("Payment Terms Code");
                                                                        "Due Date" := CALCDATE(PaymentTerms."Due Date Calculation","Document Date");
                                                                        "Pmt. Discount Date" := CALCDATE(PaymentTerms."Discount Date Calculation","Document Date");
                                                                        "Payment Discount %" := PaymentTerms."Discount %";
                                                                      END;
                                                                    "Document Type"::"Credit Memo":
                                                                      IF ("Payment Terms Code" <> '') AND ("Document Date" <> 0D) THEN BEGIN
                                                                        PaymentTerms.GET("Payment Terms Code");
                                                                        IF PaymentTerms."Calc. Pmt. Disc. on Cr. Memos" THEN BEGIN
                                                                          "Due Date" := CALCDATE(PaymentTerms."Due Date Calculation","Document Date");
                                                                          "Pmt. Discount Date" :=
                                                                            CALCDATE(PaymentTerms."Discount Date Calculation","Document Date");
                                                                          "Payment Discount %" := PaymentTerms."Discount %";
                                                                        END ELSE
                                                                          "Due Date" := "Document Date";
                                                                      END;
                                                                    ELSE
                                                                      "Due Date" := "Document Date";
                                                                  END;
                                                              END;

                                                   CaptionML=[DEU=Zlg.-Bedingungscode;
                                                              ENU=Payment Terms Code] }
    { 48  ;   ;Applies-to ID       ;Code20        ;OnValidate=BEGIN
                                                                IF ("Applies-to ID" <> xRec."Applies-to ID") AND (xRec."Applies-to ID" <> '') THEN
                                                                  ClearCustVendApplnEntry;
                                                              END;

                                                   CaptionML=[DEU=Ausgleichs-ID;
                                                              ENU=Applies-to ID] }
    { 50  ;   ;Business Unit Code  ;Code10        ;TableRelation="Business Unit";
                                                   CaptionML=[DEU=Konzernmandantencode;
                                                              ENU=Business Unit Code] }
    { 51  ;   ;Journal Batch Name  ;Code10        ;TableRelation="Gen. Journal Batch".Name WHERE (Journal Template Name=FIELD(Journal Template Name));
                                                   CaptionML=[DEU=Buch.-Blattname;
                                                              ENU=Journal Batch Name] }
    { 52  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=[DEU=Ursachencode;
                                                              ENU=Reason Code] }
    { 53  ;   ;Recurring Method    ;Option        ;OnValidate=BEGIN
                                                                IF "Recurring Method" IN
                                                                   ["Recurring Method"::"B  Balance","Recurring Method"::"RB Reversing Balance"]
                                                                THEN
                                                                  TESTFIELD("Currency Code",'');
                                                              END;

                                                   CaptionML=[DEU=Wiederholungsart;
                                                              ENU=Recurring Method];
                                                   OptionCaptionML=[DEU=" ,F  Fix,V  Variabel,A  Ausgleich,UF Umgekehrt Fix,UV Umgekehrt Variabel,UA Umgekehrt Ausgleich";
                                                                    ENU=" ,F  Fixed,V  Variable,B  Balance,RF Reversing Fixed,RV Reversing Variable,RB Reversing Balance"];
                                                   OptionString=[ ,F  Fixed,V  Variable,B  Balance,RF Reversing Fixed,RV Reversing Variable,RB Reversing Balance];
                                                   BlankZero=Yes }
    { 54  ;   ;Expiration Date     ;Date          ;CaptionML=[DEU=Ablaufdatum;
                                                              ENU=Expiration Date] }
    { 55  ;   ;Recurring Frequency ;DateFormula   ;CaptionML=[DEU=Wiederholungsrate;
                                                              ENU=Recurring Frequency] }
    { 56  ;   ;Allocated Amt. (LCY);Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Gen. Jnl. Allocation".Amount WHERE (Journal Template Name=FIELD(Journal Template Name),
                                                                                                        Journal Batch Name=FIELD(Journal Batch Name),
                                                                                                        Journal Line No.=FIELD(Line No.)));
                                                   CaptionML=[DEU=Zugeordneter Betrag (MW);
                                                              ENU=Allocated Amt. (LCY)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 57  ;   ;Gen. Posting Type   ;Option        ;OnValidate=BEGIN
                                                                IF "Account Type" IN ["Account Type"::Customer,"Account Type"::Vendor,"Account Type"::"Bank Account"] THEN
                                                                  TESTFIELD("Gen. Posting Type","Gen. Posting Type"::" ");
                                                                IF ("Gen. Posting Type" = "Gen. Posting Type"::Settlement) AND (CurrFieldNo <> 0) THEN
                                                                  ERROR(Text006,"Gen. Posting Type");
                                                                CheckVATInAlloc;
                                                                IF "Gen. Posting Type" > 0 THEN
                                                                  VALIDATE("VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[DEU=Buchungsart;
                                                              ENU=Gen. Posting Type];
                                                   OptionCaptionML=[DEU=" ,Einkauf,Verkauf,Ausgleich";
                                                                    ENU=" ,Purchase,Sale,Settlement"];
                                                   OptionString=[ ,Purchase,Sale,Settlement] }
    { 58  ;   ;Gen. Bus. Posting Group;Code10     ;TableRelation="Gen. Business Posting Group";
                                                   OnValidate=BEGIN
                                                                IF "Account Type" IN ["Account Type"::Customer,"Account Type"::Vendor,"Account Type"::"Bank Account"] THEN
                                                                  TESTFIELD("Gen. Bus. Posting Group",'');
                                                                IF xRec."Gen. Bus. Posting Group" <> "Gen. Bus. Posting Group" THEN
                                                                  IF GenBusPostingGrp.ValidateVatBusPostingGroup(GenBusPostingGrp,"Gen. Bus. Posting Group") THEN
                                                                    VALIDATE("VAT Bus. Posting Group",GenBusPostingGrp."Def. VAT Bus. Posting Group");
                                                              END;

                                                   CaptionML=[DEU=Geschftsbuchungsgruppe;
                                                              ENU=Gen. Bus. Posting Group] }
    { 59  ;   ;Gen. Prod. Posting Group;Code10    ;TableRelation="Gen. Product Posting Group";
                                                   OnValidate=BEGIN
                                                                IF "Account Type" IN ["Account Type"::Customer,"Account Type"::Vendor,"Account Type"::"Bank Account"] THEN
                                                                  TESTFIELD("Gen. Prod. Posting Group",'');
                                                                IF xRec."Gen. Prod. Posting Group" <> "Gen. Prod. Posting Group" THEN
                                                                  IF GenProdPostingGrp.ValidateVatProdPostingGroup(GenProdPostingGrp,"Gen. Prod. Posting Group") THEN
                                                                    VALIDATE("VAT Prod. Posting Group",GenProdPostingGrp."Def. VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[DEU=Produktbuchungsgruppe;
                                                              ENU=Gen. Prod. Posting Group] }
    { 60  ;   ;VAT Calculation Type;Option        ;CaptionML=[DEU=MwSt.-Berechnungsart;
                                                              ENU=VAT Calculation Type];
                                                   OptionCaptionML=[DEU=Normale MwSt.,Erwerbsbesteuerung,Nur MwSt.,Verkaufssteuer;
                                                                    ENU=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax];
                                                   OptionString=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                   Editable=No }
    { 61  ;   ;EU 3-Party Trade    ;Boolean       ;CaptionML=[DEU=EU-Dreiecksgeschft;
                                                              ENU=EU 3-Party Trade];
                                                   Editable=No }
    { 62  ;   ;Allow Application   ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[DEU=Ausgleich zulassen;
                                                              ENU=Allow Application] }
    { 63  ;   ;Bal. Account Type   ;Option        ;OnValidate=BEGIN
                                                                IF ("Account Type" IN ["Account Type"::Customer,"Account Type"::Vendor,"Account Type"::"Fixed Asset",
                                                                    "Account Type"::"IC Partner"]) AND
                                                                   ("Bal. Account Type" IN ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor,"Bal. Account Type"::"Fixed Asset",
                                                                    "Bal. Account Type"::"IC Partner"])
                                                                THEN
                                                                  ERROR(
                                                                    Text000,
                                                                    FIELDCAPTION("Account Type"),FIELDCAPTION("Bal. Account Type"));
                                                                VALIDATE("Bal. Account No.",'');
                                                                VALIDATE("IC Partner G/L Acc. No.",'');
                                                                IF "Bal. Account Type" IN
                                                                   ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor,"Bal. Account Type"::"Bank Account"]
                                                                THEN BEGIN
                                                                  VALIDATE("Bal. Gen. Posting Type","Bal. Gen. Posting Type"::" ");
                                                                  VALIDATE("Bal. Gen. Bus. Posting Group",'');
                                                                  VALIDATE("Bal. Gen. Prod. Posting Group",'');
                                                                END ELSE
                                                                  IF "Account Type" IN [
                                                                     "Bal. Account Type"::"G/L Account","Account Type"::"Bank Account","Account Type"::"Fixed Asset"]
                                                                  THEN
                                                                    VALIDATE("Payment Terms Code",'');
                                                                UpdateSource;
                                                                IF ("Account Type" <> "Account Type"::"Fixed Asset") AND
                                                                   ("Bal. Account Type" <> "Bal. Account Type"::"Fixed Asset")
                                                                THEN BEGIN
                                                                  "Depreciation Book Code" := '';
                                                                  VALIDATE("FA Posting Type","FA Posting Type"::" ");
                                                                END;
                                                                IF xRec."Bal. Account Type" IN
                                                                   [xRec."Bal. Account Type"::Customer,xRec."Bal. Account Type"::Vendor]
                                                                THEN BEGIN
                                                                  "Bill-to/Pay-to No." := '';
                                                                  "Ship-to/Order Address Code" := '';
                                                                  "Sell-to/Buy-from No." := '';
                                                                END;
                                                                IF ("Account Type" IN [
                                                                    "Account Type"::"G/L Account","Account Type"::"Bank Account","Account Type"::"Fixed Asset"]) AND
                                                                  ("Bal. Account Type" IN [
                                                                    "Bal. Account Type"::"G/L Account","Bal. Account Type"::"Bank Account","Bal. Account Type"::"Fixed Asset"])
                                                                THEN BEGIN
                                                                  VALIDATE("Payment Terms Code",'');
                                                                END;

                                                                IF ("Bal. Account Type" = "Bal. Account Type"::"IC Partner") THEN BEGIN
                                                                  GetTemplate;
                                                                  IF GenJnlTemplate.Type <> GenJnlTemplate.Type::Intercompany THEN
                                                                    FIELDERROR("Bal. Account Type");
                                                                END;
                                                                IF "Bal. Account Type" <> "Bal. Account Type"::"Bank Account" THEN
                                                                  VALIDATE("Credit Card No.",'');
                                                              END;

                                                   CaptionML=[DEU=Gegenkontoart;
                                                              ENU=Bal. Account Type];
                                                   OptionCaptionML=[DEU=Sachkonto,Debitor,Kreditor,Bankkonto,Anlage,IC-Partner;
                                                                    ENU=G/L Account,Customer,Vendor,Bank Account,Fixed Asset,IC Partner];
                                                   OptionString=G/L Account,Customer,Vendor,Bank Account,Fixed Asset,IC Partner }
    { 64  ;   ;Bal. Gen. Posting Type;Option      ;OnValidate=BEGIN
                                                                IF "Bal. Account Type" IN ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor,"Bal. Account Type"::"Bank Account"] THEN
                                                                  TESTFIELD("Bal. Gen. Posting Type","Bal. Gen. Posting Type"::" ");
                                                                IF ("Bal. Gen. Posting Type" = "Gen. Posting Type"::Settlement) AND (CurrFieldNo <> 0) THEN
                                                                  ERROR(Text006,"Bal. Gen. Posting Type");
                                                                IF "Bal. Gen. Posting Type" > 0 THEN
                                                                  VALIDATE("Bal. VAT Prod. Posting Group");

                                                                IF ("Account Type" <> "Account Type"::"Fixed Asset") AND
                                                                   ("Bal. Account Type" <> "Bal. Account Type"::"Fixed Asset")
                                                                THEN BEGIN
                                                                  "Depreciation Book Code" := '';
                                                                  VALIDATE("FA Posting Type","FA Posting Type"::" ");
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Gegenkonto Buchungsart;
                                                              ENU=Bal. Gen. Posting Type];
                                                   OptionCaptionML=[DEU=" ,Einkauf,Verkauf,Ausgleich";
                                                                    ENU=" ,Purchase,Sale,Settlement"];
                                                   OptionString=[ ,Purchase,Sale,Settlement] }
    { 65  ;   ;Bal. Gen. Bus. Posting Group;Code10;TableRelation="Gen. Business Posting Group";
                                                   OnValidate=BEGIN
                                                                IF "Bal. Account Type" IN ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor,"Bal. Account Type"::"Bank Account"] THEN
                                                                  TESTFIELD("Bal. Gen. Bus. Posting Group",'');
                                                                IF xRec."Bal. Gen. Bus. Posting Group" <> "Bal. Gen. Bus. Posting Group" THEN
                                                                  IF GenBusPostingGrp.ValidateVatBusPostingGroup(GenBusPostingGrp,"Bal. Gen. Bus. Posting Group") THEN
                                                                    VALIDATE("Bal. VAT Bus. Posting Group",GenBusPostingGrp."Def. VAT Bus. Posting Group");
                                                              END;

                                                   CaptionML=[DEU=Gegenkonto Geschftsbuchungsgr;
                                                              ENU=Bal. Gen. Bus. Posting Group] }
    { 66  ;   ;Bal. Gen. Prod. Posting Group;Code10;
                                                   TableRelation="Gen. Product Posting Group";
                                                   OnValidate=BEGIN
                                                                IF "Bal. Account Type" IN ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor,"Bal. Account Type"::"Bank Account"] THEN
                                                                  TESTFIELD("Bal. Gen. Prod. Posting Group",'');
                                                                IF xRec."Bal. Gen. Prod. Posting Group" <> "Bal. Gen. Prod. Posting Group" THEN
                                                                  IF GenProdPostingGrp.ValidateVatProdPostingGroup(GenProdPostingGrp,"Bal. Gen. Prod. Posting Group") THEN
                                                                    VALIDATE("Bal. VAT Prod. Posting Group",GenProdPostingGrp."Def. VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[DEU=Gegenkonto Produktbuchungsgr.;
                                                              ENU=Bal. Gen. Prod. Posting Group] }
    { 67  ;   ;Bal. VAT Calculation Type;Option   ;CaptionML=[DEU=Gegenkto. MwSt.-Berechnungsart;
                                                              ENU=Bal. VAT Calculation Type];
                                                   OptionCaptionML=[DEU=Normale MwSt.,Erwerbsbesteuerung,Nur MwSt.,Verkaufssteuer;
                                                                    ENU=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax];
                                                   OptionString=Normal VAT,Reverse Charge VAT,Full VAT,Sales Tax;
                                                   Editable=No }
    { 68  ;   ;Bal. VAT %          ;Decimal       ;OnValidate=BEGIN
                                                                GetCurrency;
                                                                CASE "Bal. VAT Calculation Type" OF
                                                                  "Bal. VAT Calculation Type"::"Normal VAT",
                                                                  "Bal. VAT Calculation Type"::"Reverse Charge VAT":
                                                                    BEGIN
                                                                      "Bal. VAT Amount" :=
                                                                        ROUND(-Amount * "Bal. VAT %" / (100 + "Bal. VAT %"),Currency."Amount Rounding Precision",Currency.VATRoundingDirection);
                                                                      "Bal. VAT Base Amount" :=
                                                                        ROUND(-Amount - "Bal. VAT Amount",Currency."Amount Rounding Precision");
                                                                    END;
                                                                  "Bal. VAT Calculation Type"::"Full VAT":
                                                                    "Bal. VAT Amount" := -Amount;
                                                                  "Bal. VAT Calculation Type"::"Sales Tax":
                                                                    IF ("Bal. Gen. Posting Type" = "Bal. Gen. Posting Type"::Purchase) AND
                                                                       "Bal. Use Tax"
                                                                    THEN BEGIN
                                                                      "Bal. VAT Amount" := 0;
                                                                      "Bal. VAT %" := 0;
                                                                    END ELSE BEGIN
                                                                      "Bal. VAT Amount" :=
                                                                        -(Amount -
                                                                          SalesTaxCalculate.ReverseCalculateTax(
                                                                            "Bal. Tax Area Code","Bal. Tax Group Code","Bal. Tax Liable",
                                                                            "Posting Date",Amount,Quantity,"Currency Factor"));
                                                                      IF Amount + "Bal. VAT Amount" <> 0 THEN
                                                                        "Bal. VAT %" := ROUND(100 * (-"Bal. VAT Amount") / (Amount + "Bal. VAT Amount"),0.00001)
                                                                      ELSE
                                                                        "Bal. VAT %" := 0;
                                                                      "Bal. VAT Amount" :=
                                                                        ROUND("Bal. VAT Amount",Currency."Amount Rounding Precision");
                                                                    END;
                                                                END;
                                                                "Bal. VAT Base Amount" := -(Amount + "Bal. VAT Amount");
                                                                "Bal. VAT Difference" := 0;

                                                                IF "Currency Code" = '' THEN
                                                                  "Bal. VAT Amount (LCY)" := "Bal. VAT Amount"
                                                                ELSE
                                                                  "Bal. VAT Amount (LCY)" :=
                                                                    ROUND(
                                                                      CurrExchRate.ExchangeAmtFCYToLCY(
                                                                      "Posting Date","Currency Code",
                                                                      "Bal. VAT Amount","Currency Factor"));
                                                                "Bal. VAT Base Amount (LCY)" := -("Amount (LCY)" + "Bal. VAT Amount (LCY)");

                                                                UpdateSalesPurchLCY;
                                                              END;

                                                   CaptionML=[DEU=Gegenkonto MwSt. %;
                                                              ENU=Bal. VAT %];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   Editable=No }
    { 69  ;   ;Bal. VAT Amount     ;Decimal       ;OnValidate=BEGIN
                                                                GenJnlBatch.GET("Journal Template Name","Journal Batch Name");
                                                                GenJnlBatch.TESTFIELD("Allow VAT Difference",TRUE);
                                                                IF NOT ("Bal. VAT Calculation Type" IN
                                                                  ["Bal. VAT Calculation Type"::"Normal VAT","Bal. VAT Calculation Type"::"Reverse Charge VAT"])
                                                                THEN
                                                                  ERROR(
                                                                    Text010,FIELDCAPTION("Bal. VAT Calculation Type"),
                                                                    "Bal. VAT Calculation Type"::"Normal VAT","Bal. VAT Calculation Type"::"Reverse Charge VAT");
                                                                IF "Bal. VAT Amount" <> 0 THEN BEGIN
                                                                  TESTFIELD("Bal. VAT %");
                                                                  TESTFIELD(Amount);
                                                                END;

                                                                GetCurrency;
                                                                "Bal. VAT Amount" :=
                                                                  ROUND("Bal. VAT Amount",Currency."Amount Rounding Precision",Currency.VATRoundingDirection);

                                                                IF "Bal. VAT Amount" * Amount > 0 THEN
                                                                  IF "Bal. VAT Amount" > 0 THEN
                                                                    ERROR(Text011,FIELDCAPTION("Bal. VAT Amount"))
                                                                  ELSE
                                                                    ERROR(Text012,FIELDCAPTION("Bal. VAT Amount"));

                                                                "Bal. VAT Base Amount" := -(Amount + "Bal. VAT Amount");

                                                                "Bal. VAT Difference" :=
                                                                  "Bal. VAT Amount" -
                                                                  ROUND(
                                                                    -Amount * "Bal. VAT %" / (100 + "Bal. VAT %"),
                                                                    Currency."Amount Rounding Precision",Currency.VATRoundingDirection);
                                                                IF ABS("Bal. VAT Difference") > Currency."Max. VAT Difference Allowed" THEN
                                                                  ERROR(
                                                                    Text013,FIELDCAPTION("Bal. VAT Difference"),Currency."Max. VAT Difference Allowed");

                                                                IF "Currency Code" = '' THEN
                                                                  "Bal. VAT Amount (LCY)" := "Bal. VAT Amount"
                                                                ELSE
                                                                  "Bal. VAT Amount (LCY)" :=
                                                                    ROUND(
                                                                      CurrExchRate.ExchangeAmtFCYToLCY(
                                                                      "Posting Date","Currency Code",
                                                                      "Bal. VAT Amount","Currency Factor"));
                                                                "Bal. VAT Base Amount (LCY)" := -("Amount (LCY)" + "Bal. VAT Amount (LCY)");

                                                                UpdateSalesPurchLCY;
                                                              END;

                                                   CaptionML=[DEU=Gegenkonto MwSt.-Betrag;
                                                              ENU=Bal. VAT Amount];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 70  ;   ;Bank Payment Type   ;Option        ;OnValidate=BEGIN
                                                                IF ("Bank Payment Type" <> "Bank Payment Type"::" ") AND
                                                                   ("Account Type" <> "Account Type"::"Bank Account") AND
                                                                   ("Bal. Account Type" <> "Bal. Account Type"::"Bank Account")
                                                                THEN
                                                                  ERROR(
                                                                    Text007,
                                                                    FIELDCAPTION("Account Type"),FIELDCAPTION("Bal. Account Type"));
                                                                IF ("Account Type" = "Account Type"::"Fixed Asset") AND
                                                                   ("Bank Payment Type" <> "Bank Payment Type"::" ")
                                                                THEN
                                                                  FIELDERROR("Account Type");
                                                              END;

                                                   CaptionML=[DEU=Bankkontozahlungsart;
                                                              ENU=Bank Payment Type];
                                                   OptionCaptionML=[DEU=" ,Computer Scheck,Manueller Scheck";
                                                                    ENU=" ,Computer Check,Manual Check"];
                                                   OptionString=[ ,Computer Check,Manual Check] }
    { 71  ;   ;VAT Base Amount     ;Decimal       ;OnValidate=BEGIN
                                                                GetCurrency;
                                                                "VAT Base Amount" := ROUND("VAT Base Amount",Currency."Amount Rounding Precision");
                                                                CASE "VAT Calculation Type" OF
                                                                  "VAT Calculation Type"::"Normal VAT",
                                                                  "VAT Calculation Type"::"Reverse Charge VAT":
                                                                    Amount :=
                                                                      ROUND(
                                                                        "VAT Base Amount" * (1 + "VAT %" / 100),
                                                                        Currency."Amount Rounding Precision",Currency.VATRoundingDirection);
                                                                  "VAT Calculation Type"::"Full VAT":
                                                                    IF "VAT Base Amount" <> 0 THEN
                                                                      FIELDERROR(
                                                                        "VAT Base Amount",
                                                                        STRSUBSTNO(
                                                                          Text008,FIELDCAPTION("VAT Calculation Type"),
                                                                          "VAT Calculation Type"));
                                                                  "VAT Calculation Type"::"Sales Tax":
                                                                    IF ("Gen. Posting Type" = "Gen. Posting Type"::Purchase) AND
                                                                       "Use Tax"
                                                                    THEN BEGIN
                                                                      "VAT Amount" := 0;
                                                                      "VAT %" := 0;
                                                                      Amount := "VAT Base Amount" + "VAT Amount";
                                                                    END ELSE BEGIN
                                                                      "VAT Amount" :=
                                                                        SalesTaxCalculate.CalculateTax(
                                                                          "Tax Area Code","Tax Group Code","Tax Liable","Posting Date",
                                                                          "VAT Base Amount",Quantity,"Currency Factor");
                                                                      IF "VAT Base Amount" <> 0 THEN
                                                                        "VAT %" := ROUND(100 * "VAT Amount" / "VAT Base Amount",0.00001)
                                                                      ELSE
                                                                        "VAT %" := 0;
                                                                      "VAT Amount" :=
                                                                        ROUND("VAT Amount",Currency."Amount Rounding Precision");
                                                                      Amount := "VAT Base Amount" + "VAT Amount";
                                                                    END;
                                                                END;
                                                                VALIDATE(Amount);
                                                              END;

                                                   CaptionML=[DEU=MwSt.-Bemessungsgrundlage;
                                                              ENU=VAT Base Amount];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 72  ;   ;Bal. VAT Base Amount;Decimal       ;OnValidate=BEGIN
                                                                GetCurrency;
                                                                "Bal. VAT Base Amount" := ROUND("Bal. VAT Base Amount",Currency."Amount Rounding Precision");
                                                                CASE "Bal. VAT Calculation Type" OF
                                                                  "Bal. VAT Calculation Type"::"Normal VAT",
                                                                  "Bal. VAT Calculation Type"::"Reverse Charge VAT":
                                                                    Amount :=
                                                                      ROUND(
                                                                        -"Bal. VAT Base Amount" * (1 + "Bal. VAT %" / 100),
                                                                        Currency."Amount Rounding Precision",Currency.VATRoundingDirection);
                                                                  "Bal. VAT Calculation Type"::"Full VAT":
                                                                    IF "Bal. VAT Base Amount" <> 0 THEN
                                                                      FIELDERROR(
                                                                        "Bal. VAT Base Amount",
                                                                        STRSUBSTNO(
                                                                          Text008,FIELDCAPTION("Bal. VAT Calculation Type"),
                                                                          "Bal. VAT Calculation Type"));
                                                                  "Bal. VAT Calculation Type"::"Sales Tax":
                                                                    IF ("Bal. Gen. Posting Type" = "Bal. Gen. Posting Type"::Purchase) AND
                                                                       "Bal. Use Tax"
                                                                    THEN BEGIN
                                                                      "Bal. VAT Amount" := 0;
                                                                      "Bal. VAT %" := 0;
                                                                      Amount := -"Bal. VAT Base Amount" - "Bal. VAT Amount";
                                                                    END ELSE BEGIN
                                                                      "Bal. VAT Amount" :=
                                                                        SalesTaxCalculate.CalculateTax(
                                                                          "Bal. Tax Area Code","Bal. Tax Group Code","Bal. Tax Liable",
                                                                          "Posting Date","Bal. VAT Base Amount",Quantity,"Currency Factor");
                                                                      IF "Bal. VAT Base Amount" <> 0 THEN
                                                                        "Bal. VAT %" := ROUND(100 * "Bal. VAT Amount" / "Bal. VAT Base Amount",0.00001)
                                                                      ELSE
                                                                        "Bal. VAT %" := 0;
                                                                      "Bal. VAT Amount" :=
                                                                        ROUND("Bal. VAT Amount",Currency."Amount Rounding Precision");
                                                                      Amount := -"Bal. VAT Base Amount" - "Bal. VAT Amount";
                                                                    END;
                                                                END;
                                                                VALIDATE(Amount);
                                                              END;

                                                   CaptionML=[DEU=Gegenkto. MwSt.-Bemessungsgr.;
                                                              ENU=Bal. VAT Base Amount];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 73  ;   ;Correction          ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE(Amount);
                                                              END;

                                                   CaptionML=[DEU=Storno;
                                                              ENU=Correction] }
    { 75  ;   ;Check Printed       ;Boolean       ;CaptionML=[DEU=Scheck gedruckt;
                                                              ENU=Check Printed];
                                                   Editable=No }
    { 76  ;   ;Document Date       ;Date          ;OnValidate=BEGIN
                                                                VALIDATE("Payment Terms Code");
                                                              END;

                                                   CaptionML=[DEU=Belegdatum;
                                                              ENU=Document Date];
                                                   ClosingDates=Yes }
    { 77  ;   ;External Document No.;Code20       ;CaptionML=[DEU=Externe Belegnummer;
                                                              ENU=External Document No.] }
    { 78  ;   ;Source Type         ;Option        ;OnValidate=BEGIN
                                                                IF ("Account Type" <> "Account Type"::"G/L Account") AND ("Account No." <> '') OR
                                                                   ("Bal. Account Type" <> "Bal. Account Type"::"G/L Account") AND ("Bal. Account No." <> '')
                                                                THEN
                                                                  UpdateSource
                                                                ELSE
                                                                  "Source No." := '';
                                                              END;

                                                   CaptionML=[DEU=Herkunftsart;
                                                              ENU=Source Type];
                                                   OptionCaptionML=[DEU=" ,Debitor,Kreditor,Bankkonto,Anlage";
                                                                    ENU=" ,Customer,Vendor,Bank Account,Fixed Asset"];
                                                   OptionString=[ ,Customer,Vendor,Bank Account,Fixed Asset] }
    { 79  ;   ;Source No.          ;Code20        ;TableRelation=IF (Source Type=CONST(Customer)) Customer
                                                                 ELSE IF (Source Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Source Type=CONST(Bank Account)) "Bank Account"
                                                                 ELSE IF (Source Type=CONST(Fixed Asset)) "Fixed Asset";
                                                   OnValidate=BEGIN
                                                                IF ("Account Type" <> "Account Type"::"G/L Account") AND ("Account No." <> '') OR
                                                                   ("Bal. Account Type" <> "Bal. Account Type"::"G/L Account") AND ("Bal. Account No." <> '')
                                                                THEN
                                                                  UpdateSource;
                                                              END;

                                                   CaptionML=[DEU=Herkunftsnr.;
                                                              ENU=Source No.] }
    { 80  ;   ;Posting No. Series  ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[DEU=Buchungsnr.-Serie;
                                                              ENU=Posting No. Series] }
    { 82  ;   ;Tax Area Code       ;Code20        ;TableRelation="Tax Area";
                                                   OnValidate=BEGIN
                                                                VALIDATE("VAT %");
                                                              END;

                                                   CaptionML=[DEU=Steuergebietscode;
                                                              ENU=Tax Area Code] }
    { 83  ;   ;Tax Liable          ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE("VAT %");
                                                              END;

                                                   CaptionML=[DEU=Steuerpflichtig;
                                                              ENU=Tax Liable] }
    { 84  ;   ;Tax Group Code      ;Code10        ;TableRelation="Tax Group";
                                                   OnValidate=BEGIN
                                                                VALIDATE("VAT %");
                                                              END;

                                                   CaptionML=[DEU=Steuergruppencode;
                                                              ENU=Tax Group Code] }
    { 85  ;   ;Use Tax             ;Boolean       ;OnValidate=BEGIN
                                                                TESTFIELD("Gen. Posting Type","Gen. Posting Type"::Purchase);
                                                                VALIDATE("VAT %");
                                                              END;

                                                   CaptionML=[DEU=Verbrauchssteuer;
                                                              ENU=Use Tax] }
    { 86  ;   ;Bal. Tax Area Code  ;Code20        ;TableRelation="Tax Area";
                                                   OnValidate=BEGIN
                                                                VALIDATE("Bal. VAT %");
                                                              END;

                                                   CaptionML=[DEU=Gegenkonto Steuergebietscode;
                                                              ENU=Bal. Tax Area Code] }
    { 87  ;   ;Bal. Tax Liable     ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE("Bal. VAT %");
                                                              END;

                                                   CaptionML=[DEU=Gegenkonto Steuerpflichtig;
                                                              ENU=Bal. Tax Liable] }
    { 88  ;   ;Bal. Tax Group Code ;Code10        ;TableRelation="Tax Group";
                                                   OnValidate=BEGIN
                                                                VALIDATE("Bal. VAT %");
                                                              END;

                                                   CaptionML=[DEU=Gegenkonto Steuergruppencode;
                                                              ENU=Bal. Tax Group Code] }
    { 89  ;   ;Bal. Use Tax        ;Boolean       ;OnValidate=BEGIN
                                                                TESTFIELD("Bal. Gen. Posting Type","Bal. Gen. Posting Type"::Purchase);
                                                                VALIDATE("Bal. VAT %");
                                                              END;

                                                   CaptionML=[DEU=Gegenkonto Verbrauchssteuer;
                                                              ENU=Bal. Use Tax] }
    { 90  ;   ;VAT Bus. Posting Group;Code10      ;TableRelation="VAT Business Posting Group";
                                                   OnValidate=BEGIN
                                                                IF "Account Type" IN ["Account Type"::Customer,"Account Type"::Vendor,"Account Type"::"Bank Account"] THEN
                                                                  TESTFIELD("VAT Bus. Posting Group",'');

                                                                VALIDATE("VAT Prod. Posting Group");

                                                                IF JobTaskIsSet THEN BEGIN
                                                                  CreateTempJobJnlLine;
                                                                  UpdatePricesFromJobJnlLine;
                                                                END
                                                              END;

                                                   CaptionML=[DEU=MwSt.-Geschftsbuchungsgruppe;
                                                              ENU=VAT Bus. Posting Group] }
    { 91  ;   ;VAT Prod. Posting Group;Code10     ;TableRelation="VAT Product Posting Group";
                                                   OnValidate=BEGIN
                                                                IF "Account Type" IN ["Account Type"::Customer,"Account Type"::Vendor,"Account Type"::"Bank Account"] THEN
                                                                  TESTFIELD("VAT Prod. Posting Group",'');

                                                                CheckVATInAlloc;

                                                                "VAT %" := 0;
                                                                "VAT Calculation Type" := "VAT Calculation Type"::"Normal VAT";
                                                                IF "Gen. Posting Type" <> 0 THEN BEGIN
                                                                  IF NOT VATPostingSetup.GET("VAT Bus. Posting Group","VAT Prod. Posting Group") THEN
                                                                    VATPostingSetup.INIT;
                                                                  "VAT Calculation Type" := VATPostingSetup."VAT Calculation Type";
                                                                  CASE "VAT Calculation Type" OF
                                                                    "VAT Calculation Type"::"Normal VAT":
                                                                      "VAT %" := VATPostingSetup."VAT %";
                                                                    "VAT Calculation Type"::"Full VAT":
                                                                      CASE "Gen. Posting Type" OF
                                                                        "Gen. Posting Type"::Sale:
                                                                          BEGIN
                                                                            VATPostingSetup.TESTFIELD("Sales VAT Account");
                                                                            TESTFIELD("Account No.",VATPostingSetup."Sales VAT Account");
                                                                          END;
                                                                        "Gen. Posting Type"::Purchase:
                                                                          BEGIN
                                                                            VATPostingSetup.TESTFIELD("Purchase VAT Account");
                                                                            TESTFIELD("Account No.",VATPostingSetup."Purchase VAT Account");
                                                                          END;
                                                                      END;
                                                                  END;
                                                                END;
                                                                VALIDATE("VAT %");

                                                                IF JobTaskIsSet THEN BEGIN
                                                                  CreateTempJobJnlLine;
                                                                  UpdatePricesFromJobJnlLine;
                                                                END
                                                              END;

                                                   CaptionML=[DEU=MwSt.-Produktbuchungsgruppe;
                                                              ENU=VAT Prod. Posting Group] }
    { 92  ;   ;Bal. VAT Bus. Posting Group;Code10 ;TableRelation="VAT Business Posting Group";
                                                   OnValidate=BEGIN
                                                                IF "Bal. Account Type" IN
                                                                   ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor,"Bal. Account Type"::"Bank Account"]
                                                                THEN
                                                                  TESTFIELD("Bal. VAT Bus. Posting Group",'');

                                                                VALIDATE("Bal. VAT Prod. Posting Group");
                                                              END;

                                                   CaptionML=[DEU=Gegenkto. MwSt.-Gesch.-Buch.-G;
                                                              ENU=Bal. VAT Bus. Posting Group] }
    { 93  ;   ;Bal. VAT Prod. Posting Group;Code10;TableRelation="VAT Product Posting Group";
                                                   OnValidate=BEGIN
                                                                IF "Bal. Account Type" IN
                                                                   ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor,"Bal. Account Type"::"Bank Account"]
                                                                THEN
                                                                  TESTFIELD("Bal. VAT Prod. Posting Group",'');

                                                                "Bal. VAT %" := 0;
                                                                "Bal. VAT Calculation Type" := "Bal. VAT Calculation Type"::"Normal VAT";
                                                                IF "Bal. Gen. Posting Type" <> 0 THEN BEGIN
                                                                  IF NOT VATPostingSetup.GET("Bal. VAT Bus. Posting Group","Bal. VAT Prod. Posting Group") THEN
                                                                    VATPostingSetup.INIT;
                                                                  "Bal. VAT Calculation Type" := VATPostingSetup."VAT Calculation Type";
                                                                  CASE "Bal. VAT Calculation Type" OF
                                                                    "Bal. VAT Calculation Type"::"Normal VAT":
                                                                      "Bal. VAT %" := VATPostingSetup."VAT %";
                                                                    "Bal. VAT Calculation Type"::"Full VAT":
                                                                      CASE "Bal. Gen. Posting Type" OF
                                                                        "Bal. Gen. Posting Type"::Sale:
                                                                          BEGIN
                                                                            VATPostingSetup.TESTFIELD("Sales VAT Account");
                                                                            TESTFIELD("Bal. Account No.",VATPostingSetup."Sales VAT Account");
                                                                          END;
                                                                        "Bal. Gen. Posting Type"::Purchase:
                                                                          BEGIN
                                                                            VATPostingSetup.TESTFIELD("Purchase VAT Account");
                                                                            TESTFIELD("Bal. Account No.",VATPostingSetup."Purchase VAT Account");
                                                                          END;
                                                                      END;
                                                                  END;
                                                                END;
                                                                VALIDATE("Bal. VAT %");
                                                              END;

                                                   CaptionML=[DEU=Gegenkto. MwSt.-Prod.-Buch.-Gr;
                                                              ENU=Bal. VAT Prod. Posting Group] }
    { 95  ;   ;Additional-Currency Posting;Option ;CaptionML=[DEU=Buchung (BW);
                                                              ENU=Additional-Currency Posting];
                                                   OptionCaptionML=[DEU=Kein,Nur Betrag,Nur Betrag (BW);
                                                                    ENU=None,Amount Only,Additional-Currency Amount Only];
                                                   OptionString=None,Amount Only,Additional-Currency Amount Only;
                                                   Editable=No }
    { 98  ;   ;FA Add.-Currency Factor;Decimal    ;CaptionML=[DEU=Anlagenwhrungsfaktor (BW);
                                                              ENU=FA Add.-Currency Factor];
                                                   DecimalPlaces=0:15;
                                                   MinValue=0 }
    { 99  ;   ;Source Currency Code;Code10        ;TableRelation=Currency;
                                                   CaptionML=[DEU=Urspr.-Whrungscode;
                                                              ENU=Source Currency Code];
                                                   Editable=No }
    { 100 ;   ;Source Currency Amount;Decimal     ;CaptionML=[DEU=Urspr.-Whrung Betrag;
                                                              ENU=Source Currency Amount];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 101 ;   ;Source Curr. VAT Base Amount;Decimal;
                                                   CaptionML=[DEU=Urspr.-Whrung MwSt.-Bemess.;
                                                              ENU=Source Curr. VAT Base Amount];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 102 ;   ;Source Curr. VAT Amount;Decimal    ;CaptionML=[DEU=Urspr.-Whrung MwSt.-Betrag;
                                                              ENU=Source Curr. VAT Amount];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 103 ;   ;VAT Base Discount % ;Decimal       ;CaptionML=[DEU=MwSt.-Bemessungsgr. Skonto %;
                                                              ENU=VAT Base Discount %];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   Editable=No }
    { 104 ;   ;VAT Amount (LCY)    ;Decimal       ;CaptionML=[DEU=MwSt.-Betrag (MW);
                                                              ENU=VAT Amount (LCY)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 105 ;   ;VAT Base Amount (LCY);Decimal      ;CaptionML=[DEU=MwSt.-Bemessungsgrundlage (MW);
                                                              ENU=VAT Base Amount (LCY)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 106 ;   ;Bal. VAT Amount (LCY);Decimal      ;CaptionML=[DEU=Gegenkonto MwSt.-Betrag (MW);
                                                              ENU=Bal. VAT Amount (LCY)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 107 ;   ;Bal. VAT Base Amount (LCY);Decimal ;CaptionML=[DEU=Gegenkto. MwSt.-Bemess. (MW);
                                                              ENU=Bal. VAT Base Amount (LCY)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 108 ;   ;Reversing Entry     ;Boolean       ;CaptionML=[DEU=Umkehrbuchung;
                                                              ENU=Reversing Entry];
                                                   Editable=No }
    { 109 ;   ;Allow Zero-Amount Posting;Boolean  ;CaptionML=[DEU=Buchung ohne Betrag zulassen;
                                                              ENU=Allow Zero-Amount Posting];
                                                   Editable=No }
    { 110 ;   ;Ship-to/Order Address Code;Code10  ;TableRelation=IF (Account Type=CONST(Customer)) "Ship-to Address".Code WHERE (Customer No.=FIELD(Bill-to/Pay-to No.))
                                                                 ELSE IF (Account Type=CONST(Vendor)) "Order Address".Code WHERE (Vendor No.=FIELD(Bill-to/Pay-to No.))
                                                                 ELSE IF (Bal. Account Type=CONST(Customer)) "Ship-to Address".Code WHERE (Customer No.=FIELD(Bill-to/Pay-to No.))
                                                                 ELSE IF (Bal. Account Type=CONST(Vendor)) "Order Address".Code WHERE (Vendor No.=FIELD(Bill-to/Pay-to No.));
                                                   CaptionML=[DEU=Verk. an/Eink. von Adresscode;
                                                              ENU=Ship-to/Order Address Code] }
    { 111 ;   ;VAT Difference      ;Decimal       ;CaptionML=[DEU=MwSt.-Differenz;
                                                              ENU=VAT Difference];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 112 ;   ;Bal. VAT Difference ;Decimal       ;CaptionML=[DEU=Gegenkonto MwSt.-Differenz;
                                                              ENU=Bal. VAT Difference];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Currency Code" }
    { 113 ;   ;IC Partner Code     ;Code20        ;TableRelation="IC Partner";
                                                   CaptionML=[DEU=IC-Partnercode;
                                                              ENU=IC Partner Code];
                                                   Editable=No }
    { 114 ;   ;IC Direction        ;Option        ;CaptionML=[DEU=IC-Richtung;
                                                              ENU=IC Direction];
                                                   OptionCaptionML=[DEU=Ausgehend,Eingehend;
                                                                    ENU=Outgoing,Incoming];
                                                   OptionString=Outgoing,Incoming }
    { 116 ;   ;IC Partner G/L Acc. No.;Code20     ;TableRelation="IC G/L Account";
                                                   OnValidate=VAR
                                                                ICGLAccount@1000 : Record 410;
                                                              BEGIN
                                                                IF "IC Partner G/L Acc. No." <> '' THEN BEGIN
                                                                  GetTemplate;
                                                                  GenJnlTemplate.TESTFIELD(Type,GenJnlTemplate.Type::Intercompany);
                                                                  IF ICGLAccount.GET("IC Partner G/L Acc. No.") THEN
                                                                    ICGLAccount.TESTFIELD(Blocked,FALSE);
                                                                END
                                                              END;

                                                   CaptionML=[DEU=IC-Partner Sachkontonr.;
                                                              ENU=IC Partner G/L Acc. No.] }
    { 117 ;   ;IC Partner Transaction No.;Integer ;CaptionML=[DEU=IC-Partner Transaktionsnr.;
                                                              ENU=IC Partner Transaction No.];
                                                   Editable=No }
    { 118 ;   ;Sell-to/Buy-from No.;Code20        ;TableRelation=IF (Account Type=CONST(Customer)) Customer
                                                                 ELSE IF (Bal. Account Type=CONST(Customer)) Customer
                                                                 ELSE IF (Account Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Bal. Account Type=CONST(Vendor)) Vendor;
                                                   OnValidate=BEGIN
                                                                GLSetup.GET;
                                                                IF GLSetup."Bill-to/Sell-to VAT Calc." = GLSetup."Bill-to/Sell-to VAT Calc."::"Sell-to/Buy-from No." THEN
                                                                  UpdateCountryCodeAndVATRegNo("Sell-to/Buy-from No.");
                                                              END;

                                                   CaptionML=[DEU=Verk. an/Eink. von Nr.;
                                                              ENU=Sell-to/Buy-from No.] }
    { 119 ;   ;VAT Registration No.;Text20        ;OnValidate=VAR
                                                                VATRegNoFormat@1000 : Record 381;
                                                              BEGIN
                                                                VATRegNoFormat.Test("VAT Registration No.","Country/Region Code",'',0);
                                                              END;

                                                   CaptionML=[DEU=USt-IdNr.;
                                                              ENU=VAT Registration No.] }
    { 120 ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   OnValidate=BEGIN
                                                                VALIDATE("VAT Registration No.");
                                                              END;

                                                   CaptionML=[DEU=Lnder-/Regionscode;
                                                              ENU=Country/Region Code] }
    { 121 ;   ;Prepayment          ;Boolean       ;CaptionML=[DEU=Vorauszahlung;
                                                              ENU=Prepayment] }
    { 122 ;   ;Financial Void      ;Boolean       ;CaptionML=[DEU=Finanziell entwertet;
                                                              ENU=Financial Void];
                                                   Editable=No }
    { 827 ;   ;Credit Card No.     ;Code20        ;TableRelation=IF (Account Type=CONST(Customer),
                                                                     Document Type=FILTER(Payment|Refund),
                                                                     Bal. Account Type=CONST(Bank Account)) "DO Payment Credit Card" WHERE (Customer No.=FIELD(Account No.));
                                                   OnValidate=VAR
                                                                DOPaymentMgt@1000 : Codeunit 825;
                                                              BEGIN
                                                                CheckNoCardTransactEntryExist(xRec);
                                                                IF "Credit Card No." = '' THEN
                                                                  EXIT;

                                                                DOPaymentMgt.CheckCreditCardData("Credit Card No.");
                                                              END;

                                                   CaptionML=[DEU=Kreditkartennr.;
                                                              ENU=Credit Card No.] }
    { 1001;   ;Job Task No.        ;Code20        ;TableRelation="Job Task"."Job Task No." WHERE (Job No.=FIELD(Job No.));
                                                   OnValidate=BEGIN
                                                                IF "Job Task No." = '' THEN BEGIN
                                                                  "Job Quantity" := 0;
                                                                  "Job Currency Factor" := 0;
                                                                  "Job Currency Code" := '';
                                                                  "Job Unit Price" := 0;
                                                                  "Job Total Price" := 0;
                                                                  "Job Line Amount" := 0;
                                                                  "Job Line Discount Amount" := 0;
                                                                  "Job Unit Cost" := 0;
                                                                  "Job Total Cost" := 0;
                                                                  "Job Line Discount %" := 0;

                                                                  "Job Unit Price (LCY)" := 0;
                                                                  "Job Total Price (LCY)" := 0;
                                                                  "Job Line Amount (LCY)" := 0;
                                                                  "Job Line Disc. Amount (LCY)" := 0;
                                                                  "Job Unit Cost (LCY)" := 0;
                                                                  "Job Total Cost (LCY)" := 0;
                                                                  EXIT;
                                                                END;

                                                                IF JobTaskIsSet THEN BEGIN
                                                                  CreateTempJobJnlLine;
                                                                  UpdatePricesFromJobJnlLine;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Projektaufgabennr.;
                                                              ENU=Job Task No.] }
    { 1002;   ;Job Unit Price (LCY);Decimal       ;CaptionML=[DEU=VK-Preis Projekt (MW);
                                                              ENU=Job Unit Price (LCY)];
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 1003;   ;Job Total Price (LCY);Decimal      ;CaptionML=[DEU=Verkaufsbetrag Projekt (MW);
                                                              ENU=Job Total Price (LCY)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 1004;   ;Job Quantity        ;Decimal       ;OnValidate=BEGIN
                                                                IF JobTaskIsSet THEN BEGIN
                                                                  CreateTempJobJnlLine;
                                                                  UpdatePricesFromJobJnlLine;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Projektmenge;
                                                              ENU=Job Quantity];
                                                   DecimalPlaces=0:5 }
    { 1005;   ;Job Unit Cost (LCY) ;Decimal       ;CaptionML=[DEU=Projekteinstandspreis (MW);
                                                              ENU=Job Unit Cost (LCY)];
                                                   Editable=No;
                                                   AutoFormatType=2 }
    { 1006;   ;Job Line Discount % ;Decimal       ;OnValidate=BEGIN
                                                                IF JobTaskIsSet THEN BEGIN
                                                                  CreateTempJobJnlLine;
                                                                  JobJnlLine.VALIDATE("Line Discount %","Job Line Discount %");
                                                                  UpdatePricesFromJobJnlLine;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Zeilenrabatt % Projekt;
                                                              ENU=Job Line Discount %];
                                                   AutoFormatType=1 }
    { 1007;   ;Job Line Disc. Amount (LCY);Decimal;OnValidate=BEGIN
                                                                IF JobTaskIsSet THEN BEGIN
                                                                  CreateTempJobJnlLine;
                                                                  JobJnlLine.VALIDATE("Line Discount Amount (LCY)","Job Line Disc. Amount (LCY)");
                                                                  UpdatePricesFromJobJnlLine;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Zeilenrabattbetrag Projekt (MW);
                                                              ENU=Job Line Disc. Amount (LCY)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 1008;   ;Job Unit Of Measure Code;Code10    ;TableRelation="Unit of Measure";
                                                   CaptionML=[DEU=Projekteinheitencode;
                                                              ENU=Job Unit Of Measure Code] }
    { 1009;   ;Job Line Type       ;Option        ;CaptionML=[DEU=Projektzeilenart;
                                                              ENU=Job Line Type];
                                                   OptionCaptionML=[DEU=" ,Plan,Vertrag,Plan und Vertrag";
                                                                    ENU=" ,Schedule,Contract,Both Schedule and Contract"];
                                                   OptionString=[ ,Schedule,Contract,Both Schedule and Contract] }
    { 1010;   ;Job Unit Price      ;Decimal       ;OnValidate=BEGIN
                                                                IF JobTaskIsSet THEN BEGIN
                                                                  CreateTempJobJnlLine;
                                                                  JobJnlLine.VALIDATE("Unit Price","Job Unit Price");
                                                                  UpdatePricesFromJobJnlLine;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=VK-Preis Projekt;
                                                              ENU=Job Unit Price];
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Job Currency Code" }
    { 1011;   ;Job Total Price     ;Decimal       ;CaptionML=[DEU=Verkaufsbetrag Projekt;
                                                              ENU=Job Total Price];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Job Currency Code" }
    { 1012;   ;Job Unit Cost       ;Decimal       ;CaptionML=[DEU=Projekteinstandspreis;
                                                              ENU=Job Unit Cost];
                                                   Editable=No;
                                                   AutoFormatType=2;
                                                   AutoFormatExpr="Job Currency Code" }
    { 1013;   ;Job Total Cost      ;Decimal       ;CaptionML=[DEU=Projekteinstandsbetrag;
                                                              ENU=Job Total Cost];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Job Currency Code" }
    { 1014;   ;Job Line Discount Amount;Decimal   ;OnValidate=BEGIN
                                                                IF JobTaskIsSet THEN BEGIN
                                                                  CreateTempJobJnlLine;
                                                                  JobJnlLine.VALIDATE("Line Discount Amount","Job Line Discount Amount");
                                                                  UpdatePricesFromJobJnlLine;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Zeilenrabattbetrag Projekt;
                                                              ENU=Job Line Discount Amount];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Job Currency Code" }
    { 1015;   ;Job Line Amount     ;Decimal       ;OnValidate=BEGIN
                                                                IF JobTaskIsSet THEN BEGIN
                                                                  CreateTempJobJnlLine;
                                                                  JobJnlLine.VALIDATE("Line Amount","Job Line Amount");
                                                                  UpdatePricesFromJobJnlLine;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Zeilenbetrag Projekt;
                                                              ENU=Job Line Amount];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr="Job Currency Code" }
    { 1016;   ;Job Total Cost (LCY);Decimal       ;CaptionML=[DEU=Projekteinstandsbetrag (MW);
                                                              ENU=Job Total Cost (LCY)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 1017;   ;Job Line Amount (LCY);Decimal      ;OnValidate=BEGIN
                                                                IF JobTaskIsSet THEN BEGIN
                                                                  CreateTempJobJnlLine;
                                                                  JobJnlLine.VALIDATE("Line Amount (LCY)","Job Line Amount (LCY)");
                                                                  UpdatePricesFromJobJnlLine;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Zeilenbetrag Projekt (MW);
                                                              ENU=Job Line Amount (LCY)];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 1018;   ;Job Currency Factor ;Decimal       ;CaptionML=[DEU=Whrungsfaktor Projekt;
                                                              ENU=Job Currency Factor] }
    { 1019;   ;Job Currency Code   ;Code10        ;OnValidate=BEGIN
                                                                IF ("Job Currency Code" <> xRec."Job Currency Code") OR ("Job Currency Code" <> '') THEN
                                                                  IF JobTaskIsSet THEN BEGIN
                                                                    CreateTempJobJnlLine;
                                                                    UpdatePricesFromJobJnlLine;
                                                                  END;
                                                              END;

                                                   CaptionML=[DEU=Whrungscode Projekt;
                                                              ENU=Job Currency Code] }
    { 5050;   ;Campaign No.        ;Code20        ;TableRelation=Campaign;
                                                   OnValidate=BEGIN
                                                                CreateDim(
                                                                  DATABASE::Campaign,"Campaign No.",
                                                                  DimMgt.TypeToTableID1("Account Type"),"Account No.",
                                                                  DimMgt.TypeToTableID1("Bal. Account Type"),"Bal. Account No.",
                                                                  DATABASE::Job,"Job No.",
                                                                  DATABASE::"Salesperson/Purchaser","Salespers./Purch. Code");
                                                              END;

                                                   CaptionML=[DEU=Kampagnennr.;
                                                              ENU=Campaign No.] }
    { 5400;   ;Prod. Order No.     ;Code20        ;CaptionML=[DEU=FA-Nr.;
                                                              ENU=Prod. Order No.];
                                                   Editable=No }
    { 5600;   ;FA Posting Date     ;Date          ;CaptionML=[DEU=Anlagedatum;
                                                              ENU=FA Posting Date] }
    { 5601;   ;FA Posting Type     ;Option        ;OnValidate=BEGIN
                                                                IF  NOT (("Account Type" = "Account Type"::"Fixed Asset") OR
                                                                    ("Bal. Account Type" = "Bal. Account Type"::"Fixed Asset")) AND
                                                                    ("FA Posting Type" = "FA Posting Type"::" ")
                                                                THEN BEGIN
                                                                  "FA Posting Date" := 0D;
                                                                  "Salvage Value" := 0;
                                                                  "No. of Depreciation Days" := 0;
                                                                  "Depr. until FA Posting Date" := FALSE;
                                                                  "Depr. Acquisition Cost" := FALSE;
                                                                  "Maintenance Code" := '';
                                                                  "Insurance No." := '';
                                                                  "Budgeted FA No." := '';
                                                                  "Duplicate in Depreciation Book" := '';
                                                                  "Use Duplication List" := FALSE;
                                                                  "FA Reclassification Entry" := FALSE;
                                                                  "FA Error Entry No." := 0;
                                                                END;

                                                                IF "FA Posting Type" <> "FA Posting Type"::"Acquisition Cost" THEN
                                                                  TESTFIELD("Insurance No.",'');
                                                                IF "FA Posting Type" <> "FA Posting Type"::Maintenance THEN
                                                                  TESTFIELD("Maintenance Code",'');
                                                                GetFAVATSetup;
                                                                GetFAAddCurrExchRate;
                                                              END;

                                                   CaptionML=[DEU=Anlagenbuchungsart;
                                                              ENU=FA Posting Type];
                                                   OptionCaptionML=[DEU=" ,Anschaffung,Normal-AfA,Erhhte AfA,Zuschreibung,Sonder-AfA,Benutzerdef. AfA,Verkauf,Wartung";
                                                                    ENU=" ,Acquisition Cost,Depreciation,Write-Down,Appreciation,Custom 1,Custom 2,Disposal,Maintenance"];
                                                   OptionString=[ ,Acquisition Cost,Depreciation,Write-Down,Appreciation,Custom 1,Custom 2,Disposal,Maintenance] }
    { 5602;   ;Depreciation Book Code;Code10      ;TableRelation="Depreciation Book";
                                                   OnValidate=BEGIN
                                                                IF "Depreciation Book Code" = '' THEN
                                                                  EXIT;

                                                                IF ("Account No." <> '') AND
                                                                   ("Account Type" = "Account Type"::"Fixed Asset")
                                                                THEN BEGIN
                                                                  FADeprBook.GET("Account No.","Depreciation Book Code");
                                                                  "Posting Group" := FADeprBook."FA Posting Group";
                                                                END;

                                                                IF ("Bal. Account No." <> '') AND
                                                                   ("Bal. Account Type" = "Bal. Account Type"::"Fixed Asset")
                                                                THEN BEGIN
                                                                  FADeprBook.GET("Bal. Account No.","Depreciation Book Code");
                                                                  "Posting Group" := FADeprBook."FA Posting Group";
                                                                END;
                                                                GetFAVATSetup;
                                                                GetFAAddCurrExchRate;
                                                              END;

                                                   CaptionML=[DEU=AfA Buchcode;
                                                              ENU=Depreciation Book Code] }
    { 5603;   ;Salvage Value       ;Decimal       ;CaptionML=[DEU=Restwert;
                                                              ENU=Salvage Value];
                                                   AutoFormatType=1 }
    { 5604;   ;No. of Depreciation Days;Integer   ;CaptionML=[DEU=Anzahl AfA-Tage;
                                                              ENU=No. of Depreciation Days];
                                                   BlankZero=Yes }
    { 5605;   ;Depr. until FA Posting Date;Boolean;CaptionML=[DEU=AfA bis Anlagedatum;
                                                              ENU=Depr. until FA Posting Date] }
    { 5606;   ;Depr. Acquisition Cost;Boolean     ;CaptionML=[DEU=Rckw. AfA-Korr. b. Anschaff.;
                                                              ENU=Depr. Acquisition Cost] }
    { 5609;   ;Maintenance Code    ;Code10        ;TableRelation=Maintenance;
                                                   OnValidate=BEGIN
                                                                IF "Maintenance Code" <> '' THEN
                                                                  TESTFIELD("FA Posting Type","FA Posting Type"::Maintenance);
                                                              END;

                                                   CaptionML=[DEU=Wartungscode;
                                                              ENU=Maintenance Code] }
    { 5610;   ;Insurance No.       ;Code20        ;TableRelation=Insurance;
                                                   OnValidate=BEGIN
                                                                IF "Insurance No." <> '' THEN
                                                                  TESTFIELD("FA Posting Type","FA Posting Type"::"Acquisition Cost");
                                                              END;

                                                   CaptionML=[DEU=Versicherungsnr.;
                                                              ENU=Insurance No.] }
    { 5611;   ;Budgeted FA No.     ;Code20        ;TableRelation="Fixed Asset";
                                                   OnValidate=BEGIN
                                                                IF "Budgeted FA No." <> '' THEN BEGIN
                                                                  FA.GET("Budgeted FA No.");
                                                                  FA.TESTFIELD(FA."Budgeted Asset",TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Plananlagennr.;
                                                              ENU=Budgeted FA No.] }
    { 5612;   ;Duplicate in Depreciation Book;Code10;
                                                   TableRelation="Depreciation Book";
                                                   OnValidate=BEGIN
                                                                "Use Duplication List" := FALSE;
                                                              END;

                                                   CaptionML=[DEU=In AfA-Buch kopieren;
                                                              ENU=Duplicate in Depreciation Book] }
    { 5613;   ;Use Duplication List;Boolean       ;OnValidate=BEGIN
                                                                "Duplicate in Depreciation Book" := '';
                                                              END;

                                                   CaptionML=[DEU=Kopiervorgang aktivieren;
                                                              ENU=Use Duplication List] }
    { 5614;   ;FA Reclassification Entry;Boolean  ;CaptionML=[DEU=Anlagenumbuchungsposten;
                                                              ENU=FA Reclassification Entry] }
    { 5615;   ;FA Error Entry No.  ;Integer       ;TableRelation="FA Ledger Entry";
                                                   CaptionML=[DEU=Anlagenstornoposten Lfd. Nr.;
                                                              ENU=FA Error Entry No.];
                                                   BlankZero=Yes }
    { 5616;   ;Index Entry         ;Boolean       ;CaptionML=[DEU=Indexierungsbuchung;
                                                              ENU=Index Entry] }
  }
  KEYS
  {
    {    ;Journal Template Name,Journal Batch Name,Line No.;
                                                   SumIndexFields=Balance (LCY);
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
    {    ;Journal Template Name,Journal Batch Name,Posting Date,Document No.;
                                                   MaintainSQLIndex=No }
    {    ;Account Type,Account No.,Applies-to Doc. Type,Applies-to Doc. No. }
    {    ;Journal Template Name,Journal Batch Name,Account Type,Document Type;
                                                   SumIndexFields=Amount (LCY) }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=%1 oder %2 muss Sach- oder Bankkonto sein.;ENU=%1 or %2 must be G/L Account or Bank Account.';
      Text001@1001 : TextConst 'DEU=Sie drfen nicht %1 angeben, wenn %2 %3 ist.;ENU=You must not specify %1 when %2 is %3.';
      Text002@1002 : TextConst 'DEU=kann nicht ohne %1 spezifiziert werden;ENU=cannot be specified without %1';
      Text003@1003 : TextConst 'DEU=Der %1 in der %2 wird von %3 auf %4 gendert.\;ENU=The %1 in the %2 will be changed from %3 to %4.\';
      Text004@1004 : TextConst 'DEU=Mchten Sie fortfahren?;ENU=Do you wish to continue?';
      Text005@1005 : TextConst 'DEU=Die Verarbeitung wurde unterbrochen, um die Warnung zu bercksichtigen.;ENU=The update has been interrupted to respect the warning.';
      Text006@1006 : TextConst 'DEU=Die Option %1 kann nur vom System intern verwendet werden.;ENU=The %1 option can only be used internally in the system.';
      Text007@1007 : TextConst 'DEU=%1 oder %2 muss ein Bankkonto sein.;ENU=%1 or %2 must be a Bank Account.';
      Text008@1008 : TextConst 'DEU=" muss 0 sein, wenn %1 %2 ist.";ENU=" must be 0 when %1 is %2."';
      Text009@1009 : TextConst 'DEU=MW;ENU=LCY';
      Text010@1010 : TextConst 'DEU=%1 muss %2 oder %3 sein.;ENU=%1 must be %2 or %3.';
      Text011@1011 : TextConst 'DEU=%1 muss ein negatives Vorzeichen haben.;ENU=%1 must be negative.';
      Text012@1012 : TextConst 'DEU=%1 muss positiv sein.;ENU=%1 must be positive.';
      Text013@1013 : TextConst 'DEU=Die %1 darf nicht grer sein als %2.;ENU=The %1 must not be more than %2.';
      Text016@1065 : TextConst 'DEU=Kreditkarte %1 wurde bereits fr diese(n) %2 ausgefhrt, doch es gab einen Buchungsfehler. Die Buchung von %2 %3 muss abgeschlossen werden.;ENU=Credit card %1 has already been performed for this %2, but posting failed. You must complete posting of %2 %3.';
      GenJnlTemplate@1014 : Record 80;
      GenJnlBatch@1015 : Record 232;
      GenJnlLine@1016 : Record 81;
      GLAcc@1017 : Record 15;
      Cust@1018 : Record 18;
      Cust2@1019 : Record 18;
      Vend@1020 : Record 23;
      Vend2@1021 : Record 23;
      ICPartner@1057 : Record 413;
      Currency@1022 : Record 4;
      CurrExchRate@1023 : Record 330;
      PaymentTerms@1024 : Record 3;
      CustLedgEntry@1025 : Record 21;
      VendLedgEntry@1026 : Record 25;
      GenJnlAlloc@1027 : Record 221;
      VATPostingSetup@1028 : Record 325;
      BankAcc@1029 : Record 270;
      BankAcc2@1030 : Record 270;
      BankAcc3@1031 : Record 270;
      FA@1032 : Record 5600;
      FASetup@1033 : Record 5603;
      FADeprBook@1034 : Record 5612;
      GenBusPostingGrp@1035 : Record 250;
      GenProdPostingGrp@1036 : Record 251;
      GLSetup@1037 : Record 98;
      Job@1060 : Record 167;
      JobJnlLine@1059 : TEMPORARY Record 210;
      ApplyCustEntries@1038 : Form 232;
      ApplyVendEntries@1039 : Form 233;
      NoSeriesMgt@1040 : Codeunit 396;
      CustCheckCreditLimit@1041 : Codeunit 312;
      SalesTaxCalculate@1042 : Codeunit 398;
      GenJnlApply@1043 : Codeunit 225;
      CustEntrySetApplID@1044 : Codeunit 101;
      VendEntrySetApplID@1045 : Codeunit 111;
      DimMgt@1046 : Codeunit 408;
      PaymentToleranceMgt@1053 : Codeunit 426;
      AccNo@1047 : Code[20];
      FromCurrencyCode@1048 : Code[10];
      ToCurrencyCode@1049 : Code[10];
      AccType@1050 : 'G/L Account,Customer,Vendor,Bank Account,Fixed Asset';
      ReplaceInfo@1051 : Boolean;
      CurrencyCode@1052 : Code[10];
      Text014@1054 : TextConst 'DEU=%1 %2 hat %3 %4.\Mchten Sie %1 %2 in dieser Zeile verwenden?;ENU=The %1 %2 has a %3 %4.\Do you still want to use %1 %2 in this journal line?';
      OK@1055 : Boolean;
      TemplateFound@1056 : Boolean;
      Text015@1058 : TextConst 'DEU=Sie drfen keinen Posten auf einen anderen Posten mit frherem Buchungsdatum anwenden und buchen.\\Buchen Sie stattdessen %1 %2, und wenden Sie das Ergebnis dann auf %3 %4 an.;ENU=You are not allowed to apply and post an entry to an entry with an earlier posting date.\\Instead, post %1 %2 and then apply it to %3 %4.';
      CurrencyDate@1061 : Date;
      SourceCodeSetup@1063 : Record 242;
      Text11500@1150000 : TextConst 'DEU=Diese nderung hat mglicherweise Einfluss auf Skonti. Prfen Sie den Skontobetrag.;ENU=This change may affect payment discounts. Please verify the payment discount amount.';
      Text11501@1000003 : TextConst 'DEU=Rechnung %1 ist nicht vorhanden oder bereits geschlossen.;ENU=Invoice %1 doesn''t exist or is already closed.';

    PROCEDURE EmptyLine@5() : Boolean;
    BEGIN
      EXIT(
        ("Account No." = '') AND (Amount = 0) AND
        (("Bal. Account No." = '') OR NOT "System-Created Entry"));
    END;

    PROCEDURE UpdateLineBalance@2();
    BEGIN
      IF ((Amount > 0) AND (NOT Correction)) OR
         ((Amount < 0) AND Correction)
      THEN BEGIN
        "Debit Amount" := Amount;
        "Credit Amount" := 0
      END ELSE BEGIN
        "Debit Amount" := 0;
        "Credit Amount" := -Amount;
      END;
      IF "Currency Code" = '' THEN
        "Amount (LCY)" := Amount;
      CASE TRUE OF
        ("Account No." <> '') AND ("Bal. Account No." <> ''):
          "Balance (LCY)" := 0;
        "Bal. Account No." <> '':
          "Balance (LCY)" := -"Amount (LCY)";
        ELSE
          "Balance (LCY)" := "Amount (LCY)";
      END;

      CLEAR(GenJnlAlloc);
      GenJnlAlloc.UpdateAllocations(Rec);

      UpdateSalesPurchLCY;
    END;

    PROCEDURE SetUpNewLine@9(LastGenJnlLine@1000 : Record 81;Balance@1001 : Decimal;BottomLine@1002 : Boolean);
    BEGIN
      GenJnlTemplate.GET("Journal Template Name");
      GenJnlBatch.GET("Journal Template Name","Journal Batch Name");
      GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
      GenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
      IF GenJnlLine.FIND('-') THEN BEGIN
        "Posting Date" := LastGenJnlLine."Posting Date";
        "Document Date" := LastGenJnlLine."Posting Date";
        "Document No." := LastGenJnlLine."Document No.";
        IF BottomLine AND
           (Balance - LastGenJnlLine."Balance (LCY)" = 0) AND
           NOT LastGenJnlLine.EmptyLine
        THEN
          "Document No." := INCSTR("Document No.");
      END ELSE BEGIN
        "Posting Date" := WORKDATE;
        "Document Date" := WORKDATE;
        IF GenJnlBatch."No. Series" <> '' THEN BEGIN
          CLEAR(NoSeriesMgt);
          "Document No." := NoSeriesMgt.TryGetNextNo(GenJnlBatch."No. Series","Posting Date");
        END;
      END;
      IF GenJnlTemplate.Recurring THEN
        "Recurring Method" := LastGenJnlLine."Recurring Method";
      "Account Type" := LastGenJnlLine."Account Type";
      "Document Type" := LastGenJnlLine."Document Type";
      "Source Code" := GenJnlTemplate."Source Code";
      "Reason Code" := GenJnlBatch."Reason Code";
      "Posting No. Series" := GenJnlBatch."Posting No. Series";
      "Bal. Account Type" := GenJnlBatch."Bal. Account Type";
      IF ("Account Type" IN ["Account Type"::Customer,"Account Type"::Vendor,"Account Type"::"Fixed Asset"]) AND
         ("Bal. Account Type" IN ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor,"Bal. Account Type"::"Fixed Asset"])
      THEN
        "Account Type" := "Account Type"::"G/L Account";
      VALIDATE("Bal. Account No.",GenJnlBatch."Bal. Account No.");
      Description := '';
    END;

    LOCAL PROCEDURE CheckVATInAlloc@1();
    BEGIN
      IF "Gen. Posting Type" <> 0 THEN BEGIN
        GenJnlAlloc.RESET;
        GenJnlAlloc.SETRANGE("Journal Template Name","Journal Template Name");
        GenJnlAlloc.SETRANGE("Journal Batch Name","Journal Batch Name");
        GenJnlAlloc.SETRANGE("Journal Line No.","Line No.");
        IF GenJnlAlloc.FIND('-') THEN
          REPEAT
            GenJnlAlloc.CheckVAT(Rec);
          UNTIL GenJnlAlloc.NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE SetCurrencyCode@4(AccType2@1000 : 'G/L Account,Customer,Vendor,Bank Account';AccNo2@1001 : Code[20]) : Boolean;
    BEGIN
      "Currency Code" := '';
      IF AccNo2 <> '' THEN
        CASE AccType2 OF
          AccType2::Customer:
            IF Cust2.GET(AccNo2) THEN
              "Currency Code" := Cust2."Currency Code";
          AccType2::Vendor:
            IF Vend2.GET(AccNo2) THEN
              "Currency Code" := Vend2."Currency Code";
          AccType2::"Bank Account":
            IF BankAcc2.GET(AccNo2) THEN
              "Currency Code" := BankAcc2."Currency Code";
        END;
      EXIT("Currency Code" <> '');
    END;

    LOCAL PROCEDURE GetCurrency@3();
    BEGIN
      IF "Additional-Currency Posting" =
         "Additional-Currency Posting"::"Additional-Currency Amount Only"
      THEN BEGIN
        IF GLSetup."Additional Reporting Currency" = '' THEN
          GLSetup.GET;
        CurrencyCode := GLSetup."Additional Reporting Currency";
      END ELSE
        CurrencyCode := "Currency Code";

      IF CurrencyCode = '' THEN BEGIN
        CLEAR(Currency);
        Currency.InitRoundingPrecision
      END ELSE
        IF CurrencyCode <> Currency.Code THEN BEGIN
          Currency.GET(CurrencyCode);
          Currency.TESTFIELD("Amount Rounding Precision");
        END;
    END;

    PROCEDURE UpdateSource@6();
    VAR
      SourceExists1@1000 : Boolean;
      SourceExists2@1001 : Boolean;
    BEGIN
      SourceExists1 := ("Account Type" <> "Account Type"::"G/L Account") AND ("Account No." <> '');
      SourceExists2 := ("Bal. Account Type" <> "Bal. Account Type"::"G/L Account") AND ("Bal. Account No." <> '');
      CASE TRUE OF
        SourceExists1 AND NOT SourceExists2:
          BEGIN
            "Source Type" := "Account Type";
            "Source No." := "Account No.";
          END;
        SourceExists2 AND NOT SourceExists1:
          BEGIN
            "Source Type" := "Bal. Account Type";
            "Source No." := "Bal. Account No.";
          END;
        ELSE BEGIN
          "Source Type" := "Source Type"::" ";
          "Source No." := '';
        END;
      END;
    END;

    LOCAL PROCEDURE CheckGLAcc@7();
    BEGIN
      GLAcc.CheckGLAcc;
      IF GLAcc."Direct Posting" OR ("Journal Template Name" = '') OR "System-Created Entry" THEN
        EXIT;
      IF "Posting Date" <> 0D THEN
        IF "Posting Date" = CLOSINGDATE("Posting Date") THEN
          EXIT;
      GLAcc.TESTFIELD("Direct Posting",TRUE);
    END;

    PROCEDURE GetFAAddCurrExchRate@8();
    VAR
      DeprBook@1000 : Record 5611;
      FANo@1001 : Code[20];
      UseFAAddCurrExchRate@1002 : Boolean;
    BEGIN
      "FA Add.-Currency Factor" := 0;
      IF ("FA Posting Type" <> "FA Posting Type"::" ") AND
         ("Depreciation Book Code" <> '')
      THEN BEGIN
        IF ("Account Type" = "Account Type"::"Fixed Asset") THEN
          FANo := "Account No.";
        IF ("Bal. Account Type" = "Bal. Account Type"::"Fixed Asset") THEN
          FANo := "Bal. Account No.";
        IF FANo <> '' THEN BEGIN
          DeprBook.GET("Depreciation Book Code");
          CASE "FA Posting Type" OF
            "FA Posting Type"::"Acquisition Cost":
              UseFAAddCurrExchRate := DeprBook."Add-Curr Exch Rate - Acq. Cost";
            "FA Posting Type"::Depreciation:
              UseFAAddCurrExchRate := DeprBook."Add.-Curr. Exch. Rate - Depr.";
            "FA Posting Type"::"Write-Down":
              UseFAAddCurrExchRate := DeprBook."Add-Curr Exch Rate -Write-Down";
            "FA Posting Type"::Appreciation:
              UseFAAddCurrExchRate := DeprBook."Add-Curr. Exch. Rate - Apprec.";
            "FA Posting Type"::"Custom 1":
              UseFAAddCurrExchRate := DeprBook."Add-Curr. Exch Rate - Custom 1";
            "FA Posting Type"::"Custom 2":
              UseFAAddCurrExchRate := DeprBook."Add-Curr. Exch Rate - Custom 2";
            "FA Posting Type"::Disposal:
              UseFAAddCurrExchRate := DeprBook."Add.-Curr. Exch. Rate - Disp.";
            "FA Posting Type"::Maintenance:
              UseFAAddCurrExchRate := DeprBook."Add.-Curr. Exch. Rate - Maint.";
          END;
          IF UseFAAddCurrExchRate THEN BEGIN
            FADeprBook.GET(FANo,"Depreciation Book Code");
            FADeprBook.TESTFIELD("FA Add.-Currency Factor");
            "FA Add.-Currency Factor" := FADeprBook."FA Add.-Currency Factor";
          END;
        END;
      END;
    END;

    PROCEDURE GetShowCurrencyCode@10(CurrencyCode@1000 : Code[10]) : Code[10];
    BEGIN
      IF CurrencyCode <> '' THEN
        EXIT(CurrencyCode)
      ELSE
        EXIT(Text009);
    END;

    PROCEDURE ClearCustVendApplnEntry@11();
    VAR
      TempCustLedgEntry@1000 : Record 21;
      TempVendLedgEntry@1001 : Record 25;
      CustEntryEdit@1002 : Codeunit 103;
      VendEntryEdit@1003 : Codeunit 113;
    BEGIN
      IF Rec."Bal. Account Type" IN
         ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor]
      THEN BEGIN
        AccType := Rec."Bal. Account Type";
        AccNo := Rec."Bal. Account No.";
      END ELSE BEGIN
        AccType := Rec."Account Type";
        AccNo := Rec."Account No.";
      END;
      CASE AccType OF
        AccType::Customer:
          BEGIN
            CustLedgEntry.RESET;
            IF Rec."Applies-to ID" <> '' THEN BEGIN
              CustLedgEntry.SETCURRENTKEY("Customer No.","Applies-to ID",Open);
              CustLedgEntry.SETRANGE("Customer No.",AccNo);
              CustLedgEntry.SETRANGE("Applies-to ID",Rec."Applies-to ID");
              CustLedgEntry.SETRANGE(Open,TRUE);
              IF CustLedgEntry.FIND('-') THEN BEGIN
                CustLedgEntry."Accepted Pmt. Disc. Tolerance" := FALSE;
                CustLedgEntry."Accepted Payment Tolerance" := 0;
                CustLedgEntry."Amount to Apply" := 0;
                CustEntrySetApplID.SetApplId(CustLedgEntry,TempCustLedgEntry,0,0,'');
              END;
            END ELSE IF Rec."Applies-to Doc. No." <> '' THEN BEGIN
              CustLedgEntry.SETCURRENTKEY("Document No.");
              CustLedgEntry.SETRANGE("Document No.",Rec."Applies-to Doc. No.");
              CustLedgEntry.SETRANGE("Document Type",Rec."Applies-to Doc. Type");
              CustLedgEntry.SETRANGE("Customer No.",AccNo);
              CustLedgEntry.SETRANGE(Open,TRUE);
              IF CustLedgEntry.FIND('-') THEN BEGIN
                CustLedgEntry."Accepted Pmt. Disc. Tolerance" := FALSE;
                CustLedgEntry."Accepted Payment Tolerance" := 0;
                CustLedgEntry."Amount to Apply" := 0;
                CustEntryEdit.RUN(CustLedgEntry);
              END;
            END;
          END;
        AccType::Vendor:
          BEGIN
            VendLedgEntry.RESET;
            IF Rec."Applies-to ID" <> '' THEN BEGIN
              VendLedgEntry.SETCURRENTKEY("Vendor No.","Applies-to ID",Open);
              VendLedgEntry.SETRANGE("Vendor No.",AccNo);
              VendLedgEntry.SETRANGE("Applies-to ID",Rec."Applies-to ID");
              VendLedgEntry.SETRANGE(Open,TRUE);
              IF VendLedgEntry.FIND('-') THEN BEGIN
                VendLedgEntry."Accepted Pmt. Disc. Tolerance" := FALSE;
                VendLedgEntry."Accepted Payment Tolerance" := 0;
                VendLedgEntry."Amount to Apply" := 0;
                VendEntrySetApplID.SetApplId(VendLedgEntry,TempVendLedgEntry,0,0,'');
            END;
            END ELSE IF Rec."Applies-to Doc. No." <> '' THEN BEGIN
              VendLedgEntry.SETCURRENTKEY("Document No.");
              VendLedgEntry.SETRANGE("Document No.",Rec."Applies-to Doc. No.");
              VendLedgEntry.SETRANGE("Document Type",Rec."Applies-to Doc. Type");
              VendLedgEntry.SETRANGE("Vendor No.",AccNo);
              VendLedgEntry.SETRANGE(Open,TRUE);
              IF VendLedgEntry.FIND('-') THEN BEGIN
                VendLedgEntry."Accepted Pmt. Disc. Tolerance" := FALSE;
                VendLedgEntry."Accepted Payment Tolerance" := 0;
                VendLedgEntry."Amount to Apply" := 0;
                VendEntryEdit.RUN(VendLedgEntry);
              END;
            END;
        END;
      END;
    END;

    PROCEDURE CheckFixedCurrency@12() : Boolean;
    VAR
      CurrExchRate@1000 : Record 330;
    BEGIN
      CurrExchRate.SETRANGE("Currency Code","Currency Code");
      CurrExchRate.SETRANGE("Starting Date",0D,"Posting Date");

      IF NOT CurrExchRate.FIND('+') THEN
        EXIT(FALSE);

      IF CurrExchRate."Relational Currency Code" = '' THEN
        EXIT(
          CurrExchRate."Fix Exchange Rate Amount" =
          CurrExchRate."Fix Exchange Rate Amount"::Both);

      IF CurrExchRate."Fix Exchange Rate Amount" <>
        CurrExchRate."Fix Exchange Rate Amount"::Both
      THEN
        EXIT(FALSE);

      CurrExchRate.SETRANGE("Currency Code",CurrExchRate."Relational Currency Code");
      IF CurrExchRate.FIND('+') THEN
        EXIT(
          CurrExchRate."Fix Exchange Rate Amount" =
          CurrExchRate."Fix Exchange Rate Amount"::Both);

      EXIT(FALSE);
    END;

    PROCEDURE CreateDim@13(Type1@1000 : Integer;No1@1001 : Code[20];Type2@1002 : Integer;No2@1003 : Code[20];Type3@1004 : Integer;No3@1005 : Code[20];Type4@1006 : Integer;No4@1007 : Code[20];Type5@1008 : Integer;No5@1009 : Code[20]);
    VAR
      TableID@1010 : ARRAY [10] OF Integer;
      No@1011 : ARRAY [10] OF Code[20];
    BEGIN
      TableID[1] := Type1;
      No[1] := No1;
      TableID[2] := Type2;
      No[2] := No2;
      TableID[3] := Type3;
      No[3] := No3;
      TableID[4] := Type4;
      No[4] := No4;
      TableID[5] := Type5;
      No[5] := No5;
      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
      DimMgt.GetDefaultDim(
        TableID,No,"Source Code","Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
      IF "Line No." <> 0 THEN
        DimMgt.UpdateJnlLineDefaultDim(
          DATABASE::"Gen. Journal Line","Journal Template Name",
          "Journal Batch Name","Line No.",0,
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    PROCEDURE ValidateShortcutDimCode@14(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      TESTFIELD("Check Printed",FALSE);
      IF "Line No." <> 0 THEN BEGIN
        DimMgt.SaveJnlLineDim(
          DATABASE::"Gen. Journal Line","Journal Template Name",
          "Journal Batch Name","Line No.",0,FieldNumber,ShortcutDimCode);
        IF MODIFY THEN;
      END ELSE
        DimMgt.SaveTempDim(FieldNumber,ShortcutDimCode);
    END;

    PROCEDURE LookupShortcutDimCode@18(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.LookupDimValueCode(FieldNumber,ShortcutDimCode);
      TESTFIELD("Check Printed",FALSE);
      IF "Line No." <> 0 THEN BEGIN
        DimMgt.SaveJnlLineDim(
          DATABASE::"Gen. Journal Line","Journal Template Name",
          "Journal Batch Name","Line No.",0,FieldNumber,ShortcutDimCode);
        MODIFY;
      END ELSE
        DimMgt.SaveTempDim(FieldNumber,ShortcutDimCode);
    END;

    PROCEDURE ShowShortcutDimCode@15(VAR ShortcutDimCode@1000 : ARRAY [8] OF Code[20]);
    BEGIN
      IF "Line No." <> 0 THEN
        DimMgt.ShowJnlLineDim(
          DATABASE::"Gen. Journal Line","Journal Template Name",
          "Journal Batch Name","Line No.",0,ShortcutDimCode)
      ELSE
        DimMgt.ShowTempDim(ShortcutDimCode);
    END;

    PROCEDURE GetFAVATSetup@17();
    VAR
      LocalGlAcc@1000 : Record 15;
      FAPostingGr@1001 : Record 5606;
      FABalAcc@1002 : Boolean;
    BEGIN
      IF CurrFieldNo = 0 THEN
        EXIT;
      IF ("Account Type" <> "Account Type"::"Fixed Asset") AND
         ("Bal. Account Type" <> "Bal. Account Type"::"Fixed Asset")
      THEN
        EXIT;
      FABalAcc := ("Bal. Account Type" = "Bal. Account Type"::"Fixed Asset");
      IF NOT FABalAcc THEN BEGIN
        "Gen. Posting Type" := "Gen. Posting Type"::" ";
        "Gen. Bus. Posting Group" := '';
        "Gen. Prod. Posting Group" := '';
        "VAT Bus. Posting Group" := '';
        "VAT Prod. Posting Group" := '';
        "Tax Group Code" := '';
        VALIDATE("VAT Prod. Posting Group");
      END;
      IF FABalAcc THEN BEGIN
        "Bal. Gen. Posting Type" := "Bal. Gen. Posting Type"::" ";
        "Bal. Gen. Bus. Posting Group" := '';
        "Bal. Gen. Prod. Posting Group" := '';
        "Bal. VAT Bus. Posting Group" := '';
        "Bal. VAT Prod. Posting Group" := '';
        "Bal. Tax Group Code" := '';
        VALIDATE("Bal. VAT Prod. Posting Group");
      END;
      IF NOT GenJnlBatch.GET("Journal Template Name","Journal Batch Name") OR
        GenJnlBatch."Copy VAT Setup to Jnl. Lines"
      THEN BEGIN
        IF (("FA Posting Type" = "FA Posting Type"::"Acquisition Cost") OR
           ("FA Posting Type" = "FA Posting Type"::Disposal) OR
           ("FA Posting Type" = "FA Posting Type"::Maintenance)) AND
           ("Posting Group" <> '')
        THEN BEGIN
          IF FAPostingGr.GET("Posting Group") THEN BEGIN
            IF "FA Posting Type" = "FA Posting Type"::"Acquisition Cost" THEN BEGIN
              FAPostingGr.TESTFIELD("Acquisition Cost Account");
              LocalGlAcc.GET(FAPostingGr."Acquisition Cost Account");
            END;
            IF "FA Posting Type" = "FA Posting Type"::Disposal THEN BEGIN
              FAPostingGr.TESTFIELD("Acq. Cost Acc. on Disposal");
              LocalGlAcc.GET(FAPostingGr."Acq. Cost Acc. on Disposal");
            END;
            IF "FA Posting Type" = "FA Posting Type"::Maintenance THEN BEGIN
              FAPostingGr.TESTFIELD("Maintenance Expense Account");
              LocalGlAcc.GET(FAPostingGr."Maintenance Expense Account");
            END;
            LocalGlAcc.CheckGLAcc;
            IF NOT FABalAcc THEN BEGIN
              "Gen. Posting Type" := LocalGlAcc."Gen. Posting Type";
              "Gen. Bus. Posting Group" := LocalGlAcc."Gen. Bus. Posting Group";
              "Gen. Prod. Posting Group" := LocalGlAcc."Gen. Prod. Posting Group";
              "VAT Bus. Posting Group" := LocalGlAcc."VAT Bus. Posting Group";
              "VAT Prod. Posting Group" := LocalGlAcc."VAT Prod. Posting Group";
              "Tax Group Code" := LocalGlAcc."Tax Group Code";
              VALIDATE("VAT Prod. Posting Group");
            END ELSE BEGIN;
              "Bal. Gen. Posting Type" := LocalGlAcc."Gen. Posting Type";
              "Bal. Gen. Bus. Posting Group" := LocalGlAcc."Gen. Bus. Posting Group";
              "Bal. Gen. Prod. Posting Group" := LocalGlAcc."Gen. Prod. Posting Group";
              "Bal. VAT Bus. Posting Group" := LocalGlAcc."VAT Bus. Posting Group";
              "Bal. VAT Prod. Posting Group" := LocalGlAcc."VAT Prod. Posting Group";
              "Bal. Tax Group Code" := LocalGlAcc."Tax Group Code";
              VALIDATE("Bal. VAT Prod. Posting Group");
            END;
          END;
        END;
      END;
    END;

    PROCEDURE GetTemplate@16();
    BEGIN
      IF NOT TemplateFound THEN
        GenJnlTemplate.GET("Journal Template Name");
      TemplateFound := TRUE;
    END;

    LOCAL PROCEDURE UpdateSalesPurchLCY@19();
    BEGIN
      "Sales/Purch. (LCY)" := 0;
      IF (NOT "System-Created Entry") AND ("Document Type" IN ["Document Type"::Invoice,"Document Type"::"Credit Memo"]) THEN BEGIN
        IF ("Account Type" IN ["Account Type"::Customer,"Account Type"::Vendor]) AND ("Bal. Account No." <> '') THEN
          "Sales/Purch. (LCY)" := "Amount (LCY)" + "Bal. VAT Amount (LCY)";
        IF ("Bal. Account Type" IN ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor]) AND ("Account No." <> '') THEN
          "Sales/Purch. (LCY)" := -("Amount (LCY)" - "VAT Amount (LCY)");
      END;
    END;

    PROCEDURE SetApplyToAmount@20();
    BEGIN
      IF "Account Type" = "Account Type"::Customer THEN BEGIN
        CustLedgEntry.SETCURRENTKEY("Document No.");
        CustLedgEntry.SETRANGE("Document No.",Rec."Applies-to Doc. No.");
        CustLedgEntry.SETRANGE("Customer No.","Account No.");
        CustLedgEntry.SETRANGE(Open,TRUE);
        IF CustLedgEntry.FIND('-') THEN
          IF CustLedgEntry."Amount to Apply" = 0 THEN  BEGIN
            CustLedgEntry.CALCFIELDS("Remaining Amount");
            CustLedgEntry."Amount to Apply" := CustLedgEntry."Remaining Amount";
            CODEUNIT.RUN(CODEUNIT::"Cust. Entry-Edit",CustLedgEntry);
          END;
      END ELSE IF "Account Type" = "Account Type"::Vendor THEN BEGIN
        VendLedgEntry.SETCURRENTKEY("Document No.");
        VendLedgEntry.SETRANGE("Document No.",Rec."Applies-to Doc. No.");
        VendLedgEntry.SETRANGE("Vendor No.","Account No.");
        VendLedgEntry.SETRANGE(Open,TRUE);
        IF VendLedgEntry.FIND('-') THEN
          IF VendLedgEntry."Amount to Apply" = 0 THEN  BEGIN
            VendLedgEntry.CALCFIELDS("Remaining Amount");
            VendLedgEntry."Amount to Apply" := VendLedgEntry."Remaining Amount";
            CODEUNIT.RUN(CODEUNIT::"Vend. Entry-Edit",VendLedgEntry);
          END;
      END;
    END;

    PROCEDURE ValidateApplyRequirements@21(TempGenJnlLine@1000 : TEMPORARY Record 81);
    VAR
      ExchAccGLJnlLine@1001 : Codeunit 366;
    BEGIN
      IF (TempGenJnlLine."Bal. Account Type" = TempGenJnlLine."Bal. Account Type"::Customer) OR
        (TempGenJnlLine."Bal. Account Type" = TempGenJnlLine."Bal. Account Type"::Vendor)
      THEN
        ExchAccGLJnlLine.RUN(TempGenJnlLine);

      IF TempGenJnlLine."Account Type" = TempGenJnlLine."Account Type"::Customer THEN BEGIN
        IF TempGenJnlLine."Applies-to ID" <> '' THEN BEGIN
          CustLedgEntry.SETCURRENTKEY("Customer No.","Applies-to ID",Open);
          CustLedgEntry.SETRANGE("Customer No.",TempGenJnlLine."Account No.");
          CustLedgEntry.SETRANGE("Applies-to ID",TempGenJnlLine."Applies-to ID");
          CustLedgEntry.SETRANGE(Open,TRUE);
          IF CustLedgEntry.FIND('-') THEN
            REPEAT
              IF (TempGenJnlLine."Posting Date" < CustLedgEntry."Posting Date") THEN
                ERROR(
                  Text015,TempGenJnlLine."Document Type",TempGenJnlLine."Document No.",
                  CustLedgEntry."Document Type",CustLedgEntry."Document No.");
            UNTIL CustLedgEntry.NEXT = 0;
        END ELSE IF TempGenJnlLine."Applies-to Doc. No." <> '' THEN BEGIN
          CustLedgEntry.SETCURRENTKEY("Document No.");
          CustLedgEntry.SETRANGE("Document No.",TempGenJnlLine."Applies-to Doc. No.");
          IF TempGenJnlLine."Applies-to Doc. Type" <> TempGenJnlLine."Applies-to Doc. Type"::" " THEN
            CustLedgEntry.SETRANGE("Document Type",TempGenJnlLine."Applies-to Doc. Type");
          CustLedgEntry.SETRANGE("Customer No.",TempGenJnlLine."Account No.");
          CustLedgEntry.SETRANGE(Open,TRUE);
          IF CustLedgEntry.FIND('-') THEN
            IF (TempGenJnlLine."Posting Date" < CustLedgEntry."Posting Date") THEN
              ERROR(
                Text015,TempGenJnlLine."Document Type",TempGenJnlLine."Document No.",
                CustLedgEntry."Document Type",CustLedgEntry."Document No.");
        END;
      END ELSE IF TempGenJnlLine."Account Type" = TempGenJnlLine."Account Type"::Vendor THEN BEGIN
        IF TempGenJnlLine."Applies-to ID" <> '' THEN BEGIN
          VendLedgEntry.SETCURRENTKEY("Vendor No.","Applies-to ID",Open);
          VendLedgEntry.SETRANGE("Vendor No.",TempGenJnlLine."Account No.");
          VendLedgEntry.SETRANGE("Applies-to ID",TempGenJnlLine."Applies-to ID");
          VendLedgEntry.SETRANGE(Open,TRUE);
            REPEAT
              IF (TempGenJnlLine."Posting Date" < VendLedgEntry."Posting Date") THEN
                ERROR(
                  Text015,TempGenJnlLine."Document Type",TempGenJnlLine."Document No.",
                  VendLedgEntry."Document Type",VendLedgEntry."Document No.");
            UNTIL VendLedgEntry.NEXT = 0;
          IF VendLedgEntry.FIND('-') THEN BEGIN
          END;
        END ELSE IF TempGenJnlLine."Applies-to Doc. No." <> '' THEN BEGIN
          VendLedgEntry.SETCURRENTKEY("Document No.");
          VendLedgEntry.SETRANGE("Document No.",TempGenJnlLine."Applies-to Doc. No.");
          IF TempGenJnlLine."Applies-to Doc. Type" <> TempGenJnlLine."Applies-to Doc. Type"::" " THEN
            VendLedgEntry.SETRANGE("Document Type",TempGenJnlLine."Applies-to Doc. Type");
          VendLedgEntry.SETRANGE("Vendor No.",TempGenJnlLine."Account No.");
          VendLedgEntry.SETRANGE(Open,TRUE);
          IF VendLedgEntry.FIND('-') THEN
            IF (TempGenJnlLine."Posting Date" < VendLedgEntry."Posting Date") THEN
              ERROR(
                Text015,TempGenJnlLine."Document Type",TempGenJnlLine."Document No.",
                VendLedgEntry."Document Type",VendLedgEntry."Document No.");
        END;
      END;
    END;

    LOCAL PROCEDURE UpdateCountryCodeAndVATRegNo@25(No@1000 : Code[20]);
    BEGIN
      IF No = '' THEN BEGIN
        "Country/Region Code" := '';
        "VAT Registration No." := '';
        EXIT;
      END;

      GLSetup.GET;
      CASE TRUE OF
        ("Account Type" = "Account Type"::Customer) OR ("Bal. Account Type" = "Bal. Account Type"::Customer):
          BEGIN
            Cust.GET(No);
            "Country/Region Code" := Cust."Country/Region Code";
            "VAT Registration No." := Cust."VAT Registration No.";
          END;
        ("Account Type" = "Account Type"::Vendor) OR ("Bal. Account Type" = "Bal. Account Type"::Vendor):
          BEGIN
            Vend.GET(No);
            "Country/Region Code" := Vend."Country/Region Code";
            "VAT Registration No." := Vend."VAT Registration No.";
          END;
      END;
    END;

    PROCEDURE JobTaskIsSet@28() : Boolean;
    BEGIN
      EXIT(("Job No." <> '') AND ("Job Task No." <> '') AND ("Account Type" = "Account Type"::"G/L Account"));
    END;

    PROCEDURE CreateTempJobJnlLine@27();
    VAR
      TmpJobJnlOverallCurrencyFactor@1001 : Decimal;
    BEGIN
      TESTFIELD("Posting Date");
      CLEAR(JobJnlLine);
      JobJnlLine.DontCheckStdCost;
      JobJnlLine.VALIDATE("Job No.","Job No.");
      JobJnlLine.VALIDATE("Job Task No.","Job Task No.");
      IF CurrFieldNo <> FIELDNO("Posting Date") THEN
        JobJnlLine.VALIDATE("Posting Date","Posting Date")
      ELSE
        JobJnlLine.VALIDATE("Posting Date",xRec."Posting Date");
      JobJnlLine.VALIDATE(Type,JobJnlLine.Type::"G/L Account");
      IF "Job Currency Code" <> '' THEN BEGIN
        IF "Posting Date" = 0D THEN
          CurrencyDate := WORKDATE
        ELSE
          CurrencyDate := "Posting Date";

        IF "Currency Code" = "Job Currency Code" THEN
          "Job Currency Factor" := "Currency Factor"
        ELSE
          "Job Currency Factor" := CurrExchRate.ExchangeRate(CurrencyDate,"Job Currency Code");
        JobJnlLine.SetCurrencyFactor("Job Currency Factor");
      END;
      JobJnlLine.VALIDATE("No.","Account No.");
      JobJnlLine.VALIDATE(Quantity,"Job Quantity");

      IF "Currency Factor" = 0 THEN BEGIN
        IF "Job Currency Factor" = 0 THEN
          TmpJobJnlOverallCurrencyFactor := 1
        ELSE
          TmpJobJnlOverallCurrencyFactor := "Job Currency Factor";
      END ELSE BEGIN
        IF "Job Currency Factor" = 0 THEN
          TmpJobJnlOverallCurrencyFactor := "Currency Factor"
        ELSE
          TmpJobJnlOverallCurrencyFactor := "Job Currency Factor" / "Currency Factor"
      END;

      IF "Job Quantity" <> 0 THEN
        JobJnlLine.VALIDATE("Unit Cost",((Amount - "VAT Amount") * TmpJobJnlOverallCurrencyFactor) / "Job Quantity");

      IF (xRec."Account No." = "Account No.") AND (xRec."Job Task No." = "Job Task No.") AND ("Job Unit Price" <> 0) THEN BEGIN
        JobJnlLine."Unit Price" := xRec."Job Unit Price";
        JobJnlLine."Line Amount" := xRec."Job Line Amount";
        JobJnlLine."Line Discount %" := xRec."Job Line Discount %";
        JobJnlLine."Line Discount Amount" := xRec."Job Line Discount Amount";
        JobJnlLine.VALIDATE("Unit Price");
      END;
    END;

    PROCEDURE UpdatePricesFromJobJnlLine@22();
    BEGIN
      "Job Unit Price" := JobJnlLine."Unit Price";
      "Job Total Price" := JobJnlLine."Total Price";
      "Job Line Amount" := JobJnlLine."Line Amount";
      "Job Line Discount Amount" := JobJnlLine."Line Discount Amount";
      "Job Unit Cost" := JobJnlLine."Unit Cost";
      "Job Total Cost" := JobJnlLine."Total Cost";
      "Job Line Discount %" := JobJnlLine."Line Discount %";

      "Job Unit Price (LCY)" := JobJnlLine."Unit Price (LCY)";
      "Job Total Price (LCY)" := JobJnlLine."Total Price (LCY)";
      "Job Line Amount (LCY)" := JobJnlLine."Line Amount (LCY)";
      "Job Line Disc. Amount (LCY)" := JobJnlLine."Line Discount Amount (LCY)";
      "Job Unit Cost (LCY)" := JobJnlLine."Unit Cost (LCY)";
      "Job Total Cost (LCY)" := JobJnlLine."Total Cost (LCY)";
    END;

    LOCAL PROCEDURE CheckNoCardTransactEntryExist@46(GenJnlLine@1002 : Record 81);
    VAR
      DOPaymentTransLogEntry@1000 : Record 829;
      DOPaymentTransLogMgt@1003 : Codeunit 829;
      DocumentType@1001 : Integer;
    BEGIN
      CASE GenJnlLine."Document Type" OF
        GenJnlLine."Document Type"::Payment:
          DocumentType := DOPaymentTransLogEntry."Document Type"::Payment;
        GenJnlLine."Document Type"::Refund:
          DocumentType := DOPaymentTransLogEntry."Document Type"::Refund;
      END;
      IF DOPaymentTransLogMgt.FindPostingNotFinishedEntry(DocumentType,GenJnlLine."Document No.",DOPaymentTransLogEntry) THEN
        ERROR(Text016,DOPaymentTransLogEntry."Transaction Type",GenJnlLine."Document Type",GenJnlLine."Document No.");
    END;

    PROCEDURE GetAppliesToDocDueDate@24() : Date;
    VAR
      CustLedgEntry@1000 : Record 21;
      VendLedgEntry@1001 : Record 25;
    BEGIN
      IF "Bal. Account Type" IN
        ["Bal. Account Type"::Customer,"Bal. Account Type"::Vendor]
      THEN BEGIN
        AccNo := "Bal. Account No.";
        AccType := "Bal. Account Type";
        CLEAR(CustLedgEntry);
        CLEAR(VendLedgEntry);
      END ELSE BEGIN
        AccNo := "Account No.";
        AccType := "Account Type";
        CLEAR(CustLedgEntry);
        CLEAR(VendLedgEntry);
      END;

      CASE AccType OF
        AccType::Customer:
          BEGIN
            CustLedgEntry.SETCURRENTKEY("Customer No.",Open,Positive,"Due Date");
            CustLedgEntry.SETRANGE("Customer No.",AccNo);
            CustLedgEntry.SETRANGE(Open,TRUE);
            IF "Applies-to Doc. No." <> '' THEN BEGIN
              CustLedgEntry.SETRANGE("Document Type","Applies-to Doc. Type");
              CustLedgEntry.SETRANGE("Document No.","Applies-to Doc. No.");
              IF NOT CustLedgEntry.FINDFIRST THEN BEGIN
                CustLedgEntry.SETRANGE("Document Type");
                CustLedgEntry.SETRANGE("Document No.");
              END;
            END ELSE
              IF "Applies-to ID" <> '' THEN BEGIN
                CustLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
                IF NOT CustLedgEntry.FINDFIRST THEN
                  CustLedgEntry.SETRANGE("Applies-to ID");
              END;
            EXIT(CustLedgEntry."Due Date");
          END;
        AccType::Vendor:
          BEGIN
            VendLedgEntry.SETCURRENTKEY("Vendor No.",Open,Positive,"Due Date");
            VendLedgEntry.SETRANGE("Vendor No.",AccNo);
            VendLedgEntry.SETRANGE(Open,TRUE);
            IF "Applies-to Doc. No." <> '' THEN BEGIN
              VendLedgEntry.SETRANGE("Document Type","Applies-to Doc. Type");
              VendLedgEntry.SETRANGE("Document No.","Applies-to Doc. No.");
              IF NOT VendLedgEntry.FINDFIRST THEN BEGIN
                VendLedgEntry.SETRANGE("Document Type");
                VendLedgEntry.SETRANGE("Document No.");
              END;
            END ELSE
              IF "Applies-to ID" <> '' THEN BEGIN
                VendLedgEntry.SETRANGE("Applies-to ID","Applies-to ID");
                IF NOT VendLedgEntry.FINDFIRST THEN
                  VendLedgEntry.SETRANGE("Applies-to ID");
              END;
            EXIT(VendLedgEntry."Due Date");
          END;
      END;
    END;

    LOCAL PROCEDURE GetCreditCard@23();
    VAR
      DOPaymentTransLogEntry@1000 : Record 829;
    BEGIN
      IF "Applies-to Doc. No." = xRec."Applies-to Doc. No." THEN
        EXIT;
      IF NOT (("Account Type" = "Account Type"::Customer) AND
              ("Bal. Account Type" = "Bal. Account Type"::"Bank Account")) THEN
        EXIT;

      "Credit Card No." := '';
      IF "Applies-to Doc. No." = '' THEN
        EXIT;

      IF "Document Type" <> "Document Type"::Refund THEN
        EXIT;

      DOPaymentTransLogEntry.SETRANGE("Customer No.","Account No.");
      DOPaymentTransLogEntry.SETRANGE("Transaction Type",DOPaymentTransLogEntry."Transaction Type"::Capture);
      DOPaymentTransLogEntry.SETRANGE("Document Type",DOPaymentTransLogEntry."Document Type"::Payment);
      DOPaymentTransLogEntry.SETRANGE("Document No.","Applies-to Doc. No.");

      IF DOPaymentTransLogEntry.FINDFIRST THEN
        "Credit Card No." := DOPaymentTransLogEntry."Credit Card No.";
    END;

    PROCEDURE GetCustLedgerEntry@1150000();
    BEGIN
      IF ("Account Type" = "Account Type"::Customer) AND ("Account No." = '') AND
        ("Applies-to Doc. No." <> '') AND (Amount = 0)
      THEN BEGIN
        CustLedgEntry.RESET;
        CustLedgEntry.SETCURRENTKEY("Document No.");
        CustLedgEntry.SETRANGE("Document Type","Document Type"::Invoice);
        CustLedgEntry.SETRANGE("Document No.","Applies-to Doc. No.");
        CustLedgEntry.SETRANGE(Open,TRUE);
        IF NOT CustLedgEntry.FIND('-') THEN
          ERROR(Text11501,"Applies-to Doc. No.");

        VALIDATE("Account No.",CustLedgEntry."Customer No.");
        CustLedgEntry.CALCFIELDS("Remaining Amount");

        IF CustLedgEntry."Pmt. Disc. Tolerance Date" > CustLedgEntry."Pmt. Discount Date" THEN
          CustLedgEntry."Pmt. Discount Date" := CustLedgEntry."Pmt. Disc. Tolerance Date";

        IF "Posting Date" <= CustLedgEntry."Pmt. Discount Date" THEN
          Amount := -(CustLedgEntry."Remaining Amount" - CustLedgEntry."Remaining Pmt. Disc. Possible")
        ELSE
          Amount := -CustLedgEntry."Remaining Amount";

        VALIDATE(Amount);

        "Document Type" := "Document Type"::Payment;
        "Applies-to Doc. Type" := CustLedgEntry."Document Type";
        "Applies-to Doc. No." := CustLedgEntry."Document No.";
        "Applies-to ID" := '';
        IF ("Applies-to Doc. Type" = "Applies-to Doc. Type"::Invoice) AND
           ("Document Type" = "Document Type"::Payment)
        THEN
          "External Document No." := CustLedgEntry."External Document No.";
        "Bal. Account Type" := "Bal. Account Type"::"G/L Account";

        GenJnlBatch.GET("Journal Template Name","Journal Batch Name");
        IF GenJnlBatch."Bal. Account No." <> '' THEN BEGIN
          "Bal. Account Type" := GenJnlBatch."Bal. Account Type";
          VALIDATE("Bal. Account No.",GenJnlBatch."Bal. Account No.");
        END;
      END;
    END;

    PROCEDURE ChangePostingDateWarning@1150001();
    VAR
      AppVendorEntry@1150000 : Record 25;
      OldPmtDiscPossible@1150001 : Boolean;
      NewPmtDiscPossible@1150002 : Boolean;
    BEGIN
      IF ("Applies-to Doc. No." = '') OR
         ("Document Type" <> "Document Type"::Payment) OR
         (CurrFieldNo <> FIELDNO("Posting Date")) OR
         ("Account Type" <> "Account Type"::Vendor) OR
         (Amount = 0)
      THEN
        EXIT;

      AppVendorEntry.SETCURRENTKEY("Document No.");
      AppVendorEntry.SETRANGE("Document Type","Applies-to Doc. Type");
      AppVendorEntry.SETRANGE("Document No.","Applies-to Doc. No.");
      IF AppVendorEntry.FIND('-') THEN
        IF AppVendorEntry."Remaining Pmt. Disc. Possible" <> 0 THEN BEGIN
          OldPmtDiscPossible := (xRec."Posting Date" <= AppVendorEntry."Pmt. Discount Date") OR
                                (xRec."Posting Date" <= AppVendorEntry."Pmt. Disc. Tolerance Date");
          NewPmtDiscPossible := ("Posting Date" <= AppVendorEntry."Pmt. Discount Date") OR
                                ("Posting Date" <= AppVendorEntry."Pmt. Disc. Tolerance Date");
          IF OldPmtDiscPossible <> NewPmtDiscPossible THEN
            MESSAGE(Text11500);
        END;
    END;

    BEGIN
    END.
  }
}

