OBJECT Table 90 BOM Component
{
  OBJECT-PROPERTIES
  {
    Date=14.08.09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               Item.GET("Parent Item No.");
             END;

    CaptionML=[DEU=St…klistenkomponente;
               ENU=BOM Component];
    LookupFormID=Form36;
    DrillDownFormID=Form36;
  }
  FIELDS
  {
    { 1   ;   ;Parent Item No.     ;Code20        ;TableRelation=Item;
                                                   CaptionML=[DEU=St…klistennr.;
                                                              ENU=Parent Item No.];
                                                   NotBlank=Yes }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[DEU=Zeilennr.;
                                                              ENU=Line No.] }
    { 3   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                "No." := '';
                                                                "Variant Code" := '';
                                                              END;

                                                   CaptionML=[DEU=Art;
                                                              ENU=Type];
                                                   OptionCaptionML=[DEU=" ,Artikel,Ressource";
                                                                    ENU=" ,Item,Resource"];
                                                   OptionString=[ ,Item,Resource] }
    { 4   ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Item)) Item
                                                                 ELSE IF (Type=CONST(Resource)) Resource;
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type);
                                                                "Variant Code" := '';
                                                                IF "No." = '' THEN
                                                                  EXIT;

                                                                CASE Type OF
                                                                  Type::Item:
                                                                    BEGIN
                                                                      Item.GET("No.");
                                                                      Item.CALCFIELDS("Bill of Materials");
                                                                      "Bill of Materials" := Item."Bill of Materials";
                                                                      Description := Item.Description;
                                                                      "Unit of Measure Code" := Item."Base Unit of Measure";
                                                                    END;
                                                                  Type::Resource:
                                                                    BEGIN
                                                                      Res.GET("No.");
                                                                      "Bill of Materials" := FALSE;
                                                                      Description := Res.Name;
                                                                      "Unit of Measure Code" := Res."Base Unit of Measure";
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Nr.;
                                                              ENU=No.] }
    { 5   ;   ;Bill of Materials   ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("BOM Component" WHERE (Type=CONST(Item),
                                                                                            Parent Item No.=FIELD(No.)));
                                                   CaptionML=[DEU=St…kliste;
                                                              ENU=Bill of Materials];
                                                   Editable=No }
    { 6   ;   ;Description         ;Text50        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description] }
    { 7   ;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Unit of Measure".Code WHERE (Item No.=FIELD(No.))
                                                                 ELSE IF (Type=CONST(Resource)) "Resource Unit of Measure".Code WHERE (Resource No.=FIELD(No.));
                                                   CaptionML=[DEU=Einheitencode;
                                                              ENU=Unit of Measure Code] }
    { 8   ;   ;Quantity per        ;Decimal       ;CaptionML=[DEU=Komponentenmenge;
                                                              ENU=Quantity per];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 9   ;   ;Position            ;Code10        ;CaptionML=[DEU=Position;
                                                              ENU=Position] }
    { 10  ;   ;Position 2          ;Code10        ;CaptionML=[DEU=Position 2;
                                                              ENU=Position 2] }
    { 11  ;   ;Position 3          ;Code10        ;CaptionML=[DEU=Position 3;
                                                              ENU=Position 3] }
    { 12  ;   ;Machine No.         ;Code10        ;CaptionML=[DEU=Maschinennr.;
                                                              ENU=Machine No.] }
    { 13  ;   ;Production Lead Time;Integer       ;CaptionML=[DEU=Vorlaufzeit;
                                                              ENU=Production Lead Time] }
    { 14  ;   ;BOM Description     ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Item.Description WHERE (No.=FIELD(Parent Item No.)));
                                                   CaptionML=[DEU=St…klistenbeschreibung;
                                                              ENU=BOM Description];
                                                   Editable=No }
    { 5402;   ;Variant Code        ;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Variant".Code WHERE (Item No.=FIELD(No.));
                                                   OnValidate=BEGIN
                                                                IF "Variant Code" = '' THEN
                                                                  EXIT;
                                                                TESTFIELD(Type,Type::Item);
                                                                TESTFIELD("No.");
                                                                ItemVariant.GET("No.","Variant Code");
                                                                Description := ItemVariant.Description;
                                                              END;

                                                   CaptionML=[DEU=Variantencode;
                                                              ENU=Variant Code] }
    { 5900;   ;Installed in Line No.;Integer      ;OnValidate=BEGIN
                                                                IF "Installed in Line No." <> 0 THEN BEGIN
                                                                  IF "Installed in Line No." = "Line No." THEN
                                                                    ERROR(Text000,FIELDCAPTION("Installed in Line No."));
                                                                  BOMComp.RESET;
                                                                  BOMComp.SETRANGE("Parent Item No.","Parent Item No.");
                                                                  BOMComp.SETRANGE(Type,BOMComp.Type::Item);
                                                                  BOMComp.SETRANGE("Line No.","Installed in Line No.");
                                                                  BOMComp.FIND('-');
                                                                  BOMComp.TESTFIELD("Quantity per",1);
                                                                  "Installed in Item No." := BOMComp."No.";
                                                                END ELSE
                                                                  "Installed in Item No." := '';
                                                              END;

                                                   OnLookup=BEGIN
                                                              BOMComp.RESET;
                                                              BOMComp.SETRANGE("Parent Item No.","Parent Item No.");
                                                              BOMComp.SETRANGE(Type,BOMComp.Type::Item);
                                                              BOMComp.SETFILTER("Line No.",'<>%1',"Line No.");
                                                              CLEAR(BillOfMaterials);
                                                              BillOfMaterials.SETTABLEVIEW(BOMComp);
                                                              BillOfMaterials.EDITABLE(FALSE);
                                                              BillOfMaterials.LOOKUPMODE(TRUE);
                                                              IF BillOfMaterials.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                BillOfMaterials.GETRECORD(BOMComp);
                                                                VALIDATE("Installed in Line No.",BOMComp."Line No.");
                                                              END;
                                                            END;

                                                   CaptionML=[DEU=Installiert in Zeilennr.;
                                                              ENU=Installed in Line No.] }
    { 5901;   ;Installed in Item No.;Code20       ;TableRelation=IF (Type=CONST(Item)) Item;
                                                   OnValidate=BEGIN
                                                                IF "Installed in Item No." <> '' THEN BEGIN
                                                                  BOMComp.RESET;
                                                                  BOMComp.SETRANGE("Parent Item No.","Parent Item No.");
                                                                  BOMComp.SETRANGE(Type,BOMComp.Type::Item);
                                                                  BOMComp.SETRANGE("No.","Installed in Item No.");
                                                                  BOMComp.FIND('-');
                                                                END;

                                                                VALIDATE("Installed in Line No.",BOMComp."Line No.");
                                                              END;

                                                   OnLookup=BEGIN
                                                              BOMComp.RESET;
                                                              BOMComp.SETRANGE("Parent Item No.","Parent Item No.");
                                                              BOMComp.SETRANGE(Type,BOMComp.Type::Item);
                                                              BOMComp."No." := "Installed in Item No.";
                                                              BOMComp.SETFILTER("Line No.",'<>%1',"Line No.");
                                                              CLEAR(BillOfMaterials);
                                                              BillOfMaterials.SETTABLEVIEW(BOMComp);
                                                              BillOfMaterials.EDITABLE(FALSE);
                                                              BillOfMaterials.LOOKUPMODE(TRUE);
                                                              IF BillOfMaterials.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                BillOfMaterials.GETRECORD(BOMComp);
                                                                VALIDATE("Installed in Line No.",BOMComp."Line No.");
                                                              END;
                                                            END;

                                                   CaptionML=[DEU=Installiert in Artikelnr.;
                                                              ENU=Installed in Item No.] }
  }
  KEYS
  {
    {    ;Parent Item No.,Line No.                ;Clustered=Yes }
    {    ;Type,No.                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=%1 kann keine Komponente von sich selbst sein.;ENU=%1 cannot be component of itself.';
      Item@1001 : Record 27;
      Res@1002 : Record 156;
      ItemVariant@1003 : Record 5401;
      BOMComp@1004 : Record 90;
      BillOfMaterials@1005 : Form 36;

    BEGIN
    END.
  }
}

