OBJECT Table 99000772 Production BOM Line
{
  OBJECT-PROPERTIES
  {
    Date=14.08.09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TestStatus;
             END;

    OnModify=BEGIN
               IF Type <> Type::" " THEN
                 TestStatus;
             END;

    OnDelete=VAR
               ProdBOMComment@1000 : Record 99000776;
               PlanningAssignment@1001 : Record 99000850;
             BEGIN
               IF Type <> Type::" " THEN BEGIN
                 TestStatus;
                 IF Type = Type::Item THEN
                   PlanningAssignment.AssignPlannedOrders("No.",FALSE)
                 ELSE
                   IF Type = Type::"Production BOM" THEN
                     PlanningAssignment.OldBom("No.");
               END;

               ProdBOMComment.SETRANGE("Production BOM No.","Production BOM No.");
               ProdBOMComment.SETRANGE("BOM Line No.","Line No.");
               ProdBOMComment.SETRANGE("Version Code","Version Code");
               ProdBOMComment.DELETEALL;
             END;

    CaptionML=[DEU=Fert.-StÅcklistenzeile;
               ENU=Production BOM Line];
  }
  FIELDS
  {
    { 1   ;   ;Production BOM No.  ;Code20        ;TableRelation="Production BOM Header";
                                                   CaptionML=[DEU=Fert.-StÅcklistennr.;
                                                              ENU=Production BOM No.];
                                                   NotBlank=Yes }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[DEU=Zeilennr.;
                                                              ENU=Line No.] }
    { 3   ;   ;Version Code        ;Code10        ;TableRelation="Production BOM Version"."Version Code" WHERE (Production BOM No.=FIELD(Production BOM No.));
                                                   CaptionML=[DEU=Versionscode;
                                                              ENU=Version Code] }
    { 10  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                TestStatus;

                                                                xRec.Type := Type;

                                                                INIT;
                                                                Type := xRec.Type;
                                                              END;

                                                   CaptionML=[DEU=Art;
                                                              ENU=Type];
                                                   OptionCaptionML=[DEU=" ,Artikel,PhantomstÅckliste";
                                                                    ENU=" ,Item,Production BOM"];
                                                   OptionString=[ ,Item,Production BOM] }
    { 11  ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Item)) Item
                                                                 ELSE IF (Type=CONST(Production BOM)) "Production BOM Header";
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type);

                                                                TestStatus;

                                                                CASE Type OF
                                                                  Type::Item:
                                                                    BEGIN
                                                                      Item.GET("No.");
                                                                      Description := Item.Description;
                                                                      Item.TESTFIELD("Base Unit of Measure");
                                                                      "Unit of Measure Code" := Item."Base Unit of Measure";
                                                                      IF "No." <> xRec."No." THEN
                                                                        "Variant Code" := '';
                                                                    END;
                                                                  Type::"Production BOM":
                                                                    BEGIN
                                                                      ProdBOMHeader.GET("No.");
                                                                      ProdBOMHeader.TESTFIELD("Unit of Measure Code");
                                                                      Description := ProdBOMHeader.Description;
                                                                      "Unit of Measure Code" := ProdBOMHeader."Unit of Measure Code";
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Nr.;
                                                              ENU=No.] }
    { 12  ;   ;Description         ;Text30        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description] }
    { 13  ;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Unit of Measure".Code WHERE (Item No.=FIELD(No.))
                                                                 ELSE IF (Type=CONST(Production BOM)) "Unit of Measure";
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type,Type::Item);
                                                                TESTFIELD("No.");
                                                              END;

                                                   CaptionML=[DEU=Einheitencode;
                                                              ENU=Unit of Measure Code] }
    { 14  ;   ;Quantity            ;Decimal       ;CaptionML=[DEU=Menge;
                                                              ENU=Quantity];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 15  ;   ;Position            ;Code10        ;CaptionML=[DEU=Position;
                                                              ENU=Position] }
    { 16  ;   ;Position 2          ;Code10        ;CaptionML=[DEU=Position 2;
                                                              ENU=Position 2] }
    { 17  ;   ;Position 3          ;Code10        ;CaptionML=[DEU=Position 3;
                                                              ENU=Position 3] }
    { 18  ;   ;Production Lead Time;DateFormula   ;OnValidate=BEGIN
                                                                TESTFIELD("No.");
                                                              END;

                                                   CaptionML=[DEU=Vorlaufzeit;
                                                              ENU=Production Lead Time] }
    { 19  ;   ;Routing Link Code   ;Code10        ;TableRelation="Routing Link";
                                                   OnValidate=BEGIN
                                                                IF "Routing Link Code" <> '' THEN BEGIN
                                                                  TESTFIELD(Type,Type::Item);
                                                                  TESTFIELD("No.");
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Verbindungscode;
                                                              ENU=Routing Link Code] }
    { 20  ;   ;Scrap %             ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("No.");
                                                              END;

                                                   CaptionML=[DEU=Ausschuss %;
                                                              ENU=Scrap %];
                                                   DecimalPlaces=0:5;
                                                   MaxValue=100;
                                                   BlankNumbers=BlankNeg }
    { 21  ;   ;Variant Code        ;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Variant".Code WHERE (Item No.=FIELD(No.));
                                                   OnValidate=BEGIN
                                                                IF "Variant Code" = '' THEN
                                                                  EXIT;
                                                                TESTFIELD(Type,Type::Item);
                                                                TESTFIELD("No.");
                                                                ItemVariant.GET("No.","Variant Code");
                                                                Description := ItemVariant.Description;
                                                              END;

                                                   CaptionML=[DEU=Variantencode;
                                                              ENU=Variant Code] }
    { 22  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Production BOM Comment Line" WHERE (Production BOM No.=FIELD(Production BOM No.),
                                                                                                          Version Code=FIELD(Version Code),
                                                                                                          BOM Line No.=FIELD(Line No.)));
                                                   CaptionML=[DEU=Bemerkung;
                                                              ENU=Comment];
                                                   Editable=No }
    { 28  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                TESTFIELD("No.");

                                                                IF "Starting Date" > 0D THEN
                                                                  VALIDATE("Ending Date");
                                                              END;

                                                   CaptionML=[DEU=Startdatum;
                                                              ENU=Starting Date] }
    { 29  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                TESTFIELD("No.");

                                                                IF ("Ending Date" > 0D) AND
                                                                   ("Starting Date" > 0D) AND
                                                                   ("Starting Date" > "Ending Date")
                                                                THEN
                                                                  ERROR(
                                                                    Text000,
                                                                    FIELDCAPTION("Ending Date"),
                                                                    FIELDCAPTION("Starting Date"));
                                                              END;

                                                   CaptionML=[DEU=Enddatum;
                                                              ENU=Ending Date] }
    { 40  ;   ;Length              ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[DEU=LÑnge;
                                                              ENU=Length];
                                                   DecimalPlaces=0:5 }
    { 41  ;   ;Width               ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[DEU=Breite;
                                                              ENU=Width];
                                                   DecimalPlaces=0:5 }
    { 42  ;   ;Weight              ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[DEU=Gewicht;
                                                              ENU=Weight];
                                                   DecimalPlaces=0:5 }
    { 43  ;   ;Depth               ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[DEU=Tiefe;
                                                              ENU=Depth];
                                                   DecimalPlaces=0:5 }
    { 44  ;   ;Calculation Formula ;Option        ;OnValidate=BEGIN
                                                                TESTFIELD("No.");

                                                                CASE "Calculation Formula" OF
                                                                  "Calculation Formula"::" ":
                                                                    Quantity := "Quantity per";
                                                                  "Calculation Formula"::Length:
                                                                    Quantity := ROUND(Length * "Quantity per",0.00001);
                                                                  "Calculation Formula"::"Length * Width":
                                                                    Quantity := ROUND(Length * Width * "Quantity per",0.00001);
                                                                  "Calculation Formula"::"Length * Width * Depth":
                                                                    Quantity := ROUND(Length * Width * Depth * "Quantity per",0.00001);
                                                                  "Calculation Formula"::Weight:
                                                                    Quantity := ROUND(Weight * "Quantity per",0.00001);
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Formel;
                                                              ENU=Calculation Formula];
                                                   OptionCaptionML=[DEU=" ,LÑnge,LÑnge * Breite,LÑnge * Breite * Tiefe,Gewicht";
                                                                    ENU=" ,Length,Length * Width,Length * Width * Depth,Weight"];
                                                   OptionString=[ ,Length,Length * Width,Length * Width * Depth,Weight] }
    { 45  ;   ;Quantity per        ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[DEU=Komponentenmenge;
                                                              ENU=Quantity per];
                                                   DecimalPlaces=0:5 }
  }
  KEYS
  {
    {    ;Production BOM No.,Version Code,Line No.;Clustered=Yes }
    {    ;Type,No.                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=%1 muss spÑter als %2 sein.;ENU=%1 must be later than %2.';
      Item@1001 : Record 27;
      ProdBOMHeader@1002 : Record 99000771;
      ItemVariant@1003 : Record 5401;

    PROCEDURE TestStatus@1();
    VAR
      ProdBOMVersion@1000 : Record 99000779;
    BEGIN
      IF "Version Code" = '' THEN BEGIN
        ProdBOMHeader.GET("Production BOM No.");
        IF ProdBOMHeader.Status = ProdBOMHeader.Status::Certified THEN
          ProdBOMHeader.FIELDERROR(Status);
      END ELSE BEGIN
        ProdBOMVersion.GET("Production BOM No.","Version Code");
        IF ProdBOMVersion.Status = ProdBOMVersion.Status::Certified THEN
          ProdBOMVersion.FIELDERROR(Status);
      END;
    END;

    PROCEDURE GetQtyPerUnitOfMeasure@2() : Decimal;
    VAR
      Item@1000 : Record 27;
      UOMMgt@1001 : Codeunit 5402;
    BEGIN
      IF Type = Type::Item THEN BEGIN
        Item.GET("No.");
        EXIT(
          UOMMgt.GetQtyPerUnitOfMeasure(Item,"Unit of Measure Code"));
      END;
      EXIT(1);
    END;

    BEGIN
    END.
  }
}

