OBJECT Table 5648 FA Allocation Dimension
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Dimension Value Code");
               LOCKTABLE;
               FAAlloc.GET(Code,"Allocation Type","Line No.");
               GLSetup.GET;
               IF "Dimension Code" = GLSetup."Global Dimension 1 Code" THEN
                 UpdateGlobalDimCode(1,"Dimension Value Code");
               IF "Dimension Code" = GLSetup."Global Dimension 2 Code" THEN
                 UpdateGlobalDimCode(2,"Dimension Value Code");
             END;

    OnModify=BEGIN
               GLSetup.GET;
               IF "Dimension Code" = GLSetup."Global Dimension 1 Code" THEN
                 UpdateGlobalDimCode(1,"Dimension Value Code");
               IF "Dimension Code" = GLSetup."Global Dimension 2 Code" THEN
                 UpdateGlobalDimCode(2,"Dimension Value Code");
             END;

    OnDelete=BEGIN
               GLSetup.GET;
               IF "Dimension Code" = GLSetup."Global Dimension 1 Code" THEN
                 UpdateGlobalDimCode(1,'');
               IF "Dimension Code" = GLSetup."Global Dimension 2 Code" THEN
                 UpdateGlobalDimCode(2,'');
             END;

    OnRename=BEGIN
               ERROR(Text000,TABLECAPTION);
             END;

    CaptionML=[DEU=Anlagenverteilungsdimension;
               ENU=FA Allocation Dimension];
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;TableRelation="FA Posting Group";
                                                   CaptionML=[DEU=Code;
                                                              ENU=Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Allocation Type     ;Option        ;CaptionML=[DEU=Verteilungsart;
                                                              ENU=Allocation Type];
                                                   OptionCaptionML=[DEU=Anschaffungskosten,Normal-AfA,Erh”hte AfA,Zuschreibung,Sonder-AfA,Benutzerdef. AfA,Verkauf,Wartung,Gewinn,Verlust,Buchwert (Gewinn);
                                                                    ENU=Acquisition,Depreciation,Write-Down,Appreciation,Custom 1,Custom 2,Disposal,Maintenance,Gain,Loss,Book Value];
                                                   OptionString=Acquisition,Depreciation,Write-Down,Appreciation,Custom 1,Custom 2,Disposal,Maintenance,Gain,Loss,Book Value }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[DEU=Zeilennr.;
                                                              ENU=Line No.] }
    { 4   ;   ;Dimension Code      ;Code20        ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                IF NOT DimMgt.CheckDim("Dimension Code") THEN
                                                                  ERROR(DimMgt.GetDimErr);
                                                              END;

                                                   CaptionML=[DEU=Dimensionscode;
                                                              ENU=Dimension Code];
                                                   NotBlank=Yes }
    { 5   ;   ;Dimension Value Code;Code20        ;TableRelation="Dimension Value".Code WHERE (Dimension Code=FIELD(Dimension Code));
                                                   OnValidate=BEGIN
                                                                IF NOT DimMgt.CheckDimValue("Dimension Code","Dimension Value Code") THEN
                                                                  ERROR(DimMgt.GetDimErr);
                                                              END;

                                                   CaptionML=[DEU=Dimensionswertcode;
                                                              ENU=Dimension Value Code];
                                                   NotBlank=Yes }
  }
  KEYS
  {
    {    ;Code,Allocation Type,Line No.,Dimension Code;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Sie k”nnen eine %1 nicht umbenennen.;ENU=You cannot rename a %1.';
      GLSetup@1001 : Record 98;
      FAAlloc@1002 : Record 5615;
      DimMgt@1003 : Codeunit 408;

    PROCEDURE UpdateGlobalDimCode@25(GlobalDimCodeNo@1000 : Integer;NewDimValue@1001 : Code[20]);
    VAR
      FAAlloc@1002 : Record 5615;
    BEGIN
      IF FAAlloc.GET(Code,"Allocation Type","Line No.") THEN BEGIN
        CASE GlobalDimCodeNo OF
          1: FAAlloc."Global Dimension 1 Code" := NewDimValue;
          2: FAAlloc."Global Dimension 2 Code" := NewDimValue;
        END;
          FAAlloc.MODIFY(TRUE);
      END;
    END;

    BEGIN
    END.
  }
}

