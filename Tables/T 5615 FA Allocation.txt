OBJECT Table 5615 FA Allocation
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "Global Dimension 1 Code" := '';
               "Global Dimension 2 Code" := '';
             END;

    OnDelete=BEGIN
               FAAllocDim.SETRANGE(Code,Code);
               FAAllocDim.SETRANGE("Allocation Type","Allocation Type");
               FAAllocDim.SETRANGE("Line No.","Line No.");
               FAAllocDim.DELETEALL;
             END;

    OnRename=BEGIN
               ERROR(Text000,TABLECAPTION);
             END;

    CaptionML=[DEU=Anlagenverteilung;
               ENU=FA Allocation];
    LookupFormID=Form5623;
    DrillDownFormID=Form5623;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;TableRelation="FA Posting Group";
                                                   CaptionML=[DEU=Code;
                                                              ENU=Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[DEU=Zeilennr.;
                                                              ENU=Line No.] }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description] }
    { 4   ;   ;Account No.         ;Code20        ;TableRelation="G/L Account";
                                                   OnValidate=BEGIN
                                                                IF "Account No." = '' THEN
                                                                  EXIT;
                                                                GLAcc.GET("Account No.");
                                                                GLAcc.CheckGLAcc;
                                                                IF "Allocation Type" < "Allocation Type"::Gain THEN
                                                                  GLAcc.TESTFIELD("Direct Posting");
                                                                Description := GLAcc.Name;
                                                              END;

                                                   CaptionML=[DEU=Kontonr.;
                                                              ENU=Account No.] }
    { 5   ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Global Dimension 1 Code");
                                                              END;

                                                   CaptionML=[DEU=Globaler Dimensionscode 1;
                                                              ENU=Global Dimension 1 Code];
                                                   CaptionClass='1,1,1' }
    { 6   ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Global Dimension 2 Code");
                                                              END;

                                                   CaptionML=[DEU=Globaler Dimensionscode 2;
                                                              ENU=Global Dimension 2 Code];
                                                   CaptionClass='1,1,2' }
    { 7   ;   ;Allocation %        ;Decimal       ;CaptionML=[DEU=Verteilung %;
                                                              ENU=Allocation %];
                                                   DecimalPlaces=1:1;
                                                   MinValue=0;
                                                   MaxValue=100 }
    { 8   ;   ;Allocation Type     ;Option        ;CaptionML=[DEU=Verteilungsart;
                                                              ENU=Allocation Type];
                                                   OptionCaptionML=[DEU=Anschaffungskosten,Normal-AfA,Erh”hte AfA,Zuschreibung,Sonder-AfA,Benutzerdef. AfA,Verkauf,Wartung,Gewinn,Verlust,Buchwert (Gewinn),Buchwert (Verlust);
                                                                    ENU=Acquisition,Depreciation,Write-Down,Appreciation,Custom 1,Custom 2,Disposal,Maintenance,Gain,Loss,Book Value (Gain),Book Value (Loss)];
                                                   OptionString=Acquisition,Depreciation,Write-Down,Appreciation,Custom 1,Custom 2,Disposal,Maintenance,Gain,Loss,Book Value (Gain),Book Value (Loss) }
    { 9   ;   ;Account Name        ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("G/L Account".Name WHERE (No.=FIELD(Account No.)));
                                                   CaptionML=[DEU=Kontoname;
                                                              ENU=Account Name];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code,Allocation Type,Line No.           ;SumIndexFields=Allocation %;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Sie k”nnen eine %1 nicht umbenennen.;ENU=You cannot rename a %1.';
      GLAcc@1001 : Record 15;
      FAAllocDim@1002 : Record 5648;
      DimMgt@1003 : Codeunit 408;

    LOCAL PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      SaveDefaultDim(FieldNumber,ShortcutDimCode);
    END;

    LOCAL PROCEDURE SaveDefaultDim@53(FieldNumber@1000 : Integer;ShortcutDimCode@1001 : Code[20]);
    VAR
      GLSetup@1002 : Record 98;
      FAAllocDim@1003 : Record 5648;
      GLSetupShortcutDimCode@1004 : ARRAY [8] OF Code[20];
    BEGIN
      GLSetup.GET;
      CASE FieldNumber OF
        1: GLSetupShortcutDimCode[FieldNumber] := GLSetup."Global Dimension 1 Code";
        2: GLSetupShortcutDimCode[FieldNumber] := GLSetup."Global Dimension 2 Code";
      END;
      IF ShortcutDimCode <> '' THEN BEGIN
        IF FAAllocDim.GET(
          Code,"Allocation Type","Line No.",GLSetupShortcutDimCode[FieldNumber])
        THEN BEGIN
          FAAllocDim."Dimension Value Code" := ShortcutDimCode;
          FAAllocDim.MODIFY;
        END ELSE BEGIN
          FAAllocDim.INIT;
          FAAllocDim.VALIDATE(Code,Code);
          FAAllocDim.VALIDATE("Allocation Type","Allocation Type");
          FAAllocDim.VALIDATE("Line No.","Line No.");
          FAAllocDim.VALIDATE("Dimension Code",GLSetupShortcutDimCode[FieldNumber]);
          FAAllocDim.VALIDATE("Dimension Value Code",ShortcutDimCode);
          FAAllocDim.INSERT;
        END;
      END ELSE
        IF FAAllocDim.GET(
          Code,"Allocation Type","Line No.",GLSetupShortcutDimCode[FieldNumber])
        THEN
          FAAllocDim.DELETE;
    END;

    BEGIN
    END.
  }
}

