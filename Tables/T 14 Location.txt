OBJECT Table 14 Location
{
  OBJECT-PROPERTIES
  {
    Date=14.08.09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Name;
    OnDelete=VAR
               TransferRoute@1000 : Record 5742;
             BEGIN
               WMSCheckWarehouse;

               TransferRoute.SETRANGE("Transfer-from Code",Code);
               TransferRoute.DELETEALL;
               TransferRoute.RESET;
               TransferRoute.SETRANGE("Transfer-to Code",Code);
               TransferRoute.DELETEALL;
             END;

    CaptionML=[DEU=Lagerort;
               ENU=Location];
    LookupFormID=Form15;
    DrillDownFormID=Form15;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[DEU=Code;
                                                              ENU=Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Text30        ;CaptionML=[DEU=Name;
                                                              ENU=Name] }
    { 5700;   ;Name 2              ;Text30        ;CaptionML=[DEU=Name 2;
                                                              ENU=Name 2] }
    { 5701;   ;Address             ;Text50        ;CaptionML=[DEU=Adresse;
                                                              ENU=Address] }
    { 5702;   ;Address 2           ;Text50        ;CaptionML=[DEU=Adresse 2;
                                                              ENU=Address 2] }
    { 5703;   ;City                ;Text30        ;OnValidate=BEGIN
                                                                Postcode.ValidateCity(City,"Post Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              Postcode.LookUpCity(City,"Post Code",TRUE);
                                                            END;

                                                   CaptionML=[DEU=Ort;
                                                              ENU=City] }
    { 5704;   ;Phone No.           ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=[DEU=Telefonnr.;
                                                              ENU=Phone No.] }
    { 5705;   ;Phone No. 2         ;Text30        ;ExtendedDatatype=Phone No.;
                                                   CaptionML=[DEU=Telefonnr. 2;
                                                              ENU=Phone No. 2] }
    { 5706;   ;Telex No.           ;Text30        ;CaptionML=[DEU=Telexnr.;
                                                              ENU=Telex No.] }
    { 5707;   ;Fax No.             ;Text30        ;CaptionML=[DEU=Faxnr.;
                                                              ENU=Fax No.] }
    { 5713;   ;Contact             ;Text50        ;CaptionML=[DEU=Kontakt;
                                                              ENU=Contact] }
    { 5714;   ;Post Code           ;Code20        ;TableRelation="Post Code".Code;
                                                   OnValidate=BEGIN
                                                                Postcode.ValidatePostCode(City,"Post Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              Postcode.LookUpPostCode(City,"Post Code",TRUE);
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[DEU=PLZ-Code;
                                                              ENU=Post Code] }
    { 5715;   ;County              ;Text30        ;CaptionML=[DEU=Bundesregion;
                                                              ENU=County] }
    { 5718;   ;E-Mail              ;Text80        ;ExtendedDatatype=E-Mail;
                                                   CaptionML=[DEU=E-Mail;
                                                              ENU=E-Mail] }
    { 5719;   ;Home Page           ;Text90        ;ExtendedDatatype=URL;
                                                   CaptionML=[DEU=Homepage;
                                                              ENU=Home Page] }
    { 5720;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=[DEU=LÑnder-/Regionscode;
                                                              ENU=Country/Region Code] }
    { 5724;   ;Use As In-Transit   ;Boolean       ;OnValidate=BEGIN
                                                                IF "Use As In-Transit" THEN BEGIN
                                                                  TESTFIELD("Require Put-away",FALSE);
                                                                  TESTFIELD("Require Pick",FALSE);
                                                                  TESTFIELD("Use Cross-Docking",FALSE);
                                                                  TESTFIELD("Require Receive",FALSE);
                                                                  TESTFIELD("Require Shipment",FALSE);
                                                                  TESTFIELD("Bin Mandatory",FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Als Transitlager verwenden;
                                                              ENU=Use As In-Transit] }
    { 5726;   ;Require Put-away    ;Boolean       ;OnValidate=VAR
                                                                WhseActivHeader@1000 : Record 5766;
                                                                WhseRcptHeader@1001 : Record 7316;
                                                              BEGIN
                                                                WhseRcptHeader.SETCURRENTKEY("Location Code");
                                                                WhseRcptHeader.SETRANGE("Location Code",Code);
                                                                IF WhseRcptHeader.FINDFIRST THEN
                                                                  ERROR(Text008,FIELDCAPTION("Require Put-away"),xRec."Require Put-away",WhseRcptHeader.TABLECAPTION);

                                                                IF NOT "Require Put-away" THEN BEGIN
                                                                  TESTFIELD("Directed Put-away and Pick",FALSE);
                                                                  WhseActivHeader.SETRANGE(Type,WhseActivHeader.Type::"Put-away");
                                                                  WhseActivHeader.SETRANGE("Location Code",Code);
                                                                  IF WhseActivHeader.FINDFIRST THEN
                                                                    ERROR(Text008,FIELDCAPTION("Require Put-away"),TRUE,WhseActivHeader.TABLECAPTION);
                                                                  "Use Cross-Docking" := FALSE;
                                                                  "Cross-Dock Bin Code" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Einlagerung erforderlich;
                                                              ENU=Require Put-away] }
    { 5727;   ;Require Pick        ;Boolean       ;OnValidate=VAR
                                                                WhseActivHeader@1000 : Record 5766;
                                                                WhseShptHeader@1001 : Record 7320;
                                                              BEGIN
                                                                WhseShptHeader.SETCURRENTKEY("Location Code");
                                                                WhseShptHeader.SETRANGE("Location Code",Code);
                                                                IF WhseShptHeader.FINDFIRST THEN
                                                                  ERROR(Text008,FIELDCAPTION("Require Pick"),xRec."Require Pick",WhseShptHeader.TABLECAPTION);

                                                                IF NOT "Require Pick" THEN BEGIN
                                                                  TESTFIELD("Directed Put-away and Pick",FALSE);
                                                                  WhseActivHeader.SETRANGE(Type,WhseActivHeader.Type::Pick);
                                                                  WhseActivHeader.SETRANGE("Location Code",Code);
                                                                  IF WhseActivHeader.FINDFIRST THEN
                                                                    ERROR(Text008,FIELDCAPTION("Require Pick"),TRUE,WhseActivHeader.TABLECAPTION);
                                                                  "Use Cross-Docking" := FALSE;
                                                                  "Cross-Dock Bin Code" := '';
                                                                  "Pick According to FEFO" := FALSE;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Kommissionierung erforderlich;
                                                              ENU=Require Pick] }
    { 5728;   ;Cross-Dock Due Date Calc.;DateFormula;
                                                   CaptionML=[DEU=Zuord.-FÑlligkeitsdatum ber.;
                                                              ENU=Cross-Dock Due Date Calc.] }
    { 5729;   ;Use Cross-Docking   ;Boolean       ;OnValidate=BEGIN
                                                                TESTFIELD("Require Receive");
                                                                TESTFIELD("Require Shipment");
                                                                TESTFIELD("Require Put-away");
                                                                TESTFIELD("Require Pick");
                                                                IF NOT "Use Cross-Docking" THEN
                                                                  "Cross-Dock Bin Code" := '';
                                                              END;

                                                   CaptionML=[DEU=Zuordnung verwenden;
                                                              ENU=Use Cross-Docking] }
    { 5730;   ;Require Receive     ;Boolean       ;OnValidate=VAR
                                                                WhseRcptHeader@1000 : Record 7316;
                                                                WhseActivHeader@1001 : Record 5766;
                                                              BEGIN
                                                                IF NOT "Require Receive" THEN BEGIN
                                                                  TESTFIELD("Directed Put-away and Pick",FALSE);
                                                                  WhseRcptHeader.SETCURRENTKEY("Location Code");
                                                                  WhseRcptHeader.SETRANGE("Location Code",Code);
                                                                  IF WhseRcptHeader.FINDFIRST THEN
                                                                    ERROR(Text008,FIELDCAPTION("Require Receive"),TRUE,WhseRcptHeader.TABLECAPTION);
                                                                  "Receipt Bin Code" := '';
                                                                  "Use Cross-Docking" := FALSE;
                                                                  "Cross-Dock Bin Code" := '';
                                                                END ELSE BEGIN
                                                                  WhseActivHeader.SETRANGE(Type,WhseActivHeader.Type::"Put-away");
                                                                  WhseActivHeader.SETRANGE("Location Code",Code);
                                                                  IF WhseActivHeader.FINDFIRST THEN
                                                                    ERROR(Text008,FIELDCAPTION("Require Receive"),FALSE,WhseActivHeader.TABLECAPTION);
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Wareneingang erforderlich;
                                                              ENU=Require Receive] }
    { 5731;   ;Require Shipment    ;Boolean       ;OnValidate=VAR
                                                                WhseShptHeader@1000 : Record 7320;
                                                                WhseActivHeader@1001 : Record 5766;
                                                              BEGIN
                                                                IF NOT "Require Shipment" THEN BEGIN
                                                                  TESTFIELD("Directed Put-away and Pick",FALSE);
                                                                  WhseShptHeader.SETCURRENTKEY("Location Code");
                                                                  WhseShptHeader.SETRANGE("Location Code",Code);
                                                                  IF WhseShptHeader.FINDFIRST THEN
                                                                    ERROR(Text008,FIELDCAPTION("Require Shipment"),TRUE,WhseShptHeader.TABLECAPTION);
                                                                  "Shipment Bin Code" := '';
                                                                  "Use Cross-Docking" := FALSE;
                                                                  "Cross-Dock Bin Code" := '';
                                                                END ELSE BEGIN
                                                                  WhseActivHeader.SETRANGE(Type,WhseActivHeader.Type::Pick);
                                                                  WhseActivHeader.SETRANGE("Location Code",Code);
                                                                  IF WhseActivHeader.FINDFIRST THEN
                                                                    ERROR(Text008,FIELDCAPTION("Require Shipment"),FALSE,WhseActivHeader.TABLECAPTION);
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Warenausgang erforderlich;
                                                              ENU=Require Shipment] }
    { 5732;   ;Bin Mandatory       ;Boolean       ;OnValidate=VAR
                                                                ItemLedgEntry@1004 : Record 32;
                                                                WhseEntry@1000 : Record 7312;
                                                                WhseActivHeader@1001 : Record 5766;
                                                                WhseShptHeader@1002 : Record 7320;
                                                                WhseRcptHeader@1003 : Record 7316;
                                                                Window@1005 : Dialog;
                                                              BEGIN
                                                                IF "Bin Mandatory" AND NOT xRec."Bin Mandatory" THEN BEGIN
                                                                  Window.OPEN(Text010);
                                                                  ItemLedgEntry.SETCURRENTKEY("Item No.",Open,"Variant Code",Positive,"Location Code");
                                                                  IF ItemLedgEntry.FINDFIRST THEN
                                                                    REPEAT
                                                                      ItemLedgEntry.SETRANGE("Item No.",ItemLedgEntry."Item No.");
                                                                      ItemLedgEntry.SETRANGE(Open,TRUE);
                                                                      ItemLedgEntry.SETRANGE("Location Code",Code);
                                                                      IF ItemLedgEntry.FINDLAST THEN
                                                                        ERROR(Text009,FIELDCAPTION("Bin Mandatory"));
                                                                      ItemLedgEntry.SETRANGE(Open);
                                                                      ItemLedgEntry.SETRANGE("Location Code");
                                                                      ItemLedgEntry.FINDLAST;
                                                                      ItemLedgEntry.SETRANGE("Item No.");
                                                                    UNTIL ItemLedgEntry.NEXT = 0;

                                                                  "Default Bin Selection" := "Default Bin Selection"::"Fixed Bin";
                                                                END;

                                                                WhseActivHeader.SETRANGE("Location Code",Code);
                                                                IF WhseActivHeader.FINDFIRST THEN
                                                                  ERROR(Text008,FIELDCAPTION("Bin Mandatory"),xRec."Bin Mandatory",WhseActivHeader.TABLECAPTION);

                                                                WhseRcptHeader.SETCURRENTKEY("Location Code");
                                                                WhseRcptHeader.SETRANGE("Location Code",Code);
                                                                IF WhseRcptHeader.FINDFIRST THEN
                                                                  ERROR(Text008,FIELDCAPTION("Bin Mandatory"),xRec."Bin Mandatory",WhseRcptHeader.TABLECAPTION);

                                                                WhseShptHeader.SETCURRENTKEY("Location Code");
                                                                WhseShptHeader.SETRANGE("Location Code",Code);
                                                                IF WhseShptHeader.FINDFIRST THEN
                                                                  ERROR(Text008,FIELDCAPTION("Bin Mandatory"),xRec."Bin Mandatory",WhseShptHeader.TABLECAPTION);

                                                                IF NOT "Bin Mandatory" AND xRec."Bin Mandatory" THEN BEGIN
                                                                  WhseEntry.SETCURRENTKEY("Location Code");
                                                                  WhseEntry.SETRANGE("Location Code",Code);
                                                                  WhseEntry.CALCSUMS("Qty. (Base)");
                                                                  IF WhseEntry."Qty. (Base)" <> 0 THEN
                                                                    ERROR(Text002,FIELDCAPTION("Bin Mandatory"));
                                                                END;

                                                                IF NOT "Bin Mandatory" THEN BEGIN
                                                                  "Outbound BOM Bin Code" := '';
                                                                  "Open Shop Floor Bin Code" := '';
                                                                  "Inbound Production Bin Code" := '';
                                                                  "Outbound Production Bin Code" := '';
                                                                  "Adjustment Bin Code" := '';
                                                                  "Receipt Bin Code" := '';
                                                                  "Shipment Bin Code" := '';
                                                                  "Cross-Dock Bin Code" := '';
                                                                  "Outbound BOM Bin Code" := '';
                                                                  "Inbound BOM Bin Code" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Lagerplatz notwendig;
                                                              ENU=Bin Mandatory] }
    { 5733;   ;Directed Put-away and Pick;Boolean ;OnValidate=VAR
                                                                WhseActivHeader@1002 : Record 5766;
                                                                WhseShptHeader@1001 : Record 7320;
                                                                WhseRcptHeader@1000 : Record 7316;
                                                              BEGIN
                                                                IF "Directed Put-away and Pick" THEN BEGIN
                                                                  TESTFIELD("Use As In-Transit",FALSE);
                                                                  TESTFIELD("Bin Mandatory");
                                                                  VALIDATE("Require Receive",TRUE);
                                                                  VALIDATE("Require Shipment",TRUE);
                                                                  VALIDATE("Require Put-away",TRUE);
                                                                  VALIDATE("Require Pick",TRUE);
                                                                  VALIDATE("Use Cross-Docking",TRUE);
                                                                  "Default Bin Selection" := "Default Bin Selection"::" ";
                                                                END ELSE BEGIN
                                                                  WhseActivHeader.SETRANGE("Location Code",Code);
                                                                  IF WhseActivHeader.FINDFIRST THEN
                                                                    ERROR(Text008,FIELDCAPTION("Directed Put-away and Pick"),xRec."Directed Put-away and Pick",WhseActivHeader.TABLECAPTION);

                                                                  WhseRcptHeader.SETCURRENTKEY("Location Code");
                                                                  WhseRcptHeader.SETRANGE("Location Code",Code);
                                                                  IF WhseRcptHeader.FINDFIRST THEN
                                                                    ERROR(Text008,FIELDCAPTION("Directed Put-away and Pick"),xRec."Directed Put-away and Pick",WhseRcptHeader.TABLECAPTION);

                                                                  WhseShptHeader.SETCURRENTKEY("Location Code");
                                                                  WhseShptHeader.SETRANGE("Location Code",Code);
                                                                  IF WhseShptHeader.FINDFIRST THEN
                                                                    ERROR(Text008,FIELDCAPTION("Directed Put-away and Pick"),xRec."Directed Put-away and Pick",WhseShptHeader.TABLECAPTION);

                                                                  VALIDATE("Adjustment Bin Code",'');
                                                                END;

                                                                IF (NOT "Directed Put-away and Pick") AND xRec."Directed Put-away and Pick" THEN BEGIN
                                                                  "Default Bin Selection" := "Default Bin Selection"::"Fixed Bin";
                                                                  "Use Put-away Worksheet" := FALSE;
                                                                  VALIDATE("Use Cross-Docking",FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Gesteuerte Einlag. u. Kommiss.;
                                                              ENU=Directed Put-away and Pick] }
    { 5734;   ;Default Bin Selection;Option       ;OnValidate=BEGIN
                                                                IF "Default Bin Selection" = "Default Bin Selection"::" " THEN
                                                                  TESTFIELD("Directed Put-away and Pick");
                                                              END;

                                                   CaptionML=[DEU=Vorg.-Lagerplatzauswahl;
                                                              ENU=Default Bin Selection];
                                                   OptionCaptionML=[DEU=" ,Standardlagerplatz,Zuletzt verw. Lagerplatz";
                                                                    ENU=" ,Fixed Bin,Last-Used Bin"];
                                                   OptionString=[ ,Fixed Bin,Last-Used Bin] }
    { 5790;   ;Outbound Whse. Handling Time;DateFormula;
                                                   CaptionML=[DEU=Ausgeh. Lagerdurchlaufzeit;
                                                              ENU=Outbound Whse. Handling Time] }
    { 5791;   ;Inbound Whse. Handling Time;DateFormula;
                                                   CaptionML=[DEU=Eingeh. Lagerdurchlaufzeit;
                                                              ENU=Inbound Whse. Handling Time] }
    { 7305;   ;Put-away Template Code;Code10      ;TableRelation="Put-away Template Header";
                                                   CaptionML=[DEU=Einlagerungsvorlagencode;
                                                              ENU=Put-away Template Code] }
    { 7306;   ;Use Put-away Worksheet;Boolean     ;CaptionML=[DEU=Einlagerungsvorschl. verwenden;
                                                              ENU=Use Put-away Worksheet] }
    { 7307;   ;Pick According to FEFO;Boolean     ;CaptionML=[DEU=GemÑ· FEFO kommissionieren;
                                                              ENU=Pick According to FEFO] }
    { 7308;   ;Allow Breakbulk     ;Boolean       ;CaptionML=[DEU=Gebindeanbruch zulassen;
                                                              ENU=Allow Breakbulk] }
    { 7309;   ;Bin Capacity Policy ;Option        ;CaptionML=[DEU=LagerplatzkapazitÑtsprÅfung;
                                                              ENU=Bin Capacity Policy];
                                                   OptionCaptionML=[DEU=KapazitÑt nie prÅfen,KapazitÑtsÅberschreitung zulassen,KapazitÑtsÅberschreitung verhindern;
                                                                    ENU=Never Check Capacity,Allow More Than Max. Capacity,Prohibit More Than Max. Cap.];
                                                   OptionString=Never Check Capacity,Allow More Than Max. Capacity,Prohibit More Than Max. Cap. }
    { 7313;   ;Open Shop Floor Bin Code;Code20    ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                IF "Open Shop Floor Bin Code" <> xRec."Open Shop Floor Bin Code" THEN
                                                                  CheckEmptyBin(
                                                                    xRec."Open Shop Floor Bin Code",FIELDCAPTION("Open Shop Floor Bin Code"));
                                                              END;

                                                   CaptionML=[DEU=Off. Fert.-Ber.-Lagerpl.-Code;
                                                              ENU=Open Shop Floor Bin Code] }
    { 7314;   ;Inbound Production Bin Code;Code20 ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                IF "Inbound Production Bin Code" <> xRec."Inbound Production Bin Code" THEN
                                                                  CheckEmptyBin(
                                                                    xRec."Inbound Production Bin Code",FIELDCAPTION("Inbound Production Bin Code"));
                                                              END;

                                                   CaptionML=[DEU=Fert.-Bereitst.-Lagerplatzcode;
                                                              ENU=Inbound Production Bin Code] }
    { 7315;   ;Outbound Production Bin Code;Code20;TableRelation=Bin.Code WHERE (Location Code=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                IF "Outbound Production Bin Code" <> xRec."Outbound Production Bin Code" THEN
                                                                  CheckEmptyBin(
                                                                    xRec."Outbound Production Bin Code",FIELDCAPTION("Outbound Production Bin Code"));
                                                              END;

                                                   CaptionML=[DEU=Fert.-Ausgangslagerplatzcode;
                                                              ENU=Outbound Production Bin Code] }
    { 7317;   ;Adjustment Bin Code ;Code20        ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                IF "Adjustment Bin Code" <> xRec."Adjustment Bin Code" THEN BEGIN
                                                                  IF "Adjustment Bin Code" = '' THEN
                                                                    CheckEmptyBin(
                                                                      xRec."Adjustment Bin Code",FIELDCAPTION("Adjustment Bin Code"))
                                                                  ELSE
                                                                    CheckEmptyBin(
                                                                      "Adjustment Bin Code",FIELDCAPTION("Adjustment Bin Code"));

                                                                  CheckWhseAdjmtJnl;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Ausgleichslagerplatzcode;
                                                              ENU=Adjustment Bin Code] }
    { 7319;   ;Always Create Put-away Line;Boolean;CaptionML=[DEU=Einlag.-Zeilen immer erstellen;
                                                              ENU=Always Create Put-away Line] }
    { 7320;   ;Always Create Pick Line;Boolean    ;CaptionML=[DEU=Komm.-Zeilen immer erstellen;
                                                              ENU=Always Create Pick Line] }
    { 7321;   ;Special Equipment   ;Option        ;CaptionML=[DEU=Lagerhilfsmittel;
                                                              ENU=Special Equipment];
                                                   OptionCaptionML=[DEU=" ,Nach Lagerplatz,Nach Lagerhaltungsdaten/Artikel";
                                                                    ENU=" ,According to Bin,According to SKU/Item"];
                                                   OptionString=[ ,According to Bin,According to SKU/Item] }
    { 7323;   ;Receipt Bin Code    ;Code20        ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Code));
                                                   CaptionML=[DEU=Wareneingangslagerplatzcode;
                                                              ENU=Receipt Bin Code] }
    { 7325;   ;Shipment Bin Code   ;Code20        ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Code));
                                                   CaptionML=[DEU=Warenausgangslagerplatzcode;
                                                              ENU=Shipment Bin Code] }
    { 7326;   ;Cross-Dock Bin Code ;Code20        ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Code));
                                                   CaptionML=[DEU=Zuordnungslagerplatzcode;
                                                              ENU=Cross-Dock Bin Code] }
    { 7327;   ;Outbound BOM Bin Code;Code20       ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Code));
                                                   CaptionML=[DEU=Ausgeh. StÅckl. Lagerplatzcode;
                                                              ENU=Outbound BOM Bin Code] }
    { 7328;   ;Inbound BOM Bin Code;Code20        ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Code));
                                                   CaptionML=[DEU=Eingeh. StÅckl.-Lagerplatzcode;
                                                              ENU=Inbound BOM Bin Code] }
    { 7600;   ;Base Calendar Code  ;Code10        ;TableRelation="Base Calendar";
                                                   CaptionML=[DEU=Basiskalendercode;
                                                              ENU=Base Calendar Code] }
    { 7700;   ;Use ADCS            ;Boolean       ;CaptionML=[DEU=MDE verwenden;
                                                              ENU=Use ADCS] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Name                                    ;KeyGroups=SearchCol }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Bin@1000 : Record 7354;
      Postcode@1001 : Record 225;
      WhseSetup@1002 : Record 5769;
      InvtSetup@1003 : Record 313;
      Location@1004 : Record 14;
      Text000@1005 : TextConst 'DEU=Sie kînnen den %1 %2 nicht lîschen, da dort Artikel vorhanden sind.;ENU=You cannot delete the %1 %2, because they contain items.';
      Text001@1006 : TextConst 'DEU=Sie kînnen den %1 %2 nicht lîschen, da eine oder mehrere LageraktivitÑtszeilen fÅr diesen %1 vorhanden sind.;ENU=You cannot delete the %1 %2, because one or more Warehouse Activity Lines exist for this %1.';
      Text002@1007 : TextConst 'DEU=Das Feld %1 muss ein HÑkchen enthalten, da an den LagerplÑtzen Artikel vorhanden sind.;ENU=%1 must be Yes, because the bins contain items.';
      Text003@1009 : TextConst 'DEU=Abgebrochen.;ENU=Cancelled.';
      Text004@1010 : TextConst 'DEU=Die Gesamtmenge der Artikel im Lager ist 0, doch der Ausgleichslagerplatz enthÑlt eine negative Menge und die anderen LagerplÑtze enthalten eine positive Menge.\;ENU=The total quantity of items in the warehouse is 0, but the Adjustment Bin contains a negative quantity and other bins contain a positive quantity.\';
      Text005@1012 : TextConst 'DEU=Mîchten Sie diesen %1 wirklich lîschen?;ENU=Do you still want to delete this %1?';
      Text006@1011 : TextConst 'DEU=Sie kînnen das Feld %1 erst Ñndern, wenn der auf %2 %3 vorhandene Lagerbestand 0 ist.;ENU=You cannot change the %1 until the inventory stored in %2 %3 is 0.';
      Text007@1013 : TextConst 'DEU=Sie mÅssen alle Logistik Buch.-Blattzeilen des Ausgleichs lîschen, bevor Sie den %1 Ñndern kînnen.;ENU=You have to delete all Adjustment Warehouse Journal Lines first before you can change the %1.';
      Text008@1008 : TextConst 'DEU=%1 muss %2 sein, da ein %3 (oder mehrere) vorhanden ist.;ENU=%1 must be %2, because one or more %3 exist.';
      Text009@1014 : TextConst 'DEU=Sie kînnen %1 nicht Ñndern, da zu diesem Lagerplatz ein oder mehrere offene Posten vorhanden sind.;ENU=You cannot change %1 because there are one or more open ledger entries on this location.';
      Text010@1015 : TextConst 'DEU=Artikelposten werden auf offene Posten geprÅft ...;ENU=Checking item ledger entries for open entries...';
      Text011@1016 : TextConst 'DEU=Sie kînnen das Feld %1 erst dann in %2 Ñndern, wenn der Lagerbestand an diesem Lagerplatz 0 ist.;ENU=You cannot change the %1 to %2 until the inventory stored in this bin is 0.';
      Text012@1017 : TextConst 'DEU=Bevor Sie Online Map verwenden kînnen, mÅssen Sie die Felder des Fensters ''Online Map-Einrichtung'' ausfÅllen.\Informationen hierzu finden Sie in der Hilfe zum Einrichten von Online Map.;ENU=Before you can use Online Map, you must fill in the Online Map Setup window.\See Setting Up Online Map in Help.';

    PROCEDURE RequireShipment@5(LocationCode@1000 : Code[10]) : Boolean;
    BEGIN
      IF Location.GET(LocationCode) THEN
        EXIT(Location."Require Shipment");
      WhseSetup.GET;
      EXIT(WhseSetup."Require Shipment");
    END;

    PROCEDURE RequirePicking@1(LocationCode@1000 : Code[10]) : Boolean;
    BEGIN
      IF Location.GET(LocationCode) THEN
        EXIT(Location."Require Pick");
      WhseSetup.GET;
      EXIT(WhseSetup."Require Pick");
    END;

    PROCEDURE RequireReceive@4(LocationCode@1000 : Code[10]) : Boolean;
    BEGIN
      IF Location.GET(LocationCode) THEN
        EXIT(Location."Require Receive");
      WhseSetup.GET;
      EXIT(WhseSetup."Require Receive");
    END;

    PROCEDURE RequirePutaway@2(LocationCode@1000 : Code[10]) : Boolean;
    BEGIN
      IF Location.GET(LocationCode) THEN
        EXIT(Location."Require Put-away");
      WhseSetup.GET;
      EXIT(WhseSetup."Require Put-away");
    END;

    PROCEDURE GetLocationSetup@3(LocationCode@1000 : Code[10];VAR Location2@1001 : Record 14) : Boolean;
    BEGIN
      IF NOT GET(LocationCode) THEN BEGIN
        WITH Location2 DO BEGIN
          INIT;
          WhseSetup.GET;
          Code := LocationCode;
          "Use As In-Transit" := FALSE;
          "Require Put-away" := WhseSetup."Require Put-away";
          "Require Pick" := WhseSetup."Require Pick";
          "Outbound Whse. Handling Time" := InvtSetup."Outbound Whse. Handling Time";
          "Inbound Whse. Handling Time" := InvtSetup."Inbound Whse. Handling Time";
          "Require Receive" := WhseSetup."Require Receive";
          "Require Shipment" := WhseSetup."Require Shipment";
          EXIT(FALSE);
        END;
      END;
      Location2 := Rec;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE WMSCheckWarehouse@7300();
    VAR
      Zone@1005 : Record 7300;
      Bin@1006 : Record 7354;
      BinContent@1004 : Record 7302;
      WhseActivLine@1003 : Record 5767;
      WarehouseEntry@1002 : Record 7312;
      WarehouseEntry2@1001 : Record 7312;
      WhseJnlLine@1000 : Record 7311;
    BEGIN
      WarehouseEntry.SETCURRENTKEY("Location Code");
      WarehouseEntry.SETRANGE("Location Code",Code);
      WarehouseEntry.CALCSUMS("Qty. (Base)");
      IF WarehouseEntry."Qty. (Base)" = 0 THEN BEGIN
        IF "Adjustment Bin Code" <> '' THEN BEGIN
          WarehouseEntry2.SETCURRENTKEY("Bin Code","Location Code");
          WarehouseEntry2.SETRANGE("Bin Code","Adjustment Bin Code");
          WarehouseEntry2.SETRANGE("Location Code",Code);
          WarehouseEntry2.CALCSUMS("Qty. (Base)");
          IF WarehouseEntry2."Qty. (Base)" < 0 THEN
            IF NOT CONFIRM(Text004 + Text005,FALSE,TABLECAPTION) THEN
              ERROR(Text003)
        END;
      END ELSE
        ERROR(Text000,TABLECAPTION,Code);

      WhseActivLine.SETCURRENTKEY("Location Code","Activity Type");
      WhseActivLine.SETRANGE("Location Code",Code);
      WhseActivLine.SETRANGE("Activity Type",WhseActivLine."Activity Type"::Movement);
      WhseActivLine.SETFILTER("Qty. Outstanding",'<>0');
      IF WhseActivLine.FINDFIRST THEN
        ERROR(Text001,TABLECAPTION,Code);

      WhseJnlLine.SETRANGE("Location Code",Code);
      WhseJnlLine.SETFILTER(Quantity,'<>0');
      IF WhseJnlLine.FINDFIRST THEN
        ERROR(Text001,TABLECAPTION,Code);

      Zone.SETRANGE("Location Code",Code);
      Zone.DELETEALL;
      Bin.SETRANGE("Location Code",Code);
      Bin.DELETEALL;
      BinContent.SETRANGE("Location Code",Code);
      BinContent.DELETEALL;
    END;

    LOCAL PROCEDURE CheckEmptyBin@7302(BinCode@1001 : Code[20];CaptionOfField@1002 : Text[30]);
    VAR
      WarehouseEntry@1003 : Record 7312;
      WhseEntry2@1000 : Record 7312;
    BEGIN
      WarehouseEntry.SETCURRENTKEY("Bin Code","Location Code","Item No.");
      WarehouseEntry.SETRANGE("Bin Code",BinCode);
      WarehouseEntry.SETRANGE("Location Code",Code);
      IF WarehouseEntry.FINDFIRST THEN
        REPEAT
          WarehouseEntry.SETRANGE("Item No.",WarehouseEntry."Item No.");

          WhseEntry2.SETCURRENTKEY("Item No.","Bin Code","Location Code");
          WhseEntry2.COPYFILTERS(WarehouseEntry);
          WhseEntry2.CALCSUMS("Qty. (Base)");
          IF WhseEntry2."Qty. (Base)" <> 0 THEN BEGIN
            IF (BinCode = "Adjustment Bin Code") AND (xRec."Adjustment Bin Code" = '') THEN
              ERROR(Text011,CaptionOfField,BinCode)
            ELSE
              ERROR(Text006,CaptionOfField,Bin.TABLECAPTION,BinCode);
          END;

          WarehouseEntry.FINDLAST;
          WarehouseEntry.SETRANGE("Item No.");
        UNTIL WarehouseEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE CheckWhseAdjmtJnl@7303();
    VAR
      WhseJnlTemplate@1002 : Record 7309;
      WhseJnlLine@1003 : Record 7311;
    BEGIN
      WhseJnlTemplate.SETRANGE(Type,WhseJnlTemplate.Type::Item);
      IF WhseJnlTemplate.FINDFIRST THEN
        REPEAT
          WhseJnlLine.SETRANGE("Journal Template Name",WhseJnlTemplate.Name);
          WhseJnlLine.SETRANGE("Location Code",Code);
          IF WhseJnlLine.FINDFIRST THEN
            ERROR(
              Text007,
              FIELDCAPTION("Adjustment Bin Code"));
        UNTIL WhseJnlTemplate.NEXT = 0;
    END;

    PROCEDURE GetRequirementText@6(FieldNumber@1000 : Integer) : Text[50];
    VAR
      Text000@1002 : TextConst 'DEU=Warenausgang,Wareneingang,Kommissionierung,Einlagerung;ENU=Shipment,Receive,Pick,Put-Away';
    BEGIN
      CASE FieldNumber OF
        FIELDNO("Require Shipment"):
          EXIT(SELECTSTR(1,Text000));
        FIELDNO("Require Receive"):
          EXIT(SELECTSTR(2,Text000));
        FIELDNO("Require Pick"):
          EXIT(SELECTSTR(3,Text000));
        FIELDNO("Require Put-away"):
          EXIT(SELECTSTR(4,Text000));
      END;
    END;

    PROCEDURE DisplayMap@7();
    VAR
      MapPoint@1001 : Record 800;
      MapMgt@1000 : Codeunit 802;
    BEGIN
      IF MapPoint.FIND('-') THEN
        MapMgt.MakeSelection(DATABASE::Location,GETPOSITION)
      ELSE
        MESSAGE(Text012);
    END;

    BEGIN
    END.
  }
}

