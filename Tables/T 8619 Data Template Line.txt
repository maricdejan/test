OBJECT Table 8619 Data Template Line
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               DataTemplateHeader@1000 : Record 8618;
             BEGIN
               IF DataTemplateHeader.GET("Data Template Code") THEN
                 TableID := DataTemplateHeader.TableID;
             END;

    CaptionML=[DEU=Datenvorlagenzeile;
               ENU=Data Template Line];
  }
  FIELDS
  {
    { 1   ;   ;Data Template Code  ;Code10        ;CaptionML=[DEU=Datenvorlagencode;
                                                              ENU=Data Template Code];
                                                   Editable=No }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[DEU=Zeilennr.;
                                                              ENU=Line No.];
                                                   Editable=No }
    { 3   ;   ;Type                ;Option        ;InitValue=Field;
                                                   OnValidate=BEGIN
                                                                CASE Type OF
                                                                  Type::Field : CLEAR(Template);
                                                                  Type::Template : BEGIN
                                                                                     CLEAR("Field Name");
                                                                                     CLEAR(FieldID);
                                                                                   END;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Art;
                                                              ENU=Type];
                                                   OptionCaptionML=[DEU=Feld,Vorlage;
                                                                    ENU=Field,Template];
                                                   OptionString=Field,Template }
    { 4   ;   ;FieldID             ;Integer       ;TableRelation=IF (Type=CONST(Field)) Field.No. WHERE (TableNo=FIELD(TableID));
                                                   CaptionML=[DEU=Feld-ID;
                                                              ENU=FieldID] }
    { 5   ;   ;Field Name          ;Text30        ;FieldClass=Normal;
                                                   OnValidate=VAR
                                                                DataTemplateLine@1000 : Record 8619;
                                                                TemplateMgt@1001 : Codeunit 8612;
                                                              BEGIN
                                                                DataTemplateLine.SETRANGE("Data Template Code","Data Template Code");
                                                                DataTemplateLine.SETRANGE("Field Name","Field Name");
                                                                IF DataTemplateLine.FIND('-') THEN
                                                                  ERROR(STRSUBSTNO(Text004,"Field Name"));

                                                                TemplateMgt.TestHierarchy(Rec);
                                                              END;

                                                   OnLookup=VAR
                                                              DataTemplateHeader@1000000002 : Record 8618;
                                                            BEGIN
                                                              IF Type = Type::Template THEN
                                                                EXIT;

                                                              DataTemplateHeader.GET("Data Template Code");

                                                              IF DataTemplateHeader.TableID = 0 THEN
                                                                EXIT;

                                                              CLEAR(FieldList);
                                                              FieldRec.SETRANGE(FieldRec.TableNo,DataTemplateHeader.TableID);
                                                              FieldList.SETTABLEVIEW(FieldRec);
                                                              FieldList.LOOKUPMODE := TRUE;
                                                              IF FieldList.RUNMODAL = ACTION::LookupOK THEN
                                                              BEGIN
                                                                FieldList.GETRECORD(FieldRec);
                                                                TableID:=FieldRec.TableNo;
                                                                VALIDATE(FieldID,FieldRec."No.");
                                                                VALIDATE("Field Name",FieldRec.FieldName);

                                                              END;
                                                            END;

                                                   CaptionML=[DEU=Feldname;
                                                              ENU=Field Name];
                                                   Editable=No }
    { 6   ;   ;TableID             ;Integer       ;CaptionML=[DEU=TableID;
                                                              ENU=TableID] }
    { 7   ;   ;Table Name          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=FILTER(Table),
                                                                                                                Object ID=FIELD(TableID)));
                                                   CaptionML=[DEU=Tabellenname;
                                                              ENU=Table Name] }
    { 8   ;   ;Template            ;Code10        ;OnValidate=VAR
                                                                DataTemplateHeader@1000 : Record 8618;
                                                                DataTemplateHeader2@1001 : Record 8618;
                                                                DataTemplateLine@1003 : Record 8619;
                                                                TemplateMgt@1002 : Codeunit 8612;
                                                              BEGIN
                                                                IF (Type <> Type::Template) THEN
                                                                  ERROR(Text005);

                                                                IF Template = "Data Template Code" THEN
                                                                  ERROR(Text000);

                                                                IF DataTemplateHeader.GET(Template) THEN
                                                                  IF DataTemplateHeader2.GET("Data Template Code") THEN
                                                                    IF DataTemplateHeader.TableID <> DataTemplateHeader2.TableID THEN
                                                                      ERROR(STRSUBSTNO(Text002,DataTemplateHeader.Code,DataTemplateHeader2.TableID));

                                                                TemplateMgt.TestHierarchy(Rec);

                                                                DataTemplateLine.SETRANGE("Data Template Code","Data Template Code");
                                                                DataTemplateLine.SETRANGE(Template,Template);
                                                                IF DataTemplateLine.FIND('-') THEN
                                                                  ERROR(STRSUBSTNO(Text003,Template));
                                                              END;

                                                   OnLookup=VAR
                                                              DataTemplateHeader@1002 : Record 8618;
                                                              DataTemplate@1001 : Record 8618;
                                                              DataTemplateList@1000 : Form 8620;
                                                            BEGIN
                                                              IF Type = Type::Field THEN
                                                                EXIT;

                                                              DataTemplateHeader.GET("Data Template Code");

                                                              IF DataTemplateHeader.TableID = 0 THEN
                                                                EXIT;

                                                              DataTemplate.SETRANGE(DataTemplate.TableID,DataTemplateHeader.TableID);
                                                              DataTemplateList.SETTABLEVIEW(DataTemplate);
                                                              DataTemplateList.LOOKUPMODE := TRUE;
                                                              DataTemplateList.EDITABLE := FALSE;
                                                              IF DataTemplateList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                DataTemplateList.GETRECORD(DataTemplate);
                                                                IF DataTemplate.Code = "Data Template Code" THEN
                                                                  ERROR(Text000);
                                                                "Template Description" := DataTemplate.Description;
                                                                VALIDATE(Template,DataTemplate.Code);
                                                              END;
                                                            END;

                                                   CaptionML=[DEU=Vorlage;
                                                              ENU=Template] }
    { 9   ;   ;Template Description;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Data Template Header".Description WHERE (Code=FIELD(Data Template Code)));
                                                   CaptionML=[DEU=Vorlagenbeschreibung;
                                                              ENU=Template Description];
                                                   Editable=No }
    { 10  ;   ;Mandatory           ;Boolean       ;CaptionML=[DEU=Notwendig;
                                                              ENU=Mandatory] }
    { 11  ;   ;Comment             ;Text30        ;CaptionML=[DEU=Bemerkung;
                                                              ENU=Comment] }
    { 12  ;   ;Default Value       ;Text30        ;OnValidate=VAR
                                                                MigrationMgt@1003 : Codeunit 8611;
                                                                RecRef@1002 : RecordRef;
                                                                Field@1001 : FieldRef;
                                                                ValidationError@1000 : Text[250];
                                                              BEGIN
                                                                IF (FieldID <> 0) AND ("Default Value" <> '') THEN BEGIN
                                                                  RecRef.OPEN(TableID,TRUE);
                                                                  Field := RecRef.FIELD(FieldID);
                                                                  ValidationError := MigrationMgt.EvaluateValue(Field,"Default Value");
                                                                  IF ValidationError <> '' THEN
                                                                    ERROR(ValidationError);
                                                                  IF FORMAT(Field.TYPE) <> 'Option' THEN
                                                                    "Default Value" := FORMAT(Field.VALUE)
                                                                  ELSE
                                                                    "Default Value" := MigrationMgt.GetOptionString(Field,"Default Value");

                                                                  IF NOT MigrationMgt.ValidateTableRelation(Field) THEN
                                                                    ERROR(Text001,Field.CAPTION,"Default Value");

                                                                END
                                                              END;

                                                   CaptionML=[DEU=Standardwert;
                                                              ENU=Default Value] }
    { 13  ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=FILTER(Table),
                                                                                                                Object ID=FIELD(TableID)));
                                                   CaptionML=[DEU=Tabellencaption;
                                                              ENU=Table Caption] }
    { 14  ;   ;Field Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(TableID),
                                                                                                   No.=FIELD(FieldID)));
                                                   CaptionML=[DEU=Feldbezeichnung;
                                                              ENU=Field Caption];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Data Template Code,Line No.             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Eine Vorlage kann nicht mit sich selbst verknÅpft sein.;ENU=A template cannot relate to itself.';
      Text001@1001 : TextConst 'DEU=Der %1-Wert ''%2'' ist nicht vorhanden.;ENU=%1 value ''%2'' does not exist.';
      Text002@1002 : TextConst 'DEU=Vorlage %1 bezieht sich nicht auf Tabelle %2.;ENU=Template %1 does not relate to table %2.';
      Text003@1003 : TextConst 'DEU=Vorlage %1 ist bereits in dieser Hierarchie vorhanden.;ENU=Template %1 is already in this hierarchy.';
      Text004@1004 : TextConst 'DEU=Feld %1 ist bereits in dieser Vorlage vorhanden.;ENU=Field %1 is already in this template.';
      Text005@1005 : TextConst 'DEU=Die Vorlage kann im Zeilentyp ''Feld'' nicht bearbeitet werden.;ENU=Template cannot be edited on line type Field.';
      FieldList@1006 : Form 6218;
      FieldRec@1007 : Record 2000000041;

    BEGIN
    END.
  }
}

