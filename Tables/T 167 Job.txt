OBJECT Table 167 Job
{
  OBJECT-PROPERTIES
  {
    Date=14.08.09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    DataCaptionFields=No.,Description;
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 JobSetup.GET;
                 JobSetup.TESTFIELD("Job Nos.");
                 NoSeriesMgt.InitSeries(JobSetup."Job Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;

               IF GETFILTER("Bill-to Customer No.") <> '' THEN
                 IF GETRANGEMIN("Bill-to Customer No.") = GETRANGEMAX("Bill-to Customer No.") THEN
                   VALIDATE("Bill-to Customer No.",GETRANGEMIN("Bill-to Customer No."));

               DimMgt.UpdateDefaultDim(
                 DATABASE::Job,"No.",
                 "Global Dimension 1 Code","Global Dimension 2 Code");
               InitWIPFields;

               "Creation Date" := TODAY;
               "Last Date Modified" := "Creation Date";
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
             END;

    OnDelete=VAR
               CommentLine@1004 : Record 97;
               JobTask@1000 : Record 1001;
               JobResPrice@1001 : Record 1012;
               JobItemPrice@1002 : Record 1013;
               JobGLAccPrice@1003 : Record 1014;
             BEGIN
               MoveEntries.MoveJobEntries(Rec);

               JobTask.SETCURRENTKEY("Job No.");
               JobTask.SETRANGE("Job No.","No.");
               JobTask.DELETEALL(TRUE);

               JobResPrice.SETRANGE("Job No.","No.");
               JobResPrice.DELETEALL;

               JobItemPrice.SETRANGE("Job No.","No.");
               JobItemPrice.DELETEALL;

               JobGLAccPrice.SETRANGE("Job No.","No.");
               JobGLAccPrice.DELETEALL;

               CommentLine.SETRANGE("Table Name",CommentLine."Table Name"::Job);
               CommentLine.SETRANGE("No.","No.");
               CommentLine.DELETEALL;

               DimMgt.DeleteDefaultDim(DATABASE::Job,"No.");
             END;

    OnRename=BEGIN
               "Last Date Modified" := TODAY;
             END;

    CaptionML=[DEU=Projekt;
               ENU=Job];
    LookupFormID=Form89;
    DrillDownFormID=Form89;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;AltSearchField=Search Description;
                                                   OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  JobSetup.GET;
                                                                  NoSeriesMgt.TestManual(JobSetup."Job Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Nr.;
                                                              ENU=No.] }
    { 2   ;   ;Search Description  ;Code50        ;CaptionML=[DEU=Suchbegriff;
                                                              ENU=Search Description] }
    { 3   ;   ;Description         ;Text50        ;OnValidate=BEGIN
                                                                IF ("Search Description" = UPPERCASE(xRec.Description)) OR ("Search Description" = '') THEN
                                                                  "Search Description" := Description;
                                                              END;

                                                   CaptionML=[DEU=Beschreibung;
                                                              ENU=Description] }
    { 4   ;   ;Description 2       ;Text50        ;CaptionML=[DEU=Beschreibung 2;
                                                              ENU=Description 2] }
    { 5   ;   ;Bill-to Customer No.;Code20        ;TableRelation=Customer;
                                                   OnValidate=BEGIN
                                                                IF ("Bill-to Customer No." = '') OR ("Bill-to Customer No." <> xRec."Bill-to Customer No.") THEN
                                                                  IF JobLedgEntryExist OR JobPlanningLineExist THEN
                                                                    ERROR(Text000,FIELDCAPTION("Bill-to Customer No."),TABLECAPTION);
                                                                UpdateCust;
                                                              END;

                                                   CaptionML=[DEU=Rech. an Deb.-Nr.;
                                                              ENU=Bill-to Customer No.] }
    { 12  ;   ;Creation Date       ;Date          ;CaptionML=[DEU=Errichtungsdatum;
                                                              ENU=Creation Date];
                                                   Editable=No }
    { 13  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                CheckDate;
                                                              END;

                                                   CaptionML=[DEU=Startdatum;
                                                              ENU=Starting Date] }
    { 14  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                CheckDate;
                                                              END;

                                                   CaptionML=[DEU=Enddatum;
                                                              ENU=Ending Date] }
    { 19  ;   ;Status              ;Option        ;InitValue=Order;
                                                   OnValidate=VAR
                                                                JobPlanningLine@1000 : Record 1003;
                                                              BEGIN
                                                                IF xRec.Status <> Status THEN BEGIN
                                                                  IF Status = Status::Completed THEN
                                                                    VALIDATE(Complete,TRUE);
                                                                  IF xRec.Status = xRec.Status::Completed THEN BEGIN
                                                                    IF DIALOG.CONFIRM(Text004) THEN
                                                                      VALIDATE(Complete,FALSE)
                                                                    ELSE BEGIN
                                                                      Status := xRec.Status;
                                                                    END;
                                                                  END;
                                                                  JobPlanningLine.SETCURRENTKEY("Job No.");
                                                                  JobPlanningLine.SETRANGE("Job No.","No.");
                                                                  JobPlanningLine.MODIFYALL(Status,Status);
                                                                  MODIFY;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Status;
                                                              ENU=Status];
                                                   OptionCaptionML=[DEU=Planung,Angebot,Auftrag,Abgeschlossen;
                                                                    ENU=Planning,Quote,Order,Completed];
                                                   OptionString=Planning,Quote,Order,Completed }
    { 20  ;   ;Person Responsible  ;Code20        ;TableRelation=Resource;
                                                   CaptionML=[DEU=Verantwortlich;
                                                              ENU=Person Responsible] }
    { 21  ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Global Dimension 1 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[DEU=Globaler Dimensionscode 1;
                                                              ENU=Global Dimension 1 Code];
                                                   CaptionClass='1,1,1' }
    { 22  ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Global Dimension 2 Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[DEU=Globaler Dimensionscode 2;
                                                              ENU=Global Dimension 2 Code];
                                                   CaptionClass='1,1,2' }
    { 23  ;   ;Job Posting Group   ;Code10        ;TableRelation="Job Posting Group";
                                                   CaptionML=[DEU=Projektbuchungsgruppe;
                                                              ENU=Job Posting Group] }
    { 24  ;   ;Blocked             ;Option        ;CaptionML=[DEU=Gesperrt;
                                                              ENU=Blocked];
                                                   OptionCaptionML=[DEU=" ,Buchen,Alle";
                                                                    ENU=" ,Posting,All"];
                                                   OptionString=[ ,Posting,All] }
    { 29  ;   ;Last Date Modified  ;Date          ;CaptionML=[DEU=Korrigiert am;
                                                              ENU=Last Date Modified];
                                                   Editable=No }
    { 30  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Comment Line" WHERE (Table Name=CONST(14),
                                                                                           No.=FIELD(No.)));
                                                   CaptionML=[DEU=Bemerkung;
                                                              ENU=Comment];
                                                   Editable=No }
    { 31  ;   ;Customer Disc. Group;Code10        ;TableRelation="Customer Discount Group";
                                                   CaptionML=[DEU=Debitorenrabattgruppe;
                                                              ENU=Customer Disc. Group] }
    { 32  ;   ;Customer Price Group;Code10        ;TableRelation="Customer Price Group";
                                                   CaptionML=[DEU=Debitorenpreisgruppe;
                                                              ENU=Customer Price Group] }
    { 41  ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=[DEU=Sprachcode;
                                                              ENU=Language Code] }
    { 49  ;   ;Scheduled Res. Qty. ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Job Planning Line"."Quantity (Base)" WHERE (Job No.=FIELD(No.),
                                                                                                                Schedule Line=CONST(Yes),
                                                                                                                Type=CONST(Resource),
                                                                                                                No.=FIELD(Resource Filter),
                                                                                                                Planning Date=FIELD(Planning Date Filter)));
                                                   CaptionML=[DEU=Geplante Res.-Menge;
                                                              ENU=Scheduled Res. Qty.];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 50  ;   ;Resource Filter     ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation=Resource;
                                                   CaptionML=[DEU=Ressourcenfilter;
                                                              ENU=Resource Filter] }
    { 51  ;   ;Posting Date Filter ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[DEU=Buchungsdatumsfilter;
                                                              ENU=Posting Date Filter] }
    { 55  ;   ;Resource Gr. Filter ;Code20        ;FieldClass=FlowFilter;
                                                   TableRelation="Resource Group";
                                                   CaptionML=[DEU=Res.-Gruppenfilter;
                                                              ENU=Resource Gr. Filter] }
    { 56  ;   ;Scheduled Res. Gr. Qty.;Decimal    ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Job Planning Line"."Quantity (Base)" WHERE (Job No.=FIELD(No.),
                                                                                                                Schedule Line=CONST(Yes),
                                                                                                                Type=CONST(Resource),
                                                                                                                Resource Group No.=FIELD(Resource Gr. Filter),
                                                                                                                Planning Date=FIELD(Planning Date Filter)));
                                                   CaptionML=[DEU=Geplante Res.-Grp.-Menge;
                                                              ENU=Scheduled Res. Gr. Qty.];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 57  ;   ;Picture             ;BLOB          ;CaptionML=[DEU=Bild;
                                                              ENU=Picture];
                                                   SubType=Bitmap }
    { 58  ;   ;Bill-to Name        ;Text50        ;CaptionML=[DEU=Rech. an Name;
                                                              ENU=Bill-to Name] }
    { 59  ;   ;Bill-to Address     ;Text50        ;CaptionML=[DEU=Rech. an Adresse;
                                                              ENU=Bill-to Address] }
    { 60  ;   ;Bill-to Address 2   ;Text50        ;CaptionML=[DEU=Rech. an Adresse 2;
                                                              ENU=Bill-to Address 2] }
    { 61  ;   ;Bill-to City        ;Text50        ;OnValidate=BEGIN
                                                                PostCode.ValidateCity("Bill-to City","Bill-to Post Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              PostCode.LookUpCity("Bill-to City","Bill-to Post Code",TRUE);
                                                            END;

                                                   CaptionML=[DEU=Rech. an Ort;
                                                              ENU=Bill-to City] }
    { 63  ;   ;County              ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Customer.County WHERE (No.=FIELD(Bill-to Customer No.)));
                                                   CaptionML=[DEU=Bundesregion;
                                                              ENU=County];
                                                   Editable=No }
    { 64  ;   ;Bill-to Post Code   ;Code20        ;TableRelation="Post Code";
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode("Bill-to City","Bill-to Post Code");
                                                              END;

                                                   OnLookup=BEGIN
                                                              PostCode.LookUpPostCode("Bill-to City","Bill-to Post Code",TRUE);
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[DEU=Rech. an PLZ-Code;
                                                              ENU=Bill-to Post Code] }
    { 66  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[DEU=Nummernserie;
                                                              ENU=No. Series];
                                                   Editable=No }
    { 67  ;   ;Bill-to Country/Region Code;Code10 ;TableRelation=Country/Region;
                                                   CaptionML=[DEU=Rech. an LÑnder-/Regionscode;
                                                              ENU=Bill-to Country/Region Code];
                                                   Editable=Yes }
    { 68  ;   ;Bill-to Name 2      ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Customer."Name 2" WHERE (No.=FIELD(Bill-to Customer No.)));
                                                   CaptionML=[DEU=Rech. an Name 2;
                                                              ENU=Bill-to Name 2];
                                                   Editable=No }
    { 1000;   ;WIP Method          ;Option        ;CaptionML=[DEU=WIP-Methode;
                                                              ENU=WIP Method];
                                                   OptionCaptionML=[DEU=Einstandswert,Verkaufswert,Vertriebskosten,Prozentsatz der Fertigung,bei Abschluss;
                                                                    ENU=Cost Value,Sales Value,Cost of Sales,Percentage of Completion,Completed Contract];
                                                   OptionString=Cost Value,Sales Value,Cost of Sales,Percentage of Completion,Completed Contract }
    { 1001;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   OnValidate=BEGIN
                                                                IF "Currency Code" <> xRec."Currency Code" THEN
                                                                  IF NOT JobLedgEntryExist THEN
                                                                    CurrencyUpdatePlanningLines
                                                                  ELSE
                                                                    ERROR(Text000,FIELDCAPTION("Currency Code"),TABLECAPTION);
                                                              END;

                                                   CaptionML=[DEU=WÑhrungscode;
                                                              ENU=Currency Code] }
    { 1002;   ;Bill-to Contact No. ;Code20        ;OnValidate=BEGIN
                                                                IF Blocked >= Blocked::Posting THEN
                                                                  ERROR(Text000,FIELDCAPTION("Bill-to Contact No."),TABLECAPTION);

                                                                IF ("Bill-to Contact No." <> xRec."Bill-to Contact No.") AND
                                                                   (xRec."Bill-to Contact No." <> '')
                                                                THEN BEGIN
                                                                  IF ("Bill-to Contact No." = '') AND ("Bill-to Customer No." = '') THEN BEGIN
                                                                    INIT;
                                                                    "No. Series" := xRec."No. Series";
                                                                    VALIDATE(Description,xRec.Description);
                                                                  END;
                                                                END;

                                                                IF ("Bill-to Customer No." <> '') AND ("Bill-to Contact No." <> '') THEN BEGIN
                                                                  Cont.GET("Bill-to Contact No.");
                                                                  ContBusinessRelation.RESET;
                                                                  ContBusinessRelation.SETCURRENTKEY("Link to Table","No.");
                                                                  ContBusinessRelation.SETRANGE("Link to Table",ContBusinessRelation."Link to Table"::Customer);
                                                                  ContBusinessRelation.SETRANGE("No.","Bill-to Customer No.");
                                                                  IF ContBusinessRelation.FIND('-') THEN
                                                                    IF ContBusinessRelation."Contact No." <> Cont."Company No." THEN
                                                                      ERROR(Text005,Cont."No.",Cont.Name,"Bill-to Customer No.");
                                                                END;
                                                                UpdateBillToCust("Bill-to Contact No.");
                                                              END;

                                                   OnLookup=BEGIN
                                                              IF ("Bill-to Customer No." <> '') AND Cont.GET("Bill-to Contact No.") THEN
                                                                Cont.SETRANGE("Company No.",Cont."Company No.")
                                                              ELSE
                                                                IF Cust.GET("Bill-to Customer No.") THEN BEGIN
                                                                  ContBusinessRelation.RESET;
                                                                  ContBusinessRelation.SETCURRENTKEY("Link to Table","No.");
                                                                  ContBusinessRelation.SETRANGE("Link to Table",ContBusinessRelation."Link to Table"::Customer);
                                                                  ContBusinessRelation.SETRANGE("No.","Bill-to Customer No.");
                                                                  IF ContBusinessRelation.FIND('-') THEN
                                                                    Cont.SETRANGE("Company No.",ContBusinessRelation."Contact No.");
                                                                END ELSE
                                                                  Cont.SETFILTER("Company No.",'<>''''');

                                                              IF "Bill-to Contact No." <> '' THEN
                                                                IF Cont.GET("Bill-to Contact No.") THEN ;
                                                              IF FORM.RUNMODAL(0,Cont) = ACTION::LookupOK THEN BEGIN
                                                                xRec := Rec;
                                                                VALIDATE("Bill-to Contact No.",Cont."No.");
                                                              END;
                                                            END;

                                                   CaptionML=[DEU=Rech. an Kontaktnr.;
                                                              ENU=Bill-to Contact No.] }
    { 1003;   ;Bill-to Contact     ;Text50        ;CaptionML=[DEU=Rech. an Kontakt;
                                                              ENU=Bill-to Contact] }
    { 1004;   ;Planning Date Filter;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[DEU=Planungsdatumsfilter;
                                                              ENU=Planning Date Filter] }
    { 1005;   ;Total WIP Cost Amount;Decimal      ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Job WIP Entry"."WIP Entry Amount" WHERE (Job No.=FIELD(No.),
                                                                                                             Job Complete=CONST(No),
                                                                                                             Type=FILTER(Accrued Costs|WIP Costs|Recognized Costs)));
                                                   CaptionML=[DEU=WIP-Einstandsbetrag (gesamt);
                                                              ENU=Total WIP Cost Amount];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 1006;   ;Total WIP Cost G/L Amount;Decimal  ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Job WIP G/L Entry"."WIP Entry Amount" WHERE (Job No.=FIELD(No.),
                                                                                                                 Reversed=CONST(No),
                                                                                                                 Job Complete=CONST(No),
                                                                                                                 Type=FILTER(Accrued Costs|WIP Costs|Recognized Costs)));
                                                   CaptionML=[DEU=WIP-Einstandsbetrag fÅr Sachkonten (gesamt);
                                                              ENU=Total WIP Cost G/L Amount];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 1007;   ;WIP Posted To G/L   ;Boolean       ;CaptionML=[DEU=Auf Sachkonten geb. WIP-Kosten;
                                                              ENU=WIP Posted To G/L];
                                                   Editable=No }
    { 1008;   ;WIP Posting Date    ;Date          ;CaptionML=[DEU=WIP-Buchungsdatum;
                                                              ENU=WIP Posting Date];
                                                   Editable=No }
    { 1009;   ;WIP G/L Posting Date;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Min("Job WIP G/L Entry"."WIP Posting Date" WHERE (Reversed=CONST(No),
                                                                                                                 Job No.=FIELD(No.)));
                                                   CaptionML=[DEU=Buchungsdatum WIP auf Sachposten;
                                                              ENU=WIP G/L Posting Date];
                                                   Editable=No }
    { 1010;   ;Posted WIP Method Used;Option      ;CaptionML=[DEU=Verwendete Methode fÅr WIP-Buchung;
                                                              ENU=Posted WIP Method Used];
                                                   OptionCaptionML=[DEU=" ,Einstandswert,Verkaufswert,Vertriebskosten,Prozentsatz der Fertigung,bei Abschluss";
                                                                    ENU=" ,Cost Value,Sales Value,Cost of Sales,Percentage of Completion,Completed Contract"];
                                                   OptionString=[ ,Cost Value,Sales Value,Cost of Sales,Percentage of Completion,Completed Contract];
                                                   Editable=No }
    { 1011;   ;Invoice Currency Code;Code10       ;TableRelation=Currency;
                                                   CaptionML=[DEU=WÑhrungscode Rechnung;
                                                              ENU=Invoice Currency Code] }
    { 1012;   ;Exch. Calculation (Cost);Option    ;CaptionML=[DEU=Wechselkursberechnung (Einstandspreis);
                                                              ENU=Exch. Calculation (Cost)];
                                                   OptionCaptionML=[DEU=MW fest,FW fest;
                                                                    ENU=Fixed LCY,Fixed FCY];
                                                   OptionString=Fixed LCY,Fixed FCY }
    { 1013;   ;Exch. Calculation (Price);Option   ;CaptionML=[DEU=Wechselkursberechnung (Verkaufspreis);
                                                              ENU=Exch. Calculation (Price)];
                                                   OptionCaptionML=[DEU=FW fest,MW fest;
                                                                    ENU=Fixed FCY,Fixed LCY];
                                                   OptionString=Fixed FCY,Fixed LCY }
    { 1014;   ;Allow Schedule/Contract Lines;Boolean;
                                                   CaptionML=[DEU=Plan-/Vertragszeilen zulassen;
                                                              ENU=Allow Schedule/Contract Lines] }
    { 1015;   ;Complete            ;Boolean       ;OnValidate=BEGIN
                                                                IF Complete <> xRec.Complete THEN
                                                                  ChangeJobCompletionStatus;
                                                              END;

                                                   CaptionML=[DEU=Komplettlieferung;
                                                              ENU=Complete] }
    { 1016;   ;Calc. WIP Method Used;Option       ;CaptionML=[DEU=Verwendete WIP-Berechnungsmethode;
                                                              ENU=Calc. WIP Method Used];
                                                   OptionCaptionML=[DEU=" ,Einstandswert,Verkaufswert,Vertriebskosten,Prozentsatz der Fertigung,bei Abschluss";
                                                                    ENU=" ,Cost Value,Sales Value,Cost of Sales,Percentage of Completion,Completed Contract"];
                                                   OptionString=[ ,Cost Value,Sales Value,Cost of Sales,Percentage of Completion,Completed Contract];
                                                   Editable=No }
    { 1017;   ;Recog. Sales Amount ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Job WIP Entry"."WIP Entry Amount" WHERE (Job No.=FIELD(No.),
                                                                                                             Type=FILTER(Recognized Sales)));
                                                   CaptionML=[DEU=Deaktivierter Verkaufsbetrag;
                                                              ENU=Recog. Sales Amount];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 1018;   ;Recog. Sales G/L Amount;Decimal    ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Job WIP G/L Entry"."WIP Entry Amount" WHERE (Job No.=FIELD(No.),
                                                                                                                 Type=FILTER(Recognized Sales),
                                                                                                                 Reversed=CONST(No)));
                                                   CaptionML=[DEU=Deaktivierter Verkaufsbetrag in Sachkonten;
                                                              ENU=Recog. Sales G/L Amount];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 1019;   ;Recog. Costs Amount ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Job WIP Entry"."WIP Entry Amount" WHERE (Job No.=FIELD(No.),
                                                                                                              Type=FILTER(Recognized Costs)));
                                                   CaptionML=[DEU=Deaktivierter Einstandsbetrag;
                                                              ENU=Recog. Costs Amount];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 1020;   ;Recog. Costs G/L Amount;Decimal    ;FieldClass=FlowField;
                                                   CalcFormula=-Sum("Job WIP G/L Entry"."WIP Entry Amount" WHERE (Job No.=FIELD(No.),
                                                                                                                  Type=FILTER(Recognized Costs),
                                                                                                                  Reversed=CONST(No)));
                                                   CaptionML=[DEU=Deaktivierter Einstandsbetrag auf Sachkonten;
                                                              ENU=Recog. Costs G/L Amount];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 1021;   ;Total WIP Sales Amount;Decimal     ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Job WIP Entry"."WIP Entry Amount" WHERE (Job No.=FIELD(No.),
                                                                                                             Job Complete=CONST(No),
                                                                                                             Type=FILTER(Accrued Sales|WIP Sales|Recognized Sales)));
                                                   CaptionML=[DEU=WIP-Rechnungswert gesamt;
                                                              ENU=Total WIP Sales Amount];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 1022;   ;Total WIP Sales G/L Amount;Decimal ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Job WIP G/L Entry"."WIP Entry Amount" WHERE (Job No.=FIELD(No.),
                                                                                                                 Reversed=CONST(No),
                                                                                                                 Job Complete=CONST(No),
                                                                                                                 Type=FILTER(Accrued Sales|WIP Sales|Recognized Sales)));
                                                   CaptionML=[DEU=WIP-Rechnungswert gesamt in Sachkonten;
                                                              ENU=Total WIP Sales G/L Amount];
                                                   Editable=No;
                                                   AutoFormatType=1 }
    { 1023;   ;Completion Calculated;Boolean      ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Job WIP Entry" WHERE (Job No.=FIELD(No.),
                                                                                            Job Complete=FILTER(=Yes)));
                                                   CaptionML=[DEU=Berechneter Abschluss;
                                                              ENU=Completion Calculated] }
    { 1024;   ;Next Invoice Date   ;Date          ;FieldClass=FlowField;
                                                   CalcFormula=Min("Job Planning Line"."Planning Date" WHERE (Job No.=FIELD(No.),
                                                                                                              Contract Line=FILTER(=Yes),
                                                                                                              Invoiced=FILTER(=No)));
                                                   CaptionML=[DEU=NÑchstes Rechnungsdatum;
                                                              ENU=Next Invoice Date] }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Search Description                       }
    {    ;Bill-to Customer No.                     }
    {    ;Description                             ;KeyGroups=SearchCol }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.,Description,Bill-to Customer No.,Starting Date,Status }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=%1 kann nicht geÑndert werden, weil mindestens ein Eintrag mit %2 verknÅpft ist.;ENU=You cannot change %1 because one or more entries are associated with this %2.';
      JobSetup@1004 : Record 315;
      PostCode@1015 : Record 225;
      Job@1014 : Record 167;
      Cust@1006 : Record 18;
      Cont@1005 : Record 5050;
      ContBusinessRelation@1001 : Record 5054;
      NoSeriesMgt@1010 : Codeunit 396;
      DimMgt@1012 : Codeunit 408;
      Text003@1022 : TextConst 'DEU=Sie mÅssen die Funktionen ''%1'' und ''%2'' ausfÅhren, um die Abschlussposten fÅr dieses Projekt zu erstellen und zu buchen.;ENU=You must run the %1 and %2 functions to create and post the completion entries for this job.';
      Text004@1017 : TextConst 'DEU=Hierdurch werden alle nicht gebuchten WIP-Posten fÅr dieses Projekt gelîscht, und Sie kînnen die Abschlussbuchungen fÅr das Projekt stornieren.\\Mîchten Sie den Vorgang fortsetzen?;ENU=This will delete any unposted WIP entries for this job and allow you to reverse the completion postings for this job.\\Do you wish to continue?';
      Text005@1019 : TextConst 'DEU=Kontakt %1 %2 ist mit einem anderen Unternehmen als Debitor %3 verknÅpft.;ENU=Contact %1 %2 is related to a different company than customer %3.';
      Text006@1018 : TextConst 'DEU=Kontakt %1 %2 ist nicht mit Debitor %3 verknÅpft.;ENU=Contact %1 %2 is not related to customer %3.';
      Text007@1009 : TextConst 'DEU=Kontakt %1 %2 ist nicht mit einem Debitor verknÅpft.;ENU=Contact %1 %2 is not related to a customer.';
      Text008@1002 : TextConst 'DEU=%1 %2 darf nicht mit Typ %3 gesperrt werden.;ENU=%1 %2 must not be blocked with type %3.';
      Text009@1008 : TextConst 'DEU=Sie mÅssen die %1-Funktion ausfÅhren, um die fÅr dieses Projekt bereits erstellten Abschlussposten zu stornieren.;ENU=You must run the %1 function to reverse the completion entries that have already been posted for this job.';
      MoveEntries@1003 : Codeunit 361;
      Text010@1007 : TextConst 'DEU=Bevor Sie Online Map verwenden kînnen, mÅssen Sie die Felder des Fensters ''Online Map-Einrichtung'' ausfÅllen.\Informationen hierzu finden Sie in der Hilfe zum Einrichten von Online Map.;ENU=Before you can use Online Map, you must fill in the Online Map Setup window.\See Setting Up Online Map in Help.';
      Text011@1023 : TextConst 'DEU=%1 muss mit %2 Åbereinstimmen oder davor liegen.;ENU=%1 must be equal to or earlier than %2.';

    PROCEDURE AssistEdit@2(OldJob@1000 : Record 167) : Boolean;
    BEGIN
      WITH Job DO BEGIN
        Job := Rec;
        JobSetup.GET;
        JobSetup.TESTFIELD("Job Nos.");
        IF NoSeriesMgt.SelectSeries(JobSetup."Job Nos.",OldJob."No. Series","No. Series") THEN BEGIN
          JobSetup.GET;
          JobSetup.TESTFIELD("Job Nos.");
          NoSeriesMgt.SetSeries("No.");
          Rec := Job;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE ValidateShortcutDimCode@29(FieldNumber@1000 : Integer;ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      DimMgt.SaveDefaultDim(DATABASE::Job,"No.",FieldNumber,ShortcutDimCode);
      MODIFY;
    END;

    PROCEDURE UpdateBillToCont@27(CustomerNo@1000 : Code[20]);
    VAR
      ContBusRel@1003 : Record 5054;
      Cust@1001 : Record 18;
    BEGIN
      IF Cust.GET(CustomerNo) THEN BEGIN
        IF Cust."Primary Contact No." <> '' THEN
          "Bill-to Contact No." := Cust."Primary Contact No."
        ELSE BEGIN
          ContBusRel.RESET;
          ContBusRel.SETCURRENTKEY("Link to Table","No.");
          ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);
          ContBusRel.SETRANGE("No.","Bill-to Customer No.");
          IF ContBusRel.FIND('-') THEN
            "Bill-to Contact No." := ContBusRel."Contact No.";
        END;
        "Bill-to Contact" := Cust.Contact;
      END;
    END;

    LOCAL PROCEDURE JobLedgEntryExist@5() : Boolean;
    VAR
      JobLedgEntry@1000 : Record 169;
    BEGIN
      CLEAR(JobLedgEntry);
      JobLedgEntry.SETCURRENTKEY("Job No.");
      JobLedgEntry.SETRANGE("Job No.","No.");
      EXIT(JobLedgEntry.FIND('-'));
    END;

    LOCAL PROCEDURE JobPlanningLineExist@3() : Boolean;
    VAR
      JobPlanningLine@1000 : Record 1003;
    BEGIN
      JobPlanningLine.INIT;
      JobPlanningLine.SETRANGE("Job No.","No.");
      EXIT(JobPlanningLine.FIND('-'));
    END;

    PROCEDURE UpdateBillToCust@26(ContactNo@1000 : Code[20]);
    VAR
      ContBusinessRelation@1005 : Record 5054;
      Cust@1004 : Record 18;
      Cont@1003 : Record 5050;
    BEGIN
      IF Cont.GET(ContactNo) THEN BEGIN
        "Bill-to Contact No." := Cont."No.";
        IF Cont.Type = Cont.Type::Person THEN
          "Bill-to Contact" := Cont.Name
        ELSE
          IF Cust.GET("Bill-to Customer No.") THEN
            "Bill-to Contact" := Cust.Contact
          ELSE
            "Bill-to Contact" := '';
      END ELSE BEGIN
        "Bill-to Contact" := '';
        EXIT;
      END;

      ContBusinessRelation.RESET;
      ContBusinessRelation.SETCURRENTKEY("Link to Table","Contact No.");
      ContBusinessRelation.SETRANGE("Link to Table",ContBusinessRelation."Link to Table"::Customer);
      ContBusinessRelation.SETRANGE("Contact No.",Cont."Company No.");
      IF ContBusinessRelation.FIND('-') THEN BEGIN
        IF "Bill-to Customer No." = '' THEN
          VALIDATE("Bill-to Customer No.",ContBusinessRelation."No.")
        ELSE
          IF "Bill-to Customer No." <> ContBusinessRelation."No." THEN
            ERROR(Text006,Cont."No.",Cont.Name,"Bill-to Customer No.");
      END ELSE
        ERROR(Text007,Cont."No.",Cont.Name);
    END;

    PROCEDURE UpdateCust@4();
    BEGIN
      IF "Bill-to Customer No." <> '' THEN BEGIN
        Cust.GET("Bill-to Customer No.");
        Cust.TESTFIELD("Customer Posting Group");
        Cust.TESTFIELD("Bill-to Customer No.",'');
        "Bill-to Name" := Cust.Name;
        "Bill-to Name 2" := Cust."Name 2";
        "Bill-to Address" := Cust.Address;
        "Bill-to Address 2" := Cust."Address 2";
        "Bill-to City" := Cust.City;
        "Bill-to Post Code" := Cust."Post Code";
        "Bill-to Country/Region Code" := Cust."Country/Region Code";
        "Currency Code" := Cust."Currency Code";
        "Customer Disc. Group" := Cust."Customer Disc. Group";
        "Customer Price Group" := Cust."Customer Price Group";
        "Language Code" := Cust."Language Code";
        County := Cust.County;
        UpdateBillToCont("Bill-to Customer No.");
      END ELSE BEGIN
        "Bill-to Name" := '';
        "Bill-to Name 2" := '';
        "Bill-to Address" := '';
        "Bill-to Address 2" := '';
        "Bill-to City" := '';
        "Bill-to Post Code" := '';
        "Bill-to Country/Region Code" := '';
        "Currency Code" := '';
        "Customer Disc. Group" := '';
        "Customer Price Group" := '';
        "Language Code" := '';
        County := '';
        VALIDATE("Bill-to Contact No.",'');
      END;
    END;

    PROCEDURE InitWIPFields@1();
    BEGIN
      "WIP Posted To G/L" := FALSE;
      "WIP Posting Date" := 0D;
      "WIP G/L Posting Date" := 0D;
      "Posted WIP Method Used" := 0;
    END;

    PROCEDURE TestBlocked@6();
    BEGIN
      IF Blocked = Blocked::" " THEN
        EXIT;
      ERROR(Text008,TABLECAPTION,"No.",Blocked);
    END;

    PROCEDURE CurrencyUpdatePlanningLines@10();
    VAR
      PlaningLine@1000 : Record 1003;
    BEGIN
      PlaningLine.SETRANGE("Job No.","No.");
      IF PlaningLine.FIND('-') THEN
        REPEAT
          IF PlaningLine.Transferred THEN
            ERROR(Text000,FIELDCAPTION("Currency Code"),TABLECAPTION);
          PlaningLine.VALIDATE("Currency Code","Currency Code");
          PlaningLine.VALIDATE("Currency Date");
          PlaningLine.MODIFY;
        UNTIL PlaningLine.NEXT = 0;
    END;

    PROCEDURE ChangeJobCompletionStatus@7();
    VAR
      AllObjwithCaption@1003 : Record 2000000058;
      JobWIPGLEntry@1000 : Record 1005;
      JobCalcWIP@1001 : Codeunit 1000;
      ReportCaption1@1002 : Text[250];
      ReportCaption2@1004 : Text[250];
    BEGIN
      AllObjwithCaption.GET(AllObjwithCaption."Object Type"::Report,REPORT::"Job Calculate WIP");
      ReportCaption1 := AllObjwithCaption."Object Caption";
      AllObjwithCaption.GET(AllObjwithCaption."Object Type"::Report,REPORT::"Job Post WIP to G/L");
      ReportCaption2 := AllObjwithCaption."Object Caption";

      IF Complete = TRUE THEN
        MESSAGE(Text003,ReportCaption1,ReportCaption2)
      ELSE BEGIN
        JobCalcWIP.ReOpenJob("No.");
        "WIP Posting Date" := 0D;
        "Calc. WIP Method Used" := 0;
        MESSAGE(Text009,ReportCaption2);
      END;
    END;

    PROCEDURE DisplayMap@8();
    VAR
      MapPoint@1001 : Record 800;
      MapMgt@1000 : Codeunit 802;
    BEGIN
      IF MapPoint.FIND('-') THEN
        MapMgt.MakeSelection(DATABASE::Job,GETPOSITION)
      ELSE
        MESSAGE(Text010);
    END;

    PROCEDURE GetQuantityAvailable@9(ItemNo@1000 : Code[20];LocationCode@1001 : Code[10];VariantCode@1002 : Code[10];InEntryType@1004 : 'Usage,Sale,Both';Direction@1005 : 'Possitive,Negative,Both') QtyBase : Decimal;
    VAR
      JobLedgEntry@1003 : Record 169;
    BEGIN
      CLEAR(JobLedgEntry);
      WITH JobLedgEntry DO BEGIN
        SETCURRENTKEY("Job No.","Entry Type",Type,"No.");
        SETRANGE("Job No.",Rec."No.");
        IF NOT (InEntryType = InEntryType::Both) THEN
          SETRANGE("Entry Type",InEntryType);
        SETRANGE(Type,Type::Item);
        SETRANGE("No.",ItemNo);
        IF FINDSET THEN
          REPEAT
            IF ("Location Code" = LocationCode) AND
               ("Variant Code" = VariantCode) AND
               ((Direction = Direction::Both) OR
                ((Direction = Direction::Possitive) AND ("Quantity (Base)" > 0)) OR
                ((Direction = Direction::Negative) AND ("Quantity (Base)" < 0)))
            THEN
              QtyBase := QtyBase + "Quantity (Base)";
          UNTIL NEXT = 0;
      END;
    END;

    LOCAL PROCEDURE CheckDate@30();
    BEGIN
      IF ("Starting Date" > "Ending Date") AND ("Ending Date" <> 0D) THEN
        ERROR(Text011,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"));
    END;

    BEGIN
    END.
  }
}

