OBJECT Table 7311 Warehouse Journal Line
{
  OBJECT-PROPERTIES
  {
    Date=14.08.09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "User ID" := USERID;
             END;

    OnDelete=BEGIN
               ItemTrackingMgt.DeleteWhseItemTrkgLines(
                 DATABASE::"Warehouse Journal Line",0,"Journal Batch Name",
                 "Journal Template Name",0,"Line No.","Location Code",TRUE);
             END;

    CaptionML=[DEU=Logistik Buch.-Blattzeile;
               ENU=Warehouse Journal Line];
    LookupFormID=Form7319;
    DrillDownFormID=Form7319;
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Warehouse Journal Template";
                                                   CaptionML=[DEU=Buch.-Blattvorlagenname;
                                                              ENU=Journal Template Name] }
    { 2   ;   ;Journal Batch Name  ;Code10        ;TableRelation="Warehouse Journal Batch".Name WHERE (Journal Template Name=FIELD(Journal Template Name));
                                                   CaptionML=[DEU=Buch.-Blattname;
                                                              ENU=Journal Batch Name] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[DEU=Zeilennr.;
                                                              ENU=Line No.] }
    { 4   ;   ;Registering Date    ;Date          ;CaptionML=[DEU=Registriert am;
                                                              ENU=Registering Date] }
    { 5   ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=[DEU=Lagerortcode;
                                                              ENU=Location Code] }
    { 6   ;   ;From Zone Code      ;Code10        ;TableRelation=Zone.Code WHERE (Location Code=FIELD(Location Code));
                                                   OnValidate=BEGIN
                                                                IF NOT PhysInvtEntered THEN
                                                                  TESTFIELD("Phys. Inventory",FALSE);

                                                                IF "From Zone Code" <> xRec."From Zone Code" THEN BEGIN
                                                                  "From Bin Code" := '';
                                                                  "From Bin Type Code" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Von Zonencode;
                                                              ENU=From Zone Code] }
    { 7   ;   ;From Bin Code       ;Code20        ;TableRelation=IF (Phys. Inventory=CONST(No),
                                                                     Item No.=FILTER(''),
                                                                     From Zone Code=FILTER('')) "Bin Content"."Bin Code" WHERE (Location Code=FIELD(Location Code))
                                                                     ELSE IF (Phys. Inventory=CONST(No),
                                                                              Item No.=FILTER(<>''),
                                                                              From Zone Code=FILTER('')) "Bin Content"."Bin Code" WHERE (Location Code=FIELD(Location Code),
                                                                                                                                         Item No.=FIELD(Item No.))
                                                                                                                                         ELSE IF (Phys. Inventory=CONST(No),
                                                                                                                                                  Item No.=FILTER(''),
                                                                                                                                                  From Zone Code=FILTER(<>'')) "Bin Content"."Bin Code" WHERE (Location Code=FIELD(Location Code),
                                                                                                                                                                                                               Zone Code=FIELD(From Zone Code))
                                                                                                                                                                                                               ELSE IF (Phys. Inventory=CONST(No),
                                                                                                                                                                                                                        Item No.=FILTER(<>''),
                                                                                                                                                                                                                        From Zone Code=FILTER(<>'')) "Bin Content"."Bin Code" WHERE (Location Code=FIELD(Location Code),
                                                                                                                                                                                                                                                                                     Item No.=FIELD(Item No.),
                                                                                                                                                                                                                                                                                     Zone Code=FIELD(From Zone Code))
                                                                                                                                                                                                                                                                                     ELSE IF (Phys. Inventory=CONST(Yes),
                                                                                                                                                                                                                                                                                              From Zone Code=FILTER('')) Bin.Code WHERE (Location Code=FIELD(Location Code))
                                                                                                                                                                                                                                                                                              ELSE IF (Phys. Inventory=CONST(Yes),
                                                                                                                                                                                                                                                                                                       From Zone Code=FILTER(<>'')) Bin.Code WHERE (Location Code=FIELD(Location Code),
                                                                                                                                                                                                                                                                                                                                                    Zone Code=FIELD(From Zone Code));
                                                   OnValidate=BEGIN
                                                                IF NOT PhysInvtEntered THEN
                                                                  TESTFIELD("Phys. Inventory",FALSE);

                                                                IF CurrFieldNo = FIELDNO("From Bin Code") THEN
                                                                  IF "From Bin Code" <> xRec."From Bin Code" THEN
                                                                    CheckBin("Location Code","From Bin Code",FALSE);

                                                                "From Bin Type Code" :=
                                                                  GetBinType("Location Code","From Bin Code");

                                                                Bin.CALCFIELDS("Adjustment Bin");
                                                                Bin.TESTFIELD("Adjustment Bin",FALSE);

                                                                IF "From Bin Code" <> '' THEN
                                                                  "From Zone Code" := Bin."Zone Code";
                                                              END;

                                                   CaptionML=[DEU=Von Lagerplatzcode;
                                                              ENU=From Bin Code] }
    { 8   ;   ;Description         ;Text50        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description] }
    { 9   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                IF NOT PhysInvtEntered THEN
                                                                  TESTFIELD("Phys. Inventory",FALSE);

                                                                IF "Item No." <> '' THEN BEGIN
                                                                  GetItemUnitOfMeasure;
                                                                  Description := Item.Description;
                                                                  VALIDATE("Unit of Measure Code",ItemUnitOfMeasure.Code);
                                                                END ELSE BEGIN
                                                                  Description := '';
                                                                  "Variant Code" := '';
                                                                  VALIDATE("Unit of Measure Code",'');
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Artikelnr.;
                                                              ENU=Item No.] }
    { 10  ;   ;Quantity            ;Decimal       ;OnValidate=BEGIN
                                                                IF NOT PhysInvtEntered THEN
                                                                  TESTFIELD("Phys. Inventory",FALSE);

                                                                WhseJnlTemplate.GET("Journal Template Name");
                                                                IF WhseJnlTemplate.Type = WhseJnlTemplate.Type::Reclassification THEN BEGIN
                                                                  IF Quantity < 0 THEN
                                                                    FIELDERROR(Quantity,Text000);
                                                                END ELSE BEGIN
                                                                  GetLocation("Location Code");
                                                                  Location.TESTFIELD("Adjustment Bin Code");
                                                                END;

                                                                "Qty. (Base)" := CalcBaseQty(Quantity);
                                                                "Qty. (Absolute)" := ABS(Quantity);
                                                                "Qty. (Absolute, Base)" := ABS("Qty. (Base)");
                                                                IF (xRec.Quantity < 0) AND (Quantity >= 0) OR
                                                                   (xRec.Quantity >= 0) AND (Quantity < 0)
                                                                THEN
                                                                  ExchangeFromToBin;

                                                                IF Quantity > 0 THEN
                                                                  WMSMgt.CalcCubageAndWeight(
                                                                    "Item No.","Unit of Measure Code","Qty. (Absolute)",Cubage,Weight)
                                                                ELSE BEGIN
                                                                  Cubage := 0;
                                                                  Weight := 0;
                                                                END;

                                                                IF Quantity <> xRec.Quantity THEN BEGIN
                                                                  CheckBin("Location Code","From Bin Code",FALSE);
                                                                  CheckBin("Location Code","To Bin Code",TRUE);
                                                                END;

                                                                ItemTrackingMgt.CheckWhseItemTrkgSetup("Item No.",SNRequired,LNRequired,FALSE);
                                                                IF SNRequired AND NOT "Phys. Inventory" AND
                                                                   ("Serial No." <> '') AND ((Quantity < 0) OR (Quantity > 1))
                                                                THEN
                                                                  ERROR(Text006,FIELDCAPTION(Quantity));
                                                              END;

                                                   CaptionML=[DEU=Menge;
                                                              ENU=Quantity];
                                                   DecimalPlaces=0:5 }
    { 11  ;   ;Qty. (Base)         ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("Qty. per Unit of Measure",1);
                                                                VALIDATE(Quantity,"Qty. (Base)");
                                                              END;

                                                   CaptionML=[DEU=Menge (Basis);
                                                              ENU=Qty. (Base)];
                                                   DecimalPlaces=0:5 }
    { 12  ;   ;Qty. (Absolute)     ;Decimal       ;OnValidate=BEGIN
                                                                IF NOT PhysInvtEntered THEN
                                                                  TESTFIELD("Phys. Inventory",FALSE);

                                                                "Qty. (Absolute, Base)" := CalcBaseQty("Qty. (Absolute)");

                                                                IF Quantity > 0 THEN
                                                                  WMSMgt.CalcCubageAndWeight(
                                                                    "Item No.","Unit of Measure Code","Qty. (Absolute)",Cubage,Weight)
                                                                ELSE BEGIN
                                                                  Cubage := 0;
                                                                  Weight := 0;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Menge (Absolut);
                                                              ENU=Qty. (Absolute)];
                                                   DecimalPlaces=0:5 }
    { 13  ;   ;Qty. (Absolute, Base);Decimal      ;CaptionML=[DEU=Menge (Absolut, Basis);
                                                              ENU=Qty. (Absolute, Base)];
                                                   DecimalPlaces=0:5 }
    { 14  ;   ;Zone Code           ;Code10        ;TableRelation=Zone.Code WHERE (Location Code=FIELD(Location Code));
                                                   OnValidate=BEGIN
                                                                IF NOT PhysInvtEntered THEN
                                                                  TESTFIELD("Phys. Inventory",FALSE);

                                                                IF "Zone Code" <> xRec."Zone Code" THEN
                                                                  "Bin Code" := '';

                                                                IF Quantity < 0 THEN
                                                                  VALIDATE("From Zone Code","Zone Code")
                                                                ELSE
                                                                  VALIDATE("To Zone Code","Zone Code");
                                                              END;

                                                   CaptionML=[DEU=Zonencode;
                                                              ENU=Zone Code] }
    { 15  ;   ;Bin Code            ;Code20        ;TableRelation=IF (Zone Code=FILTER('')) Bin.Code WHERE (Location Code=FIELD(Location Code))
                                                                 ELSE IF (Zone Code=FILTER(<>'')) Bin.Code WHERE (Location Code=FIELD(Location Code),
                                                                                                                  Zone Code=FIELD(Zone Code));
                                                   OnValidate=BEGIN
                                                                IF NOT PhysInvtEntered THEN
                                                                  TESTFIELD("Phys. Inventory",FALSE);

                                                                IF Quantity < 0 THEN BEGIN
                                                                  VALIDATE("From Bin Code","Bin Code");
                                                                  IF "Bin Code" <> xRec."Bin Code" THEN
                                                                    CheckBin("Location Code","Bin Code",FALSE);
                                                                END ELSE BEGIN
                                                                  VALIDATE("To Bin Code","Bin Code");
                                                                  IF "Bin Code" <> xRec."Bin Code" THEN
                                                                    CheckBin("Location Code","Bin Code",TRUE);
                                                                END;

                                                                IF "Bin Code" <> '' THEN BEGIN
                                                                  GetBin("Location Code","Bin Code");
                                                                  "Zone Code" := Bin."Zone Code";
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Lagerplatzcode;
                                                              ENU=Bin Code] }
    { 20  ;   ;Source Type         ;Integer       ;CaptionML=[DEU=Herkunftsart;
                                                              ENU=Source Type];
                                                   Editable=No }
    { 21  ;   ;Source Subtype      ;Option        ;CaptionML=[DEU=Herkunftsunterart;
                                                              ENU=Source Subtype];
                                                   OptionCaptionML=[DEU=0,1,2,3,4,5,6,7,8,9,10;
                                                                    ENU=0,1,2,3,4,5,6,7,8,9,10];
                                                   OptionString=0,1,2,3,4,5,6,7,8,9,10;
                                                   Editable=No }
    { 22  ;   ;Source No.          ;Code20        ;CaptionML=[DEU=Herkunftsnr.;
                                                              ENU=Source No.];
                                                   Editable=No }
    { 23  ;   ;Source Line No.     ;Integer       ;CaptionML=[DEU=Herkunftszeilennr.;
                                                              ENU=Source Line No.];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 24  ;   ;Source Subline No.  ;Integer       ;CaptionML=[DEU=Herkunftsunterzeilennr.;
                                                              ENU=Source Subline No.];
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 25  ;   ;Source Document     ;Option        ;CaptionML=[DEU=Herkunftsbeleg;
                                                              ENU=Source Document];
                                                   OptionCaptionML=[DEU=,Auftrag,Verkaufsrechnung,Verkaufsgutschrift,Verkaufsreklamation,Bestellung,Einkaufsrechnung,Einkaufsgutschrift,Einkaufsreklamation,Eingeh. Umlagerung,Ausgeh. Umlagerung,FA-Verbrauch,Artikel Buch.-Blatt,Inventur Buch.-Blatt,Umbuch. Buch.-Blatt,FA-Verbrauchs Buch.-Blatt,FA-Istmeldungs Buch.-Blatt,StÅckliste Buch.-Blatt,Serviceauftrag;
                                                                    ENU=,S. Order,S. Invoice,S. Credit Memo,S. Return Order,P. Order,P. Invoice,P. Credit Memo,P. Return Order,Inb. Transfer,Outb. Transfer,Prod. Consumption,Item Jnl.,Phys. Invt. Jnl.,Reclass. Jnl.,Consumption Jnl.,Output Jnl.,BOM Jnl.,Serv Order];
                                                   OptionString=,S. Order,S. Invoice,S. Credit Memo,S. Return Order,P. Order,P. Invoice,P. Credit Memo,P. Return Order,Inb. Transfer,Outb. Transfer,Prod. Consumption,Item Jnl.,Phys. Invt. Jnl.,Reclass. Jnl.,Consumption Jnl.,Output Jnl.,BOM Jnl.,Serv Order;
                                                   BlankZero=Yes;
                                                   Editable=No }
    { 26  ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   CaptionML=[DEU=Herkunftscode;
                                                              ENU=Source Code];
                                                   Editable=No }
    { 27  ;   ;To Zone Code        ;Code10        ;TableRelation=Zone.Code WHERE (Location Code=FIELD(Location Code));
                                                   OnValidate=BEGIN
                                                                IF NOT PhysInvtEntered THEN
                                                                  TESTFIELD("Phys. Inventory",FALSE);

                                                                IF "To Zone Code" <> xRec."To Zone Code" THEN
                                                                  "To Bin Code" := '';
                                                              END;

                                                   CaptionML=[DEU=Nach Zonencode;
                                                              ENU=To Zone Code] }
    { 28  ;   ;To Bin Code         ;Code20        ;TableRelation=IF (To Zone Code=FILTER('')) Bin.Code WHERE (Location Code=FIELD(Location Code))
                                                                 ELSE IF (To Zone Code=FILTER(<>'')) Bin.Code WHERE (Location Code=FIELD(Location Code),
                                                                                                                     Zone Code=FIELD(To Zone Code));
                                                   OnValidate=BEGIN
                                                                IF NOT PhysInvtEntered THEN
                                                                  TESTFIELD("Phys. Inventory",FALSE);

                                                                IF CurrFieldNo = FIELDNO("To Bin Code") THEN
                                                                  IF "To Bin Code" <> xRec."To Bin Code" THEN
                                                                    CheckBin("Location Code","To Bin Code",TRUE);

                                                                GetBin("Location Code","To Bin Code");

                                                                Bin.CALCFIELDS("Adjustment Bin");
                                                                Bin.TESTFIELD("Adjustment Bin",FALSE);

                                                                IF "To Bin Code" <> '' THEN
                                                                  "To Zone Code" := Bin."Zone Code";
                                                              END;

                                                   CaptionML=[DEU=Nach Lagerplatzcode;
                                                              ENU=To Bin Code] }
    { 29  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=[DEU=Ursachencode;
                                                              ENU=Reason Code] }
    { 33  ;   ;Registering No. Series;Code10      ;TableRelation="No. Series";
                                                   CaptionML=[DEU=Registrierungsnr.-Serie;
                                                              ENU=Registering No. Series] }
    { 35  ;   ;From Bin Type Code  ;Code10        ;TableRelation="Bin Type";
                                                   CaptionML=[DEU=Von Lagerplatzartencode;
                                                              ENU=From Bin Type Code] }
    { 40  ;   ;Cubage              ;Decimal       ;CaptionML=[DEU=Volumen;
                                                              ENU=Cubage];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 41  ;   ;Weight              ;Decimal       ;CaptionML=[DEU=Gewicht;
                                                              ENU=Weight];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 50  ;   ;Whse. Document No.  ;Code20        ;CaptionML=[DEU=Logistikbelegnr.;
                                                              ENU=Whse. Document No.] }
    { 51  ;   ;Whse. Document Type ;Option        ;CaptionML=[DEU=Logistikbelegart;
                                                              ENU=Whse. Document Type];
                                                   OptionCaptionML=[DEU="Logistik Buch.-Blatt,Wareneingang,Warenausgang,Interne Einlag.-Anforderung,Interne Kommiss.-Anforderung,Fertigungsauftrag,Logistik Inventur, ";
                                                                    ENU="Whse. Journal,Receipt,Shipment,Internal Put-away,Internal Pick,Production,Whse. Phys. Inventory, "];
                                                   OptionString=[Whse. Journal,Receipt,Shipment,Internal Put-away,Internal Pick,Production,Whse. Phys. Inventory, ] }
    { 52  ;   ;Whse. Document Line No.;Integer    ;CaptionML=[DEU=Logistikbelegzeilennr.;
                                                              ENU=Whse. Document Line No.];
                                                   BlankZero=Yes }
    { 53  ;   ;Qty. (Calculated)   ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Qty. (Phys. Inventory)");
                                                              END;

                                                   CaptionML=[DEU=Menge (berechnet);
                                                              ENU=Qty. (Calculated)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 54  ;   ;Qty. (Phys. Inventory);Decimal     ;OnValidate=BEGIN
                                                                TESTFIELD("Phys. Inventory",TRUE);

                                                                IF "Serial No." <> '' THEN
                                                                  IF ("Qty. (Phys. Inventory)" < 0) OR ("Qty. (Phys. Inventory)" > 1) THEN
                                                                    ERROR(Text006,FIELDCAPTION("Qty. (Phys. Inventory)"));

                                                                PhysInvtEntered := TRUE;
                                                                Quantity := 0;
                                                                VALIDATE(Quantity,"Qty. (Phys. Inventory)" - "Qty. (Calculated)");
                                                                IF "Qty. (Phys. Inventory)" = "Qty. (Calculated)" THEN
                                                                  VALIDATE("Qty. (Phys. Inventory) (Base)","Qty. (Calculated) (Base)")
                                                                ELSE
                                                                  VALIDATE("Qty. (Phys. Inventory) (Base)",ROUND("Qty. (Phys. Inventory)" * "Qty. per Unit of Measure",0.00001));
                                                                PhysInvtEntered := FALSE;
                                                              END;

                                                   CaptionML=[DEU=Inventurmenge;
                                                              ENU=Qty. (Phys. Inventory)];
                                                   DecimalPlaces=0:5 }
    { 55  ;   ;Entry Type          ;Option        ;CaptionML=[DEU=Postenart;
                                                              ENU=Entry Type];
                                                   OptionCaptionML=[DEU=Abgang,Zugang,Lagerplatzumlagerung;
                                                                    ENU=Negative Adjmt.,Positive Adjmt.,Movement];
                                                   OptionString=Negative Adjmt.,Positive Adjmt.,Movement }
    { 56  ;   ;Phys. Inventory     ;Boolean       ;CaptionML=[DEU=Inventur;
                                                              ENU=Phys. Inventory];
                                                   Editable=No }
    { 60  ;   ;Reference Document  ;Option        ;CaptionML=[DEU=Referenzbeleg;
                                                              ENU=Reference Document];
                                                   OptionCaptionML=[DEU=" ,Geb. Wareneingang,Geb. Einkaufsrechnung,Geb. RÅcksendung,Geb. Einkaufsgutschrift,Geb. Warenausgang,Geb. Verkaufsrechnung,Geb. RÅcklieferung,Geb. Verkaufsgutschrift,Geb. Umlag.-Eingang,Geb. Umlag.-Ausgang,Artikel Buch.-Blatt,Fertigungsauftrag,Einlagerung,Kommissionierung,Lagerplatzumlagerung,StÅckliste Buch.-Blatt";
                                                                    ENU=" ,Posted Rcpt.,Posted P. Inv.,Posted Rtrn. Rcpt.,Posted P. Cr. Memo,Posted Shipment,Posted S. Inv.,Posted Rtrn. Shipment,Posted S. Cr. Memo,Posted T. Receipt,Posted T. Shipment,Item Journal,Prod.,Put-away,Pick,Movement,BOM Journal"];
                                                   OptionString=[ ,Posted Rcpt.,Posted P. Inv.,Posted Rtrn. Rcpt.,Posted P. Cr. Memo,Posted Shipment,Posted S. Inv.,Posted Rtrn. Shipment,Posted S. Cr. Memo,Posted T. Receipt,Posted T. Shipment,Item Journal,Prod.,Put-away,Pick,Movement,BOM Journal] }
    { 61  ;   ;Reference No.       ;Code20        ;CaptionML=[DEU=Referenznr.;
                                                              ENU=Reference No.] }
    { 67  ;   ;User ID             ;Code20        ;TableRelation=User;
                                                   TestTableRelation=No;
                                                   CaptionML=[DEU=Benutzer-ID;
                                                              ENU=User ID] }
    { 68  ;   ;Qty. (Calculated) (Base);Decimal   ;CaptionML=[DEU=Menge (berechnet) (Basis);
                                                              ENU=Qty. (Calculated) (Base)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 69  ;   ;Qty. (Phys. Inventory) (Base);Decimal;
                                                   OnValidate=BEGIN
                                                                "Qty. (Base)" := "Qty. (Phys. Inventory) (Base)" - "Qty. (Calculated) (Base)";
                                                                "Qty. (Absolute, Base)" := ABS("Qty. (Base)");
                                                              END;

                                                   CaptionML=[DEU=Menge (Inventur) (Basis);
                                                              ENU=Qty. (Phys. Inventory) (Base)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 5402;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   OnValidate=VAR
                                                                ItemVariant@1000 : Record 5401;
                                                              BEGIN
                                                                IF NOT PhysInvtEntered THEN
                                                                  TESTFIELD("Phys. Inventory",FALSE);

                                                                IF "Variant Code" <> '' THEN BEGIN
                                                                  ItemVariant.GET("Item No.","Variant Code");
                                                                  Description := ItemVariant.Description;
                                                                END ELSE
                                                                  GetItem("Item No.",Description);

                                                                IF "Variant Code" <> xRec."Variant Code" THEN BEGIN
                                                                  CheckBin("Location Code","From Bin Code",FALSE);
                                                                  CheckBin("Location Code","To Bin Code",TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Variantencode;
                                                              ENU=Variant Code] }
    { 5404;   ;Qty. per Unit of Measure;Decimal   ;InitValue=1;
                                                   CaptionML=[DEU=Menge pro Einheit;
                                                              ENU=Qty. per Unit of Measure];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 5407;   ;Unit of Measure Code;Code10        ;TableRelation="Item Unit of Measure".Code WHERE (Item No.=FIELD(Item No.));
                                                   OnValidate=BEGIN
                                                                IF NOT PhysInvtEntered THEN
                                                                  TESTFIELD("Phys. Inventory",FALSE);

                                                                IF "Item No." <> '' THEN BEGIN
                                                                  GetItemUnitOfMeasure;
                                                                  "Qty. per Unit of Measure" := ItemUnitOfMeasure."Qty. per Unit of Measure";
                                                                  CheckBin("Location Code","From Bin Code",FALSE);
                                                                  CheckBin("Location Code","To Bin Code",TRUE);
                                                                END ELSE
                                                                  "Qty. per Unit of Measure" := 1;
                                                                VALIDATE(Quantity);
                                                              END;

                                                   CaptionML=[DEU=Einheitencode;
                                                              ENU=Unit of Measure Code] }
    { 6500;   ;Serial No.          ;Code20        ;OnValidate=BEGIN
                                                                IF "Serial No." <> '' THEN
                                                                  ItemTrackingMgt.CheckWhseItemTrkgSetup("Item No.",SNRequired,LNRequired,TRUE);

                                                                IF (Quantity < 0) OR (Quantity > 1) THEN
                                                                  ERROR(Text006,FIELDCAPTION(Quantity));
                                                              END;

                                                   OnLookup=BEGIN
                                                              ItemTrackingMgt.LookupLotSerialNoInfo("Item No.","Variant Code",0,"Serial No.");
                                                            END;

                                                   CaptionML=[DEU=Seriennr.;
                                                              ENU=Serial No.] }
    { 6501;   ;Lot No.             ;Code20        ;OnValidate=BEGIN
                                                                IF "Lot No." <> '' THEN
                                                                  ItemTrackingMgt.CheckWhseItemTrkgSetup("Item No.",SNRequired,LNRequired,TRUE);
                                                              END;

                                                   OnLookup=BEGIN
                                                              ItemTrackingMgt.LookupLotSerialNoInfo("Item No.","Variant Code",1,"Lot No.");
                                                            END;

                                                   CaptionML=[DEU=Chargennr.;
                                                              ENU=Lot No.] }
    { 6502;   ;Warranty Date       ;Date          ;CaptionML=[DEU=Garantiedatum;
                                                              ENU=Warranty Date];
                                                   Editable=No }
    { 6503;   ;Expiration Date     ;Date          ;CaptionML=[DEU=Ablaufdatum;
                                                              ENU=Expiration Date];
                                                   Editable=No }
    { 6504;   ;New Serial No.      ;Code20        ;CaptionML=[DEU=Neue Seriennr.;
                                                              ENU=New Serial No.];
                                                   Editable=No }
    { 6505;   ;New Lot No.         ;Code20        ;CaptionML=[DEU=Neue Chargennr.;
                                                              ENU=New Lot No.];
                                                   Editable=No }
    { 6506;   ;New Expiration Date ;Date          ;CaptionML=[DEU=Neues Ablaufdatum;
                                                              ENU=New Expiration Date];
                                                   Editable=No }
    { 7380;   ;Phys Invt Counting Period Code;Code10;
                                                   TableRelation="Phys. Invt. Counting Period";
                                                   CaptionML=[DEU=InventurhÑufigkeitscode;
                                                              ENU=Phys Invt Counting Period Code];
                                                   Editable=No }
    { 7381;   ;Phys Invt Counting Period Type;Option;
                                                   CaptionML=[DEU=InventurhÑufigk.-Codeherkunft;
                                                              ENU=Phys Invt Counting Period Type];
                                                   OptionCaptionML=[DEU=" ,Artikel,Lagerhaltungsdaten";
                                                                    ENU=" ,Item,SKU"];
                                                   OptionString=[ ,Item,SKU];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Journal Template Name,Journal Batch Name,Location Code,Line No.;
                                                   Clustered=Yes }
    {    ;Item No.,Location Code,Entry Type,From Bin Type Code,Variant Code,Unit of Measure Code;
                                                   SumIndexFields="Qty. (Absolute, Base)";
                                                   MaintainSIFTIndex=No }
    {    ;Item No.,From Bin Code,Location Code,Entry Type,Variant Code,Unit of Measure Code,Lot No.,Serial No.;
                                                   SumIndexFields="Qty. (Absolute, Base)",Qty. (Absolute),Cubage,Weight;
                                                   MaintainSIFTIndex=No }
    {    ;Item No.,To Bin Code,Location Code,Variant Code,Unit of Measure Code,Lot No.,Serial No.;
                                                   SumIndexFields="Qty. (Absolute, Base)",Qty. (Absolute);
                                                   MaintainSIFTIndex=No }
    {    ;To Bin Code,Location Code               ;SumIndexFields=Cubage,Weight,Qty. (Absolute);
                                                   MaintainSIFTIndex=No }
    {    ;Location Code,Item No.,Variant Code      }
    {    ;Location Code,Bin Code,Item No.,Variant Code }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Location@1001 : Record 14;
      Item@1000 : Record 27;
      Bin@1015 : Record 7354;
      WhseJnlTemplate@1004 : Record 7309;
      WhseJnlBatch@1003 : Record 7310;
      WhseJnlLine@1005 : Record 7311;
      ItemUnitOfMeasure@1002 : Record 5404;
      NoSeriesMgt@1006 : Codeunit 396;
      WMSMgt@1007 : Codeunit 7302;
      ItemTrackingMgt@1016 : Codeunit 6500;
      OldItemNo@1055 : Code[20];
      PhysInvtEntered@1008 : Boolean;
      Text000@1009 : TextConst 'DEU=darf nicht negativ sein;ENU=must not be negative';
      Text001@1014 : TextConst 'DEU=%1 Buch.-Blatt;ENU=%1 Journal';
      Text002@1013 : TextConst 'DEU=STANDARD;ENU=DEFAULT';
      Text003@1012 : TextConst 'DEU=Standard Buch.-Blatt;ENU=Default Journal';
      Text004@1010 : TextConst 'DEU=Sie mÅssen den Benutzer %1 erst als Lagermitarbeiter einrichten.;ENU=You must first set up user %1 as a warehouse employee.';
      Text005@1011 : TextConst 'DEU=Der Lagerort %1 in Logistik Buch.-Blattname %2 ist fÅr den Benutzer %3 nicht aktiviert.;ENU=The location %1 of warehouse journal batch %2 is not enabled for user %3.';
      Text006@1017 : TextConst 'DEU=FÅr die Artikelverfolgung anhand der Seriennr. muss %1 0 oder 1 sein.;ENU=%1 must be 0 or 1 for an Item tracked by Serial Number.';
      SNRequired@1019 : Boolean;
      LNRequired@1018 : Boolean;
      OpenFromBatch@1020 : Boolean;

    PROCEDURE GetItem@18(ItemNo@1000 : Code[20];VAR ItemDescription@1001 : Text[50]);
    BEGIN
      IF ItemNo <> OldItemNo THEN BEGIN
        ItemDescription := '';
        IF ItemNo <> '' THEN
          IF Item.GET(ItemNo) THEN
            ItemDescription := Item.Description;
        OldItemNo := ItemNo;
      END ELSE
        ItemDescription := Item.Description;
    END;

    PROCEDURE SetUpNewLine@8(LastWhseJnlLine@1000 : Record 7311);
    VAR
      Location@1001 : Record 14;
    BEGIN
      WhseJnlTemplate.GET("Journal Template Name");
      WhseJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
      WhseJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
      WhseJnlLine.SETRANGE("Location Code","Location Code");
      IF WhseJnlLine.FIND('-') THEN BEGIN
        WhseJnlBatch.GET(
          "Journal Template Name","Journal Batch Name",LastWhseJnlLine."Location Code");
        "Registering Date" := LastWhseJnlLine."Registering Date";
        "Whse. Document No." := LastWhseJnlLine."Whse. Document No.";
        "Entry Type" :=  LastWhseJnlLine."Entry Type";
        "Location Code" := LastWhseJnlLine."Location Code";
      END ELSE BEGIN
        "Registering Date" := WORKDATE;
        WhseJnlBatch.GET("Journal Template Name","Journal Batch Name","Location Code");
        IF WhseJnlBatch."No. Series" <> '' THEN BEGIN
          CLEAR(NoSeriesMgt);
          "Whse. Document No." :=
            NoSeriesMgt.TryGetNextNo(WhseJnlBatch."No. Series","Registering Date");
        END;
      END;
      IF WhseJnlTemplate.Type = WhseJnlTemplate.Type::"Physical Inventory" THEN BEGIN
        "Source Document" := "Source Document"::"Phys. Invt. Jnl.";
        "Whse. Document Type" := "Whse. Document Type"::"Whse. Phys. Inventory";
      END;
      "Source Code" := WhseJnlTemplate."Source Code";
      "Reason Code" := WhseJnlBatch."Reason Code";
      "Registering No. Series" := WhseJnlBatch."Registering No. Series";
      Location.GET("Location Code");
      IF WhseJnlTemplate.Type <> WhseJnlTemplate.Type::Reclassification THEN BEGIN
        IF Quantity >= 0 THEN
          "Entry Type" := "Entry Type"::"Positive Adjmt."
        ELSE
          "Entry Type" := "Entry Type"::"Negative Adjmt.";
        GetBin(Location.Code,Location."Adjustment Bin Code");
        "From Zone Code" := Bin."Zone Code";
        "From Bin Code" := Bin.Code;
        "From Bin Type Code" := Bin."Bin Type Code";
      END ELSE
        "Entry Type" := "Entry Type"::Movement;
    END;

    LOCAL PROCEDURE CalcBaseQty@14(Qty@1000 : Decimal) : Decimal;
    BEGIN
      TESTFIELD("Qty. per Unit of Measure");
      EXIT(ROUND(Qty * "Qty. per Unit of Measure",0.00001));
    END;

    PROCEDURE CalcQty@3(QtyBase@1000 : Decimal) : Decimal;
    BEGIN
      TESTFIELD("Qty. per Unit of Measure");
      EXIT(ROUND(QtyBase / "Qty. per Unit of Measure",0.00001));
    END;

    LOCAL PROCEDURE GetItemUnitOfMeasure@1();
    BEGIN
      GetItem("Item No.",Description);
      IF (Item."No." <> ItemUnitOfMeasure."Item No.") OR
         ("Unit of Measure Code" <> ItemUnitOfMeasure.Code)
      THEN
        IF NOT ItemUnitOfMeasure.GET(Item."No.","Unit of Measure Code") THEN
          ItemUnitOfMeasure.GET(Item."No.",Item."Base Unit of Measure");
    END;

    PROCEDURE EmptyLine@5() : Boolean;
    BEGIN
      EXIT(
        ("Item No." = '') AND (Quantity = 0));
    END;

    LOCAL PROCEDURE ExchangeFromToBin@16();
    VAR
      WhseJnlLine@1001 : Record 7311;
    BEGIN
      GetLocation("Location Code");
      WhseJnlLine := Rec;
      "From Zone Code" := WhseJnlLine."To Zone Code";
      "From Bin Code" := WhseJnlLine."To Bin Code";
      "From Bin Type Code" :=
        GetBinType("Location Code","From Bin Code");
      IF ("Location Code" = Location.Code) AND
         ("From Bin Code" = Location."Adjustment Bin Code")
      THEN
        WMSMgt.CheckAdjmtBin(Location,"Qty. (Absolute)",Quantity > 0);

      "To Zone Code" := WhseJnlLine."From Zone Code";
      "To Bin Code" := WhseJnlLine."From Bin Code";
      IF ("Location Code" = Location.Code) AND
         ("To Bin Code" = Location."Adjustment Bin Code")
      THEN
        WMSMgt.CheckAdjmtBin(Location,"Qty. (Absolute)",Quantity > 0);

      IF WhseJnlTemplate.Type <> WhseJnlTemplate.Type::Reclassification THEN BEGIN
        IF Quantity >= 0 THEN
          "Entry Type" := "Entry Type"::"Positive Adjmt."
        ELSE
          "Entry Type" := "Entry Type"::"Negative Adjmt.";
      END;
    END;

    LOCAL PROCEDURE GetLocation@2(LocationCode@1000 : Code[10]);
    BEGIN
      IF Location.Code <> LocationCode THEN
        Location.GET(LocationCode);
      Location.TESTFIELD(Location."Directed Put-away and Pick");
    END;

    LOCAL PROCEDURE GetBin@19(LocationCode@1000 : Code[10];BinCode@1002 : Code[20]);
    BEGIN
      IF (LocationCode = '') OR (BinCode = '') THEN
        Bin.INIT
      ELSE
        IF (Bin."Location Code" <> LocationCode) OR
           (Bin.Code <> BinCode)
        THEN
          Bin.GET(LocationCode,BinCode);
    END;

    LOCAL PROCEDURE CheckBin@4(LocationCode@1000 : Code[10];BinCode@1001 : Code[20];Inbound@1002 : Boolean);
    VAR
      BinContent@1003 : Record 7302;
      WhseJnlLine@1007 : Record 7311;
    BEGIN
      IF (BinCode <> '') AND ("Item No." <> '') THEN BEGIN
        GetLocation(LocationCode);
        IF BinCode = Location."Adjustment Bin Code" THEN
          EXIT;
        IF Inbound THEN BEGIN
          GetBinType(LocationCode,BinCode);
          IF Location."Bin Capacity Policy" IN
             [Location."Bin Capacity Policy"::"Allow More Than Max. Capacity",
             Location."Bin Capacity Policy"::"Prohibit More Than Max. Cap."]
          THEN BEGIN
            WhseJnlLine.SETCURRENTKEY("To Bin Code","Location Code");
            WhseJnlLine.SETRANGE("To Bin Code",BinCode);
            WhseJnlLine.SETRANGE("Location Code",LocationCode);
            WhseJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
            WhseJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
            WhseJnlLine.SETRANGE("Line No.","Line No.");
            WhseJnlLine.CALCSUMS("Qty. (Absolute)",Cubage,Weight);
          END;
          IF BinContent.GET(
            "Location Code",BinCode,"Item No.","Variant Code","Unit of Measure Code")
          THEN
            BinContent.CheckIncreaseBinContent(
              "Qty. (Absolute, Base)",WhseJnlLine."Qty. (Absolute, Base)",
              WhseJnlLine.Cubage,WhseJnlLine.Weight,Cubage,Weight,FALSE)
          ELSE BEGIN
            GetBin(LocationCode,BinCode);
            Bin.CheckIncreaseBin(
              BinCode,"Item No.","Qty. (Absolute)",
              WhseJnlLine.Cubage,WhseJnlLine.Weight,Cubage,Weight,FALSE);
          END;
        END ELSE BEGIN
          BinContent.GET(
            "Location Code",BinCode,"Item No.","Variant Code","Unit of Measure Code");
          IF BinContent."Block Movement" IN [
             BinContent."Block Movement"::Outbound,BinContent."Block Movement"::All]
          THEN
            BinContent.FIELDERROR("Block Movement");
        END;
      END;
    END;

    LOCAL PROCEDURE GetBinType@20(LocationCode@1002 : Code[10];BinCode@1001 : Code[20]) : Code[10];
    VAR
      BinType@1000 : Record 7303;
    BEGIN
      GetBin(LocationCode,BinCode);
      WhseJnlTemplate.GET("Journal Template Name");
      IF WhseJnlTemplate.Type = WhseJnlTemplate.Type::Reclassification THEN
        IF Bin."Bin Type Code" <> '' THEN
          IF BinType.GET(Bin."Bin Type Code") THEN
            BinType.TESTFIELD(Receive,FALSE);

      EXIT(Bin."Bin Type Code");
    END;

    PROCEDURE TemplateSelection@13(FormID@1004 : Integer;FormTemplate@1000 : 'Adjustment,Phys. Inventory,Reclassification';VAR WhseJnlLine@1003 : Record 7311;VAR JnlSelected@1001 : Boolean);
    VAR
      WhseJnlTemplate@1002 : Record 7309;
    BEGIN
      JnlSelected := TRUE;

      WhseJnlTemplate.RESET;
      IF NOT OpenFromBatch THEN
        WhseJnlTemplate.SETRANGE("Form ID",FormID);
      WhseJnlTemplate.SETRANGE(Type,FormTemplate);

      CASE WhseJnlTemplate.COUNT OF
        0:
          BEGIN
            WhseJnlTemplate.INIT;
            WhseJnlTemplate.VALIDATE(Type,FormTemplate);
            WhseJnlTemplate.VALIDATE("Form ID");
            WhseJnlTemplate.Name := FORMAT(WhseJnlTemplate.Type,MAXSTRLEN(WhseJnlTemplate.Name));
            WhseJnlTemplate.Description := STRSUBSTNO(Text001,WhseJnlTemplate.Type);
            WhseJnlTemplate.INSERT;
            COMMIT;
          END;
        1:
          WhseJnlTemplate.FIND('-');
        ELSE
          JnlSelected := FORM.RUNMODAL(0,WhseJnlTemplate) = ACTION::LookupOK;
      END;
      IF JnlSelected THEN BEGIN
        WhseJnlLine.FILTERGROUP := 2;
        WhseJnlLine.SETRANGE("Journal Template Name",WhseJnlTemplate.Name);
        WhseJnlLine.FILTERGROUP := 0;
        IF OpenFromBatch THEN BEGIN
          WhseJnlLine."Journal Template Name" := '';
          FORM.RUN(WhseJnlTemplate."Form ID",WhseJnlLine);
        END;
      END;
    END;

    PROCEDURE TemplateSelectionFromBatch@21(VAR WhseJnlBatch@1000 : Record 7310);
    VAR
      WhseJnlLine@1002 : Record 7311;
      JnlSelected@1001 : Boolean;
    BEGIN
      OpenFromBatch := TRUE;
      WhseJnlBatch.CALCFIELDS("Template Type");
      WhseJnlLine."Journal Batch Name" := WhseJnlBatch.Name;
      TemplateSelection(0,WhseJnlBatch."Template Type",WhseJnlLine,JnlSelected);
    END;

    PROCEDURE OpenJnl@12(VAR CurrentJnlBatchName@1000 : Code[10];VAR CurrentLocationCode@1002 : Code[10];VAR WhseJnlLine@1001 : Record 7311);
    BEGIN
      CheckWhseEmployee;
      CheckTemplateName(
        WhseJnlLine.GETRANGEMAX("Journal Template Name"),CurrentLocationCode,CurrentJnlBatchName);
      WhseJnlLine.FILTERGROUP := 2;
      WhseJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      IF CurrentLocationCode <> '' THEN
        WhseJnlLine.SETRANGE("Location Code",CurrentLocationCode);
      WhseJnlLine.FILTERGROUP := 0;
    END;

    PROCEDURE CheckTemplateName@11(CurrentJnlTemplateName@1000 : Code[10];VAR CurrentLocationCode@1005 : Code[10];VAR CurrentJnlBatchName@1001 : Code[10]);
    VAR
      WhseJnlBatch@1002 : Record 7310;
      WhseEmployee@1004 : Record 7301;
      WmsMgt@1003 : Codeunit 7302;
    BEGIN
      WhseJnlBatch.SETRANGE("Journal Template Name",CurrentJnlTemplateName);
      IF NOT WhseJnlBatch.GET(CurrentJnlTemplateName,CurrentJnlBatchName,CurrentLocationCode) OR
         NOT WhseEmployee.GET(USERID,CurrentLocationCode)
      THEN BEGIN
        IF USERID <> '' THEN BEGIN
          CurrentLocationCode := WmsMgt.GetDefaultLocation;
          WhseJnlBatch.SETRANGE("Location Code",CurrentLocationCode);
        END;
        IF NOT WhseJnlBatch.FIND('-') THEN BEGIN
          WhseJnlBatch.INIT;
          WhseJnlBatch."Journal Template Name" := CurrentJnlTemplateName;
          WhseJnlBatch.SetupNewBatch;
          WhseJnlBatch.Name := Text002;
          WhseJnlBatch.Description := Text003;
          WhseJnlBatch.INSERT(TRUE);
          CurrentLocationCode := WhseJnlBatch."Location Code";
          COMMIT;
        END;
        CurrentJnlBatchName := WhseJnlBatch.Name;
        CurrentLocationCode := WhseJnlBatch."Location Code";
      END;
    END;

    PROCEDURE CheckName@10(CurrentJnlBatchName@1000 : Code[10];CurrentLocationCode@1003 : Code[10];VAR WhseJnlLine@1001 : Record 7311);
    VAR
      WhseJnlBatch@1002 : Record 7310;
      WhseEmployee@1004 : Record 7301;
    BEGIN
      WhseJnlBatch.GET(
        WhseJnlLine.GETRANGEMAX("Journal Template Name"),CurrentJnlBatchName,CurrentLocationCode);
      IF (USERID <> '') AND NOT WhseEmployee.GET(USERID,CurrentLocationCode) THEN
        ERROR(Text005,CurrentLocationCode,CurrentJnlBatchName,USERID);
    END;

    LOCAL PROCEDURE CheckWhseEmployee@15();
    VAR
      WhseEmployee@1000 : Record 7301;
    BEGIN
      IF USERID <> '' THEN BEGIN
        WhseEmployee.SETRANGE("User ID",USERID);
        IF NOT WhseEmployee.FIND('-') THEN
          ERROR(Text004,USERID);
      END;
    END;

    PROCEDURE SetName@9(CurrentJnlBatchName@1000 : Code[10];CurrentLocationCode@1002 : Code[10];VAR WhseJnlLine@1001 : Record 7311);
    BEGIN
      WhseJnlLine.FILTERGROUP := 2;
      WhseJnlLine.SETRANGE("Journal Batch Name",CurrentJnlBatchName);
      WhseJnlLine.SETRANGE("Location Code",CurrentLocationCode);
      WhseJnlLine.FILTERGROUP := 0;
      IF WhseJnlLine.FIND('-') THEN;
    END;

    PROCEDURE LookupName@7(VAR CurrentJnlBatchName@1000 : Code[10];VAR CurrentLocationCode@1003 : Code[10];VAR WhseJnlLine@1001 : Record 7311) : Boolean;
    VAR
      WhseJnlBatch@1002 : Record 7310;
    BEGIN
      COMMIT;
      WhseJnlBatch."Journal Template Name" := WhseJnlLine.GETRANGEMAX("Journal Template Name");
      WhseJnlBatch.Name := WhseJnlLine.GETRANGEMAX("Journal Batch Name");
      WhseJnlBatch.SETRANGE("Journal Template Name",WhseJnlBatch."Journal Template Name");
      IF FORM.RUNMODAL(FORM::"Whse. Journal Batches List",WhseJnlBatch) = ACTION::LookupOK THEN BEGIN
        CurrentJnlBatchName := WhseJnlBatch.Name;
        CurrentLocationCode := WhseJnlBatch."Location Code";
        SetName(CurrentJnlBatchName,CurrentLocationCode,WhseJnlLine);
      END;
    END;

    PROCEDURE OpenItemTrackingLines@6500();
    VAR
      TempWhseWkshLine@1000 : TEMPORARY Record 7326;
      WhseItemTrackingForm@1001 : Form 6550;
    BEGIN
      TESTFIELD("Item No.");
      TESTFIELD("Qty. (Base)");
      TempWhseWkshLine.INIT;
      TempWhseWkshLine."Worksheet Template Name" := "Journal Template Name";
      TempWhseWkshLine.Name := "Journal Batch Name";
      TempWhseWkshLine."Location Code" := "Location Code";
      TempWhseWkshLine."Line No." := "Line No.";
      TempWhseWkshLine."Item No." :="Item No.";
      TempWhseWkshLine."Variant Code" := "Variant Code";
      TempWhseWkshLine."Qty. (Base)" := "Qty. (Base)";
      TempWhseWkshLine."Qty. to Handle (Base)" := "Qty. (Base)";
      WhseItemTrackingForm.SetSource(TempWhseWkshLine,DATABASE::"Warehouse Journal Line");
      WhseItemTrackingForm.RUNMODAL;
    END;

    PROCEDURE ItemTrackingExist@17(TemplateName@1000 : Code[10];BatchName@1001 : Code[10];LocationCode@1004 : Code[10];LineNo@1002 : Integer;VAR WhseItemTrkgLine@1005 : Record 6550) : Boolean;
    BEGIN
      WITH WhseItemTrkgLine DO BEGIN
        RESET;
        SETCURRENTKEY(
          "Source ID","Source Type","Source Subtype","Source Batch Name",
          "Source Prod. Order Line","Source Ref. No.","Location Code");
        SETRANGE("Source Type",DATABASE::"Warehouse Journal Line");
        SETRANGE("Source Subtype",0);
        SETRANGE("Source Batch Name",TemplateName);
        SETRANGE("Source ID",BatchName);
        SETRANGE("Location Code",LocationCode);
        IF LineNo <> 0 THEN
          SETRANGE("Source Ref. No.",LineNo);
        SETRANGE("Source Prod. Order Line",0);

        EXIT(NOT ISEMPTY);
      END;
    END;

    PROCEDURE ItemTrackingReclass@22(TemplateName@1000 : Code[10];BatchName@1001 : Code[10];LocationCode@1004 : Code[10];LineNo@1002 : Integer) : Boolean;
    VAR
      WhseItemTrkgLine@1003 : Record 6550;
    BEGIN
      IF NOT IsReclass(TemplateName) THEN
        EXIT(FALSE);

      WITH WhseItemTrkgLine DO BEGIN
        IF ItemTrackingExist(TemplateName,BatchName,LocationCode,LineNo,WhseItemTrkgLine) THEN BEGIN
          FINDSET;
          REPEAT
            IF ("Lot No." <> "New Lot No.") OR
               ("Serial No." <> "New Serial No.") OR
               ("Expiration Date" <> "New Expiration Date")
            THEN
              EXIT(TRUE);
          UNTIL NEXT = 0;
        END;
      END;

      EXIT(FALSE);
    END;

    PROCEDURE IsReclass@6(CurrentJnlTemplateName@1000 : Code[10]) : Boolean;
    VAR
      WhseJnlTemplate@1001 : Record 7309;
    BEGIN
      IF WhseJnlTemplate.GET(CurrentJnlTemplateName) THEN
        EXIT(WhseJnlTemplate.Type = WhseJnlTemplate.Type::Reclassification);

      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}

