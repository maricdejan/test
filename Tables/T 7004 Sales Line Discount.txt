OBJECT Table 7004 Sales Line Discount
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Sales Type" = "Sales Type"::"All Customers" THEN
                 "Sales Code" := ''
               ELSE
                 TESTFIELD("Sales Code");
               TESTFIELD(Code);
             END;

    OnRename=BEGIN
               IF "Sales Type" <> "Sales Type"::"All Customers" THEN
                 TESTFIELD("Sales Code");
               TESTFIELD(Code);
             END;

    CaptionML=[DEU=Verkaufszeilenrabatt;
               ENU=Sales Line Discount];
    LookupFormID=Form7004;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code20        ;TableRelation=IF (Type=CONST(Item)) Item
                                                                 ELSE IF (Type=CONST(Item Disc. Group)) "Item Discount Group";
                                                   OnValidate=BEGIN
                                                                IF xRec.Code <> Code THEN BEGIN
                                                                  "Variant Code" := '';
                                                                  "Unit of Measure Code" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Code;
                                                              ENU=Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Sales Code          ;Code20        ;TableRelation=IF (Sales Type=CONST(Customer Disc. Group)) "Customer Discount Group"
                                                                 ELSE IF (Sales Type=CONST(Customer)) Customer
                                                                 ELSE IF (Sales Type=CONST(Campaign)) Campaign;
                                                   OnValidate=BEGIN
                                                                IF "Sales Code" <> '' THEN BEGIN
                                                                  CASE "Sales Type" OF
                                                                    "Sales Type"::"All Customers":
                                                                      ERROR(Text001,FIELDCAPTION("Sales Code"));
                                                                    "Sales Type"::Campaign:
                                                                      BEGIN
                                                                        Campaign.GET("Sales Code");
                                                                        "Starting Date" := Campaign."Starting Date";
                                                                        "Ending Date" := Campaign."Ending Date";
                                                                      END;
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Verkaufscode;
                                                              ENU=Sales Code] }
    { 3   ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[DEU=W„hrungscode;
                                                              ENU=Currency Code] }
    { 4   ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                IF ("Starting Date" > "Ending Date") AND ("Ending Date" <> 0D) THEN
                                                                  ERROR(Text000,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"));

                                                                IF CurrFieldNo = 0 THEN
                                                                  EXIT ELSE
                                                                    IF "Sales Type" = "Sales Type"::Campaign THEN
                                                                      ERROR(Text003,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"),FIELDCAPTION("Sales Type"),("Sales Type"));
                                                              END;

                                                   CaptionML=[DEU=Startdatum;
                                                              ENU=Starting Date] }
    { 5   ;   ;Line Discount %     ;Decimal       ;CaptionML=[DEU=Zeilenrabatt %;
                                                              ENU=Line Discount %];
                                                   MinValue=0;
                                                   MaxValue=100;
                                                   AutoFormatType=2 }
    { 13  ;   ;Sales Type          ;Option        ;OnValidate=BEGIN
                                                                IF "Sales Type" <> xRec."Sales Type" THEN
                                                                  VALIDATE("Sales Code",'');
                                                              END;

                                                   CaptionML=[DEU=Verkaufsart;
                                                              ENU=Sales Type];
                                                   OptionCaptionML=[DEU=Debitor,Debitorenrabattgruppe,Alle Debitoren,Kampagne;
                                                                    ENU=Customer,Customer Disc. Group,All Customers,Campaign];
                                                   OptionString=Customer,Customer Disc. Group,All Customers,Campaign }
    { 14  ;   ;Minimum Quantity    ;Decimal       ;CaptionML=[DEU=Mindestmenge;
                                                              ENU=Minimum Quantity];
                                                   MinValue=0 }
    { 15  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                VALIDATE("Starting Date");

                                                                IF CurrFieldNo = 0 THEN
                                                                  EXIT ELSE
                                                                    IF "Sales Type" = "Sales Type"::Campaign THEN
                                                                      ERROR(Text003,FIELDCAPTION("Starting Date"),FIELDCAPTION("Ending Date"),FIELDCAPTION("Sales Type"),("Sales Type"));
                                                              END;

                                                   CaptionML=[DEU=Enddatum;
                                                              ENU=Ending Date] }
    { 21  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF xRec.Type <> Type THEN
                                                                  VALIDATE(Code,'');
                                                              END;

                                                   CaptionML=[DEU=Art;
                                                              ENU=Type];
                                                   OptionCaptionML=[DEU=Artikel,Artikelrabattgruppe;
                                                                    ENU=Item,Item Disc. Group];
                                                   OptionString=Item,Item Disc. Group }
    { 5400;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Unit of Measure".Code WHERE (Item No.=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type,Type::Item);
                                                              END;

                                                   CaptionML=[DEU=Einheitencode;
                                                              ENU=Unit of Measure Code] }
    { 5700;   ;Variant Code        ;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Variant".Code WHERE (Item No.=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type,Type::Item);
                                                              END;

                                                   CaptionML=[DEU=Variantencode;
                                                              ENU=Variant Code] }
  }
  KEYS
  {
    {    ;Type,Code,Sales Type,Sales Code,Starting Date,Currency Code,Variant Code,Unit of Measure Code,Minimum Quantity;
                                                   Clustered=Yes }
    {    ;Sales Type,Sales Code,Type,Code,Starting Date,Currency Code,Variant Code,Unit of Measure Code,Minimum Quantity }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      CustPriceGr@1000 : Record 6;
      Text000@1001 : TextConst 'DEU=Das %1 kann nicht nach dem %2 sein.;ENU=%1 cannot be after %2';
      Cust@1002 : Record 18;
      Text001@1003 : TextConst 'DEU=%1 muss leer sein.;ENU=%1 must be blank.';
      Campaign@1004 : Record 5071;
      Text003@1006 : TextConst 'DEU="Sie k”nnen das %1 und %2 nur in der Kampagnenkarte „ndern, wenn die %3 = %4 ist.";ENU="You can only change the %1 and %2 from the Campaign Card when %3 = %4"';

    BEGIN
    END.
  }
}

