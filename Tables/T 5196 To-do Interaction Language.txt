OBJECT Table 5196 To-do Interaction Language
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               RemoveAttachment(FALSE);
             END;

    CaptionML=[DEU=Aufgabensprache;
               ENU=To-do Interaction Language];
    LookupFormID=Form5196;
  }
  FIELDS
  {
    { 1   ;   ;To-do No.           ;Code20        ;TableRelation=To-do;
                                                   CaptionML=[DEU=Aufgabennr.;
                                                              ENU=To-do No.] }
    { 2   ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=[DEU=Sprachcode;
                                                              ENU=Language Code] }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description] }
    { 4   ;   ;Attachment No.      ;Integer       ;TableRelation=Attachment;
                                                   CaptionML=[DEU=Dateianhangsnr.;
                                                              ENU=Attachment No.] }
  }
  KEYS
  {
    {    ;To-do No.,Language Code                 ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1002 : TextConst 'DEU=Sie haben den Erstellungsprozess abgebrochen.;ENU=You have canceled the create process.';
      Text001@1001 : TextConst 'DEU=M”chten Sie den vorhandenen Dateianhang ersetzen?;ENU=Replace existing attachment?';
      Text002@1000 : TextConst 'DEU=Sie haben den Importvorgang abgebrochen.;ENU=You have canceled the import process.';
      Text003@1003 : TextConst 'DEU=Sie k”nnen hier keine Dateianh„nge erstellen.;ENU=You cannot create attachments here.';

    PROCEDURE CreateAttachment@1(FormNotEditable@1004 : Boolean) : Boolean;
    VAR
      Attachment@1000 : Record 5062;
      AttachmentManagement@1001 : Codeunit 5052;
      WordManagement@1002 : Codeunit 5054;
      NewAttachNo@1003 : Integer;
    BEGIN
      IF FormNotEditable THEN
        ERROR(Text003);
      IF "Attachment No." <> 0 THEN BEGIN
        IF Attachment.GET("Attachment No.") THEN
          Attachment.TESTFIELD("Read Only",FALSE);
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
      END;

      IF AttachmentManagement.UseComServer('DOC',TRUE) THEN;

      NewAttachNo := WordManagement.CreateWordAttachment("To-do No." + ' ' + Description);
      IF NewAttachNo <> 0 THEN BEGIN
        IF "Attachment No." <> 0  THEN
          RemoveAttachment(FALSE);
        "Attachment No." := NewAttachNo;
        MODIFY;
        EXIT(TRUE);
      END ELSE
        ERROR(Text000);
    END;

    PROCEDURE OpenAttachment@2(FormNotEditable@1001 : Boolean);
    VAR
      Attachment@1000 : Record 5062;
    BEGIN
      IF "Attachment No." = 0 THEN
        EXIT;
      Attachment.GET("Attachment No.");
      IF FormNotEditable THEN
        Attachment."Read Only" := TRUE;
      Attachment.OpenAttachment("To-do No." + ' ' + Description,FALSE);
    END;

    PROCEDURE CopyFromAttachment@3();
    VAR
      TodoTmplLanguage@1000 : Record 5196;
      Attachment@1001 : Record 5062;
      AttachmentManagement@1002 : Codeunit 5052;
      NewAttachNo@1003 : Integer;
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        Attachment.TESTFIELD("Read Only",FALSE);

      IF "Attachment No." <> 0 THEN BEGIN
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
        RemoveAttachment(FALSE);
        "Attachment No." := 0;
        MODIFY;
        COMMIT;
      END;

      TodoTmplLanguage.SETFILTER("Attachment No.",'<>%1',0);
      IF FORM.RUNMODAL(0,TodoTmplLanguage) = ACTION::LookupOK THEN BEGIN
        NewAttachNo := AttachmentManagement.InsertAttachment(TodoTmplLanguage."Attachment No.");
        IF NewAttachNo <> 0 THEN BEGIN
          "Attachment No." := NewAttachNo;
          MODIFY;
        END;
      END;
    END;

    PROCEDURE ImportAttachment@5();
    VAR
      Attachment@1000 : Record 5062;
      TempAttachment@1001 : TEMPORARY Record 5062;
      AttachmentManagement@1002 : Codeunit 5052;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF Attachment.GET("Attachment No.") THEN
          Attachment.TESTFIELD("Read Only",FALSE);
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
      END;

      CLEAR(TempAttachment);
      IF TempAttachment.ImportAttachment('',TRUE,FALSE) THEN BEGIN
        IF "Attachment No." = 0 THEN
          Attachment.GET(AttachmentManagement.InsertAttachment(0))
        ELSE
          Attachment.GET("Attachment No.");
        TempAttachment."No." := Attachment."No.";
        TempAttachment."Storage Pointer" :=  Attachment."Storage Pointer";
        TempAttachment.WizSaveAttachment;
        Attachment."Storage Type" := TempAttachment."Storage Type";
        Attachment."Storage Pointer" := TempAttachment."Storage Pointer";
        Attachment.Attachment := TempAttachment.Attachment;
        Attachment."File Extension" := TempAttachment."File Extension";
        Attachment.MODIFY;
        "Attachment No." := Attachment."No.";
        MODIFY;
      END ELSE
        ERROR(Text002);
    END;

    PROCEDURE ExportAttachment@6();
    VAR
      Attachment@1000 : Record 5062;
      FileName@1001 : Text[1024];
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        Attachment.ExportAttachment(FileName);
    END;

    PROCEDURE RemoveAttachment@7(Prompt@1000 : Boolean) : Boolean;
    VAR
      Attachment@1001 : Record 5062;
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        IF Attachment.RemoveAttachment(Prompt) THEN BEGIN
          "Attachment No." := 0;
          MODIFY;
          EXIT(TRUE);
        END;
    END;

    BEGIN
    END.
  }
}

