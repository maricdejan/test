OBJECT Table 368 Dimension Selection Buffer
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00,LIQEM6.00,NAVDACH5.00.01;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Dimensionsauswahlpuffer;
               ENU=Dimension Selection Buffer];
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Text30        ;CaptionML=[DEU=Code;
                                                              ENU=Code] }
    { 2   ;   ;Description         ;Text30        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description] }
    { 3   ;   ;Selected            ;Boolean       ;OnValidate=BEGIN
                                                                "New Dimension Value Code" := '';
                                                                "Dimension Value Filter" := '';
                                                                Level := Level::" ";
                                                              END;

                                                   CaptionML=[DEU=AusgewÑhlt;
                                                              ENU=Selected] }
    { 4   ;   ;New Dimension Value Code;Code20    ;TableRelation=IF (Code=CONST(G/L Account)) "G/L Account".No.
                                                                 ELSE IF (Code=CONST(Business Unit)) "Business Unit".Code
                                                                 ELSE "Dimension Value".Code WHERE (Dimension Code=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                Selected := TRUE;
                                                              END;

                                                   CaptionML=[DEU=Neuer Dimensionswertcode;
                                                              ENU=New Dimension Value Code] }
    { 5   ;   ;Dimension Value Filter;Code250     ;TableRelation=IF (Filter Lookup Table No.=CONST(15)) "G/L Account".No.
                                                                 ELSE IF (Filter Lookup Table No.=CONST(220)) "Business Unit".Code
                                                                 ELSE "Dimension Value".Code WHERE (Dimension Code=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                IF (Level = Level::" ") AND ("Dimension Value Filter" = '') THEN
                                                                  Selected := FALSE
                                                                ELSE
                                                                  Selected := TRUE;
                                                              END;

                                                   ValidateTableRelation=No;
                                                   CaptionML=[DEU=Dimensionswertfilter;
                                                              ENU=Dimension Value Filter] }
    { 6   ;   ;Level               ;Option        ;OnValidate=BEGIN
                                                                IF (Level = Level::" ") AND ("Dimension Value Filter" = '') THEN
                                                                  Selected := FALSE
                                                                ELSE
                                                                  Selected := TRUE;
                                                              END;

                                                   CaptionML=[DEU=Ebene;
                                                              ENU=Level];
                                                   OptionCaptionML=[DEU=" ,Ebene 1,Ebene 2,Ebene 3,Ebene 4";
                                                                    ENU=" ,Level 1,Level 2,Level 3,Level 4"];
                                                   OptionString=[ ,Level 1,Level 2,Level 3,Level 4] }
    { 7   ;   ;Filter Lookup Table No.;Integer    ;InitValue=349;
                                                   CaptionML=[DEU=Filter-Lookuptabellennr.;
                                                              ENU=Filter Lookup Table No.];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Level,Code                               }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Ein anderer Benutzer hat die ausgewÑhlte Dimension fÅr das Feld %1 verÑndert, nachdem Sie sie von der Datenbank abgerufen haben.\;ENU=Another user has modified the selected dimensions for the %1 field after you retrieved it from the database.\';
      Text002@1002 : TextConst 'DEU="Geben Sie Ihre énderungen erneut in das Fenster Dimensionsauswahl ein, indem Sie auf die AssistButton im Feld %1 klicken. ";ENU="Enter your changes again in the Dimension Selection window by clicking the AssistButton on the %1 field. "';
      SelectedDim@1004 : Record 369;

    PROCEDURE SetDimSelectionMultiple@4(ObjectType@1000 : Integer;ObjectID@1001 : Integer;VAR SelectedDimText@1002 : Text[250]);
    VAR
      Dim@1003 : Record 348;
      TempDimSelectionBuf@1004 : TEMPORARY Record 368;
      DimSelectionMultiple@1005 : Form 562;
    BEGIN
      CLEAR(DimSelectionMultiple);
      IF Dim.FIND('-') THEN
        REPEAT
          DimSelectionMultiple.InsertDimSelBuf(
            SelectedDim.GET(USERID,ObjectType,ObjectID,'',Dim.Code),
            Dim.Code,Dim.GetMLName(GLOBALLANGUAGE));
        UNTIL Dim.NEXT = 0;

      DimSelectionMultiple.LOOKUPMODE := TRUE;
      IF DimSelectionMultiple.RUNMODAL = ACTION::LookupOK THEN BEGIN
        DimSelectionMultiple.GetDimSelBuf(TempDimSelectionBuf);
        SetDimSelection(ObjectType,ObjectID,'',SelectedDimText,TempDimSelectionBuf);
      END;
    END;

    PROCEDURE SetDimSelectionChange@1(ObjectType@1000 : Integer;ObjectID@1001 : Integer;VAR SelectedDimText@1002 : Text[250]);
    VAR
      Dim@1003 : Record 348;
      TempDimSelectionBuf@1004 : TEMPORARY Record 368;
      DimSelectionChange@1005 : Form 567;
    BEGIN
      CLEAR(DimSelectionChange);
      IF Dim.FIND('-') THEN
        REPEAT
          DimSelectionChange.InsertDimSelBuf(
            SelectedDim.GET(USERID,ObjectType,ObjectID,'',Dim.Code),
            Dim.Code,Dim.GetMLName(GLOBALLANGUAGE),
            SelectedDim."New Dimension Value Code",
            SelectedDim."Dimension Value Filter");
        UNTIL Dim.NEXT = 0;

      DimSelectionChange.LOOKUPMODE := TRUE;
      IF DimSelectionChange.RUNMODAL = ACTION::LookupOK THEN BEGIN
        DimSelectionChange.GetDimSelBuf(TempDimSelectionBuf);
        SetDimSelection(ObjectType,ObjectID,'',SelectedDimText,TempDimSelectionBuf);
      END;
    END;

    PROCEDURE CompareDimText@2(ObjectType@1000 : Integer;ObjectID@1001 : Integer;AnalysisViewCode@1002 : Code[10];SelectedDimText@1003 : Text[250];DimTextFieldName@1004 : Text[100]);
    VAR
      SelectedDim@1005 : Record 369;
      SelectedDimTextFromDb@1006 : Text[250];
    BEGIN
      SelectedDimTextFromDb := '';
      SelectedDim.SETCURRENTKEY(
        "User ID","Object Type","Object ID","Analysis View Code",Level,"Dimension Code");
      SelectedDim.SETRANGE("User ID",USERID);
      SelectedDim.SETRANGE("Object Type",ObjectType);
      SelectedDim.SETRANGE("Object ID",ObjectID);
      SelectedDim.SETRANGE("Analysis View Code",AnalysisViewCode);
      IF SelectedDim.FIND('-') THEN
        REPEAT
          AddDimCodeToText(SelectedDim."Dimension Code",SelectedDimTextFromDb);
        UNTIL SelectedDim.NEXT = 0;
      IF SelectedDimTextFromDb <> SelectedDimText THEN
        ERROR(
          Text000 +
          Text002,
          DimTextFieldName);
    END;

    PROCEDURE AddDimCodeToText@3(DimCode@1000 : Code[20];VAR Text@1001 : Text[250]);
    BEGIN
      IF Text = '' THEN
        Text := DimCode
      ELSE
        IF (STRLEN(Text) + STRLEN(DimCode)) <= (MAXSTRLEN(Text) - 4) THEN
          Text := STRSUBSTNO('%1; %2',Text,DimCode)
        ELSE
          Text := STRSUBSTNO('%1;...',Text)
    END;

    PROCEDURE SetDimSelection@5(ObjectType@1000 : Integer;ObjectID@1001 : Integer;AnalysisViewCode@1002 : Code[10];VAR SelectedDimText@1003 : Text[250];VAR DimSelectionBuf@1004 : Record 368);
    BEGIN
      SelectedDim.SETRANGE("User ID",USERID);
      SelectedDim.SETRANGE("Object Type",ObjectType);
      SelectedDim.SETRANGE("Object ID",ObjectID);
      SelectedDim.SETRANGE("Analysis View Code",AnalysisViewCode);
      SelectedDim.DELETEALL;
      SelectedDimText := '';
      DimSelectionBuf.SETCURRENTKEY(Level,Code);
      DimSelectionBuf.SETRANGE(Selected,TRUE);
      IF DimSelectionBuf.FIND('-') THEN
        REPEAT
          SelectedDim."User ID" := USERID;
          SelectedDim."Object Type" := ObjectType;
          SelectedDim."Object ID" := ObjectID;
          SelectedDim."Analysis View Code" := AnalysisViewCode;
          SelectedDim."Dimension Code" := DimSelectionBuf.Code;
          SelectedDim."New Dimension Value Code" := DimSelectionBuf."New Dimension Value Code";
          SelectedDim."Dimension Value Filter" := DimSelectionBuf."Dimension Value Filter";
          SelectedDim.Level := DimSelectionBuf.Level;
          SelectedDim.INSERT;
          AddDimCodeToText(SelectedDim."Dimension Code",SelectedDimText);
        UNTIL DimSelectionBuf.NEXT = 0;
    END;

    PROCEDURE SetDimSelectionLevel@40(ObjectType@1000 : Integer;ObjectID@1001 : Integer;AnalysisViewCode@1002 : Code[10];VAR SelectedDimText@1003 : Text[250]);
    VAR
      AnalysisView@1004 : Record 363;
      Dim@1005 : Record 348;
      TempDimSelectionBuf@1006 : TEMPORARY Record 368;
      GLAcc@1007 : Record 15;
      DimSelectionLevel@1008 : Form 564;
    BEGIN
      CLEAR(DimSelectionLevel);
      IF AnalysisView.GET(AnalysisViewCode) THEN BEGIN
        IF Dim.GET(AnalysisView."Dimension 1 Code") THEN BEGIN
          DimSelectionLevel.InsertDimSelBuf(
            SelectedDim.GET(USERID,ObjectType,ObjectID,AnalysisViewCode,Dim.Code),
            Dim.Code,Dim.GetMLName(GLOBALLANGUAGE),
            SelectedDim."Dimension Value Filter",SelectedDim.Level);
        END;
        IF Dim.GET(AnalysisView."Dimension 2 Code") THEN BEGIN
          DimSelectionLevel.InsertDimSelBuf(
            SelectedDim.GET(USERID,ObjectType,ObjectID,AnalysisViewCode,Dim.Code),
            Dim.Code,Dim.GetMLName(GLOBALLANGUAGE),
            SelectedDim."Dimension Value Filter",SelectedDim.Level);
        END;
        IF Dim.GET(AnalysisView."Dimension 3 Code") THEN BEGIN
          DimSelectionLevel.InsertDimSelBuf(
            SelectedDim.GET(USERID,ObjectType,ObjectID,AnalysisViewCode,Dim.Code),
            Dim.Code,Dim.GetMLName(GLOBALLANGUAGE),
            SelectedDim."Dimension Value Filter",SelectedDim.Level);
        END;
        IF Dim.GET(AnalysisView."Dimension 4 Code") THEN BEGIN
          DimSelectionLevel.InsertDimSelBuf(
            SelectedDim.GET(USERID,ObjectType,ObjectID,AnalysisViewCode,Dim.Code),
            Dim.Code,Dim.GetMLName(GLOBALLANGUAGE),
            SelectedDim."Dimension Value Filter",SelectedDim.Level);
        END;
        DimSelectionLevel.InsertDimSelBuf(
          SelectedDim.GET(USERID,ObjectType,ObjectID,AnalysisViewCode,GLAcc.TABLECAPTION),
          GLAcc.TABLECAPTION,GLAcc.TABLECAPTION,
          SelectedDim."Dimension Value Filter",SelectedDim.Level);
      END;

      DimSelectionLevel.LOOKUPMODE := TRUE;
      IF DimSelectionLevel.RUNMODAL = ACTION::LookupOK THEN BEGIN
        DimSelectionLevel.GetDimSelBuf(TempDimSelectionBuf);
        SetDimSelection(ObjectType,ObjectID,AnalysisViewCode,SelectedDimText,TempDimSelectionBuf);
      END;
    END;

    PROCEDURE GetDimSelectionText@6(ObjectType@1000 : Integer;ObjectID@1001 : Integer;AnalysisViewCode@1002 : Code[10]) : Text[250];
    VAR
      SelectedDim@1003 : Record 369;
      SelectedDimText@1004 : Text[250];
    BEGIN
      WITH SelectedDim DO BEGIN
        SETRANGE("User ID",USERID);
        SETRANGE("Object Type",ObjectType);
        SETRANGE("Object ID",ObjectID);
        SETRANGE("Analysis View Code",AnalysisViewCode);
        IF FIND('-') THEN
          REPEAT
            AddDimCodeToText("Dimension Code",SelectedDimText);
          UNTIL NEXT = 0;
      END;
      EXIT(SelectedDimText);
    END;

    PROCEDURE SetDimSelectionLevelLiqAcc@1461000(ObjectType@1461000 : Integer;ObjectID@1461001 : Integer;AnalysisViewCode@1461002 : Code[10];VAR SelectedDimText@1461003 : Text[250]);
    VAR
      AnalysisView@1461004 : Record 5055269;
      Dim@1461005 : Record 348;
      TempDimSelectionBuf@1461006 : TEMPORARY Record 368;
      LiqAcc@1461007 : Record 5055251;
      DimSelectionLevel@1461008 : Form 564;
    BEGIN
      CLEAR(DimSelectionLevel);
      IF AnalysisView.GET(AnalysisViewCode) THEN BEGIN
        IF Dim.GET(AnalysisView."Dimension 1 Code") THEN BEGIN
          DimSelectionLevel.InsertDimSelBuf(
            SelectedDim.GET(USERID,ObjectType,ObjectID,AnalysisViewCode,Dim.Code),
            Dim.Code,Dim.GetMLName(GLOBALLANGUAGE),
            SelectedDim."Dimension Value Filter",SelectedDim.Level);
        END;
        IF Dim.GET(AnalysisView."Dimension 2 Code") THEN BEGIN
          DimSelectionLevel.InsertDimSelBuf(
            SelectedDim.GET(USERID,ObjectType,ObjectID,AnalysisViewCode,Dim.Code),
            Dim.Code,Dim.GetMLName(GLOBALLANGUAGE),
            SelectedDim."Dimension Value Filter",SelectedDim.Level);
        END;
        IF Dim.GET(AnalysisView."Dimension 3 Code") THEN BEGIN
          DimSelectionLevel.InsertDimSelBuf(
            SelectedDim.GET(USERID,ObjectType,ObjectID,AnalysisViewCode,Dim.Code),
            Dim.Code,Dim.GetMLName(GLOBALLANGUAGE),
            SelectedDim."Dimension Value Filter",SelectedDim.Level);
        END;
        IF Dim.GET(AnalysisView."Dimension 4 Code") THEN BEGIN
          DimSelectionLevel.InsertDimSelBuf(
            SelectedDim.GET(USERID,ObjectType,ObjectID,AnalysisViewCode,Dim.Code),
            Dim.Code,Dim.GetMLName(GLOBALLANGUAGE),
            SelectedDim."Dimension Value Filter",SelectedDim.Level);
        END;
        DimSelectionLevel.InsertDimSelBuf(
          SelectedDim.GET(USERID,ObjectType,ObjectID,AnalysisViewCode,LiqAcc.TABLECAPTION),
          LiqAcc.TABLECAPTION,LiqAcc.TABLECAPTION,
          SelectedDim."Dimension Value Filter",SelectedDim.Level);
      END;

      DimSelectionLevel.LOOKUPMODE := TRUE;
      IF DimSelectionLevel.RUNMODAL = ACTION::LookupOK THEN BEGIN
        DimSelectionLevel.GetDimSelBuf(TempDimSelectionBuf);
        SetDimSelection(ObjectType,ObjectID,AnalysisViewCode,SelectedDimText,TempDimSelectionBuf);
      END;
    END;

    BEGIN
    END.
  }
}

