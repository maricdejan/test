OBJECT Table 6550 Whse. Item Tracking Line
{
  OBJECT-PROPERTIES
  {
    Date=14.08.09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               TESTFIELD("Quantity Handled (Base)",0);
             END;

    CaptionML=[DEU=Logistik Artikelverfolg.-Zeile;
               ENU=Whse. Item Tracking Line];
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;CaptionML=[DEU=Lfd. Nr.;
                                                              ENU=Entry No.] }
    { 2   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   CaptionML=[DEU=Artikelnr.;
                                                              ENU=Item No.] }
    { 3   ;   ;Location Code       ;Code10        ;TableRelation=Location;
                                                   CaptionML=[DEU=Lagerortcode;
                                                              ENU=Location Code] }
    { 4   ;   ;Quantity (Base)     ;Decimal       ;OnValidate=BEGIN
                                                                IF "Quantity (Base)" < 0 THEN
                                                                  FIELDERROR("Quantity (Base)",Text004);

                                                                IF "Quantity (Base)" < "Quantity Handled (Base)" THEN
                                                                  FIELDERROR("Quantity (Base)",STRSUBSTNO(Text002,FIELDCAPTION("Quantity Handled (Base)")));

                                                                CheckSerialNoQty;

                                                                InitQtyToHandle;
                                                              END;

                                                   CaptionML=[DEU=Menge (Basis);
                                                              ENU=Quantity (Base)];
                                                   DecimalPlaces=0:5 }
    { 7   ;   ;Description         ;Text50        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description] }
    { 10  ;   ;Source Type         ;Integer       ;CaptionML=[DEU=Herkunftsart;
                                                              ENU=Source Type] }
    { 11  ;   ;Source Subtype      ;Option        ;CaptionML=[DEU=Herkunftsunterart;
                                                              ENU=Source Subtype];
                                                   OptionCaptionML=[DEU=0,1,2,3,4,5,6,7,8,9,10;
                                                                    ENU=0,1,2,3,4,5,6,7,8,9,10];
                                                   OptionString=0,1,2,3,4,5,6,7,8,9,10 }
    { 12  ;   ;Source ID           ;Code20        ;CaptionML=[DEU=Herkunfts-ID;
                                                              ENU=Source ID] }
    { 13  ;   ;Source Batch Name   ;Code10        ;CaptionML=[DEU=Herkunftsbuch.-Blattname;
                                                              ENU=Source Batch Name] }
    { 14  ;   ;Source Prod. Order Line;Integer    ;CaptionML=[DEU=Herkunfts-FA-Zeile;
                                                              ENU=Source Prod. Order Line] }
    { 15  ;   ;Source Ref. No.     ;Integer       ;CaptionML=[DEU=Herkunftsref.-Nr.;
                                                              ENU=Source Ref. No.] }
    { 24  ;   ;Serial No.          ;Code20        ;OnValidate=BEGIN
                                                                IF "Serial No." <> xRec."Serial No." THEN BEGIN
                                                                  TESTFIELD("Quantity Handled (Base)",0);
                                                                  IF IsReclass("Source Type","Source Batch Name") THEN
                                                                    "New Serial No." := "Serial No.";
                                                                  CheckSerialNoQty;
                                                                  InitExpirationDate;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Seriennr.;
                                                              ENU=Serial No.] }
    { 29  ;   ;Qty. per Unit of Measure;Decimal   ;InitValue=1;
                                                   CaptionML=[DEU=Menge pro Einheit;
                                                              ENU=Qty. per Unit of Measure];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 40  ;   ;Warranty Date       ;Date          ;CaptionML=[DEU=Garantiedatum;
                                                              ENU=Warranty Date] }
    { 41  ;   ;Expiration Date     ;Date          ;OnValidate=BEGIN
                                                                "New Expiration Date" := "Expiration Date";
                                                              END;

                                                   CaptionML=[DEU=Ablaufdatum;
                                                              ENU=Expiration Date] }
    { 50  ;   ;Qty. to Handle (Base);Decimal      ;OnValidate=BEGIN
                                                                IF "Qty. to Handle (Base)" < 0 THEN
                                                                  FIELDERROR("Qty. to Handle (Base)",Text004);

                                                                IF "Qty. to Handle (Base)" > ("Quantity (Base)" - "Quantity Handled (Base)")
                                                                THEN
                                                                  ERROR(
                                                                    Text001,
                                                                    "Quantity (Base)" - "Quantity Handled (Base)");

                                                                "Qty. to Handle" := CalcQty("Qty. to Handle (Base)");
                                                              END;

                                                   CaptionML=[DEU=Bewegungsmenge (Basis);
                                                              ENU=Qty. to Handle (Base)];
                                                   DecimalPlaces=0:5 }
    { 51  ;   ;Qty. to Invoice (Base);Decimal     ;CaptionML=[DEU=Menge akt. Rechnung (Basis);
                                                              ENU=Qty. to Invoice (Base)];
                                                   DecimalPlaces=0:5 }
    { 52  ;   ;Quantity Handled (Base);Decimal    ;CaptionML=[DEU=Geb. Bewegungsmenge (Basis);
                                                              ENU=Quantity Handled (Base)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 53  ;   ;Quantity Invoiced (Base);Decimal   ;CaptionML=[DEU=Bereits berech. Menge (Basis);
                                                              ENU=Quantity Invoiced (Base)];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 60  ;   ;Qty. to Handle      ;Decimal       ;CaptionML=[DEU=Bewegungsmenge;
                                                              ENU=Qty. to Handle];
                                                   DecimalPlaces=0:5 }
    { 70  ;   ;Buffer Status       ;Option        ;CaptionML=[DEU=Pufferstatus;
                                                              ENU=Buffer Status];
                                                   OptionCaptionML=[DEU=" ,éNDERN";
                                                                    ENU=" ,MODIFY"];
                                                   OptionString=[ ,MODIFY];
                                                   Editable=No }
    { 71  ;   ;Buffer Status2      ;Option        ;CaptionML=[DEU=Pufferstatus2;
                                                              ENU=Buffer Status2];
                                                   OptionCaptionML=[DEU=,Abl.-Dat. gesperrt;
                                                                    ENU=,ExpDate blocked];
                                                   OptionString=,ExpDate blocked;
                                                   Editable=No }
    { 80  ;   ;New Serial No.      ;Code20        ;CaptionML=[DEU=Neue Seriennr.;
                                                              ENU=New Serial No.] }
    { 81  ;   ;New Lot No.         ;Code20        ;CaptionML=[DEU=Neue Chargennr.;
                                                              ENU=New Lot No.] }
    { 90  ;   ;Source Type Filter  ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=[DEU=Herkunftsartenfilter;
                                                              ENU=Source Type Filter];
                                                   OptionCaptionML=[DEU=" ,Wareneingang,Warenausgang,Interne Einlag.-Anforderung,Interne Kommiss.-Anforderung,Produktion,Umlagerungsvorschlag";
                                                                    ENU=" ,Receipt,Shipment,Internal Put-away,Internal Pick,Production,Movement Worksheet"];
                                                   OptionString=[ ,Receipt,Shipment,Internal Put-away,Internal Pick,Production,Movement Worksheet] }
    { 91  ;   ;Qty. Registered (Base);Decimal     ;CaptionML=[DEU=Registrierte Menge (Basis);
                                                              ENU=Qty. Registered (Base)] }
    { 92  ;   ;Put-away Qty. (Base);Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Warehouse Activity Line"."Qty. Outstanding (Base)" WHERE (Activity Type=FILTER(Put-away),
                                                                                                                              Whse. Document Type=FIELD(Source Type Filter),
                                                                                                                              Whse. Document No.=FIELD(Source ID),
                                                                                                                              Whse. Document Line No.=FIELD(Source Ref. No.),
                                                                                                                              Serial No.=FIELD(Serial No.),
                                                                                                                              Lot No.=FIELD(Lot No.),
                                                                                                                              Action Type=FILTER(' '|Take)));
                                                   CaptionML=[DEU=Menge fÅr Einlagerung (Basis);
                                                              ENU=Put-away Qty. (Base)] }
    { 93  ;   ;Pick Qty. (Base)    ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Warehouse Activity Line"."Qty. Outstanding (Base)" WHERE (Activity Type=FILTER(Pick|Movement),
                                                                                                                              Whse. Document Type=FIELD(Source Type Filter),
                                                                                                                              Whse. Document No.=FIELD(Source ID),
                                                                                                                              Whse. Document Line No.=FIELD(Source Ref. No.),
                                                                                                                              Serial No.=FIELD(Serial No.),
                                                                                                                              Lot No.=FIELD(Lot No.),
                                                                                                                              Action Type=FILTER(' '|Place)));
                                                   CaptionML=[DEU=Menge fÅr Kommiss. (Basis);
                                                              ENU=Pick Qty. (Base)] }
    { 5400;   ;Lot No.             ;Code20        ;OnValidate=BEGIN
                                                                IF "Lot No." <> xRec."Lot No." THEN BEGIN
                                                                  TESTFIELD("Quantity Handled (Base)",0);
                                                                  IF IsReclass("Source Type","Source Batch Name") THEN
                                                                    "New Lot No." := "Lot No.";
                                                                  InitExpirationDate;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Chargennr.;
                                                              ENU=Lot No.] }
    { 5401;   ;Variant Code        ;Code10        ;TableRelation="Item Variant".Code WHERE (Item No.=FIELD(Item No.));
                                                   CaptionML=[DEU=Variantencode;
                                                              ENU=Variant Code] }
    { 6505;   ;New Expiration Date ;Date          ;CaptionML=[DEU=Neues Ablaufdatum;
                                                              ENU=New Expiration Date] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
    {    ;Source ID,Source Type,Source Subtype,Source Batch Name,Source Prod. Order Line,Source Ref. No.,Location Code;
                                                   SumIndexFields=Quantity (Base),Qty. to Handle (Base),Qty. to Invoice (Base),Quantity Handled (Base),Quantity Invoiced (Base);
                                                   MaintainSQLIndex=No;
                                                   MaintainSIFTIndex=No }
    {    ;Serial No.,Lot No.                       }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'DEU=Sie kînnen nicht mehr als %1 Einheiten bewegen.;ENU=You cannot handle more than %1 units.';
      Text002@1000 : TextConst 'DEU=darf nicht weniger als %1 sein;ENU=must not be less than %1';
      Text003@1002 : TextConst 'DEU=Die %1 muss 0 oder 1 sein, wenn eine %2 angegeben ist.;ENU=%1 must be 0 or 1 when %2 is stated.';
      Text004@1004 : TextConst 'DEU=darf nicht negativ sein;ENU=must not be negative';

    PROCEDURE CheckSerialNoQty@1();
    BEGIN
      IF "Serial No." <> '' THEN
        IF NOT ("Quantity (Base)" IN [0,1]) THEN
          ERROR(Text003,FIELDCAPTION("Quantity (Base)"),FIELDCAPTION("Serial No."));
    END;

    PROCEDURE CalcQty@14(BaseQty@1000 : Decimal) : Decimal;
    BEGIN
      IF "Qty. per Unit of Measure" = 0 THEN
        "Qty. per Unit of Measure" := 1;
      EXIT(ROUND(BaseQty/"Qty. per Unit of Measure",0.00001));
    END;

    PROCEDURE InitQtyToHandle@15();
    BEGIN
      "Qty. to Handle (Base)" := "Quantity (Base)" - "Quantity Handled (Base)";
      "Qty. to Handle" := CalcQty("Qty. to Handle (Base)");
    END;

    PROCEDURE InitExpirationDate@2();
    VAR
      Location@1003 : Record 14;
      ItemTrackingMgt@1001 : Codeunit 6500;
      ExpDate@1000 : Date;
      WarDate@1004 : Date;
      EntriesExist@1002 : Boolean;
    BEGIN
      IF ("Serial No." = xRec."Serial No.") AND ("Lot No." = xRec."Lot No.") THEN
        EXIT;

      "Expiration Date" := 0D;
      "Buffer Status2" := 0;

      Location.INIT;
      IF "Location Code" <> '' THEN
        Location.GET("Location Code");

      IF ItemTrackingMgt.GetWhseExpirationDate("Item No.","Variant Code",Location,"Lot No.","Serial No.",ExpDate) THEN BEGIN
        "Expiration Date" := ExpDate;
        "Buffer Status2" := "Buffer Status2"::"ExpDate blocked";
      END;

      IF IsReclass("Source Type","Source Batch Name") THEN BEGIN
        "New Expiration Date" := "Expiration Date";
        IF ItemTrackingMgt.GetWhseWarrantyDate("Item No.","Variant Code",Location,"Lot No.","Serial No.",WarDate) THEN
          "Warranty Date" := WarDate;
      END;
    END;

    PROCEDURE IsReclass@4(SourceType@1001 : Integer;SourceBatchName@1002 : Code[10]) : Boolean;
    VAR
      WhseJnlLine@1000 : Record 7311;
    BEGIN
      IF SourceType = DATABASE::"Warehouse Journal Line" THEN
        EXIT(WhseJnlLine.IsReclass(SourceBatchName));

      EXIT(FALSE);
    END;

    PROCEDURE LookUpTrackingSummary@50(VAR WhseItemTrackingLine@1000 : Record 6550;TrackingType@1001 : 'SerialNo,LotNo';MaxQuantity@1002 : Decimal;SignFactor@1003 : Integer;SearchForSupply@1004 : Boolean);
    VAR
      TempTrackingSpecification@1005 : TEMPORARY Record 336;
      WhseJnlLine@1006 : Record 7311;
      WhseWorksheetLine@1007 : Record 7326;
      ItemTrackingDataCollection@1008 : Codeunit 6501;
      BinCode@1009 : Code[20];
    BEGIN
      WITH WhseItemTrackingLine DO BEGIN

        CASE "Source Type" OF
          DATABASE::"Warehouse Journal Line":
            BEGIN
              WhseJnlLine.GET("Source Batch Name","Source ID","Location Code","Source Ref. No.");
              BinCode := WhseJnlLine."Bin Code";
            END;
          DATABASE::"Whse. Worksheet Line":
            BEGIN
              WhseWorksheetLine.GET("Source Batch Name","Source ID","Location Code","Source Ref. No.");
              BinCode := WhseWorksheetLine."From Bin Code";
            END;
          ELSE
            EXIT;
        END;

        TempTrackingSpecification.INIT;
        TempTrackingSpecification."Source Type" := "Source Type";
        TempTrackingSpecification."Item No." := "Item No.";
        TempTrackingSpecification."Location Code" := "Location Code";
        TempTrackingSpecification.Description := Description;
        TempTrackingSpecification."Variant Code" := "Variant Code";
        TempTrackingSpecification."Source Subtype" := "Source Subtype";
        TempTrackingSpecification."Source ID" := "Source ID";
        TempTrackingSpecification."Source Batch Name" := "Source Batch Name";
        TempTrackingSpecification."Source Prod. Order Line" := "Source Prod. Order Line";
        TempTrackingSpecification."Source Ref. No." := "Source Ref. No.";
        TempTrackingSpecification."Quantity (Base)" := "Quantity (Base)";
        TempTrackingSpecification."Qty. to Handle" := "Qty. to Handle";
        TempTrackingSpecification."Qty. to Handle (Base)" := "Qty. to Handle (Base)";
        TempTrackingSpecification."Qty. to Invoice" := 0;
        TempTrackingSpecification."Qty. to Invoice (Base)" := 0;
        TempTrackingSpecification."Quantity Handled (Base)" := 0;
        TempTrackingSpecification."Quantity Invoiced (Base)" := 0;
        TempTrackingSpecification."Qty. per Unit of Measure" := "Qty. per Unit of Measure";
        TempTrackingSpecification."Bin Code" := BinCode;

        ItemTrackingDataCollection.AssistEditLotSerialNo(
          TempTrackingSpecification,SearchForSupply,SignFactor,TrackingType,MaxQuantity);

        VALIDATE("Quantity (Base)",TempTrackingSpecification."Quantity (Base)");

        CASE TrackingType OF
          TrackingType::SerialNo:
            IF TempTrackingSpecification."Serial No." <> '' THEN
              VALIDATE("Serial No.",TempTrackingSpecification."Serial No.");
          TrackingType::LotNo:
            IF TempTrackingSpecification."Lot No." <> '' THEN
              VALIDATE("Lot No.",TempTrackingSpecification."Lot No.");
        END;
      END;
    END;

    BEGIN
    END.
  }
}

