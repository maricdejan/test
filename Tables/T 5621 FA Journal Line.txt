OBJECT Table 5621 FA Journal Line
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               FAJnlTemplate@1000 : Record 5619;
               FAJnlBatch@1001 : Record 5620;
               JnlLineDim@1002 : Record 356;
             BEGIN
               JnlLineDim.LOCKTABLE;
               LOCKTABLE;
               FAJnlTemplate.GET("Journal Template Name");
               "Source Code" := FAJnlTemplate."Source Code";
               FAJnlBatch.GET("Journal Template Name","Journal Batch Name");
               "Reason Code" := FAJnlBatch."Reason Code";

               ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
               ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
               DimMgt.InsertJnlLineDim(
                 DATABASE::"FA Journal Line",
                 "Journal Template Name","Journal Batch Name","Line No.",0,
                 "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
             END;

    OnDelete=BEGIN
               DimMgt.DeleteJnlLineDim(
                 DATABASE::"FA Journal Line",
                 "Journal Template Name","Journal Batch Name","Line No.",0);
             END;

    CaptionML=[DEU=Anlagen Buch.-Blattzeile;
               ENU=FA Journal Line];
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="FA Journal Template";
                                                   CaptionML=[DEU=Buch.-Blattvorlagenname;
                                                              ENU=Journal Template Name] }
    { 2   ;   ;Journal Batch Name  ;Code10        ;TableRelation="FA Journal Batch".Name WHERE (Journal Template Name=FIELD(Journal Template Name));
                                                   CaptionML=[DEU=Buch.-Blattname;
                                                              ENU=Journal Batch Name] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[DEU=Zeilennr.;
                                                              ENU=Line No.] }
    { 4   ;   ;Depreciation Book Code;Code10      ;TableRelation="Depreciation Book";
                                                   OnValidate=BEGIN
                                                                IF ("FA No." = '') OR ("Depreciation Book Code" = '') THEN
                                                                  EXIT;
                                                                FADeprBook.GET("FA No.","Depreciation Book Code");
                                                                "FA Posting Group" := FADeprBook."FA Posting Group";
                                                              END;

                                                   CaptionML=[DEU=AfA Buchcode;
                                                              ENU=Depreciation Book Code] }
    { 5   ;   ;FA Posting Type     ;Option        ;OnValidate=BEGIN
                                                                IF "FA Posting Type" <> "FA Posting Type"::"Acquisition Cost" THEN
                                                                  TESTFIELD("Insurance No.",'');
                                                                IF "FA Posting Type" <> "FA Posting Type"::Maintenance THEN
                                                                  TESTFIELD("Maintenance Code",'');
                                                              END;

                                                   CaptionML=[DEU=Anlagenbuchungsart;
                                                              ENU=FA Posting Type];
                                                   OptionCaptionML=[DEU=Anschaffungskosten,Normal-AfA,Erh”hte AfA,Zuschreibung,Sonder-AfA,Benutzerdef. AfA,Verkauf,Wartung,Restwert;
                                                                    ENU=Acquisition Cost,Depreciation,Write-Down,Appreciation,Custom 1,Custom 2,Disposal,Maintenance,Salvage Value];
                                                   OptionString=Acquisition Cost,Depreciation,Write-Down,Appreciation,Custom 1,Custom 2,Disposal,Maintenance,Salvage Value }
    { 6   ;   ;FA No.              ;Code20        ;TableRelation="Fixed Asset";
                                                   OnValidate=BEGIN
                                                                IF "FA No." = '' THEN BEGIN
                                                                  CreateDim(DATABASE::"Fixed Asset","FA No.");
                                                                  EXIT;
                                                                END;

                                                                FA.GET("FA No.");
                                                                FA.TESTFIELD(Blocked,FALSE);
                                                                FA.TESTFIELD(Inactive,FALSE);
                                                                Description := FA.Description;
                                                                IF "Depreciation Book Code" = '' THEN BEGIN
                                                                  FASetup.GET;
                                                                  "Depreciation Book Code" := FASetup."Default Depr. Book";
                                                                  IF NOT FADeprBook.GET("FA No.","Depreciation Book Code") THEN
                                                                    "Depreciation Book Code" := '';
                                                                END;
                                                                IF "Depreciation Book Code" <> '' THEN BEGIN
                                                                  FADeprBook.GET("FA No.","Depreciation Book Code");
                                                                  "FA Posting Group" := FADeprBook."FA Posting Group";
                                                                END;

                                                                CreateDim(DATABASE::"Fixed Asset","FA No.");
                                                              END;

                                                   CaptionML=[DEU=Anlagennr.;
                                                              ENU=FA No.] }
    { 7   ;   ;FA Posting Date     ;Date          ;CaptionML=[DEU=Anlagedatum;
                                                              ENU=FA Posting Date] }
    { 8   ;   ;Posting Date        ;Date          ;CaptionML=[DEU=Buchungsdatum;
                                                              ENU=Posting Date] }
    { 9   ;   ;Document Type       ;Option        ;CaptionML=[DEU=Belegart;
                                                              ENU=Document Type];
                                                   OptionCaptionML=[DEU=" ,,Rechnung,Gutschrift";
                                                                    ENU=" ,,Invoice,Credit Memo"];
                                                   OptionString=[ ,,Invoice,Credit Memo] }
    { 10  ;   ;Document Date       ;Date          ;CaptionML=[DEU=Belegdatum;
                                                              ENU=Document Date] }
    { 11  ;   ;Document No.        ;Code20        ;CaptionML=[DEU=Belegnr.;
                                                              ENU=Document No.] }
    { 12  ;   ;External Document No.;Code20       ;CaptionML=[DEU=Externe Belegnummer;
                                                              ENU=External Document No.] }
    { 13  ;   ;Description         ;Text50        ;CaptionML=[DEU=Beschreibung;
                                                              ENU=Description] }
    { 14  ;   ;Amount              ;Decimal       ;OnValidate=BEGIN
                                                                IF ((Amount > 0) AND (NOT Correction)) OR
                                                                  ((Amount < 0) AND Correction)
                                                                THEN BEGIN
                                                                  "Debit Amount" := Amount;
                                                                  "Credit Amount" := 0
                                                                END ELSE BEGIN
                                                                  "Debit Amount" := 0;
                                                                  "Credit Amount" := -Amount;
                                                                END;
                                                              END;

                                                   CaptionML=[DEU=Betrag;
                                                              ENU=Amount];
                                                   AutoFormatType=1 }
    { 15  ;   ;Debit Amount        ;Decimal       ;OnValidate=BEGIN
                                                                Correction := ("Debit Amount" < 0);
                                                                Amount := "Debit Amount";
                                                                VALIDATE(Amount);
                                                              END;

                                                   CaptionML=[DEU=Sollbetrag;
                                                              ENU=Debit Amount];
                                                   AutoFormatType=1 }
    { 16  ;   ;Credit Amount       ;Decimal       ;OnValidate=BEGIN
                                                                Correction := ("Credit Amount" < 0);
                                                                Amount := -"Credit Amount";
                                                                VALIDATE(Amount);
                                                              END;

                                                   CaptionML=[DEU=Habenbetrag;
                                                              ENU=Credit Amount];
                                                   AutoFormatType=1 }
    { 17  ;   ;Salvage Value       ;Decimal       ;CaptionML=[DEU=Restwert;
                                                              ENU=Salvage Value];
                                                   AutoFormatType=1 }
    { 18  ;   ;Quantity            ;Decimal       ;CaptionML=[DEU=Menge;
                                                              ENU=Quantity];
                                                   DecimalPlaces=0:5 }
    { 19  ;   ;Correction          ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE(Amount);
                                                              END;

                                                   CaptionML=[DEU=Storno;
                                                              ENU=Correction] }
    { 20  ;   ;No. of Depreciation Days;Integer   ;CaptionML=[DEU=Anzahl AfA-Tage;
                                                              ENU=No. of Depreciation Days];
                                                   BlankZero=Yes }
    { 21  ;   ;Depr. until FA Posting Date;Boolean;CaptionML=[DEU=AfA bis Anlagedatum;
                                                              ENU=Depr. until FA Posting Date] }
    { 22  ;   ;Depr. Acquisition Cost;Boolean     ;CaptionML=[DEU=Rckw. AfA-Korr. b. Anschaff.;
                                                              ENU=Depr. Acquisition Cost] }
    { 24  ;   ;FA Posting Group    ;Code10        ;TableRelation="FA Posting Group";
                                                   CaptionML=[DEU=Anlagenbuchungsgruppe;
                                                              ENU=FA Posting Group] }
    { 26  ;   ;Maintenance Code    ;Code10        ;TableRelation=Maintenance;
                                                   OnValidate=BEGIN
                                                                IF "Maintenance Code" <> '' THEN
                                                                  TESTFIELD("FA Posting Type","FA Posting Type"::Maintenance);
                                                              END;

                                                   CaptionML=[DEU=Wartungscode;
                                                              ENU=Maintenance Code] }
    { 27  ;   ;Shortcut Dimension 1 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                              END;

                                                   CaptionML=[DEU=Shortcutdimensionscode 1;
                                                              ENU=Shortcut Dimension 1 Code];
                                                   CaptionClass='1,2,1' }
    { 28  ;   ;Shortcut Dimension 2 Code;Code20   ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                              END;

                                                   CaptionML=[DEU=Shortcutdimensionscode 2;
                                                              ENU=Shortcut Dimension 2 Code];
                                                   CaptionClass='1,2,2' }
    { 30  ;   ;Insurance No.       ;Code20        ;TableRelation=Insurance;
                                                   OnValidate=BEGIN
                                                                IF "Insurance No." <> '' THEN
                                                                  TESTFIELD("FA Posting Type","FA Posting Type"::"Acquisition Cost");
                                                              END;

                                                   CaptionML=[DEU=Versicherungsnr.;
                                                              ENU=Insurance No.] }
    { 31  ;   ;Budgeted FA No.     ;Code20        ;TableRelation="Fixed Asset";
                                                   OnValidate=BEGIN
                                                                IF "Budgeted FA No." = '' THEN
                                                                  EXIT;
                                                                FA.GET("Budgeted FA No.");
                                                                FA.TESTFIELD("Budgeted Asset",TRUE);
                                                              END;

                                                   CaptionML=[DEU=Plananlagennr.;
                                                              ENU=Budgeted FA No.] }
    { 32  ;   ;Use Duplication List;Boolean       ;OnValidate=BEGIN
                                                                "Duplicate in Depreciation Book" := '';
                                                              END;

                                                   CaptionML=[DEU=Kopiervorgang aktivieren;
                                                              ENU=Use Duplication List] }
    { 33  ;   ;Duplicate in Depreciation Book;Code10;
                                                   TableRelation="Depreciation Book";
                                                   OnValidate=BEGIN
                                                                "Use Duplication List" := FALSE;
                                                              END;

                                                   CaptionML=[DEU=In AfA-Buch kopieren;
                                                              ENU=Duplicate in Depreciation Book] }
    { 34  ;   ;FA Reclassification Entry;Boolean  ;CaptionML=[DEU=Anlagenumbuchungsposten;
                                                              ENU=FA Reclassification Entry] }
    { 35  ;   ;FA Error Entry No.  ;Integer       ;TableRelation="FA Ledger Entry";
                                                   CaptionML=[DEU=Anlagenstornoposten Lfd. Nr.;
                                                              ENU=FA Error Entry No.];
                                                   BlankZero=Yes }
    { 36  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=[DEU=Ursachencode;
                                                              ENU=Reason Code] }
    { 37  ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   CaptionML=[DEU=Herkunftscode;
                                                              ENU=Source Code] }
    { 38  ;   ;Recurring Method    ;Option        ;CaptionML=[DEU=Wiederholungsart;
                                                              ENU=Recurring Method];
                                                   OptionCaptionML=[DEU=" ,F Fest,V Variabel";
                                                                    ENU=" ,F Fixed,V Variable"];
                                                   OptionString=[ ,F Fixed,V Variable] }
    { 39  ;   ;Recurring Frequency ;DateFormula   ;CaptionML=[DEU=Wiederholungsrate;
                                                              ENU=Recurring Frequency] }
    { 41  ;   ;Expiration Date     ;Date          ;CaptionML=[DEU=Ablaufdatum;
                                                              ENU=Expiration Date] }
    { 42  ;   ;Index Entry         ;Boolean       ;CaptionML=[DEU=Indexierungsbuchung;
                                                              ENU=Index Entry] }
    { 43  ;   ;Posting No. Series  ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[DEU=Buchungsnr.-Serie;
                                                              ENU=Posting No. Series] }
  }
  KEYS
  {
    {    ;Journal Template Name,Journal Batch Name,Line No.;
                                                   Clustered=Yes }
    {    ;Journal Template Name,Journal Batch Name,FA Posting Date;
                                                   MaintainSQLIndex=No }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      FASetup@1000 : Record 5603;
      FA@1001 : Record 5600;
      FAJnlTemplate@1002 : Record 5619;
      FAJnlBatch@1003 : Record 5620;
      FAJnlLine@1004 : Record 5621;
      FADeprBook@1005 : Record 5612;
      NoSeriesMgt@1006 : Codeunit 396;
      DimMgt@1007 : Codeunit 408;

    PROCEDURE ConvertToLedgEntry@1(VAR FAJnlLine@1000 : Record 5621) : Option;
    VAR
      FALedgEntry@1001 : Record 5601;
    BEGIN
      WITH FALedgEntry DO BEGIN
        CASE FAJnlLine."FA Posting Type" OF
          FAJnlLine."FA Posting Type"::"Acquisition Cost":
            "FA Posting Type" := "FA Posting Type"::"Acquisition Cost";
          FAJnlLine."FA Posting Type"::Depreciation:
            "FA Posting Type" := "FA Posting Type"::Depreciation;
          FAJnlLine."FA Posting Type"::"Write-Down":
            "FA Posting Type" := "FA Posting Type"::"Write-Down";
          FAJnlLine."FA Posting Type"::Appreciation:
            "FA Posting Type" := "FA Posting Type"::Appreciation;
          FAJnlLine."FA Posting Type"::"Custom 1":
            "FA Posting Type" := "FA Posting Type"::"Custom 1";
          FAJnlLine."FA Posting Type"::"Custom 2":
            "FA Posting Type" := "FA Posting Type"::"Custom 2";
          FAJnlLine."FA Posting Type"::Disposal:
            "FA Posting Type" := "FA Posting Type"::"Proceeds on Disposal";
          FAJnlLine."FA Posting Type"::"Salvage Value":
            "FA Posting Type" := "FA Posting Type"::"Salvage Value";
        END;
        EXIT("FA Posting Type");
      END;
    END;

    PROCEDURE SetUpNewLine@8(LastFAJnlLine@1000 : Record 5621);
    BEGIN
      FAJnlTemplate.GET("Journal Template Name");
      FAJnlBatch.GET("Journal Template Name","Journal Batch Name");
      FAJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
      FAJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
      IF FAJnlLine.FIND('-') THEN BEGIN
        "FA Posting Date" := LastFAJnlLine."FA Posting Date";
        "Document No." := LastFAJnlLine."Document No.";
      END ELSE BEGIN
        "FA Posting Date" := WORKDATE;
        IF FAJnlBatch."No. Series" <> '' THEN BEGIN
          CLEAR(NoSeriesMgt);
          "Document No." := NoSeriesMgt.TryGetNextNo(FAJnlBatch."No. Series","FA Posting Date");
        END;
      END;
      "Recurring Method" := LastFAJnlLine."Recurring Method";
      "Source Code" := FAJnlTemplate."Source Code";
      "Reason Code" := FAJnlBatch."Reason Code";
      "Posting No. Series" := FAJnlBatch."Posting No. Series";
    END;

    PROCEDURE CreateDim@13(Type1@1000 : Integer;No1@1001 : Code[20]);
    VAR
      TableID@1002 : ARRAY [10] OF Integer;
      No@1003 : ARRAY [10] OF Code[20];
    BEGIN
      TableID[1] := Type1;
      No[1] := No1;
      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
      DimMgt.GetDefaultDim(
        TableID,No,"Source Code",
        "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
      IF "Line No." <> 0 THEN
        DimMgt.UpdateJnlLineDefaultDim(
          DATABASE::"FA Journal Line",
          "Journal Template Name","Journal Batch Name","Line No.",0,
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
    END;

    PROCEDURE ValidateShortcutDimCode@14(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      IF "Line No." <> 0 THEN BEGIN
        DimMgt.SaveJnlLineDim(
          DATABASE::"FA Journal Line","Journal Template Name",
          "Journal Batch Name","Line No.",0,FieldNumber,ShortcutDimCode);
        IF MODIFY THEN;
      END ELSE
        DimMgt.SaveTempDim(FieldNumber,ShortcutDimCode);
    END;

    PROCEDURE LookupShortcutDimCode@18(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.LookupDimValueCode(FieldNumber,ShortcutDimCode);
      IF "Line No." <> 0 THEN BEGIN
        DimMgt.SaveJnlLineDim(
          DATABASE::"FA Journal Line","Journal Template Name",
          "Journal Batch Name","Line No.",0,FieldNumber,ShortcutDimCode);
        MODIFY;
      END ELSE
        DimMgt.SaveTempDim(FieldNumber,ShortcutDimCode);
    END;

    PROCEDURE ShowShortcutDimCode@15(VAR ShortcutDimCode@1000 : ARRAY [8] OF Code[20]);
    BEGIN
      IF "Line No." <> 0 THEN
        DimMgt.ShowJnlLineDim(
          DATABASE::"FA Journal Line","Journal Template Name",
          "Journal Batch Name","Line No.",0,ShortcutDimCode)
      ELSE
        DimMgt.ShowTempDim(ShortcutDimCode);
    END;

    BEGIN
    END.
  }
}

