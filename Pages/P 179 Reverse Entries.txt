OBJECT Page 179 Reverse Entries
{
  OBJECT-PROPERTIES
  {
    Date=14.08.09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Posten stornieren;
               ENU=Reverse Entries];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table179;
    DataCaptionExpr=Caption;
    PageType=List;
    OnInit=BEGIN
             DescriptionEditable := TRUE;
           END;

    OnOpenPage=BEGIN
                 FIND('-');
                 ReversalEntry := Rec;
                 IF "Reversal Type" = "Reversal Type"::Transaction THEN BEGIN
                   CurrPage.CAPTION := Text000;
                   ReversalEntry.SetReverseFilter("Transaction No.","Reversal Type");
                 END ELSE BEGIN
                   CurrPage.CAPTION := Text001;
                   ReversalEntry.SetReverseFilter("G/L Register No.","Reversal Type");
                 END;
               END;

    OnAfterGetRecord=BEGIN
                       OnAfterGetCurrRecord;
                       EntryTypeText := FORMAT("Entry Type");
                       EntryTypeTextOnFormat(EntryTypeText);
                     END;

    OnNewRecord=BEGIN
                  OnAfterGetCurrRecord;
                END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 48      ;1   ;ActionGroup;
                      CaptionML=[DEU=&Posten;
                                 ENU=Ent&ry] }
      { 49      ;2   ;Action    ;
                      CaptionML=[DEU=Sachposten;
                                 ENU=General Ledger];
                      Image=GLRegisters;
                      OnAction=BEGIN
                                 ReversalEntry.ShowGLEntries;
                               END;
                                }
      { 50      ;2   ;Action    ;
                      CaptionML=[DEU=Debitorenposten;
                                 ENU=Customer Ledger];
                      Image=CustomerLedger;
                      OnAction=BEGIN
                                 ReversalEntry.ShowCustLedgEntries;
                               END;
                                }
      { 51      ;2   ;Action    ;
                      CaptionML=[DEU=Kreditorenposten;
                                 ENU=Vendor Ledger];
                      Image=VendorLedger;
                      OnAction=BEGIN
                                 ReversalEntry.ShowVendLedgEntries;
                               END;
                                }
      { 52      ;2   ;Action    ;
                      CaptionML=[DEU=Bankposten;
                                 ENU=Bank Account Ledger];
                      Image=BankAccountLedger;
                      OnAction=BEGIN
                                 ReversalEntry.ShowBankAccLedgEntries;
                               END;
                                }
      { 53      ;2   ;Action    ;
                      CaptionML=[DEU=Anlagenposten;
                                 ENU=Fixed Asset Ledger];
                      Image=FixedAssetLedger;
                      OnAction=BEGIN
                                 ReversalEntry.ShowFALedgEntries;
                               END;
                                }
      { 54      ;2   ;Action    ;
                      CaptionML=[DEU=Wartungsposten;
                                 ENU=Maintenance Ledger];
                      Image=MaintenanceLedgerEntries;
                      OnAction=BEGIN
                                 ReversalEntry.ShowMaintenanceLedgEntries;
                               END;
                                }
      { 55      ;2   ;Action    ;
                      CaptionML=[DEU=MwSt.-Posten;
                                 ENU=VAT Ledger];
                      OnAction=BEGIN
                                 ReversalEntry.ShowVATEntries;
                               END;
                                }
      { 45      ;1   ;ActionGroup;
                      CaptionML=[DEU=&Stornieren;
                                 ENU=Re&versing] }
      { 46      ;2   ;Action    ;
                      ShortCutKey=F9;
                      CaptionML=[DEU=Stornieren;
                                 ENU=Reverse];
                      OnAction=BEGIN
                                 Post(FALSE);
                               END;
                                }
      { 58      ;2   ;Action    ;
                      ShortCutKey=Shift+F9;
                      CaptionML=[DEU=Stornieren und &Drucken;
                                 ENU=Reverse and &Print];
                      OnAction=BEGIN
                                 Post(TRUE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 12  ;2   ;Field     ;
                SourceExpr="Transaction No.";
                Editable=FALSE }

    { 2   ;2   ;Field     ;
                SourceExpr=EntryTypeText;
                CaptionClass=FIELDCAPTION("Entry Type");
                Editable=FALSE }

    { 64  ;2   ;Field     ;
                SourceExpr="Account No.";
                Editable=FALSE }

    { 66  ;2   ;Field     ;
                SourceExpr="Account Name";
                Visible=FALSE;
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                DrillDown=No;
                SourceExpr="Entry No.";
                Editable=FALSE }

    { 47  ;2   ;Field     ;
                SourceExpr="Posting Date";
                Editable=FALSE }

    { 20  ;2   ;Field     ;
                SourceExpr=Description;
                Editable=DescriptionEditable }

    { 37  ;2   ;Field     ;
                SourceExpr="Document Type";
                Editable=FALSE }

    { 39  ;2   ;Field     ;
                SourceExpr="Document No.";
                Editable=FALSE }

    { 28  ;2   ;Field     ;
                SourceExpr="Amount (LCY)";
                Editable=FALSE }

    { 56  ;2   ;Field     ;
                SourceExpr="VAT Amount";
                Editable=FALSE }

    { 30  ;2   ;Field     ;
                SourceExpr="Debit Amount (LCY)";
                Visible=FALSE;
                Editable=FALSE }

    { 32  ;2   ;Field     ;
                SourceExpr="Credit Amount (LCY)";
                Visible=FALSE;
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                SourceExpr="G/L Register No.";
                Visible=FALSE;
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                SourceExpr="Source Code";
                Visible=FALSE;
                Editable=FALSE }

    { 10  ;2   ;Field     ;
                SourceExpr="Journal Batch Name";
                Editable=FALSE }

    { 14  ;2   ;Field     ;
                SourceExpr="Source Type";
                Editable=FALSE }

    { 16  ;2   ;Field     ;
                SourceExpr="Source No.";
                Editable=FALSE }

    { 18  ;2   ;Field     ;
                SourceExpr="Currency Code";
                Editable=FALSE }

    { 41  ;2   ;Field     ;
                SourceExpr="Bal. Account Type";
                Editable=FALSE }

    { 43  ;2   ;Field     ;
                SourceExpr="Bal. Account No.";
                Editable=FALSE }

    { 22  ;2   ;Field     ;
                SourceExpr=Amount;
                Visible=FALSE;
                Editable=FALSE }

    { 24  ;2   ;Field     ;
                SourceExpr="Debit Amount";
                Visible=FALSE;
                Editable=FALSE }

    { 26  ;2   ;Field     ;
                SourceExpr="Credit Amount";
                Visible=FALSE;
                Editable=FALSE }

    { 59  ;2   ;Field     ;
                SourceExpr="FA Posting Category";
                Editable=FALSE }

    { 61  ;2   ;Field     ;
                SourceExpr="FA Posting Type";
                Editable=FALSE }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Text000@1005 : TextConst 'DEU=Transaktionsposten stornieren;ENU=Reverse Transaction Entries';
      Text001@1006 : TextConst 'DEU=Journalbuchungen stornieren;ENU=Reverse Register Entries';
      ReversalEntry@1002 : Record 179;
      Text002@1007 : TextConst 'DEU=Sollen die Posten storniert werden?;ENU=Do you want to reverse the entries?';
      Text003@1008 : TextConst 'DEU=Die Posten wurden erfolgreich storniert.;ENU=The entries were successfully reversed.';
      Text004@1012 : TextConst 'DEU=Zum Stornieren der Posten bucht das Programm Korrekturposten.;ENU=To reverse these entries, the program will post correcting entries.';
      Text005@1000 : TextConst 'DEU=Mîchten Sie die Posten stornieren und den Bericht drucken?;ENU=Do you want to reverse the entries and print the report?';
      Text006@1001 : TextConst 'DEU=Es kann nichts storniert werden.;ENU=There is nothing to reverse.';
      Text007@1102601000 : TextConst 'DEU=\Es ist mindestens ein Anlagenposten vorhanden. Verwenden Sie ggf. die Funktion ''Posten stornieren'' fÅr Anlagen.;ENU=\There are one or more FA Ledger Entries. You should consider using the fixed asset function Cancel Entries.';
      Text008@1003 : TextConst 'DEU=Nach dem ôffnen des Fensters wurden énderungen an gebuchten Posten vorgenommen.\Schlie·en Sie das Fenster, und îffnen Sie es anschlie·end erneut, um den Vorgang fortzusetzen.;ENU=Changes have been made to posted entries after the window was opened.\Close and reopen the window to continue.';
      DescriptionEditable@19061412 : Boolean INDATASET;
      EntryTypeText@19008937 : Text[1024] INDATASET;

    PROCEDURE Post@20(PrintRegister@1000 : Boolean);
    VAR
      GLReg@1003 : Record 45;
      GenJnlTemplate@1002 : Record 80;
      GenJnlPostLine@1001 : Codeunit 12;
      Txt@1004 : Text[250];
      WarningText@1102601000 : Text[250];
      Number@1005 : Integer;
    BEGIN
      RESET;
      SETRANGE("Entry Type","Entry Type"::"Fixed Asset");
      IF FINDFIRST THEN
        WarningText := Text007;
      SETRANGE("Entry Type");
      IF PrintRegister THEN
        Txt := Text004 + WarningText + '\' + Text005
      ELSE
        Txt := Text004 + WarningText + '\' + Text002;
      IF NOT FIND('-') THEN
        ERROR(Text006);
      IF CONFIRM(Txt,FALSE) THEN BEGIN
        ReversalEntry.CheckEntries;
        GET(1);
        IF "Reversal Type" = "Reversal Type"::Register THEN
          Number := "G/L Register No."
        ELSE
          Number := "Transaction No.";
        IF NOT ReversalEntry.VerifyReversalEntries(Rec,Number,"Reversal Type") THEN
          ERROR(Text008);
        GenJnlPostLine.Reverse(ReversalEntry,Rec);
        IF PrintRegister THEN BEGIN
          GenJnlTemplate.VALIDATE(Type);
          IF GenJnlTemplate."Posting Report ID" <> 0 THEN BEGIN
            IF GLReg.FIND('+') THEN BEGIN
              GLReg.SETRECFILTER;
              REPORT.RUN(GenJnlTemplate."Posting Report ID",FALSE,FALSE,GLReg);
            END;
          END;
        END;
        DELETEALL;
        CurrPage.UPDATE(FALSE);
        MESSAGE(Text003);
        CurrPage.CLOSE;
      END;
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    BEGIN
      xRec := Rec;
      DescriptionEditable := "Entry Type" <> "Entry Type"::VAT;
    END;

    LOCAL PROCEDURE EntryTypeTextOnFormat@19076397(VAR Text@19075229 : Text[1024]);
    VAR
      GLEntry@1008 : Record 17;
      CustLedgEntry@1007 : Record 21;
      VendLedgEntry@1006 : Record 25;
      BankAccLedgEntry@1005 : Record 271;
      FALedgEntry@1004 : Record 5601;
      MaintenanceLedgEntry@1003 : Record 5625;
      VATEntry@1002 : Record 254;
    BEGIN
      IF "Entry Type" = "Entry Type"::"G/L Account" THEN
        Text := GLEntry.TABLECAPTION;
      IF "Entry Type" = "Entry Type"::Customer THEN
        Text := CustLedgEntry.TABLECAPTION;
      IF "Entry Type" = "Entry Type"::Vendor THEN
        Text := VendLedgEntry.TABLECAPTION;
      IF "Entry Type" = "Entry Type"::"Bank Account"  THEN
        Text := BankAccLedgEntry.TABLECAPTION;
      IF "Entry Type" = "Entry Type"::"Fixed Asset" THEN
        Text := FALedgEntry.TABLECAPTION;
      IF "Entry Type" = "Entry Type"::Maintenance THEN
        Text := MaintenanceLedgEntry.TABLECAPTION;
      IF "Entry Type" = "Entry Type"::VAT THEN
        Text := VATEntry.TABLECAPTION;
    END;

    BEGIN
    END.
  }
}

