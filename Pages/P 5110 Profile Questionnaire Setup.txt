OBJECT Page 5110 Profile Questionnaire Setup
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Profilbefragung Einrichtung;
               ENU=Profile Questionnaire Setup];
    SaveValues=Yes;
    SourceTable=Table5088;
    DataCaptionExpr=CaptionExpr;
    PageType=Worksheet;
    AutoSplitKey=Yes;
    OnInit=BEGIN
             ProfileQuestionnaireCodeNameVi := TRUE;
           END;

    OnOpenPage=VAR
                 ProfileQuestionnaireHeader@1000 : Record 5087;
               BEGIN
                 IF GETFILTER("Profile Questionnaire Code") <> '' THEN BEGIN
                   ProfileQuestionnaireHeader.SETFILTER(Code,GETFILTER("Profile Questionnaire Code"));
                   IF ProfileQuestionnaireHeader.COUNT = 1 THEN
                     CurrentQuestionsChecklistCode := GETFILTER("Profile Questionnaire Code");
                 END;
                 IF (CurrentQuestionsChecklistCode = '') OR
                   (NOT ProfileQuestnHeader.GET(CurrentQuestionsChecklistCode))
                 THEN
                   CurrentQuestionsChecklistCode := ProfileManagement.GetQuestionnaire;

                 ProfileManagement.SetName(CurrentQuestionsChecklistCode,Rec,0);

                 IF ISSERVICETIER THEN BEGIN
                   CaptionExpr := "Profile Questionnaire Code";
                   ProfileQuestionnaireCodeNameVi := FALSE;
                 END;
               END;

    OnAfterGetRecord=BEGIN
                       PriorityHideValue := FALSE;
                       DescriptionIndent := 0;
                       TypeOnFormat;
                       DescriptionOnFormat;
                       PriorityOnFormat;
                     END;

    OnNewRecord=BEGIN
                  "Profile Questionnaire Code" := CurrentQuestionsChecklistCode;
                  Type := Type::Answer;
                END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 23      ;1   ;ActionGroup;
                      CaptionML=[DEU=&Zeile;
                                 ENU=&Line] }
      { 25      ;2   ;Action    ;
                      ShortCutKey=Shift+F7;
                      CaptionML=[DEU=Fragendetails;
                                 ENU=Question Details];
                      OnAction=BEGIN
                                 CASE Type OF
                                   Type::Question: FORM.RUNMODAL(FORM::"Profile Question Details",Rec);
                                   Type::Answer: ERROR(Text000);
                                 END;
                               END;
                                }
      { 21      ;2   ;Action    ;
                      CaptionML=[DEU=Antwortdetails;
                                 ENU=Answer Where-Used];
                      OnAction=VAR
                                 Rating@1000 : Record 5111;
                               BEGIN
                                 CASE Type OF
                                   Type::Question: ERROR(Text001);
                                   Type::Answer:
                                     BEGIN
                                       Rating.SETRANGE("Rating Profile Quest. Code","Profile Questionnaire Code");
                                       Rating.SETRANGE("Rating Profile Quest. Line No.","Line No.");
                                       FORM.RUNMODAL(FORM::"Answer Where-Used",Rating);
                                     END;
                                 END;
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 24      ;1   ;ActionGroup;
                      CaptionML=[DEU=F&unktion;
                                 ENU=F&unctions] }
      { 22      ;2   ;Action    ;
                      CaptionML=[DEU=&Bewertung erstellen;
                                 ENU=Create &Rating];
                      OnAction=VAR
                                 TempProfileQuestnLine@1001 : TEMPORARY Record 5088;
                               BEGIN
                                 TempProfileQuestnLine.CreateRatingFromProfQuestnLine(Rec);
                               END;
                                }
      { 34      ;2   ;Separator  }
      { 33      ;2   ;Action    ;
                      CaptionML=[DEU=&Kontaktklassifizierung aktual.;
                                 ENU=Update &Classification];
                      OnAction=VAR
                                 ProfileQuestnHeader@1001 : Record 5087;
                               BEGIN
                                 ProfileQuestnHeader.GET(CurrentQuestionsChecklistCode);
                                 ProfileQuestnHeader.SETRECFILTER;
                                 REPORT.RUN(REPORT::"Update Contact Classification",TRUE,FALSE,ProfileQuestnHeader);
                               END;
                                }
      { 31      ;2   ;Separator  }
      { 26      ;2   ;Action    ;
                      CaptionML=[DEU=Nach &oben;
                                 ENU=Move &Up];
                      OnAction=BEGIN
                                 MoveUp;
                               END;
                                }
      { 27      ;2   ;Action    ;
                      CaptionML=[DEU=Nach &unten;
                                 ENU=Move &Down];
                      OnAction=BEGIN
                                 MoveDown
                               END;
                                }
      { 32      ;2   ;Separator ;
                      CaptionML=[DEU="";
                                 ENU=""] }
      { 28      ;2   ;Action    ;
                      CaptionML=[DEU=Drucken;
                                 ENU=Print];
                      Image=Print;
                      OnAction=VAR
                                 ProfileQuestnHeader@1001 : Record 5087;
                               BEGIN
                                 ProfileQuestnHeader.SETRANGE(Code,CurrentQuestionsChecklistCode);
                                 REPORT.RUN(REPORT::"Questionnaire - Handouts",TRUE,FALSE,ProfileQuestnHeader);
                               END;
                                }
      { 30      ;2   ;Action    ;
                      CaptionML=[DEU=Testbericht;
                                 ENU=Test Report];
                      Image=TestReport;
                      OnAction=VAR
                                 ProfileQuestnHeader@1001 : Record 5087;
                               BEGIN
                                 ProfileQuestnHeader.SETRANGE(Code,CurrentQuestionsChecklistCode);
                                 REPORT.RUN(REPORT::"Questionnaire - Test",TRUE,FALSE,ProfileQuestnHeader);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 19  ;1   ;Field     ;
                Name=ProfileQuestionnaireCodeName;
                CaptionML=[DEU=Profilbefragungscode;
                           ENU=Profile Questionnaire Code];
                SourceExpr=CurrentQuestionsChecklistCode;
                Visible=ProfileQuestionnaireCodeNameVi;
                OnValidate=BEGIN
                             ProfileQuestnHeader.GET(CurrentQuestionsChecklistCode);
                               CurrentQuestionsChecklistCodeO;
                           END;

                OnLookup=BEGIN
                           CurrPage.SAVERECORD;
                           COMMIT;
                           IF FORM.RUNMODAL(0, ProfileQuestnHeader) = ACTION::LookupOK THEN BEGIN
                             ProfileQuestnHeader.GET(ProfileQuestnHeader.Code);
                             CurrentQuestionsChecklistCode := ProfileQuestnHeader.Code;
                             ProfileManagement.SetName(CurrentQuestionsChecklistCode,Rec,0);
                             CurrPage.UPDATE(FALSE);
                           END;
                         END;
                          }

    { 1   ;1   ;Group     ;
                IndentationColumnName=DescriptionIndent;
                IndentationControls=Description;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr=Type }

    { 4   ;2   ;Field     ;
                SourceExpr=Description }

    { 6   ;2   ;Field     ;
                SourceExpr="Multiple Answers" }

    { 16  ;2   ;Field     ;
                SourceExpr=Priority;
                HideValue=PriorityHideValue }

    { 8   ;2   ;Field     ;
                SourceExpr="Auto Contact Classification";
                Editable=FALSE }

    { 10  ;2   ;Field     ;
                SourceExpr="From Value" }

    { 12  ;2   ;Field     ;
                SourceExpr="To Value" }

    { 14  ;2   ;Field     ;
                SourceExpr="No. of Contacts" }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Details sind nur fÅr Fragen verfÅgbar.;ENU=Details only available for questions.';
      ProfileQuestnHeader@1001 : Record 5087;
      ProfileManagement@1002 : Codeunit 5059;
      CurrentQuestionsChecklistCode@1003 : Code[10];
      Text001@1004 : TextConst 'DEU=Details sind nur fÅr Antworten verfÅgbar.;ENU=Where-Used only available for answers.';
      CaptionExpr@1102601000 : Text[100];
      ProfileQuestionnaireCodeNameVi@19036386 : Boolean INDATASET;
      TypeEmphasize@19012868 : Boolean INDATASET;
      DescriptionEmphasize@19010547 : Boolean INDATASET;
      DescriptionIndent@19057867 : Integer INDATASET;
      PriorityHideValue@19011917 : Boolean INDATASET;

    LOCAL PROCEDURE CurrentQuestionsChecklistCodeO@19076168();
    BEGIN
      ProfileManagement.SetName(CurrentQuestionsChecklistCode,Rec,0);
    END;

    LOCAL PROCEDURE TypeOnFormat@19025827();
    BEGIN
      IF Type = Type::Question THEN
        TypeEmphasize := TRUE
    END;

    LOCAL PROCEDURE DescriptionOnFormat@19023855();
    BEGIN
      IF Type = Type::Question THEN
        DescriptionEmphasize := TRUE
      ELSE
        DescriptionIndent := 1;
    END;

    LOCAL PROCEDURE PriorityOnFormat@19057707();
    BEGIN
      IF Type = Type::Question THEN
        PriorityHideValue := TRUE;
    END;

    BEGIN
    END.
  }
}

