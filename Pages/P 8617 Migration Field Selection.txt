OBJECT Page 8617 Migration Field Selection
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Migrationsfeldauswahl;
               ENU=Migration Field Selection];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table2000000041;
    PageType=List;
    OnClosePage=VAR
                  FieldSelection@1004 : Record 8616;
                  RecRef@1000 : RecordRef;
                  KeyRef@1002 : KeyRef;
                  FieldRef@1001 : FieldRef;
                  KeyFieldCount@1003 : Integer;
                BEGIN
                  IF GETFILTER(TableNo) <> '' THEN BEGIN
                    RecRef.OPEN(TableNo);
                    KeyRef := RecRef.KEYINDEX(1);
                    FOR KeyFieldCount := 1 TO KeyRef.FIELDCOUNT DO BEGIN
                      FieldRef := KeyRef.FIELDINDEX(KeyFieldCount);
                      IF NOT FieldSelection.GET(RecRef.NUMBER,FieldRef.NUMBER) THEN BEGIN
                        FieldSelection.TableID := RecRef.NUMBER;
                        FieldSelection.FieldID := FieldRef.NUMBER;
                        FieldSelection.INSERT;
                      END;
                    END;
                  END;
                END;

    OnAfterGetRecord=BEGIN
                       GetRec;
                     END;

    OnDeleteRecord=BEGIN
                     ERROR(Text001);
                   END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                CaptionML=[DEU=Nr.;
                           ENU=No.];
                SourceExpr="No.";
                Editable=FALSE }

    { 4   ;2   ;Field     ;
                CaptionML=[DEU=Feldname;
                           ENU=Field Name];
                SourceExpr=FieldName;
                Editable=FALSE }

    { 6   ;2   ;Field     ;
                CaptionML=[DEU=Feldbezeichnung;
                           ENU=Field Caption];
                SourceExpr="Field Caption";
                Visible=FALSE;
                Editable=FALSE }

    { 8   ;2   ;Field     ;
                CaptionML=[DEU=Einschlie·en;
                           ENU=Include];
                SourceExpr=Include;
                OnValidate=BEGIN
                             UpdateRec;
                           END;
                            }

    { 12  ;2   ;Field     ;
                CaptionML=[DEU=öberprÅfen;
                           ENU=Validate];
                SourceExpr=ValidateImport }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Include@1000000000 : Boolean;
      Text001@1001 : TextConst 'DEU=Felder kînnen nicht gelîscht werden.;ENU=Fields can not be deleted.';
      ValidateImport@1000 : Boolean;

    PROCEDURE GetRec@1000000000();
    VAR
      FieldSelection@1000000000 : Record 8616;
    BEGIN
      IF FieldSelection.GET(TableNo,"No.") THEN BEGIN
        Include := TRUE;
        ValidateImport := FieldSelection.Validate;
      END ELSE BEGIN
        IF IsKeyField THEN
          Include := TRUE
        ELSE
          Include := FALSE;
      END;
    END;

    PROCEDURE UpdateRec@1000000001();
    VAR
      FieldSelection@1000000000 : Record 8616;
    BEGIN
      IF Include THEN BEGIN
        FieldSelection.TableID := TableNo;
        FieldSelection.FieldID := "No.";
        FieldSelection.Validate := ValidateImport;
        IF NOT FieldSelection.INSERT THEN
          FieldSelection.MODIFY;
      END ELSE
        IF FieldSelection.GET(TableNo,"No.") THEN BEGIN
          FieldSelection.DELETE;
        END;
    END;

    PROCEDURE IsKeyField@2() : Boolean;
    VAR
      FieldSelection@1004 : Record 8616;
      RecRef@1003 : RecordRef;
      KeyRef@1002 : KeyRef;
      FieldRef@1001 : FieldRef;
      KeyFieldCount@1000 : Integer;
    BEGIN
      RecRef.OPEN(TableNo);
      KeyRef := RecRef.KEYINDEX(1);
      FOR KeyFieldCount := 1 TO KeyRef.FIELDCOUNT DO BEGIN
        FieldRef := KeyRef.FIELDINDEX(KeyFieldCount);
        IF FieldRef.NUMBER = "No." THEN
          EXIT(TRUE);
      END;

      EXIT(FALSE);
    END;

    BEGIN
    END.
  }
}

