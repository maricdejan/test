OBJECT Page 256 Payment Journal
{
  OBJECT-PROPERTIES
  {
    Date=05.11.08;
    Time=12:00:00;
    Version List=NAVW16.00,NAVDACH6.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Zahlungsausgangs Buch.-Blatt;
               ENU=Payment Journal];
    SaveValues=Yes;
    SourceTable=Table81;
    DelayedInsert=Yes;
    DataCaptionFields=Journal Batch Name;
    PageType=Worksheet;
    AutoSplitKey=Yes;
    OnOpenPage=VAR
                 JnlSelected@1000 : Boolean;
               BEGIN
                 BalAccName := '';
                 OpenedFromBatch := ("Journal Batch Name" <> '') AND ("Journal Template Name" = '');
                 IF OpenedFromBatch THEN BEGIN
                   CurrentJnlBatchName := "Journal Batch Name";
                   GenJnlManagement.OpenJnl(CurrentJnlBatchName,Rec);
                   EXIT;
                 END;
                 GenJnlManagement.TemplateSelection(FORM::"Payment Journal",4,FALSE,Rec,JnlSelected);
                 IF NOT JnlSelected THEN
                   ERROR('');
                 GenJnlManagement.OpenJnl(CurrentJnlBatchName,Rec);
               END;

    OnAfterGetRecord=BEGIN
                       ShowShortcutDimCode(ShortcutDimCode);
                       OnAfterGetCurrRecord;
                     END;

    OnNewRecord=BEGIN
                  UpdateBalance;
                  SetUpNewLine(xRec,Balance,BelowxRec);
                  CLEAR(ShortcutDimCode);

                  IF "Document Type" = "Document Type"::" " THEN BEGIN
                    "Document Type" := "Document Type"::Payment;
                    "Account Type" := "Account Type"::Vendor;
                  END;
                  OnAfterGetCurrRecord;
                END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 57      ;1   ;ActionGroup;
                      CaptionML=[DEU=&Zeile;
                                 ENU=&Line] }
      { 58      ;2   ;Action    ;
                      ShortCutKey=Shift+Ctrl+D;
                      CaptionML=[DEU=Dimensionen;
                                 ENU=Dimensions];
                      RunObject=Page 545;
                      RunFormLink=Table ID=CONST(81),
                                  Journal Template Name=FIELD(Journal Template Name),
                                  Journal Batch Name=FIELD(Journal Batch Name),
                                  Journal Line No.=FIELD(Line No.);
                      Promoted=Yes;
                      Image=Dimensions;
                      PromotedCategory=Process }
      { 37      ;1   ;ActionGroup;
                      CaptionML=[DEU=&Konto;
                                 ENU=A&ccount] }
      { 38      ;2   ;Action    ;
                      ShortCutKey=Shift+F7;
                      CaptionML=[DEU=Karte;
                                 ENU=Card];
                      RunObject=Codeunit 15;
                      Image=EditLines }
      { 39      ;2   ;Action    ;
                      ShortCutKey=Ctrl+F7;
                      CaptionML=[DEU=&Posten;
                                 ENU=Ledger E&ntries];
                      RunObject=Codeunit 14;
                      Promoted=No;
                      Image=GLRegisters;
                      PromotedCategory=Process }
      { 41      ;1   ;ActionGroup;
                      CaptionML=[DEU=Za&hlungen;
                                 ENU=&Payments] }
      { 42      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Zahlungsvorschlag;
                                 ENU=Suggest Vendor Payments];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=SuggestVendorPayments;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CreateVendorPmtSuggestion.SetGenJnlLine(Rec);
                                 CreateVendorPmtSuggestion.RUNMODAL;
                                 CLEAR(CreateVendorPmtSuggestion);
                               END;
                                }
      { 63      ;2   ;Action    ;
                      CaptionML=[DEU=&Scheckansicht;
                                 ENU=P&review Check];
                      RunObject=Page 404;
                      RunFormLink=Journal Template Name=FIELD(Journal Template Name),
                                  Journal Batch Name=FIELD(Journal Batch Name),
                                  Line No.=FIELD(Line No.) }
      { 64      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Scheck drucken;
                                 ENU=Print Check];
                      Promoted=Yes;
                      Image=PrintCheck;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 GenJnlLine.RESET;
                                 GenJnlLine.COPY(Rec);
                                 GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
                                 GenJnlLine.SETRANGE("Journal Batch Name","Journal Batch Name");
                                 DocPrint.PrintCheck(GenJnlLine);
                                 CODEUNIT.RUN(CODEUNIT::"Adjust Gen. Journal Balance",Rec);
                               END;
                                }
      { 65      ;2   ;Action    ;
                      CaptionML=[DEU=Scheck annullieren;
                                 ENU=Void Check];
                      Promoted=Yes;
                      Image=VoidCheck;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 TESTFIELD("Bank Payment Type","Bank Payment Type"::"Computer Check");
                                 TESTFIELD("Check Printed",TRUE);
                                 IF CONFIRM(Text000,FALSE,"Document No.") THEN
                                   CheckManagement.VoidCheck(Rec);
                               END;
                                }
      { 66      ;2   ;Action    ;
                      CaptionML=[DEU=Alle Sc&hecks annullieren;
                                 ENU=Void &All Checks];
                      OnAction=BEGIN
                                 IF CONFIRM(Text001,FALSE) THEN BEGIN
                                   GenJnlLine.RESET;
                                   GenJnlLine.COPY(Rec);
                                   GenJnlLine.SETRANGE("Bank Payment Type","Bank Payment Type"::"Computer Check");
                                   GenJnlLine.SETRANGE("Check Printed",TRUE);
                                   IF GenJnlLine.FIND('-') THEN
                                     REPEAT
                                       GenJnlLine2 := GenJnlLine;
                                       CheckManagement.VoidCheck(GenJnlLine2);
                                     UNTIL GenJnlLine.NEXT = 0;
                                 END;
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 40      ;1   ;ActionGroup;
                      CaptionML=[DEU=F&unktion;
                                 ENU=F&unctions] }
      { 93      ;2   ;Action    ;
                      ShortCutKey=Shift+F11;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Posten ausgleichen;
                                 ENU=Apply Entries];
                      RunObject=Codeunit 225;
                      Promoted=Yes;
                      Image=ApplyEntries;
                      PromotedCategory=Process }
      { 94      ;2   ;Action    ;
                      CaptionML=[DEU=Rundungszeilen f. MW-Konvertierung einfÅgen;
                                 ENU=Insert Conv. LCY Rndg. Lines];
                      RunObject=Codeunit 407 }
      { 43      ;1   ;ActionGroup;
                      CaptionML=[DEU=Bu&chen;
                                 ENU=P&osting] }
      { 44      ;2   ;Action    ;
                      ShortCutKey=Ctrl+F11;
                      CaptionML=[DEU=Abstimmen;
                                 ENU=Reconcile];
                      Image=Reconcile;
                      OnAction=BEGIN
                                 GLReconcile.SetGenJnlLine(Rec);
                                 GLReconcile.RUN;
                               END;
                                }
      { 45      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Testbericht;
                                 ENU=Test Report];
                      Image=TestReport;
                      OnAction=BEGIN
                                 ReportPrint.PrintGenJnlLine(Rec);
                               END;
                                }
      { 46      ;2   ;Action    ;
                      ShortCutKey=F9;
                      CaptionML=[DEU=Bu&chen;
                                 ENU=P&ost];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Post;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CODEUNIT.RUN(CODEUNIT::"Gen. Jnl.-Post",Rec);
                                 CurrentJnlBatchName := GETRANGEMAX("Journal Batch Name");
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
      { 47      ;2   ;Action    ;
                      ShortCutKey=Shift+F9;
                      CaptionML=[DEU=Buchen und d&rucken;
                                 ENU=Post and &Print];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=PostPrint;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CODEUNIT.RUN(CODEUNIT::"Gen. Jnl.-Post+Print",Rec);
                                 CurrentJnlBatchName := GETRANGEMAX("Journal Batch Name");
                                 CurrPage.UPDATE(FALSE);
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 33  ;1   ;Field     ;
                Lookup=Yes;
                CaptionML=[DEU=Buch.-Blattname;
                           ENU=Batch Name];
                SourceExpr=CurrentJnlBatchName;
                OnValidate=BEGIN
                             GenJnlManagement.CheckName(CurrentJnlBatchName,Rec);
                               CurrentJnlBatchNameOnAfterVali;
                           END;

                OnLookup=BEGIN
                           CurrPage.SAVERECORD;
                           GenJnlManagement.LookupName(CurrentJnlBatchName,Rec);
                           CurrPage.UPDATE(FALSE);
                         END;
                          }

    { 1   ;1   ;Group     ;
                GroupType=Repeater }

    { 2   ;2   ;Field     ;
                SourceExpr="Posting Date" }

    { 35  ;2   ;Field     ;
                SourceExpr="Document Date";
                Visible=FALSE }

    { 4   ;2   ;Field     ;
                SourceExpr="Document Type" }

    { 6   ;2   ;Field     ;
                SourceExpr="Document No." }

    { 83  ;2   ;Field     ;
                SourceExpr="External Document No." }

    { 8   ;2   ;Field     ;
                SourceExpr="Account Type";
                OnValidate=BEGIN
                             GenJnlManagement.GetAccounts(Rec,AccName,BalAccName);
                           END;
                            }

    { 10  ;2   ;Field     ;
                SourceExpr="Account No.";
                OnValidate=BEGIN
                             GenJnlManagement.GetAccounts(Rec,AccName,BalAccName);
                             ShowShortcutDimCode(ShortcutDimCode);
                           END;
                            }

    { 12  ;2   ;Field     ;
                SourceExpr=Description }

    { 53  ;2   ;Field     ;
                SourceExpr="Salespers./Purch. Code";
                Visible=FALSE }

    { 95  ;2   ;Field     ;
                SourceExpr="Campaign No.";
                Visible=FALSE }

    { 55  ;2   ;Field     ;
                AssistEdit=Yes;
                SourceExpr="Currency Code";
                OnAssistEdit=BEGIN
                               ChangeExchangeRate.SetParameter("Currency Code","Currency Factor","Posting Date");
                               IF ChangeExchangeRate.RUNMODAL = ACTION::OK THEN BEGIN
                                 VALIDATE("Currency Factor",ChangeExchangeRate.GetParameter);
                               END;
                               CLEAR(ChangeExchangeRate);
                             END;
                              }

    { 77  ;2   ;Field     ;
                SourceExpr="Gen. Posting Type";
                Visible=FALSE }

    { 79  ;2   ;Field     ;
                SourceExpr="Gen. Bus. Posting Group";
                Visible=FALSE }

    { 81  ;2   ;Field     ;
                SourceExpr="Gen. Prod. Posting Group";
                Visible=FALSE }

    { 85  ;2   ;Field     ;
                SourceExpr="VAT Bus. Posting Group";
                Visible=FALSE }

    { 87  ;2   ;Field     ;
                SourceExpr="VAT Prod. Posting Group";
                Visible=FALSE }

    { 14  ;2   ;Field     ;
                SourceExpr=Amount }

    { 97  ;2   ;Field     ;
                SourceExpr="VAT Amount";
                Visible=FALSE }

    { 101 ;2   ;Field     ;
                SourceExpr="VAT Difference";
                Visible=FALSE }

    { 99  ;2   ;Field     ;
                SourceExpr="Bal. VAT Amount";
                Visible=FALSE }

    { 103 ;2   ;Field     ;
                SourceExpr="Bal. VAT Difference";
                Visible=FALSE }

    { 61  ;2   ;Field     ;
                SourceExpr="Bal. Account Type" }

    { 16  ;2   ;Field     ;
                SourceExpr="Bal. Account No.";
                OnValidate=BEGIN
                             GenJnlManagement.GetAccounts(Rec,AccName,BalAccName);
                             ShowShortcutDimCode(ShortcutDimCode);
                           END;
                            }

    { 71  ;2   ;Field     ;
                SourceExpr="Bal. Gen. Posting Type";
                Visible=FALSE }

    { 73  ;2   ;Field     ;
                SourceExpr="Bal. Gen. Bus. Posting Group";
                Visible=FALSE }

    { 75  ;2   ;Field     ;
                SourceExpr="Bal. Gen. Prod. Posting Group";
                Visible=FALSE }

    { 89  ;2   ;Field     ;
                SourceExpr="Bal. VAT Bus. Posting Group";
                Visible=FALSE }

    { 91  ;2   ;Field     ;
                SourceExpr="Bal. VAT Prod. Posting Group";
                Visible=FALSE }

    { 49  ;2   ;Field     ;
                SourceExpr="Shortcut Dimension 1 Code";
                Visible=FALSE }

    { 51  ;2   ;Field     ;
                SourceExpr="Shortcut Dimension 2 Code";
                Visible=FALSE }

    { 300 ;2   ;Field     ;
                SourceExpr=ShortcutDimCode[3];
                CaptionClass='1,2,3';
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(3,ShortcutDimCode[3]);
                           END;

                OnLookup=BEGIN
                           LookupShortcutDimCode(3,ShortcutDimCode[3]);
                         END;
                          }

    { 302 ;2   ;Field     ;
                SourceExpr=ShortcutDimCode[4];
                CaptionClass='1,2,4';
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(4,ShortcutDimCode[4]);
                           END;

                OnLookup=BEGIN
                           LookupShortcutDimCode(4,ShortcutDimCode[4]);
                         END;
                          }

    { 304 ;2   ;Field     ;
                SourceExpr=ShortcutDimCode[5];
                CaptionClass='1,2,5';
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(5,ShortcutDimCode[5]);
                           END;

                OnLookup=BEGIN
                           LookupShortcutDimCode(5,ShortcutDimCode[5]);
                         END;
                          }

    { 306 ;2   ;Field     ;
                SourceExpr=ShortcutDimCode[6];
                CaptionClass='1,2,6';
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(6,ShortcutDimCode[6]);
                           END;

                OnLookup=BEGIN
                           LookupShortcutDimCode(6,ShortcutDimCode[6]);
                         END;
                          }

    { 308 ;2   ;Field     ;
                SourceExpr=ShortcutDimCode[7];
                CaptionClass='1,2,7';
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(7,ShortcutDimCode[7]);
                           END;

                OnLookup=BEGIN
                           LookupShortcutDimCode(7,ShortcutDimCode[7]);
                         END;
                          }

    { 310 ;2   ;Field     ;
                SourceExpr=ShortcutDimCode[8];
                CaptionClass='1,2,8';
                Visible=FALSE;
                OnValidate=BEGIN
                             ValidateShortcutDimCode(8,ShortcutDimCode[8]);
                           END;

                OnLookup=BEGIN
                           LookupShortcutDimCode(8,ShortcutDimCode[8]);
                         END;
                          }

    { 1140002;2;Field     ;
                SourceExpr=Correction }

    { 18  ;2   ;Field     ;
                SourceExpr="Applies-to Doc. Type" }

    { 20  ;2   ;Field     ;
                SourceExpr="Applies-to Doc. No." }

    { 22  ;2   ;Field     ;
                SourceExpr="Applies-to ID";
                Visible=FALSE }

    { 105 ;2   ;Field     ;
                Name=GetAppliesToDocDueDate;
                CaptionML=[DEU=FÑlligkeitsdatum fÅr Ausgleich mit Beleg;
                           ENU=Applies-to Doc. Due Date];
                SourceExpr=GetAppliesToDocDueDate }

    { 67  ;2   ;Field     ;
                SourceExpr="Bank Payment Type" }

    { 59  ;2   ;Field     ;
                SourceExpr="Check Printed";
                Visible=FALSE }

    { 69  ;2   ;Field     ;
                SourceExpr="Reason Code";
                Visible=FALSE }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900919607;1;Part   ;
                SubFormLink=Table ID=CONST(81),
                            Journal Template Name=FIELD(Journal Template Name),
                            Journal Batch Name=FIELD(Journal Batch Name),
                            Journal Line No.=FIELD(Line No.);
                PagePartID=Page545;
                Visible=FALSE;
                PartType=Page }

    { 1906888707;1;Part   ;
                SubFormLink=Journal Template Name=FIELD(Journal Template Name),
                            Journal Batch Name=FIELD(Journal Batch Name),
                            Line No.=FIELD(Line No.);
                PagePartID=Page35517;
                Visible=TRUE;
                PartType=Page }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'DEU=Scheck %1 annullieren?;ENU=Void Check %1?';
      Text001@1001 : TextConst 'DEU=Alle gedruckten Schecks annullieren?;ENU=Void all printed checks?';
      ChangeExchangeRate@1002 : Form 511;
      GenJnlLine@1003 : Record 81;
      GenJnlLine2@1004 : Record 81;
      GLReconcile@1005 : Form 345;
      CreateVendorPmtSuggestion@1006 : Report 393;
      GenJnlManagement@1007 : Codeunit 230;
      ReportPrint@1008 : Codeunit 228;
      DocPrint@1009 : Codeunit 229;
      CheckManagement@1010 : Codeunit 367;
      CurrentJnlBatchName@1011 : Code[10];
      AccName@1012 : Text[50];
      BalAccName@1013 : Text[50];
      Balance@1014 : Decimal;
      TotalBalance@1015 : Decimal;
      ShowBalance@1016 : Boolean;
      ShowTotalBalance@1017 : Boolean;
      ShortcutDimCode@1018 : ARRAY [8] OF Code[20];
      OpenedFromBatch@1019 : Boolean;
      VendLedgEntry@1150000 : Record 25;
      Vend@1150014 : Record 23;
      AgeDays@1150013 : Integer;
      PaymDiscDays@1150012 : Integer;
      DueDays@1150011 : Integer;
      OeRemainAmountFC@1150010 : Decimal;
      PaymDiscDeductAmount@1150008 : Decimal;
      RemainAfterPayment@1150006 : Decimal;
      TotalPayAmount@1150005 : Decimal;
      PmtDiscount@1150007 : Decimal;
      AcceptedPaymentTol@1150009 : Decimal;
      BalanceVisible@19073040 : Boolean INDATASET;
      TotalBalanceVisible@19063333 : Boolean INDATASET;
      TotalPaymentVisible@19074440 : Boolean INDATASET;
      RemainAfterPaymentVisible@19042899 : Boolean INDATASET;

    LOCAL PROCEDURE UpdateBalance@1();
    VAR
      TempGenJourLine@1150000 : Record 81;
      TmpLineNo@1150001 : Integer;
    BEGIN
      GenJnlManagement.CalcBalance(
        Rec,xRec,Balance,TotalBalance,ShowBalance,ShowTotalBalance);
      BalanceVisible := ShowBalance;
      TotalBalanceVisible := ShowTotalBalance;

      TotalPayAmount := 0;
      TempGenJourLine.COPY(Rec);

      IF "Line No." = 0 THEN
        TmpLineNo := xRec."Line No."
      ELSE
        TmpLineNo := "Line No.";

      TempGenJourLine.MARKEDONLY(TRUE);
      IF TempGenJourLine.FIND('-') THEN
      REPEAT
        TotalBalance := TotalBalance - TempGenJourLine."Balance (LCY)";
        IF TempGenJourLine."Line No." <= TmpLineNo THEN
          Balance := Balance - TempGenJourLine."Balance (LCY)";
        IF (TempGenJourLine."Document Type" = TempGenJourLine."Document Type"::Payment) AND
           (TempGenJourLine."Account Type" = TempGenJourLine."Account Type"::Vendor)
        THEN
          TotalPayAmount := TotalPayAmount - (-TempGenJourLine."Amount (LCY)");
      UNTIL TempGenJourLine.NEXT = 0;
      TempGenJourLine.MARKEDONLY(FALSE);

      IF NOT TempGenJourLine.FIND('-') THEN BEGIN
        Balance := 0;
        TotalBalance := 0;
      END;

      TempGenJourLine.SETCURRENTKEY("Journal Template Name","Journal Batch Name","Account Type","Document Type");
      TempGenJourLine.SETRANGE("Document Type",TempGenJourLine."Document Type"::Payment);
      TempGenJourLine.SETRANGE("Account Type",TempGenJourLine."Account Type"::Vendor);
      IF TempGenJourLine.CALCSUMS("Amount (LCY)") THEN BEGIN
        TotalPayAmount := TotalPayAmount + (-TempGenJourLine."Amount (LCY)");
        TotalPaymentVisible := TRUE;
      END ELSE
        TotalPaymentVisible := FALSE;
    END;

    PROCEDURE UpdateInfoBox@2();
    VAR
      ExchRate@1150000 : Record 330;
      Currency@1150001 : Record 4;
    BEGIN
      AgeDays := 0;
      PaymDiscDays := 0;
      DueDays := 0;
      OeRemainAmountFC := 0;
      PaymDiscDeductAmount := 0;
      RemainAfterPayment := 0;
      PmtDiscount := 0;
      AcceptedPaymentTol := 0;

      VendLedgEntry.INIT;
      Vend.INIT;

      IF "Applies-to Doc. No." = '' THEN
        EXIT;

      VendLedgEntry.SETCURRENTKEY("Document No.");
      VendLedgEntry.SETRANGE("Document Type","Applies-to Doc. Type");
      VendLedgEntry.SETRANGE("Document No.","Applies-to Doc. No.");
      IF NOT VendLedgEntry.FIND('-') THEN
        EXIT;

      IF Currency.READPERMISSION THEN
        IF Currency.GET("Currency Code") THEN
          Currency.InitRoundingPrecision;
      // Std. Zahlungsbedinungen holen
      IF NOT Vend.GET(VendLedgEntry."Vendor No.") THEN;

      // Calculate Days for Age, Payment Discount
      IF "Posting Date" > 0D THEN BEGIN
        IF VendLedgEntry."Posting Date" > 0D THEN
          AgeDays := "Posting Date" - VendLedgEntry."Posting Date";
        IF VendLedgEntry."Pmt. Discount Date" > 0D THEN
          PaymDiscDays := VendLedgEntry."Pmt. Discount Date" - "Posting Date";
        IF VendLedgEntry."Due Date" > 0D THEN
          DueDays := VendLedgEntry."Due Date" - "Posting Date";
      END;

      VendLedgEntry.CALCFIELDS("Remaining Amount","Remaining Amt. (LCY)");
      OeRemainAmountFC := -VendLedgEntry."Remaining Amount";
      PmtDiscount :=  -VendLedgEntry."Remaining Pmt. Disc. Possible";
      PmtDiscount :=
        ExchRate.ExchangeAmtFCYToFCY("Posting Date",VendLedgEntry."Currency Code",
          "Currency Code",-VendLedgEntry."Remaining Pmt. Disc. Possible");
      PmtDiscount := ROUND(PmtDiscount,Currency."Amount Rounding Precision");


      // calculate FC-amount of open entries and remaining amount
      IF VendLedgEntry."Currency Code" <> "Currency Code" THEN BEGIN
        OeRemainAmountFC :=
          ExchRate.ExchangeAmtLCYToFCY(
          "Posting Date","Currency Code",-VendLedgEntry."Remaining Amt. (LCY)","Currency Factor");
        OeRemainAmountFC := ROUND(OeRemainAmountFC,Currency."Amount Rounding Precision");
      END;

      IF (VendLedgEntry."Pmt. Discount Date" >= "Posting Date") OR
         ((VendLedgEntry."Pmt. Disc. Tolerance Date" >= "Posting Date") AND
         (VendLedgEntry."Accepted Pmt. Disc. Tolerance"))
      THEN BEGIN
        PaymDiscDeductAmount := -VendLedgEntry."Remaining Pmt. Disc. Possible";
        IF VendLedgEntry."Currency Code" <> "Currency Code" THEN
          PaymDiscDeductAmount :=
            ExchRate.ExchangeAmtFCYToFCY(
              "Posting Date",VendLedgEntry."Currency Code","Currency Code",PaymDiscDeductAmount);
      END;
      PaymDiscDeductAmount := ROUND(PaymDiscDeductAmount,Currency."Amount Rounding Precision");

      AcceptedPaymentTol := -VendLedgEntry."Accepted Payment Tolerance";
        IF VendLedgEntry."Currency Code" <> "Currency Code" THEN
          AcceptedPaymentTol :=
            ExchRate.ExchangeAmtFCYToFCY(
              "Posting Date",VendLedgEntry."Currency Code","Currency Code",AcceptedPaymentTol);
      AcceptedPaymentTol := ROUND(AcceptedPaymentTol,Currency."Amount Rounding Precision");

      CASE TRUE OF
        "Account Type" = "Account Type"::Vendor:
          RemainAfterPayment := OeRemainAmountFC - Amount - PaymDiscDeductAmount -
            AcceptedPaymentTol;
        "Bal. Account Type" = "Bal. Account Type"::Vendor:
          RemainAfterPayment := OeRemainAmountFC + Amount - PaymDiscDeductAmount -
            AcceptedPaymentTol;
      END;

      RemainAfterPaymentVisible := TRUE;
      IF ("Currency Code" <> VendLedgEntry."Currency Code") AND
        (("Currency Code" <> '') AND (VendLedgEntry."Currency Code" <> ''))
      THEN BEGIN
         RemainAfterPaymentVisible := FALSE;
         EXIT;
      END;

      // Pmt. Disc is not applied if entry is not closed
      IF RemainAfterPayment > 0 THEN BEGIN
        RemainAfterPayment := RemainAfterPayment + PaymDiscDeductAmount;
        PaymDiscDeductAmount := 0;
      END;
    END;

    LOCAL PROCEDURE CurrentJnlBatchNameOnAfterVali@19002411();
    BEGIN
      CurrPage.SAVERECORD;
      GenJnlManagement.SetName(CurrentJnlBatchName,Rec);
      CurrPage.UPDATE(FALSE);
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    BEGIN
      xRec := Rec;
      GenJnlManagement.GetAccounts(Rec,AccName,BalAccName);
      UpdateBalance;

      UpdateInfoBox;
    END;

    BEGIN
    END.
  }
}

