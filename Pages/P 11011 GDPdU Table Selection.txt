OBJECT Page 11011 GDPdU Table Selection
{
  OBJECT-PROPERTIES
  {
    Date=01.10.04;
    Time=12:00:00;
    Version List=NAVDACH4.00;
  }
  PROPERTIES
  {
    CaptionML=[DEU=GDPdU Tabellenauswahl;
               ENU=GDPdU Table Selection];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table2000000058;
    SourceTableView=SORTING(Object Type,Object ID)
                    WHERE(Object Type=CONST(Table),
                          Object ID=FILTER(<2000000000));
    PageType=Card;
    OnFindRecord=BEGIN
                   IF FIND(Which) THEN BEGIN

                     AllObj := Rec;
                     WHILE TRUE DO
                       IF NOT GDPdUMgt.FindGDPdURecDefTable(GDPdUDefGrpCode,GDPdURecordCode,"Object ID") THEN
                         EXIT(TRUE)
                       ELSE
                         IF NEXT(1) = 0 THEN BEGIN
                           Rec := AllObj;
                           IF FIND(Which) THEN
                             WHILE TRUE DO
                               IF NOT GDPdUMgt.FindGDPdURecDefTable(GDPdUDefGrpCode,GDPdURecordCode,"Object ID") THEN
                                 EXIT(TRUE)
                               ELSE
                                 IF NEXT(-1) = 0 THEN
                                   EXIT(FALSE);
                         END;
                   END;
                   EXIT(FALSE);
                 END;

    OnNextRecord=VAR
                   Nextsteps@1140000 : Integer;
                   Realsteps@1140001 : Integer;
                 BEGIN
                   IF Steps = 0 THEN
                     EXIT;

                   AllObj := Rec;
                   REPEAT
                     Nextsteps := NEXT(Steps/ABS(Steps));
                     IF NOT GDPdUMgt.FindGDPdURecDefTable(GDPdUDefGrpCode,GDPdURecordCode,"Object ID") THEN BEGIN
                       Realsteps := Realsteps + Nextsteps;
                       AllObj := Rec;
                     END;
                   UNTIL (Nextsteps = 0) OR (Realsteps = Steps);
                   Rec := AllObj;
                   FIND;
                   EXIT(Realsteps);
                 END;

  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1140000;1;Group     ;
                GroupType=Repeater }

    { 1140001;2;Field     ;
                CaptionML=[DEU=Ausgew„hlt;
                           ENU=Selected];
                SourceExpr=Selected;
                OnValidate=BEGIN
                             SelectedOnPush;
                           END;
                            }

    { 1140003;2;Field     ;
                CaptionML=[DEU=Tabellennr.;
                           ENU=Table No.];
                SourceExpr="Object ID";
                Editable=FALSE }

    { 1140005;2;Field     ;
                CaptionML=[DEU=Tabellenname;
                           ENU=Table Name];
                SourceExpr="Object Caption";
                Editable=FALSE }

  }
  CODE
  {
    VAR
      Text1140000@1140000 : TextConst 'DEU=Sie drfen nur eine Tabelle ausw„hlen.;ENU=You must only choose one table.';
      TempAllObj@11400001 : TEMPORARY Record 2000000058;
      AllObj@1140002 : Record 2000000058;
      GDPdUMgt@1140003 : Codeunit 11000;
      GDPdUDefGrpCode@1140004 : Code[10];
      GDPdURecordCode@1140005 : Code[10];

    LOCAL PROCEDURE SelectObject@1140000();
    BEGIN
      IF TempAllObj.FIND('-') THEN
        IF TempAllObj."Object ID" <> "Object ID" THEN
          ERROR(Text1140000);

      IF NOT Selected THEN BEGIN
        TempAllObj := Rec;
        TempAllObj.INSERT;
      END ELSE BEGIN
        TempAllObj := Rec;
        TempAllObj.DELETE;
      END;
    END;

    LOCAL PROCEDURE Selected@1140001() : Boolean;
    BEGIN
      EXIT(TempAllObj.GET("Object Type","Object ID"));
    END;

    PROCEDURE SetGDPdUCode@1140002(NewGDPdUDefGrpCode@1140000 : Code[10];NewGDPdURecordCode@1140001 : Code[10]);
    BEGIN
      GDPdUDefGrpCode := NewGDPdUDefGrpCode;
      GDPdURecordCode := NewGDPdURecordCode;
      CurrPage.UPDATE;
    END;

    PROCEDURE GetTableNo@1140003() : Integer;
    BEGIN
      EXIT(TempAllObj."Object ID");
    END;

    PROCEDURE DeleteTempTable@1140004();
    BEGIN
      TempAllObj.DELETEALL;
      CurrPage.UPDATE;
    END;

    LOCAL PROCEDURE SelectedOnPush@19002808();
    BEGIN
      SelectObject;
      CurrPage.UPDATE;
    END;

    BEGIN
    END.
  }
}

