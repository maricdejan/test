OBJECT Page 5050 Contact Card
{
  OBJECT-PROPERTIES
  {
    Date=11.12.10;
    Time=12:00:00;
    Version List=NAVW16.00.10;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Kontaktkarte;
               ENU=Contact Card];
    SourceTable=Table5050;
    PageType=ListPlus;
    OnInit=BEGIN
             NoofJobResponsibilitiesEnable := TRUE;
             OrganizationalLevelCodeEnable := TRUE;
             "Company NameEnable" := TRUE;
             "Company No.Enable" := TRUE;
             "VAT Registration No.Enable" := TRUE;
             "Currency CodeEnable" := TRUE;
             MapPointVisible := TRUE;
           END;

    OnOpenPage=VAR
                 MapMgt@1000 : Codeunit 802;
               BEGIN
                 IF NOT MapMgt.TestSetup THEN
                   MapPointVisible := FALSE;
               END;

    OnFindRecord=VAR
                   RecordFound@1000 : Boolean;
                 BEGIN
                   RecordFound := FIND(Which);
                   CurrPage.EDITABLE := RecordFound OR (GETFILTER("No.") = '');
                   EXIT(RecordFound);
                 END;

    OnAfterGetRecord=BEGIN
                       OnAfterGetCurrRecord;
                     END;

    OnNewRecord=BEGIN
                  OnAfterGetCurrRecord;
                END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 76      ;1   ;ActionGroup;
                      CaptionML=[DEU=Ko&ntakt;
                                 ENU=C&ontact] }
      { 79      ;2   ;Action    ;
                      CaptionML=[DEU=Zugehîrige Ko&ntakte;
                                 ENU=Relate&d Contacts];
                      RunObject=Page 5052;
                      RunFormLink=Company No.=FIELD(Company No.) }
      { 82      ;2   ;ActionGroup;
                      CaptionML=[DEU=Un&ternehmen;
                                 ENU=Comp&any] }
      { 85      ;3   ;Action    ;
                      CaptionML=[DEU=GeschÑftsbeziehungen;
                                 ENU=Business Relations];
                      RunObject=Page 5061;
                      RunFormLink=Contact No.=FIELD(Company No.) }
      { 83      ;3   ;Action    ;
                      CaptionML=[DEU=Branchen;
                                 ENU=Industry Groups];
                      RunObject=Page 5067;
                      RunFormLink=Contact No.=FIELD(Company No.) }
      { 84      ;3   ;Action    ;
                      CaptionML=[DEU=Internetfavoriten;
                                 ENU=Web Sources];
                      RunObject=Page 5070;
                      RunFormLink=Contact No.=FIELD(Company No.) }
      { 80      ;2   ;ActionGroup;
                      CaptionML=[DEU=&Person;
                                 ENU=P&erson] }
      { 81      ;3   ;Action    ;
                      CaptionML=[DEU=Verantwortlichkeiten;
                                 ENU=Job Responsibilities];
                      OnAction=VAR
                                 ContJobResp@1001 : Record 5067;
                               BEGIN
                                 TESTFIELD(Type,Type::Person);
                                 ContJobResp.SETRANGE("Contact No.","No.");
                                 FORM.RUNMODAL(FORM::"Contact Job Responsibilities",ContJobResp);
                               END;
                                }
      { 86      ;2   ;Action    ;
                      CaptionML=[DEU=&Verteiler;
                                 ENU=Mailing &Groups];
                      RunObject=Page 5064;
                      RunFormLink=Contact No.=FIELD(No.) }
      { 87      ;2   ;Action    ;
                      CaptionML=[DEU=Pr&ofile;
                                 ENU=Pro&files];
                      OnAction=VAR
                                 ProfileManagement@1001 : Codeunit 5059;
                               BEGIN
                                 ProfileManagement.ShowContactQuestionnaireCard(Rec,'',0);
                               END;
                                }
      { 88      ;2   ;Action    ;
                      ShortCutKey=F7;
                      CaptionML=[DEU=Statistik;
                                 ENU=Statistics];
                      RunObject=Page 5053;
                      RunFormLink=No.=FIELD(No.);
                      Promoted=Yes;
                      Image=Statistics;
                      PromotedCategory=Process }
      { 89      ;2   ;Action    ;
                      CaptionML=[DEU=Bi&ld;
                                 ENU=&Picture];
                      RunObject=Page 5104;
                      RunFormLink=No.=FIELD(No.) }
      { 90      ;2   ;Action    ;
                      CaptionML=[DEU=Be&merkungen;
                                 ENU=Co&mments];
                      RunObject=Page 5072;
                      RunFormLink=Table Name=CONST(Contact),
                                  No.=FIELD(No.),
                                  Sub No.=CONST(0);
                      Image=ViewComments }
      { 91      ;2   ;ActionGroup;
                      CaptionML=[DEU=Al&ternative Adressen;
                                 ENU=Alternati&ve Address] }
      { 92      ;3   ;Action    ;
                      CaptionML=[DEU=Karte;
                                 ENU=Card];
                      RunObject=Page 5057;
                      RunFormLink=Contact No.=FIELD(No.);
                      Image=EditLines }
      { 93      ;3   ;Action    ;
                      CaptionML=[DEU=GÅltigkeiten;
                                 ENU=Date Ranges];
                      RunObject=Page 5059;
                      RunFormLink=Contact No.=FIELD(No.) }
      { 94      ;2   ;Separator ;
                      CaptionML=[DEU="";
                                 ENU=""] }
      { 95      ;2   ;Action    ;
                      ShortCutKey=Ctrl+F7;
                      CaptionML=[DEU=Aktivit&Ñtenprotokollposten;
                                 ENU=Interaction Log E&ntries];
                      RunObject=Page 5076;
                      RunFormView=SORTING(Contact Company No.,Date,Contact No.,Canceled,Initiated By,Attempt Failed);
                      RunFormLink=Contact Company No.=FIELD(Company No.),
                                  Contact No.=FILTER(<>''),
                                  Contact No.=FIELD(FILTER(Lookup Contact No.)) }
      { 149     ;2   ;Action    ;
                      CaptionML=[DEU=ZurÅckgestellte &AktivitÑten;
                                 ENU=Postponed &Interactions];
                      RunObject=Page 5082;
                      RunFormView=SORTING(Contact Company No.,Date,Contact No.,Canceled,Initiated By,Attempt Failed);
                      RunFormLink=Contact Company No.=FIELD(Company No.),
                                  Contact No.=FILTER(<>''),
                                  Contact No.=FIELD(FILTER(Lookup Contact No.)) }
      { 96      ;2   ;Action    ;
                      CaptionML=[DEU=Aufga&ben;
                                 ENU=T&o-dos];
                      RunObject=Page 5096;
                      RunFormView=SORTING(Contact Company No.,Date,Contact No.,Closed);
                      RunFormLink=Contact Company No.=FIELD(Company No.),
                                  Contact No.=FIELD(FILTER(Lookup Contact No.)),
                                  System To-do Type=FILTER(Contact Attendee) }
      { 151     ;2   ;ActionGroup;
                      CaptionML=[DEU=Verkaufs&chancen;
                                 ENU=Oppo&rtunities] }
      { 97      ;3   ;Action    ;
                      CaptionML=[DEU=öbersicht;
                                 ENU=List];
                      RunObject=Page 5123;
                      RunFormView=SORTING(Contact Company No.,Contact No.);
                      RunFormLink=Contact Company No.=FIELD(Company No.),
                                  Contact No.=FILTER(<>''),
                                  Contact No.=FIELD(FILTER(Lookup Contact No.)) }
      { 100     ;2   ;Action    ;
                      CaptionML=[DEU=S&egmente;
                                 ENU=Segmen&ts];
                      RunObject=Page 5150;
                      RunFormView=SORTING(Contact No.,Segment No.);
                      RunFormLink=Contact Company No.=FIELD(Company No.),
                                  Contact No.=FILTER(<>''),
                                  Contact No.=FIELD(FILTER(Lookup Contact No.));
                      Image=Segment }
      { 98      ;2   ;Separator ;
                      CaptionML=[DEU="";
                                 ENU=""] }
      { 116     ;2   ;Action    ;
                      CaptionML=[DEU=&Angebote;
                                 ENU=Sales &Quotes];
                      RunObject=Page 9300;
                      RunFormView=SORTING(Document Type,Sell-to Contact No.);
                      RunFormLink=Sell-to Contact No.=FIELD(No.);
                      Promoted=Yes;
                      Image=Quote;
                      PromotedCategory=Process }
      { 119     ;2   ;Separator  }
      { 99      ;2   ;Action    ;
                      CaptionML=[DEU=&Debitor/Kreditor/Bankkonto;
                                 ENU=C&ustomer/Vendor/Bank Acc.];
                      OnAction=BEGIN
                                 ShowCustVendBank;
                               END;
                                }
      { 62      ;2   ;Separator  }
      { 63      ;2   ;Action    ;
                      CaptionML=[DEU=Online Map;
                                 ENU=Online Map];
                      OnAction=BEGIN
                                 DisplayMap;
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 75      ;1   ;ActionGroup;
                      CaptionML=[DEU=F&unktion;
                                 ENU=F&unctions] }
      { 102     ;2   ;Action    ;
                      CaptionML=[DEU=&Internetfavorit aufrufen;
                                 ENU=Launch &Web Source];
                      OnAction=VAR
                                 ContactWebSource@1001 : Record 5060;
                               BEGIN
                                 ContactWebSource.SETRANGE("Contact No.","Company No.");
                                 IF FORM.RUNMODAL(FORM::"Web Source Launch",ContactWebSource) = ACTION::LookupOK THEN
                                   ContactWebSource.Launch;
                               END;
                                }
      { 103     ;2   ;Action    ;
                      CaptionML=[DEU=&Deckblatt drucken;
                                 ENU=Print Cover &Sheet];
                      OnAction=VAR
                                 Cont@1001 : Record 5050;
                               BEGIN
                                 Cont := Rec;
                                 Cont.SETRECFILTER;
                                 REPORT.RUN(REPORT::"Contact - Cover Sheet",TRUE,FALSE,Cont);
                               END;
                                }
      { 105     ;2   ;ActionGroup;
                      CaptionML=[DEU=Erstellen als;
                                 ENU=Create as] }
      { 106     ;3   ;Action    ;
                      CaptionML=[DEU=Debitor;
                                 ENU=Customer];
                      OnAction=BEGIN
                                 CreateCustomer(ChooseCustomerTemplate);
                               END;
                                }
      { 107     ;3   ;Action    ;
                      CaptionML=[DEU=Kreditor;
                                 ENU=Vendor];
                      OnAction=BEGIN
                                 CreateVendor;
                               END;
                                }
      { 108     ;3   ;Action    ;
                      CaptionML=[DEU=Bankkonto;
                                 ENU=Bank];
                      OnAction=BEGIN
                                 CreateBankAccount;
                               END;
                                }
      { 109     ;2   ;ActionGroup;
                      CaptionML=[DEU=VerknÅpfen mit;
                                 ENU=Link with existing] }
      { 110     ;3   ;Action    ;
                      CaptionML=[DEU=Debitor;
                                 ENU=Customer];
                      OnAction=BEGIN
                                 CreateCustomerLink;
                               END;
                                }
      { 111     ;3   ;Action    ;
                      CaptionML=[DEU=Kreditor;
                                 ENU=Vendor];
                      OnAction=BEGIN
                                 CreateVendorLink;
                               END;
                                }
      { 112     ;3   ;Action    ;
                      CaptionML=[DEU=Bankkonto;
                                 ENU=Bank];
                      OnAction=BEGIN
                                 CreateBankAccountLink;
                               END;
                                }
      { 146     ;2   ;Action    ;
                      CaptionML=[DEU=&Kontaktsuche;
                                 ENU=Co&ntact Search];
                      OnAction=VAR
                                 SearchResult@1001 : Record 5113;
                                 ContactSearchForm@1000 : Form 5177;
                               BEGIN
                                 ContactSearchForm.LOOKUPMODE(TRUE);
                                 IF ContactSearchForm.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                   ContactSearchForm.GETRECORD(SearchResult);
                                   IF GET(SearchResult."Contact No.") THEN;
                                 END;
                               END;
                                }
      { 152     ;2   ;Separator  }
      { 153     ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Vorlage anwenden;
                                 ENU=Apply Template];
                      Promoted=Yes;
                      Image=ApplyTemplate;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 TemplateMgt@1000 : Codeunit 8612;
                                 RecRef@1001 : RecordRef;
                               BEGIN
                                 RecRef.GETTABLE(Rec);
                                 TemplateMgt.UpdateFromTemplateSelection(RecRef);
                               END;
                                }
      { 77      ;1   ;Action    ;
                      CaptionML=[DEU=AktivitÑt &erst.;
                                 ENU=Create &Interact];
                      Promoted=Yes;
                      Image=CreateInteraction;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CreateInteraction;
                               END;
                                }
      { 1900000006;0 ;ActionContainer;
                      ActionContainerType=Reports }
      { 1907415106;1 ;Action    ;
                      CaptionML=[DEU=Kontakt Deckblatt;
                                 ENU=Contact Cover Sheet];
                      RunObject=Report 5055;
                      Promoted=Yes;
                      PromotedCategory=Report }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                CaptionML=[DEU=Allgemein;
                           ENU=General] }

    { 2   ;2   ;Field     ;
                SourceExpr="No.";
                OnAssistEdit=BEGIN
                               IF AssistEdit(xRec) THEN
                                 CurrPage.UPDATE;
                             END;
                              }

    { 4   ;2   ;Field     ;
                SourceExpr=Type;
                OnValidate=BEGIN
                             TypeOnAfterValidate;
                           END;
                            }

    { 6   ;2   ;Field     ;
                SourceExpr="Company No.";
                Enabled="Company No.Enable" }

    { 8   ;2   ;Field     ;
                AssistEdit=Yes;
                SourceExpr="Company Name";
                Enabled="Company NameEnable";
                OnAssistEdit=BEGIN
                               Cont.SETRANGE("No.","Company No.");
                               CLEAR(CompanyDetails);
                               CompanyDetails.SETTABLEVIEW(Cont);
                               CompanyDetails.SETRECORD(Cont);
                               IF Type = Type::Person THEN
                                 CompanyDetails.EDITABLE := FALSE;
                               CompanyDetails.RUNMODAL;
                             END;
                              }

    { 10  ;2   ;Field     ;
                AssistEdit=Yes;
                SourceExpr=Name;
                OnAssistEdit=BEGIN
                               MODIFY;
                               COMMIT;
                               Cont.SETRANGE("No.","No.");
                               IF Type = Type::Person THEN BEGIN
                                 CLEAR(NameDetails);
                                 NameDetails.SETTABLEVIEW(Cont);
                                 NameDetails.SETRECORD(Cont);
                                 NameDetails.RUNMODAL;
                               END ELSE BEGIN
                                 CLEAR(CompanyDetails);
                                 CompanyDetails.SETTABLEVIEW(Cont);
                                 CompanyDetails.SETRECORD(Cont);
                                 CompanyDetails.RUNMODAL;
                               END;
                               GET("No.");
                               CurrPage.UPDATE;
                             END;
                              }

    { 12  ;2   ;Field     ;
                SourceExpr=Address }

    { 14  ;2   ;Field     ;
                SourceExpr="Address 2" }

    { 16  ;2   ;Field     ;
                SourceExpr="Post Code" }

    { 18  ;2   ;Field     ;
                SourceExpr="Country/Region Code" }

    { 34  ;2   ;Field     ;
                SourceExpr=City }

    { 20  ;2   ;Field     ;
                SourceExpr="Search Name" }

    { 22  ;2   ;Field     ;
                SourceExpr="Phone No." }

    { 24  ;2   ;Field     ;
                SourceExpr="Salesperson Code" }

    { 117 ;2   ;Field     ;
                SourceExpr="Salutation Code" }

    { 26  ;2   ;Field     ;
                SourceExpr="Last Date Modified" }

    { 28  ;2   ;Field     ;
                SourceExpr="Date of Last Interaction";
                OnDrillDown=VAR
                              InteractionLogEntry@1000 : Record 5065;
                            BEGIN
                              InteractionLogEntry.SETCURRENTKEY("Contact Company No.",Date,"Contact No.",Canceled,"Initiated By","Attempt Failed");
                              InteractionLogEntry.SETRANGE("Contact Company No.","Company No.");
                              InteractionLogEntry.SETFILTER("Contact No.","Lookup Contact No.");
                              InteractionLogEntry.SETRANGE("Attempt Failed",FALSE);
                              IF InteractionLogEntry.FIND('+') THEN
                                FORM.RUN(0,InteractionLogEntry);
                            END;
                             }

    { 30  ;2   ;Field     ;
                SourceExpr="Last Date Attempted";
                OnDrillDown=VAR
                              InteractionLogEntry@1000 : Record 5065;
                            BEGIN
                              InteractionLogEntry.SETCURRENTKEY("Contact Company No.",Date,"Contact No.",Canceled,"Initiated By","Attempt Failed");
                              InteractionLogEntry.SETRANGE("Contact Company No.","Company No.");
                              InteractionLogEntry.SETFILTER("Contact No.","Lookup Contact No.");
                              InteractionLogEntry.SETRANGE("Initiated By",InteractionLogEntry."Initiated By"::Us);
                              IF InteractionLogEntry.FIND('+') THEN
                                FORM.RUN(0,InteractionLogEntry);
                            END;
                             }

    { 32  ;2   ;Field     ;
                SourceExpr="Next To-do Date" }

    { 72  ;1   ;Part      ;
                SubFormLink=Contact No.=FIELD(No.);
                PagePartID=Page5051 }

    { 1902768601;1;Group  ;
                CaptionML=[DEU=Kommunikation;
                           ENU=Communication] }

    { 35  ;2   ;Field     ;
                SourceExpr="Phone No." }

    { 36  ;2   ;Field     ;
                SourceExpr="Mobile Phone No." }

    { 38  ;2   ;Field     ;
                SourceExpr="Fax No." }

    { 40  ;2   ;Field     ;
                SourceExpr="Telex No." }

    { 42  ;2   ;Field     ;
                SourceExpr=Pager }

    { 44  ;2   ;Field     ;
                SourceExpr="Telex Answer Back" }

    { 46  ;2   ;Field     ;
                SourceExpr="E-Mail" }

    { 48  ;2   ;Field     ;
                SourceExpr="Home Page" }

    { 130 ;2   ;Field     ;
                SourceExpr="Language Code" }

    { 128 ;2   ;Field     ;
                SourceExpr="Salutation Code" }

    { 126 ;2   ;Field     ;
                SourceExpr="Correspondence Type" }

    { 125 ;2   ;Field     ;
                SourceExpr=IntegrationCustomerNo;
                Visible=FALSE;
                OnValidate=VAR
                             Customer@1000 : Record 18;
                             ContactBusinessRelation@1001 : Record 5054;
                           BEGIN
                             IF NOT (IntegrationCustomerNo = '') THEN BEGIN
                               Customer.GET(IntegrationCustomerNo);
                               ContactBusinessRelation.SETCURRENTKEY("Link to Table","No.");
                               ContactBusinessRelation.SETRANGE("Link to Table",ContactBusinessRelation."Link to Table"::Customer);
                               ContactBusinessRelation.SETRANGE("No.",Customer."No.");
                               IF ContactBusinessRelation.FINDFIRST THEN
                                 VALIDATE("Company No.",ContactBusinessRelation."Contact No.");
                             END ELSE
                               VALIDATE("Company No.",'');
                           END;
                            }

    { 101 ;2   ;Field     ;
                SourceExpr="First Name";
                Visible=FALSE }

    { 121 ;2   ;Field     ;
                SourceExpr="Middle Name";
                Visible=FALSE }

    { 123 ;2   ;Field     ;
                SourceExpr=Surname;
                Visible=FALSE }

    { 1904829601;1;Group  ;
                CaptionML=[DEU=Segmentierung;
                           ENU=Segmentation] }

    { 50  ;2   ;Field     ;
                SourceExpr="No. of Mailing Groups";
                OnDrillDown=VAR
                              ContMailingGrp@1001 : Record 5056;
                            BEGIN
                              CurrPage.SAVERECORD;
                              COMMIT;
                              ContMailingGrp.SETRANGE("Contact No.","No.");
                              FORM.RUNMODAL(FORM::"Contact Mailing Groups",ContMailingGrp);
                              CurrPage.UPDATE(FALSE);
                            END;
                             }

    { 52  ;2   ;Field     ;
                SourceExpr="No. of Business Relations";
                OnDrillDown=VAR
                              ContBusRel@1001 : Record 5054;
                            BEGIN
                              CurrPage.SAVERECORD;
                              COMMIT;
                              ContBusRel.SETRANGE("Contact No.","Company No.");
                              FORM.RUNMODAL(FORM::"Contact Business Relations",ContBusRel);
                              CurrPage.UPDATE(FALSE);
                            END;
                             }

    { 54  ;2   ;Field     ;
                SourceExpr="No. of Industry Groups";
                OnDrillDown=VAR
                              ContIndustGrp@1001 : Record 5058;
                            BEGIN
                              CurrPage.SAVERECORD;
                              COMMIT;
                              ContIndustGrp.SETRANGE("Contact No.","Company No.");
                              FORM.RUNMODAL(FORM::"Contact Industry Groups",ContIndustGrp);
                              CurrPage.UPDATE(FALSE);
                            END;
                             }

    { 56  ;2   ;Field     ;
                SourceExpr="No. of Job Responsibilities";
                Enabled=NoofJobResponsibilitiesEnable;
                OnDrillDown=VAR
                              ContJobResp@1001 : Record 5067;
                            BEGIN
                              CurrPage.SAVERECORD;
                              COMMIT;
                              ContJobResp.SETRANGE("Contact No.","No.");
                              FORM.RUNMODAL(FORM::"Contact Job Responsibilities",ContJobResp);
                              CurrPage.UPDATE(FALSE);
                            END;
                             }

    { 58  ;2   ;Field     ;
                SourceExpr="Organizational Level Code";
                Enabled=OrganizationalLevelCodeEnable }

    { 60  ;2   ;Field     ;
                SourceExpr="Exclude from Segment" }

    { 1907468901;1;Group  ;
                CaptionML=[DEU=Au·enhandel;
                           ENU=Foreign Trade] }

    { 64  ;2   ;Field     ;
                SourceExpr="Currency Code";
                Enabled="Currency CodeEnable" }

    { 68  ;2   ;Field     ;
                SourceExpr="Territory Code" }

    { 66  ;2   ;Field     ;
                SourceExpr="VAT Registration No.";
                Enabled="VAT Registration No.Enable" }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=TRUE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=TRUE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      Cont@1000 : Record 5050;
      CompanyDetails@1001 : Form 5054;
      NameDetails@1002 : Form 5055;
      IntegrationCustomerNo@1003 : Code[20];
      MapPointVisible@19022708 : Boolean INDATASET;
      "Currency CodeEnable"@19025310 : Boolean INDATASET;
      "VAT Registration No.Enable"@19054634 : Boolean INDATASET;
      "Company No.Enable"@19064179 : Boolean INDATASET;
      "Company NameEnable"@19044404 : Boolean INDATASET;
      OrganizationalLevelCodeEnable@19070485 : Boolean INDATASET;
      NoofJobResponsibilitiesEnable@19004457 : Boolean INDATASET;

    PROCEDURE EnableFields@1();
    BEGIN
      "Currency CodeEnable" := Type = Type::Company;
      "VAT Registration No.Enable" := Type = Type::Company;
      "Company No.Enable" := Type = Type::Person;
      "Company NameEnable" := Type = Type::Person;
      OrganizationalLevelCodeEnable := Type = Type::Person;
      NoofJobResponsibilitiesEnable := Type = Type::Person;
    END;

    PROCEDURE IntegrationFindCustomerNo@2();
    VAR
      ContactBusinessRelation@1000 : Record 5054;
    BEGIN
      ContactBusinessRelation.SETCURRENTKEY("Link to Table","Contact No.");
      ContactBusinessRelation.SETRANGE("Link to Table",ContactBusinessRelation."Link to Table"::Customer);
      ContactBusinessRelation.SETRANGE("Contact No.","Company No.");
      IF ContactBusinessRelation.FINDFIRST THEN BEGIN
        IntegrationCustomerNo := ContactBusinessRelation."No.";
      END ELSE BEGIN
        IntegrationCustomerNo := '';
      END;
    END;

    LOCAL PROCEDURE TypeOnAfterValidate@19069045();
    BEGIN
      EnableFields;
    END;

    LOCAL PROCEDURE OnAfterGetCurrRecord@19077479();
    BEGIN
      xRec := Rec;
      EnableFields;

      IF Type = Type::Person THEN
        IntegrationFindCustomerNo;
    END;

    BEGIN
    END.
  }
}

