OBJECT Page 9170 Profile Card
{
  OBJECT-PROPERTIES
  {
    Date=14.08.09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[DEU=Profilkarte;
               ENU=Profile Card];
    SourceTable=Table2000000072;
    DataCaptionExpr="Profile ID" + ' ' + Description;
    PageType=Card;
    OnOpenPage=BEGIN
                 RoleCenterSubtype := 'RoleCenter';
               END;

    OnDeleteRecord=BEGIN
                     ConfPersMgt.DeleteProfile(Rec);
                   END;

    ActionList=ACTIONS
    {
      { 1900000003;0 ;ActionContainer;
                      ActionContainerType=RelatedInformation }
      { 13      ;1   ;ActionGroup;
                      CaptionML=[DEU=&Profil;
                                 ENU=&Profile] }
      { 17      ;2   ;Action    ;
                      ShortCutKey=Shift+Ctrl+L;
                      CaptionML=[DEU=šbersicht;
                                 ENU=List];
                      OnAction=VAR
                                 ProfileList@1102601000 : Form 9171;
                               BEGIN
                                 ProfileList.LOOKUPMODE := TRUE;
                                 ProfileList.SETRECORD(Rec);
                                 IF ProfileList.RUNMODAL = ACTION::LookupOK THEN
                                   ProfileList.GETRECORD(Rec);
                               END;
                                }
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 20      ;1   ;ActionGroup;
                      CaptionML=[DEU=F&unktion;
                                 ENU=F&unctions] }
      { 33      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Profil kopieren;
                                 ENU=Copy Profile];
                      OnAction=VAR
                                 Profile@1035 : Record 2000000072;
                                 CopyProfile@1034 : Report 9170;
                               BEGIN
                                 Profile.SETRANGE("Profile ID","Profile ID");
                                 CopyProfile.SETTABLEVIEW(Profile);
                                 CopyProfile.RUNMODAL;

                                 IF GET(CopyProfile.GetProfileID) THEN;
                               END;
                                }
      { 23      ;2   ;Action    ;
                      CaptionML=[DEU=Konfigurierte Seiten &l”schen;
                                 ENU=C&lear Configured Pages];
                      OnAction=BEGIN
                                 ConfPersMgt.ClearProfileConfiguration(Rec);
                               END;
                                }
      { 50      ;2   ;Separator  }
      { 22      ;2   ;Action    ;
                      Ellipsis=Yes;
                      CaptionML=[DEU=Profile e&xportieren;
                                 ENU=E&xport Profiles];
                      OnAction=VAR
                                 Profile@1001 : Record 2000000072;
                                 ExportProfiles@1000 : Report 9171;
                               BEGIN
                                 Profile.SETRANGE("Profile ID","Profile ID");
                                 ExportProfiles.SETTABLEVIEW(Profile);
                                 ExportProfiles.RUNMODAL;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1   ;1   ;Group     ;
                CaptionML=[DEU=Allgemein;
                           ENU=General] }

    { 2   ;2   ;Field     ;
                CaptionML=[DEU=Profil-ID;
                           ENU=Profile ID];
                SourceExpr="Profile ID" }

    { 6   ;2   ;Field     ;
                DrillDown=No;
                CaptionML=[DEU=Besitzer-ID;
                           ENU=Owner ID];
                SourceExpr="Owner ID";
                Editable=FALSE;
                OnAssistEdit=VAR
                               LoginMgt@1002 : Codeunit 418;
                               SID@1003 : Text[169];
                               UserID@1001 : Code[20];
                             BEGIN
                               LoginMgt.LookupUser(UserID,SID,FALSE);

                               IF (SID <> "Owner SID") AND (SID <> '') THEN BEGIN
                                 VALIDATE("Owner SID",SID);
                                 CALCFIELDS("Owner ID");

                                 CurrPage.UPDATE;
                               END;
                             END;
                              }

    { 8   ;2   ;Field     ;
                SourceExpr=Description }

    { 11  ;2   ;Field     ;
                CaptionML=[DEU=Rollencenter-ID;
                           ENU=Role Center ID];
                BlankZero=Yes;
                SourceExpr="Role Center ID";
                OnValidate=VAR
                             AllObjWithCaption@1102601001 : Record 2000000058;
                           BEGIN
                             IF "Default Role Center" THEN
                               TESTFIELD("Role Center ID");

                             AllObjWithCaption.GET(AllObjWithCaption."Object Type"::Page,"Role Center ID");
                             AllObjWithCaption.TESTFIELD("Object Subtype",RoleCenterSubtype);
                           END;

                OnLookup=VAR
                           AllObjWithCaption@1003 : Record 2000000058;
                           AllObjectsWithCaption@1002 : Form 9174;
                         BEGIN
                           AllObjWithCaption.SETRANGE("Object Type",AllObjWithCaption."Object Type"::Page);
                           AllObjWithCaption.SETRANGE("Object Subtype",RoleCenterSubtype);
                           AllObjectsWithCaption.SETTABLEVIEW(AllObjWithCaption);

                           IF AllObjWithCaption.GET(AllObjWithCaption."Object Type"::Page,"Role Center ID") THEN
                             AllObjectsWithCaption.SETRECORD(AllObjWithCaption);

                           AllObjectsWithCaption.LOOKUPMODE := TRUE;
                           IF AllObjectsWithCaption.RUNMODAL = ACTION::LookupOK THEN BEGIN
                             AllObjectsWithCaption.GETRECORD(AllObjWithCaption);
                             VALIDATE("Role Center ID",AllObjWithCaption."Object ID");
                           END;
                         END;
                          }

    { 4   ;2   ;Field     ;
                CaptionML=[DEU=Standardrollencenter;
                           ENU=Default Role Center];
                SourceExpr="Default Role Center";
                OnValidate=BEGIN
                             TESTFIELD("Profile ID");
                             TESTFIELD("Role Center ID");

                             ConfPersMgt.ChangeDefaultRoleCenter("Profile ID");
                           END;
                            }

    { 1900000007;0;Container;
                ContainerType=FactBoxArea }

    { 1900383207;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=RecordLinks }

    { 1905767507;1;Part   ;
                Visible=FALSE;
                PartType=System;
                SystemPartID=Notes }

  }
  CODE
  {
    VAR
      ConfPersMgt@1000 : Codeunit 9170;
      RoleCenterSubtype@1102601000 : Text[30];

    BEGIN
    END.
  }
}

